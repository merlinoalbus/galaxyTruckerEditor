SCRIPTS

SCRIPT newbieDlg

  IF class3
    SUB_SCRIPT tutorClass3Dlg
    RETURN
  END_OF_IF
  IF tutor_moremissions
    Delay 200
    ShowDlgScene
    ChangeChar tutor campaign/tutor-hidden.png
    ShowChar tutor left
    Delay 1500
    ChangeChar tutor campaign/tutor-pizza.png
    Delay 500
    SayChar tutor "Can't you see I'm busy? Get lost!"
    Delay 500
    ChangeChar tutor campaign/tutor-hidden.png
    Delay 500
    HideChar tutor
    Delay 500
    HideDlgScene
    RETURN
  END_OF_IF
  Delay 200
  ShowDlgScene
  ShowChar tutor left
  Delay 200
  IF tutor_somemissions
    Say "Hello, again. Hope you are happy with your boring regular route."
    MENU
      OPT "Yes, I just wanted to say hello."
        HideDlgScene
        RETURN
      END_OF_OPT
      OPT "Could you recommend a different route?"
        GO more_missions
      END_OF_OPT
    END_OF_MENU
  END_OF_IF
  IF tutorialRefused
    IF playerIsCocky
      Ask "Ha, [g(Mr.|Miss)] I-Know-Everything! Changed your mind?"
    ELSE
      Ask "Ha, [g(Mr.|Miss)] I-Hate-Tutorials! Changed your mind?"
    END_OF_IF
    MENU
      OPT "Nah, just show me how to get outta here."
        Say "Okay. You can take a regular route from here to the Space Bar and back."
        Delay 500
        ShowNode "bar"
        CenterMapByNode bar
        Delay 750
        ShowPath "R1-1a"
        ShowPath "R1-1b"
        Delay 500
        CenterMapByPath R1-1a
        Delay 1000
        SET tutor_somemissions
        Ask "It's a safe and boring route. You can spend the rest of your miserable life going back and forth."
        MENU
          OPT "Sounds good."
            GO basic_route_agreed
          END_OF_OPT
          OPT "Hey, give me a real mission!"
            GO more_missions
          END_OF_OPT
        END_OF_MENU
      END_OF_OPT
    END_OF_MENU
  ELSE
    Ask "Ha! It's the newbie! Your training is over. What else do you need?"
    MENU
      OPT "You didn't tell me what to do next."
        Say "Oh, right. You should take a regular route from here to the Space Bar."
        Delay 250
        ShowNode "bar"
        CenterMapByNode bar
        Delay 750
        ShowPath "R1-1a"
        ShowPath "R1-1b"
        Delay 500
        CenterMapByPath R1-1a
        Delay 1000
        SET tutor_somemissions
        Ask "It's a safe and boring route. You can spend the rest of your miserable life going back and forth."
        MENU
          OPT "Sounds good. Thanks."
            Say "Great. Now just [v(tap|click)] on that pulsing rocket, and you can go."
            GO basic_route_agreed
          END_OF_OPT
          OPT "Any other options?"
            GO more_missions
          END_OF_OPT
        END_OF_MENU
      END_OF_OPT
    END_OF_MENU
  END_OF_IF

  LABEL basic_route_agreed
  HideDlgScene
  RETURN

  LABEL more_missions
  Ask "I think that's about all you can handle."
  MENU
    OPT "Okay, you are probably right. I'll take the regular route."
      GO newbie_end
    END_OF_OPT
    OPT "No, I can handle a lot more than that."
      Say "A cocky one, you are. You asked for it."
      Say "There is another route, to a distant outpost."
      Delay 1000
      ChangeChar tutor campaign/tutor-scary.png
      Delay 500
      AskChar tutor "Full of dangers and horrible things, it is."
      MENU
        OPT "Tell me more about these dangers."
          Say "Just terrible and unspeakable things."
          Say "No more questions will I answer now, young padawan."
          Ask "Too much have I said already ..."
          MENU
            OPT "Hey, come on."
              Delay 250
              ChangeChar tutor campaign/tutor.png
              Delay 250
              Say "Well, there are some nasty meteors . Happy?"
              Say "And now, get lost. They're going to deliver me a pizza soon."
              GO more_missions1
            END_OF_OPT
          END_OF_MENU
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL more_missions1
  HideChar tutor
  ResetFocus btutor
  Delay 250
  ShowNode outpost
  CenterMapByNode outpost
  Delay 500
  ShowPath "R1-2a"
  ShowPath "R1-2b"
  CenterMapByPath "R1-2a"
  Delay 500
  SET tutor_moremissions

  LABEL newbie_end
  HideDlgScene
  RETURN
END_OF_SCRIPT

SCRIPT embassyDlg

  ShowDlgScene
  IF purpleEscortFinished
    ShowChar ambassador left
    Delay 250
    ChangeChar ambassador campaign/ambassador-happy.png
    Delay 100
    Say "Thank you for your service. It helped a lot."
    Say "Here, take a reward of 10 credits. It is from our Unexpected Expenses Paid to Unknown Strangers Fund."
    AddCredits 10
    Announce "You received 10 credits."
    Delay 250
    HideButton bambassador
    HideChar ambasador
    HideDlgScene
    RETURN
  END_OF_IF
  IF purpleAccepted
    ShowChar ambassador left
    Delay 250
    Say "Please, we do need to hurry."
    Say "We cannot afford any problems with the aliens from Th'kzw. They are crack marksmen. Ah, I mean, marksaliens."
    Delay 250
    HideChar ambassador
    HideDlgScene
    RETURN
  END_OF_IF
  IFNOT wasAtEmbassy
    SET wasAtEmbassy
    ChangeChar ambassador campaign/ambassador-peek.png
    ShowChar ambassador right
    Delay 250
    Ask "Pssst! Could you please follow me down this dark corridor?"
    MENU
      OPT "Of course. Why not?"
        GO corridor
      END_OF_OPT
      OPT "My mom told me not to follow people down dark corridors."
        Ask "Please. It is a very delicate issue. I can't discuss it in public."
        MENU
          OPT "Oh, all right."
            GO corridor
          END_OF_OPT
          OPT "No, no, no!"
            SET corridorRefused
            GO refused
          END_OF_OPT
        END_OF_MENU
      END_OF_OPT
    END_OF_MENU
  ELSE
    IF purpleOfferMade
      ChangeChar ambassador campaign/ambassador.png
      ShowChar ambassador left
      Delay 250
      Ask "Yes?"
      MENU
        OPT "Do you still need someone to escort Mr. you-know-who to you-know-where?"
          Ask "Yes. Are you offering to do so?"
          MENU
            OPT "Yeah, count me in."
              GO acceptPurple
            END_OF_OPT
            OPT "Nah, I was just curious."
              GO refused
            END_OF_OPT
          END_OF_MENU
        END_OF_OPT
      END_OF_MENU
    ELSE
      ChangeChar ambassador campaign/ambassador-peek.png
      ShowChar ambassador right
      Delay 250
      Say "Yes?"
      MENU
        OPT "I changed my mind. Let's go down the dark corridor."
          GO corridor
        END_OF_OPT
        OPT "Just looking around."
          GO refused
        END_OF_OPT
      END_OF_MENU
    END_OF_IF
  END_OF_IF

  LABEL corridor
  Say "Great, thank you."
  Delay 250
  HideChar ambassador
  Delay 1500
  ChangeChar ambassador campaign/ambassador.png
  ShowChar ambassador left
  Delay 250
  Say "So ... we have a delicate situation here. We recently hosted a delegation from Th'kzw-r. When they took off, they accidentally left one of their officials behind."
  Ask "Would you be so kind as to escort Mr. K'wwfrg to his home planet?"
  SET purpleOfferMade
  MENU
    OPT "Sure. I'll take Mr. K'wwfrg to Th'kzw-r. No problem."
      GO acceptPurple
    END_OF_OPT
    OPT "Why don't you find some fancy diplomatic ship to do the job?"
      Ask "Well, the situation is a bit embarrassing for Mr. K'wwfrg. We would like to handle it quietly and discreetly."
      MENU
        OPT "Okay, count me in."
          GO acceptPurple
        END_OF_OPT
        OPT "I think you better find somebody else."
          GO refused
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL acceptPurple
  Say "Oh, that's great. Thank you."
  Delay 250
  ChangeChar ambassador campaign/ambassador-happy.png
  Delay 250
  Say "Mister K'wwfrg?"
  Delay 1000
  ShowChar purpleDipl right
  Delay 250
  SayChar ambassador "This [g(gentleman|lady)] will accompany you home."
  Delay 250
  AskChar purpleDipl "Wzrr kh'gwh qt Th'kzw-r!"
  MENU
    OPT "You're welcome."
      Delay 250
      HideChar purpleDipl
      Delay 500
      ChangeChar ambassador campaign/ambassador.png
      Delay 250
      SayChar ambassador "Thank you very much. We will reward you as soon as you return."
      SET purpleAccepted
      ShowNode "purpleHome"
      ShowPath "U1-5purple"
      SET purpleAccepted
      ResetFocus bambassador
      IF VIPTried
        HideButton bvip
        ShowButton bvip2
      END_OF_IF
      HideChar ambassador
      HideDlgScene
      RETURN
    END_OF_OPT
  END_OF_MENU

  LABEL refused
  Say "Um, well ... have a good day, then."

  LABEL sceneEnd
  Delay 1000
  HideChar ambassador
  HideDlgScene
  RETURN
END_OF_SCRIPT

SCRIPT barDlg

  ShowDlgScene
  ShowChar bartender left
  Ask "Hello. What can I do for you?"
  IF boozeQuestHeard
    MENU
      OPT_IF mechDebtPending "A mechanic sent me here."
        GO bar_debt
      END_OF_OPT
      OPT_IF boozeQuestTried "Do you need me to make another delivery?"
        GO booze_quest
      END_OF_OPT
      OPT_IF boozeQuestRefused "Do you still need someone to make a delivery?"
        GO booze_quest
      END_OF_OPT
      OPT "Today I want to spend some money here. Pour me your finest drink!"
        GO drinking
      END_OF_OPT
      OPT "Oh, sorry. Just entered your bar accidentally."
        Say "Quite all right. Have a good day."
        GO leaving_bar
      END_OF_OPT
    END_OF_MENU
  END_OF_IF
  MENU
    OPT_IF mechDebtPending "A mechanic sent me here."
      GO bar_debt
    END_OF_OPT
    OPT "Make me your best cocktail!"
      Ask "Of course. That will be 1 credit, please."
      IF_HAS_CREDITS 1
        MENU
          OPT "I can afford it. I made some money on the way here. Here you go:"
            Say "Thank you."
            AddCredits -1
            Announce "You have paid 1 credit."
            GO anything_else
          END_OF_OPT
          OPT "That's way too expensive. I'm leaving."
            Say "All right. Have a good day."
            GO leaving_bar
          END_OF_OPT
        END_OF_MENU
      ELSE
        MENU
          OPT "Eh, forget it. I'm kind of short on money right now."
            Say "Very well. Have a good day."
            GO leaving_bar
          END_OF_OPT
        END_OF_MENU
      END_OF_IF
    END_OF_OPT
    OPT "Just looking around. You have a nice bar."
      GO nice_bar
    END_OF_OPT
    OPT "This dingy place stinks. I'm outta here."
      Say "All right. Have a good day."
      GO leaving_bar
    END_OF_OPT
  END_OF_MENU
  HideChar programmer
  HideDlgScene

  LABEL anything_else
  Say "Anything else?"
  MENU
    OPT "That's what I wanted to ask. Anything else? No mission or anything?"
      Say "Excuse me?"
      MENU
        OPT "I just spent some game money for a virtual beverage. It should lead to a mission!"
          Delay 200
          ChangeChar bartender campaign/bartender-close.png
          Delay 200
          Say "Ah, I understand now. I'm sorry, you picked the wrong dialogue branch."
          Say "The one leading to a mission was the looking-around one. And it was free."
          Delay 200
          ChangeChar bartender campaign/bartender.png
          Delay 200
          Say "Anything else?"
          MENU
            OPT_IFNOT bar_quest_insist "Gimme a mission."
              SET bar_quest_insist
              Ask "Very sorry, but we just make drinks here. Anything else?"
            END_OF_OPT
            OPT "One more drink, please!"
              GO drinking
            END_OF_OPT
            OPT "Nah. I'm okay. Just looking around. You have a nice bar."
              GO nice_bar
            END_OF_OPT
          END_OF_MENU
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL drinking
  IF_HAS_CREDITS 1
    Say "Coming right up! Here you go. One credit, please."
    AddCredits -1
    Announce "You have paid 1 credit."
    MENU
      OPT "*Gulp*"
        Ask "Anything else?"
        MENU
          OPT "Eh, pour me another."
            GO drinking
          END_OF_OPT
          OPT "Hm. I think that was one drink too many. Maybe I should leave."
            Say "Thank you. Have a nice day."
            GO leaving_bar_in_shame
          END_OF_OPT
        END_OF_MENU
      END_OF_OPT
    END_OF_MENU
  ELSE
    Say "One credit, please ... Oh, I see you're out of money."
    Say "You may take this lollipop. It is on the house. And now, please leave quietly."
    GO leaving_bar_in_shame
  END_OF_IF

  LABEL nice_bar
  Say "Thank you. This was a great old place, once."
  MENU
    OPT "Once?"
      Say "Business is not going so well these days. Actually, we're about to shut down."
      Say "I have lots of high quality beverages in stock. But truckers prefer cheaper stuff these days."
      Ask "I wish I had opened a bar on Regula instead. Those people appreciate quality."
      MENU
        OPT "Why don't you sell some of your surplus beverages to Regula?"
          SET boozeQuestHeard
          Say "That's not so easy. You know, it is a dangerous route for such a fragile commodity."
          Ask "I doubt I could find a trucker willing to deliver to Regula."
          MENU
            OPT "You just found one!"
              GO booze_quest
            END_OF_OPT
            OPT "I doubt, it, too. Good luck, I'm leaving."
              SET boozeQuestRefused
              Say "Thank you. Have a good day."
              GO leaving_bar
            END_OF_OPT
          END_OF_MENU
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL leaving_bar_in_shame
  Delay 200
  ChangeChar bartender campaign/bartender-close.png
  Delay 200
  Say "Oh ... And would you be so kind as to leave by the back door? Thank you."

  LABEL leaving_bar
  Delay 300
  HideChar bartender
  HideDlgScene
  RETURN

  LABEL booze_quest
  Delay 200
  ChangeChar bartender campaign/bartender-close.png
  Delay 200
  Say "Oh, would you be so kind?"
  Ask "How many crates of beverage would you be willing to take?"
  MENU
    OPT "Give me two crates. No problem to deliver them."
      HidePath "R1-3a"
      ShowPath "R1-3a2"
      Say "You are very generous!"
      Say "The crates will be ready at the component warehouse."
      GO quest_agreed
    END_OF_OPT
    OPT "Well, my ship is kinda small. One crate should be enough."
      HidePath "R1-3a"
      ShowPath "R1-3a1"
      Say "You are very kind."
      Say "The crate will be ready at the component warehouse."
      GO quest_agreed
    END_OF_OPT
    OPT "You know what? Forget it. That run is too dangerous for fragile stuff."
      SET boozeQuestRefused
      RESET boozeQuestTried
      Say "*Sigh*... You are probably right. Have a nice day."
      GO leaving_bar
    END_OF_OPT
  END_OF_MENU

  LABEL quest_agreed
  ResetFocus bbartender
  SET boozeQuestAccepted
  RESET boozeQuestRefused
  Delay 300
  ChangeChar bartender campaign/bartender.png
  Say "Thank you and have a nice day."
  GO leaving_bar

  LABEL bar_debt
  IF debtRefused
    ChangeChar bartender campaign/bartender-close.png
    Ask "And have you come to settle her debt?"
  ELSE
    Say "Ah. I think I know the lady you mean. She had such a memorable name."
    Say "And the party she had here was also memorable."
    Say "Unfortunately, I also remember how much money she spent."
    ChangeChar bartender campaign/bartender-close.png
    Ask "Did she, by any chance, send you to settle her debt?"
  END_OF_IF
  MENU
    OPT "Yep. That's why I'm here."
      IF_HAS_CREDITS 21
        SET hasEnoughForDebt
      END_OF_IF
      Ask "Oh, wonderful! It was 21 credits."
      MENU
        OPT_IFNOT hasEnoughForDebt "Oh. I ... seem to have left my [g(wallet|pocketbook)] back on my ship."
          GO bar_debt_refused
        END_OF_OPT
        OPT_IF hasEnoughForDebt "Of course. Here you go:"
          RESET mechDebtPending
          Delay 300
          AddCredits -21
          Announce "You have paid 21 credits."
          ChangeChar bartender campaign/bartender.png
          ResetFocus bbartender
          Ask "Thank you very much, [g(sir|miss)]. I hope you have a wonderful day."
          MENU
            OPT "Right back at ya."
              UnlockAchievement camp_honesty
              GO leaving_bar
            END_OF_OPT
            OPT "Wait. That's it? Just a thank you?"
              EXIT_MENU
            END_OF_OPT
          END_OF_MENU
          Ask "Well, the debt is settled. Everything is okay. What more were you expecting?"
          MENU
            OPT "I don't know. Some sort of reward. Or at least another mission."
              Say "I'm sorry. I don't have a new mission for you."
              Say "Would you care for an achievement?"
              UnlockAchievement camp_honesty
              Delay 1500
              Say "There. I hope that is satisfactory."
              Say "Have a nice day."
              GO leaving_bar
            END_OF_OPT
          END_OF_MENU
        END_OF_OPT
        OPT_IFNOT debtConfusion "Hm. She said it was only 20 credits."
          SET debtConfusion
          ChangeChar bartender campaign/bartender.png
          Ask "No, I am pretty sure it was 21. I still have the bill here."
        END_OF_OPT
        OPT "Hm. You know what? I don't think we're talking about the same mechanic. I'll keep my money."
          GO bar_debt_refused
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
    OPT "Hm. You know what? I don't think we're talking about the same mechanic. I'll keep my money."
      GO bar_debt_refused
    END_OF_OPT
  END_OF_MENU

  LABEL bar_debt_refused
  SET debtRefused
  Delay 500
  Say "Ah. I see."
  ChangeChar bartender campaign/bartender.png
  Say "Well. Have a nice day."
  GO leaving_bar
END_OF_SCRIPT

SCRIPT regulaDlg

  ShowDlgScene
  ShowChar clerk left
  Delay 1200
  ChangeChar clerk campaign/clerk-frown.png
  Delay 500
  IF class3
    Ask "You again? What do you want now?"
    MENU
      OPT "Um, actually, nothing."
        GO leaveClerk
      END_OF_OPT
      OPT "How about a license for class IV ships?"
        ChangeChar clerk campaign/clerk.png
        Ask "There is no such class. Where did you get such a ridiculous idea?"
        MENU
          OPT_IFNOT gtabe "Um, probably from Galaxy Trucker: Another Big Expansion."
            SET gtabe
            Ask "I have no idea what you are talking about. There is no class IV."
          END_OF_OPT
          OPT "Well, what should I do now?"
            Say "That is not my concern. I am not your nanny."
            Delay 300
            ChangeChar clerk campaign/clerk-frown.png
            Say "Go ask your flight instructor, if you can't think for yourself."
            GO leaveClerk
          END_OF_OPT
        END_OF_MENU
      END_OF_OPT
    END_OF_MENU
  END_OF_IF
  IF class3Asked
    Say "You again? I told you, class III ships aren't for the likes of you."
    ChangeChar clerk campaign/clerk.png
    Ask "Or ... do you have 450 credits? Do you have vouchers from two alien species?"
    RESET oneApproval
    RESET noApproval
    IF brownLicenseApproval
      IF purpleLicenseApproval
        SET twoApprovals
      ELSE
        SET oneApproval
      END_OF_IF
    ELSE
      IF purpleLicenseApproval
        SET oneApproval
      ELSE
        SET noApproval
      END_OF_IF
    END_OF_IF
    IF_HAS_CREDITS 450
      MENU
        OPT_IF twoApprovals "Of course I do. What did you think?"
          ChangeChar clerk campaign/clerk.png
          Delay 200
          Say "Really? Let me check ..."
          Delay 500
          ChangeChar clerk campaign/clerk-frown.png
          Delay 1500
          ChangeChar clerk campaign/clerk.png
          Delay 300
          Say "Hm ... your papers seem to be in order ... bank account looks good."
          Delay 300
          Say "Uh ..."
          Delay 300
          Say "Very well. Allow me to congratulate you on obtaining your prestigious class III license, [g(sir|ma'am)]."
          AddCredits -450
          Announce "You paid 450 credits. Class III ships unlocked!"
          UnlockAchievement camp_class3
          Delay 500
          SUB_SCRIPT class3_inits
          ResetFocus bclerk
          GO afterUnlock
        END_OF_OPT
        OPT_IF oneApproval "The money's no problem. And I already have one voucher."
          Say "Until you have everything, you have nothing."
          Say "Please go away. I have work to do."
          GO leaveClerk
        END_OF_OPT
        OPT_IF noApproval "Well ... I do have the money."
          Say "Good for you. Enjoy your money. It will buy you nothing here until you get those vouchers."
          Say "Now go away. I have work to do."
          GO leaveClerk
        END_OF_OPT
      END_OF_MENU
    ELSE
      MENU
        OPT_IF twoApprovals "Well ... I have the documents you asked for ..."
          Say "The documents are useless if you cannot pay the processing fee."
          Say "Now, if you'll excuse me, I have work to do."
          GO leaveClerk
        END_OF_OPT
        OPT_IF oneApproval "I've got a voucher."
          Say "One voucher? You interrupted my work to tell me you have one voucher? Hmf!"
          GO leaveClerk
        END_OF_OPT
        OPT_IF noApproval "Well, not yet. But I'm working on it."
          Say "No, you are not. What you are doing, [g(sir|madam)], is wasting my time!"
          GO leaveClerk
        END_OF_OPT
      END_OF_MENU
    END_OF_IF
  END_OF_IF
  IF class2
    Ask "Oh. It's you. You have your class II license. What are you doing here now?"
    MENU
      OPT "Um, actually, nothing."
        GO leaveClerk
      END_OF_OPT
      OPT "How about a license for class III ships?"
        Say "Class III ships? No, I don't think so."
        ChangeChar clerk campaign/clerk.png
        Say "The class III requirements are quite strict."
        Say "The license costs 450 credits and you need signed vouchers from two alien species."
        Say "With all due respect, [g(sir|madam)], I think you lack the diplomatic tact required to gain the approval of the aliens."
        SET class3Asked
        ResetFocus bclerk
        IF purpleUnlocked
          SetFocus bpurple
        END_OF_IF
        IF brownUnlocked
          ShowButton bmerch2
          SetFocus bmerch2
        END_OF_IF
        Say "Now please go away. I have work to do."
        GO leaveClerk
      END_OF_OPT
    END_OF_MENU
  END_OF_IF
  Ask "What do you want?"
  MENU
    OPT "Um ... nothing."
      GO leaveClerk
    END_OF_OPT
    OPT "I would like to get license for class II ships."
      ResetFocus bclerk
      SetFocusIfCredits bclerk 100
      Ask "The license costs 100 credits!"
      RESET tooMuchFor2
      RESET whereToGetSoMuch
      RESET hasFor2
      RESET hasFewer
      IF_HAS_CREDITS 100
        SET hasFor2
      ELSE
        SET tooMuchFor2
      END_OF_IF
      IF_HAS_CREDITS 50
        SET hasFewer
        IF clerkBribe
          IF clerkDiscount
            RESET hasFewer
          END_OF_IF
        END_OF_IF
      END_OF_IF
      MENU
        OPT_IF hasFor2 "No problem. Take the money and give me the license."
          Delay 100
          ChangeChar clerk campaign/clerk.png
          Delay 300
          Say "Oh, you really have that much?"
          Say "In that case, here is your license[g(, sir.|.)]"
          Delay 300
          AddCredits -100
          Announce "You paid 100 credits. Class II ships unlocked!"
          UnlockAchievement camp_class2
          Delay 500
          SUB_SCRIPT class2_inits
          HideButton bclerk
          GO afterUnlock
        END_OF_OPT
        OPT_IF tooMuchFor2 "That's way too expensive!"
          RESET tooMuchFor2
          SET whereToGetSoMuch
          Delay 300
          ChangeChar clerk campaign/clerk.png
          Say "I assure you, the cost is exactly as much as it should be."
          Delay 500
          ChangeChar clerk campaign/clerk-frown.png
          Ask "We can't let riff-raff fly upper-class ships."
        END_OF_OPT
        OPT_IF whereToGetSoMuch "Where I am supposed to get that kind of money?"
          RESET whereToGetSoMuch
          Say "You work for it. Just like I work for my money."
          Say "Sitting in this office, four hours a day with just a one-hour lunch break, three days a week."
          Say "From what I hear, truckers get to just fly around, scraping together money every which way."
          Ask "And then they spend that money here. In exchange for my signature on this form."
        END_OF_OPT
        OPT_IF hasFewer "How about if I give you 50 and we call it close enough?"
          Delay 900
          Ask "Are you trying to bribe me?"
          MENU
            OPT_IFNOT clerkDiscount "No, I am trying to haggle a discount."
              Delay 300
              ChangeChar clerk campaign/clerk.png
              Say "[g(Sir|Madam)], you are on Regula."
              Say "No discount has been recorded on Regula in the past 200 years."
              Delay 200
              ChangeChar clerk campaign/clerk-frown.png
              Delay 300
              Say "In fact, I am certain that if I search the regulations, I can find a few surcharges that apply to you."
              Ask "Or, you can just pay 100 credits."
              SET clerkDiscount
              EXIT_MENU
            END_OF_OPT
            OPT_IFNOT clerkBribe "And what if I am?"
              Delay 300
              ChangeChar clerk campaign/clerk.png
              Say "Oh. Well then you would have to apply for a form B-29, Bribes and Other Nonstandard Remunerations."
              Say "Which I doubt you are clever enough to fill out correctly."
              Say "The regulations governing bribery are quite complex."
              Say "I suggest you forget it. The art of corruption was perfected here centuries ago."
              Delay 200
              ChangeChar clerk campaign/clerk-frown.png
              Delay 300
              Ask "As an amateur, you are better off just paying the 100 credits."
              SET clerkBribe
              EXIT_MENU
            END_OF_OPT
            OPT "Um ... forget I said that, please."
              Ask "Of course. So, will you pay the 100 credits?"
              EXIT_MENU
            END_OF_OPT
          END_OF_MENU
        END_OF_OPT
        OPT "Well, I will consider it. Good bye."
          GO leaveClerk
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL afterUnlock
  MENU
    OPT "Good. I am outta here."
      GO leaveClerk
    END_OF_OPT
    OPT "Thank you. You are very kind."
      GO leaveClerk
    END_OF_OPT
  END_OF_MENU

  LABEL leaveClerk
  Delay 500
  ChangeChar clerk campaign/clerk-frown.png
  Delay 500
  HideChar clerk
  HideDlgScene
  RETURN
END_OF_SCRIPT

END_OF_SCRIPTS
