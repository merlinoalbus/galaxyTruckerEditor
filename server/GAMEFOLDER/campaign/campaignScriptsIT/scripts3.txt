SCRIPTS

SCRIPT forgeDlg

  ShowDlgScene
  ShowChar foreman left
  Delay 750
  IF ingotsPending
    Say "Oh, hi! You still here?"
    ChangeChar foreman campaign/foreman-no.png
    Say "Say, you know, they still need those ingots at the space factory, yeah?"
    GO forgeEnd
  END_OF_IF
  IF explosivesPending
    Say "Oh, hi! You still here?"
    ChangeChar foreman campaign/foreman-no.png
    Say "Say, you know, they're still expecting you to deliver those explosives to that asteroid, yeah?"
    IFNOT explosivesFailed
      GO forgeEnd
    END_OF_IF
    IF toMine0
      GO forgeEnd
    END_OF_IF
    MENU
      OPT "Yeah. I had some problems with that. Going to try it again."
        ChangeChar foreman campaign/foreman.png
        Say "Okay."
        GO forgeEnd
      END_OF_OPT
      OPT "That turned out to be a lot of explosives. Do you have an easier job?"
        ChangeChar foreman campaign/foreman.png
        RESET explosivesFailed
        IF toMine2
          RESET toMine2
          HidePath U2-16a
          Ask "Oh. Sure. You could take four bundles of explosives to Gamma."
          MENU
            OPT "Okay."
              SET toMine1
              ShowPath U2-15a
              EXIT_MENU
            END_OF_OPT
            OPT "That ... still sounds like a lot of explosives."
              SET toMine0
              ShowPath U2-14a
              Say "Well, haul two bundles to Delta, then. And don't give me any more excuses."
              EXIT_MENU
            END_OF_OPT
          END_OF_MENU
          EXIT_MENU
        ELSE
          RESET toMine1
          HidePath U2-15a
          SET toMine0
          ShowPath U2-14a
          Say "Hm. I suppose I could give you two bundles for Delta. It doesn't get much easier than that."
          EXIT_MENU
        END_OF_IF
      END_OF_OPT
    END_OF_MENU
    GO forgeEnd
  END_OF_IF
  IF forgeAccepted
    ChangeChar foreman campaign/foreman-hello.png
    Say "Ho boy! It's my trucker!"
    ChangeChar foreman campaign/foreman-smile.png
    Ask "Are you ready for another job?"
    MENU
      OPT_IFNOT ingotsAccepted "Yeah, I'm ready to fly to the space factory."
        GO forgeIngots
      END_OF_OPT
      OPT_IF ingotsAccepted "Yeah, I can haul some more ingots to the space factory for you."
        GO forgeIngotsCont
      END_OF_OPT
      OPT_IFNOT explosivesAccepted "Yeah, I'm ready to visit the mining asteroids."
        GO forgeExplosives
      END_OF_OPT
      OPT_IF explosivesAccepted "Yeah, I can haul some more explosives to the mining asteroids for you."
        GO forgeExplosivesCont
      END_OF_OPT
      OPT "Not right now. Maybe later."
        ChangeChar foreman campaign/foreman-whatsup.png
        Say "Oh, okay. Yeah. Maybe later."
        GO forgeEnd
      END_OF_OPT
    END_OF_MENU
  END_OF_IF
  IF forgeOffered
    Ask "Hey, it's you! Did you change your mind?"
    MENU
      OPT "Yeah. I'm ready to visit the space factory now."
        Say "Great."
        GO forgeIngots
      END_OF_OPT
      OPT "Yeah. I'm ready to go to the mining asteroids."
        Say "Great."
        GO forgeExplosives
      END_OF_OPT
      OPT_IFNOT mineUnlocked "Not really. But I'd like to know how to find the mining asteroids."
        SET mineUnlocked
        Say "Oh, I see."
        Say "You don't wanna help."
        Say "But you're not ashamed to ask for directions."
        Say "Oh well. Okay. The asteroids are this way:"
        Delay 500
        ShowNode mine0
        Delay 500
        ShowPath R2-14a
        Delay 500
        Say "But you'll be in for a surprise! (Heh, heh.)"
        Say "Now take off."
        GO forgeEnd
      END_OF_OPT
      OPT "No. I'm just hanging out."
        ChangeChar foreman campaign/foreman-no.png
        Say "Take off!"
        Say "I got work to do."
        GO forgeEnd
      END_OF_OPT
    END_OF_MENU
  END_OF_IF
  Ask "Ho, boy! It's about time you got here."
  MENU
    OPT "Are you talking to me?"
      Say "Yeah, I'm talking to you."
      Say "You're the trucker, yeah?"
      GO forgeExpl
    END_OF_OPT
    OPT "You were expecting me?"
      Say "Of course!"
      GO forgeExpl
    END_OF_OPT
    OPT "Yeah, I feel the same way."
      Ask "Okay, good. Let's get to work."
      MENU
        OPT "Um, work?"
          Say "Yeah. Let's get to work."
          GO forgeExpl
        END_OF_OPT
        OPT "Um ... yeah, sure. Let's get to work."
          ChangeChar foreman campaign/foreman-smile.png
          Say "Good! You're my kinda trucker."
          GO forgeExpl
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL forgeExpl
  SET forgeOffered
  Say "You know, I asked Regula to send you here 2 weeks ago! Meanwhile, everything's piling up."
  Say "I'm getting calls from the space factory and the mining asteroids. Ho boy!"
  ChangeChar foreman campaign/foreman-smile.png
  Ask "Where do you want to go first?"
  MENU
    OPT "How about the space factory?"
      Say "Okay."
      GO forgeIngots
    END_OF_OPT
    OPT "How about the mining asteroids?"
      Say "Okay."
      GO forgeExplosives
    END_OF_OPT
    OPT "I hate to disappoint you, but I'm not the [g(guy|gal)] you've been expecting."
      Ask "You're not my trucker? Who are you, then?"
      MENU
        OPT "I'm just a trucker who happened to be passing through."
          ChangeChar foreman campaign/foreman-whatsup.png
          Ask "Oh ... Well, you wanna be my trucker anyway?"
          MENU
            OPT "No way."
              ChangeChar foreman campaign/foreman.png
              Say "Oh ... well ... okay. When you get back to Regula, tell them I'm still waiting for a trucker."
              Say "One who doesn't mind a little work, okay?"
              GO forgeEnd
            END_OF_OPT
            OPT "Well, I suppose I could haul something to that space factory for you."
              ChangeChar foreman campaign/foreman-smile.png
              Say "Ho boy! Thanks a lot!"
              GO forgeIngots
            END_OF_OPT
            OPT "Well, I suppose I could haul something to the mining asteroids for you."
              ChangeChar foreman campaign/foreman-smile.png
              Say "Ho boy! Thanks a lot!"
              GO forgeExplosives
            END_OF_OPT
          END_OF_MENU
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL forgeIngots
  IFNOT workshopInitialized
    Say "We've been forging megamassive ingots for the factory and ... uh ... let's see here ..."
    ChangeChar foreman campaign/foreman-list.png
    Say "Oh, never mind."
    Say "It says here, the factory isn't running yet. They need workers."
    Say "I guess I'm supposed to send you to Regula to pick up some new workers for the factory."
    ChangeChar foreman campaign/foreman.png
    Ask "Ho boy! I ask Regula to send me a trucker just so I can send [g(him|her)] back to Regula! Crazy, yeah?"
    MENU
      OPT "No problem. I can go to Regula."
        GO forgeEnd
      END_OF_OPT
      OPT "Well, what about the mining asteroids?"
        GO forgeExplosives
      END_OF_OPT
    END_OF_MENU
  END_OF_IF
  Say "We've been forging megamassive ingots."
  Say "We need to hook them onto a ship that can haul them to the factory."
  ChangeChar foreman campaign/foreman-whatsup.png
  Ask "You'll need to build a ship that has some real hauling power, yeah? Lotsa engines."
  MENU
    OPT "No problem. Let's do it."
      ChangeChar foreman campaign/foreman-smile.png
      Say "Fer sure!"
      SET ingotsAccepted
      SET ingotsPending
      SET oneingot
      SET ing3
      HideAllPaths forge workshop
      ShowPath U2-13a1
      GO forgeAcceptEnd
    END_OF_OPT
    OPT "Hmm ... No. I don't think so."
      ChangeChar foreman campaign/foreman-smile.png
      Say "Oh ... well ... okay."
      Delay 300
      ChangeChar foreman campaign/foreman.png
      Delay 300
      Say "(Truckers today. What a bunch of loafers!)"
      GO forgeEnd
    END_OF_OPT
  END_OF_MENU

  LABEL forgeIngotsCont
  IF threeingots
    IF ing2
      RESET ingots3small
      SET ingots3medium
    END_OF_IF
  ELSE
    IF twoingots
      IF ing2
        RESET ingots2small
        SET ingots2medium
        SET ingots3small
      END_OF_IF
      IF ing3
        RESET ingots2medium
        SET ingots2big
      END_OF_IF
    ELSE
      IF ing3
        SET ingots2small
        SET ingot1big
      END_OF_IF
      IF ing5
        RESET ingot1big
        SET ingot1bigger
      END_OF_IF
      IF ing6
        RESET ingot1bigger
        SET ingot1biggest
      END_OF_IF
    END_OF_IF
  END_OF_IF
  RESET ing2
  RESET ing3
  RESET ing4
  RESET ing5
  RESET ing6
  RESET ing7
  RESET ing8
  RESET oneingot
  RESET twoingots
  RESET threeingots
  ChangeChar foreman campaign/foreman-list.png
  Ask "Okay. Here's what we need you to haul. How much can you take in one trip?"
  MENU
    OPT_IF ingots2small "I'll take 2 small ingots."
      SET twoingots
      SET ing2
      EXIT_MENU
    END_OF_OPT
    OPT_IF ingots2medium "I'll take 2 medium-sized ingots."
      SET twoingots
      SET ing3
      EXIT_MENU
    END_OF_OPT
    OPT_IF ingots2big "Gimme 2 big ones."
      SET twoingots
      SET ing4
      EXIT_MENU
    END_OF_OPT
    OPT_IF ingots3small "I'll take 3 small ingots."
      SET threeingots
      SET ing2
      EXIT_MENU
    END_OF_OPT
    OPT_IF ingots3medium "I'll take 3 medium-sized ingots."
      SET threeingots
      SET ing3
      EXIT_MENU
    END_OF_OPT
    OPT_IF ingot1big "I'll take 1 big ingot."
      SET oneingot
      SET ing5
      EXIT_MENU
    END_OF_OPT
    OPT_IF ingot1bigger "Give me a bigger ingot."
      SET oneingot
      SET ing6
      EXIT_MENU
    END_OF_OPT
    OPT_IF ingot1biggest "I'll take that humongous ingot."
      SET oneingot
      SET ing8
      EXIT_MENU
    END_OF_OPT
    OPT "Hm ... nothing today. Maybe later."
      GO forgeEnd
    END_OF_OPT
  END_OF_MENU
  ChangeChar foreman campaign/foreman-smile.png
  Say "Okeydokey. It'll be there when you build your ship. Good luck!"
  HideAllPaths forge workshop
  IF oneingot
    ShowPath U2-13a1
  END_OF_IF
  IF twoingots
    ShowPath U2-13a2
  END_OF_IF
  IF threeingots
    ShowPath U2-13a3
  END_OF_IF
  SET ingotsPending
  GO forgeAcceptEnd

  LABEL forgeExplosives
  ChangeChar foreman campaign/foreman-smile.png
  Delay 300
  Say "Did ya know that asteroid mining is fully automated?"
  Say "Nobody works there but robots."
  Delay 300
  ChangeChar foreman campaign/foreman-list.png
  Delay 700
  Ask "Anyway, the robots say they need two 2 bundles of explosives."
  MENU
    OPT "Sounds good."
      IF mineUnlocked
        HideAllPaths forge mine0
        ShowPath U2-14a
        CenterMapByPath U2-14a
        Say "Okay. You know the way."
      ELSE
        Say "Okay. Let me show you how to get there."
        Delay 500
        ShowNode mine0
        Delay 500
        ShowPath U2-14a
        CenterMapByPath U2-14a
      END_OF_IF
      Delay 500
      Say "You'll find the explosives when you get ready to build your ship."
      SET explosivesAccepted
      SET explosivesPending
      SET toMine0
      GO forgeAcceptEnd
    END_OF_OPT
    OPT_IFNOT forgeExcuseExpl "Actually, I've got kind of a phobia about explosives."
      Say "Okeydokey. You can just put them at the far end of the ship, yeah?"
      SET forgeExcuseExpl
      IF forgeExcuseBots
        SET forgeBothExcuses
      END_OF_IF
    END_OF_OPT
    OPT_IFNOT forgeExcuseBots "Actually, I think robots are kind of creepy."
      Say "Oh dontcha worry. You'll be protected by the twenty-seven laws of robotics."
      Say "Ya know, there used to be only three laws ..."
      Say "But since the invention of lawyerbots ... well, ya know how it is. Anyway. You'll be safe with them."
      SET forgeExcuseBots
      IF forgeExcuseExpl
        SET forgeBothExcuses
      END_OF_IF
    END_OF_OPT
    OPT_IF forgeBothExcuses "I guess I'm out of excuses. But I still don't want to haul explosives to robots."
      Say "Oh, so you're a loafer, are ya?"
      Say "Well, take off!"
      RESET forgeBothExcuses
      GO forgeEnd
    END_OF_OPT
  END_OF_MENU

  LABEL forgeExplosivesCont
  Ask "Thanks a bunch. Which asteroid are you going to?"

  LABEL forgeQuerry
  RESET canAsk3
  IF mine3Initialized
    IFNOT smallestAsked
      SET canAsk3
    END_OF_IF
  END_OF_IF
  RESET toMine0
  RESET toMine1
  RESET toMine2
  MENU
    OPT_IFNOT mine1Initialized "Um ... I only know one."
      Delay 300
      Say "Right. Delta. Just a minute ..."
      Delay 300
      ChangeChar foreman campaign/foreman-list.png
      Delay 500
      Say "Okayyyy. Says here they need 2 bundles of explosives."
      SET toMine0
      EXIT_MENU
    END_OF_OPT
    OPT_IF mine1Initialized "The big one."
      Say "Right. Delta. Just a minute ..."
      Delay 300
      ChangeChar foreman campaign/foreman-list.png
      Delay 500
      Say "Okayyy. Says here they need 2 bundles of explosives."
      SET toMine0
      EXIT_MENU
    END_OF_OPT
    OPT_IF mine1Initialized "That smaller one."
      Say "Gamma? Let me see here ..."
      Delay 300
      ChangeChar foreman campaign/foreman-list.png
      Delay 500
      Say "They ordered 4 bundles of explosives."
      SET toMine1
      EXIT_MENU
    END_OF_OPT
    OPT_IF mine2Initialized "That even smaller one."
      Say "Oh, yeah, Beta. Nice mine. Let's see ..."
      Delay 300
      ChangeChar foreman campaign/foreman-list.png
      Delay 500
      Say "They want 6 bundles of explosives."
      SET toMine2
      EXIT_MENU
    END_OF_OPT
    OPT_IF canAsk3 "The eensiest teensiest one."
      Say "Oh, Alpha is all mined out. We don't ship there."
      Ask "Pick a different one."
      SET smallestAsked
      RESET canAsk3
    END_OF_OPT
    OPT_IFNOT canAsk3 "You know, maybe I won't go to any asteroids today."
      GO forgeEnd
    END_OF_OPT
  END_OF_MENU
  MENU
    OPT "Sounds good."
      Say "Okay! Good luck!"
      SET explosivesPending
      IF toMine0
        ShowPath U2-14a
      END_OF_IF
      IF toMine1
        ShowPath U2-15a
      END_OF_IF
      IF toMine2
        ShowPath U2-16a
      END_OF_IF
      GO forgeAcceptEnd
    END_OF_OPT
    OPT_IFNOT minesWithoutDelivery "Out of curiosity, can I visit the asteroids without making a delivery for you?"
      SET minesWithoutDelivery
      Say "No way. Those robots like their privacy, ya know?"
      Say "If you don't have a job there, you should just leave them alone."
    END_OF_OPT
    OPT "Hmm ... you know, that just sounds too risky."
      GO forgeEnd
    END_OF_OPT
  END_OF_MENU

  LABEL forgeAcceptEnd
  IFNOT class3
    IFNOT forgeAccepted
      SET forgeAccepted
    ELSE
      IFNOT class3Recommended
        SET class3Recommended
        Say "Ya know, I wish I had more truckers like you. Smart. Fast. And not afraid of a little work, yeah?"
        Ask "Hey, have you applied for a class III license yet?"
        MENU
          OPT_IFNOT class3Asked "No, not yet. How would I do that?"
            Say "Oh, you go to Regula. See some guy at the Department of Stellar Vehicles. He'll tell you all about it."
            Say "You should stop in the next time you're there. But right now, you got a job to do, yeah?"
            SetFocus bclerk
            EXIT_MENU
          END_OF_OPT
          OPT_IF class3Asked "Well, I've looked into it. It's not that easy."
            Say "Oh, nothing's too hard for my trucker. I'm sure you can handle it."
            Say "But right now, you got a job to do, yeah?"
            EXIT_MENU
          END_OF_OPT
        END_OF_MENU
      ELSE
        IFNOT class3Asked
          Say "Oh, and don't forget: Stop by that office and ask about a class III license the next time you're on Regula. You're good enough to get one. Fer sure."
        END_OF_IF
      END_OF_IF
    END_OF_IF
  ELSE
    SET forgeAccepted
  END_OF_IF

  LABEL forgeEnd
  HideChar foreman
  HideDlgScene
  RETURN
END_OF_SCRIPT

SCRIPT sailorZoomOut

  Delay 200
  ChangeChar sailor campaign/sailor.png
  SET_TO sailor 4
END_OF_SCRIPT

SCRIPT sailorAnim

  Delay 200
  IF_IS sailor 4
    SET_TO sailor 0
  ELSE
    IF_MIN sailor 2
      IF_IS sailor 3
        IF_PROB 33
          SET_TO sailor 2
        ELSE
          IF_PROB 50
            SET_TO sailor 1
          ELSE
            SET_TO sailor 0
          END_OF_IF
        END_OF_IF
      ELSE
        IF_PROB 33
          SET_TO sailor 3
        ELSE
          IF_PROB 50
            SET_TO sailor 1
          ELSE
            SET_TO sailor 0
          END_OF_IF
        END_OF_IF
      END_OF_IF
    ELSE
      IF_IS sailor 1
        IF_PROB 33
          SET_TO sailor 3
        ELSE
          IF_PROB 50
            SET_TO sailor 2
          ELSE
            SET_TO sailor 0
          END_OF_IF
        END_OF_IF
      ELSE
        IF_PROB 33
          SET_TO sailor 3
        ELSE
          IF_PROB 50
            SET_TO sailor 2
          ELSE
            SET_TO sailor 1
          END_OF_IF
        END_OF_IF
      END_OF_IF
    END_OF_IF
  END_OF_IF
  IF_IS sailor 0
    ChangeChar sailor campaign/sailor0.png
  END_OF_IF
  IF_IS sailor 1
    ChangeChar sailor campaign/sailor1.png
  END_OF_IF
  IF_IS sailor 2
    ChangeChar sailor campaign/sailor2.png
  END_OF_IF
  IF_IS sailor 3
    ChangeChar sailor campaign/sailor3.png
  END_OF_IF
  Delay 200
END_OF_SCRIPT

SCRIPT treasureFound

  ShowDlgScene
  Announce "This planet appears to be marrrked with a rrred X."
  Delay 500
  Announce "After a bit of work, you manage to dig up a larrrge black chest."
  Delay 1000
  Announce "You open the lid ..."
  Delay 1000
  Announce "... and find 20 credits!"
  MENU
    OPT "Well, it's not a fortune, but it's still better than nothing."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  AddMissionCredits 20
  Delay 500
  UnlockAchievement camp_treasure
  Delay 500
  RESET treasurePlanet
  HideDlgScene
END_OF_SCRIPT

SCRIPT barGuestsDlg

  ShowDlgScene
  Delay 300
  IF sailorAvailable
    IF ratAvailable
      ShowChar sailor left
      Delay 300
      ShowChar rat right
      Delay 500
      MENU
        OPT "Sit down next to the old sailor on the left."
          Delay 500
          HideChar rat
          Delay 300
          GO sailorDlg
        END_OF_OPT
        OPT "Sit down next to the sleazy looking guy on the right."
          Delay 500
          HideChar sailor
          Delay 300
          GO ratDlg
        END_OF_OPT
        OPT "Or you could just leave. It's kinda crowded here."
          HideChar sailor
          Delay 100
          HideChar rat
          HideDlgScene
          RETURN
        END_OF_OPT
      END_OF_MENU
    ELSE
      ShowChar sailor left
      Delay 300
      GO sailorDlg
    END_OF_IF
  ELSE
    IF ratAvailable
      ShowChar rat right
      Delay 300
      GO ratDlg
    ELSE
      Announce "Awfully quiet in the bar right now."
      IF ratFinished
        IF sailorFinished
          HideButton bguests
        END_OF_IF
      END_OF_IF
      HideDlgScene
      RETURN
    END_OF_IF
  END_OF_IF

  LABEL sailorDlg
  SUB_SCRIPT sailorZoomOut
  FocusChar sailor
  Delay 1000
  IF sailorVisited
    SUB_SCRIPT sailorAnim
    Say "Avast! It's the greenie!"
    IF sailorFulfilled
      SUB_SCRIPT sailorAnim
      Ask "Have ye found the trrreasure?"
      IF treasurePlanet
        MENU
          OPT "Nope. Still looking."
            GO sailorEnd
          END_OF_OPT
        END_OF_MENU
      ELSE
        MENU
          OPT "Yeah, thanks. Let me buy you a drink."
            SUB_SCRIPT sailorAnim
            Say "Nay, that's arrright."
            SUB_SCRIPT sailorZoomOut
            Say "Now that the trrreasure has been found, I can rrrest in peace."
            Delay 1500
            ChangeChar sailor campaign/sailor-dying.png
            Delay 800
            ChangeChar sailor campaign/sailor-dead.png
            Delay 2000
            HideChar sailor
            HideDlgScene
            RESET sailorAvailable
            SET sailorFinished
            RETURN
          END_OF_OPT
          OPT "Uh ... no. No, I haven't found it yet."
            SUB_SCRIPT sailorAnim
            Say "Arr. That saddens my hearrrt, it does."
            GO sailorEnd
          END_OF_OPT
          OPT_IFNOT sailorRude "It's none of your business."
            SUB_SCRIPT sailorAnim
            SUB_SCRIPT sailorAnim
            SUB_SCRIPT sailorAnim
            Say "Aye, but ... well the captain who lost the trrreasure sorely wishes it were found."
            SET sailorRude
          END_OF_OPT
        END_OF_MENU
      END_OF_IF
    END_OF_IF
    SUB_SCRIPT sailorAnim
    Ask "Tell me, [g(laddie|lassie)], have ye visited 7 stations yet?"
    IF_MIN visited 7
      RESET almostEnoughSeen
      IF_MIN visited 9
        SET moreSeen
      ELSE
        SET enoughSeen
      END_OF_IF
    ELSE
      IF_MIN visited 5
        SET almostEnoughSeen
      END_OF_IF
    END_OF_IF
    MENU
      OPT_IF moreSeen "I've seen more than that, old space dog."
        GO sailorSecret
      END_OF_OPT
      OPT_IF enoughSeen "Yes I have, old space dog."
        GO sailorSecret
      END_OF_OPT
      OPT_IF almostEnoughSeen "Well, I've seen quite a few, but not 7 yet."
        SUB_SCRIPT sailorAnim
        Say "Then why arrr ye bothering me?"
        GO sailorEnd
      END_OF_OPT
      OPT "I just stopped by to say hi to your parrot."
        SUB_SCRIPT sailorAnim
        Say "Parrot? Oh, you mean my translatarrr."
        SUB_SCRIPT sailorAnim
        Say "Rrright. Well ... uh ... tell your refrigeratarrr hi from me."
        GO sailorEnd
      END_OF_OPT
    END_OF_MENU
  END_OF_IF
  SUB_SCRIPT sailorAnim
  Ask "Avast! Look at the greenie who just stumbled into our barrr!"
  MENU
    OPT_IFNOT sailorAnswer "Hello. I'm new to space."
      SUB_SCRIPT sailorAnim
      Ask "Arrr! I can see that just by lookin' at ye, [g(laddie|lassie)]. Your space suit's still clean!"
      SET sailorAnswer
    END_OF_OPT
    OPT_IFNOT sailorAnswer "I happen to be a ray-hardened, rough-drivin' galaxy trucker."
      SUB_SCRIPT sailorAnim
      Ask "Arrr, don't tell tales to the old salt, [g(laddie|lassie)]. Your space suit's still clean!"
      SET sailorAnswer
    END_OF_OPT
    OPT "It's cool that your parrot can talk, but he's a little rude."
      SUB_SCRIPT sailorAnim
      Say "Parrot? What parrot?"
      Delay 1000
      SUB_SCRIPT sailorAnim
      Say "Oh, this! That's not a parrot, [g(lad|lass)]. That's my telepathic translatarrr!"
      SUB_SCRIPT sailorAnim
      Say "All those cold, damp nights on the bridge of my vessel took a brrrutal toll."
      SUB_SCRIPT sailorAnim
      Ask "Durrring a yearrr-long crrrossing thrrrough a solarrr storrrm, I crrracked my larrrynx."
      MENU
        OPT "Oh. I'm very sorry to hear that."
          SUB_SCRIPT sailorAnim
          Say "Oh, don't worrry. Thanks to my translatarrr I can talk for hours without scrrratching my thrrroat."
          GO sailorCont
        END_OF_OPT
        OPT "Serves you right."
          SUB_SCRIPT sailorAnim
          Say "Ye're a rude one. Harrr! I like that."
          GO sailorCont
        END_OF_OPT
        OPT "But a solar storm wouldn't ..."
          SUB_SCRIPT sailorAnim
          Delay 200
          SUB_SCRIPT sailorAnim
          Delay 200
          SUB_SCRIPT sailorAnim
          Ask "... ?"
          MENU
            OPT "... Forget it."
              GO sailorCont
            END_OF_OPT
          END_OF_MENU
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL sailorCont
  SET sailorVisited
  SUB_SCRIPT sailorZoomOut
  Ask "So what brrrings ye to this old space dog's table?"
  MENU
    OPT_IFNOT sailorButton "I dunno. Bored, I guess."
      SUB_SCRIPT sailorAnim
      Say "Greenies! Ye know nothing of the secrets of the cosmos."
      EXIT_MENU
    END_OF_OPT
    OPT "I'd like to learn the secrets of the cosmos."
      SUB_SCRIPT sailorAnim
      Say "Arrright! Ye've come to the rrright man."
      EXIT_MENU
    END_OF_OPT
    OPT_IFNOT sailorButton "I just [v(tapp|click)]ed on something, and there you were."
      SUB_SCRIPT sailorAnim
      Say "Kids! Ye know, in my day, we never [v(tapp|click)]ed our way into a barrr."
      SUB_SCRIPT sailorAnim
      Ask "If we wanted to visit a barrr, we had to type GO BARRR."
      SET sailorButton
    END_OF_OPT
  END_OF_MENU
  SUB_SCRIPT sailorAnim
  Say "I know a few secrets of the cosmos."
  SUB_SCRIPT sailorAnim
  Say "And there's one in particularrr that I'd like to share."
  SUB_SCRIPT sailorAnim
  Say "But not with a greenie such as ye. Though ye do seem to be an able [g(lad|lass)]."
  SUB_SCRIPT sailorAnim
  Ask "Tell ye what: after ye have visited 7 space stations, I'll reveal my secret."
  IF_MIN visited 7
    MENU
      OPT "You underestimate me, old space dog. I've already visited 7 stations."
        SUB_SCRIPT sailorAnim
        Say "Blow me down! Maybe ye arrr not as green as I think ye arrr."
        GO sailorSecret
      END_OF_OPT
    END_OF_MENU
  ELSE
    MENU
      OPT "Yeah, right. You'll probably just give me some advice that's as obvious as it is useless."
        SUB_SCRIPT sailorAnim
        Say "Wrong, ye young scallywag! I happen to know the secret of a hidden trrreasure!"
        EXIT_MENU
      END_OF_OPT
      OPT "Really? Can you tell me what sort of secret?"
        SUB_SCRIPT sailorAnim
        Say "No. I think it would spoil the fun if I came out and blabbed that I know of a hidden trrreasure."
        SUB_SCRIPT sailorAnim
        Say "I'd rrrather let ye savorrrr the mystery for now."
        EXIT_MENU
      END_OF_OPT
      OPT "Okay, I like secrets."
        EXIT_MENU
      END_OF_OPT
    END_OF_MENU
    SUB_SCRIPT sailorAnim
    Say "Now go, and leave me to my memorrries."
    SUB_SCRIPT sailorAnim
    Say "Then there was the time I rrromanced the entire constellation of Virrrgo ..."
    GO sailorEnd
  END_OF_IF

  LABEL sailorSecret
  ResetFocus bguests
  SET sailorFulfilled
  SUB_SCRIPT sailorAnim
  Say "Arrright. Here is my secret ..."
  SUB_SCRIPT sailorAnim
  Say "Once there was this captain. Bold. Brrrave. Fierrrce! And a real ladies' man, if ye know what I mean."
  SUB_SCRIPT sailorAnim
  Say "But oh, he was a scoundrel. A pirate he was! And as his bearrrd turned gray, he regretted his deeds."
  SUB_SCRIPT sailorAnim
  Say "Most especially, he regretted how well he had hidden his trrreasure."
  SUB_SCRIPT sailorAnim
  Say "He hid it on a planet farrr, farrr away. And he locked the coordinates inside an impenetrable safe."
  SUB_SCRIPT sailorAnim
  Say "The only way to open the safe was with a secret passworrrd spoken in his own voice."
  SUB_SCRIPT sailorAnim
  Say "But then the bold captain lost his voice. And so, he could never open the safe."
  SUB_SCRIPT sailorAnim
  Ask "All he remembers is that the planet was marrrked with a rrred X."
  MENU
    OPT "If I find that planet, should I tell you ... I mean, should I tell the captain where it is?"
      SUB_SCRIPT sailorAnim
      Say "Nay, [g(laddie|lassie)]. He wasn't that rrrich and his trrreasure wasn't that larrrge."
      SUB_SCRIPT sailorAnim
      Say "He just wishes someone would find it before it stops being history and becomes arrrchaeology."
      SUB_SCRIPT sailorAnim
      Ask "If ye can find it, ye can keep it!"
      MENU
        OPT "Okay. A planet marked with a red X. I'll keep an eye out for it."
          SUB_SCRIPT sailorAnim
          SET treasurePlanet
          Say "I wish ye fair solarrr winds!"
          GO sailorEnd
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL sailorEnd
  SUB_SCRIPT sailorAnim
  Delay 300
  SUB_SCRIPT sailorAnim
  Delay 300
  SUB_SCRIPT sailorZoomOut
  Delay 750
  HideChar sailor
  HideDlgScene
  RETURN

  LABEL ratDlg
  IFNOT ratVisited
    SET ratVisited
    Delay 500
    Say "Pssst! Hey, [g(buddy|lady)]."
    Delay 250
    ChangeChar rat campaign/rat-turn.png
    Delay 600
    ChangeChar rat campaign/rat.png
    Delay 400
    ChangeChar rat campaign/rat-smile.png
    Delay 1500
    ChangeChar rat campaign/rat.png
    Delay 150
    Ask "Yeah, you. C'mere. I wanna tell you a secret."
    MENU
      OPT "Really? I like secrets."
        Say "Then you'll really like this one. It's extra super secret."
        ChangeChar rat campaign/rat-finger-smile.png
        Say "I can help you make some easy money."
        GO ratWaiting
      END_OF_OPT
      OPT "Oh, brother. I'm not sure I want to hear this."
        ChangeChar rat campaign/rat-finger-smile.png
        Say "Of course you do! I'm gonna help you make some easy money."
        GO ratWaiting
      END_OF_OPT
      OPT "I don't talk to strangers. Especially not to sleazy strangers."
        ChangeChar rat campaign/rat-frown.png
        Say "What? Sleazy? ... You know what? You don't deserve to hear my secret!"
        Delay 1500
        ChangeChar rat campaign/rat.png
        Delay 500
        Say "Well, all right. Because you're [g(a pal|cute)], I'll tell you anyway: I know how you can make some easy money."
        GO ratWaiting
      END_OF_OPT
    END_OF_MENU
  ELSE
    Delay 300
    ChangeChar rat campaign/rat-smile.png
    Delay 300
    Say "Hey, it's you!"
    ChangeChar rat campaign/rat-finger-smile.png
    Ask "You want to hear my secret, so you've decided to buy me a drink, haven't you?"
    MENU
      OPT "No."
        Delay 300
        ChangeChar rat campaign/rat-frown.png
        GO ratBegging
      END_OF_OPT
      OPT "Well ... all right."
        GO ratOrder
      END_OF_OPT
    END_OF_MENU
  END_OF_IF

  LABEL ratWaiting
  ChangeChar rat campaign/rat.png
  Delay 1000
  ChangeChar rat campaign/rat-turn.png
  Delay 500
  ChangeChar rat campaign/rat.png
  Delay 1500
  ChangeChar rat campaign/rat-turn.png
  Delay 800
  ChangeChar rat campaign/rat.png
  Delay 600
  MENU
    OPT "What are we waiting for?"
      Say "We're waiting for you to buy me a drink. That's the way these things work. I say I have a secret ..."
      Ask "And you say ... ?"
      MENU
        OPT "Forget it."
          GO ratBegging
        END_OF_OPT
        OPT "(Sigh.) All right. Let me buy you a drink."
          GO ratOrder
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL ratBegging
  Ask "Aw c'mon! You just have to buy me one little drink and then I'll tell you how to make easy money! That's a good deal, right?"
  MENU
    OPT "No. Forget it."
      Delay 300
      ChangeChar rat campaign/rat-frown.png
      Say "Then get lost, [g(buddy|lady)]! Why're you taking up my valuable time?"
      Delay 1500
      Ask "... No, wait. Let me tell you anyway. It's hard for me to keep secrets when I'm so thirsty."
      MENU
        OPT "All right. Spill it."
          GO ratSaying
        END_OF_OPT
        OPT "I said forget it. I'm not interested."
          GO ratLeaving
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
    OPT "Eh. All right. Just one little drink."
      GO ratOrder
    END_OF_OPT
  END_OF_MENU

  LABEL ratOrder
  ChangeChar rat campaign/rat-finger-smile.png
  Say "Yeah! Now you're talkin'! Hey, bartender?"
  Delay 500
  ShowChar bartender left
  Delay 500
  SayChar rat "This [g(fella|lady)] wants to buy me a few rounds. ... And dinner. [g(He|She)] wants to buy me dinner. And a room. Maybe one with a shower this time."
  ChangeChar rat campaign/rat-smile.png
  AskChar bartender "Are you certain, [g(sir|miss)]? That will cost 3 credits."
  RESET hasEnough
  IF_HAS_CREDITS 3
    SET hasEnough
  END_OF_IF
  MENU
    OPT_IF hasEnough "Okay. Whatever."
      Delay 300
      SayChar bartender "As you wish."
      AddCredits -3
      Announce "You have paid 3 credits."
      SET paidForRat
      Delay 300
      HideChar bartender
      GO ratSaying
    END_OF_OPT
    OPT_IFNOT hasEnough "Oh. Well, sorry. I don't have 3 credits on me. Maybe next time."
      Delay 300
      HideChar bartender
      GO ratLeaving
    END_OF_OPT
    OPT "You know what? Let's just forget it. I'm outta here."
      Delay 300
      HideChar bartender
      GO ratLeaving
    END_OF_OPT
  END_OF_MENU

  LABEL ratLeaving
  ChangeChar rat campaign/rat-frown.png
  Delay 500
  HideChar rat
  Delay 250
  HideDlgScene
  RETURN

  LABEL ratSaying
  ChangeChar rat campaign/rat.png
  SayChar rat "Okay, here's my secret: I know a sector of fabulous wealth. But it's hidden away. So no one else knows about it. Except me."
  ChangeChar rat campaign/rat-finger-smile.png
  Say "And it's a baby-safe route. Nothing but abundantly wealthy planets. Let me mark it for you on your map."
  Delay 250
  Delay 250
  ShowNode pirateTrap
  CenterMapByNode pirateTrap
  Delay 500
  ShowPath U2-17a
  Delay 300
  ChangeChar rat campaign/rat.png
  Say "Baby safe. You only need cargo holds and other valuable components."
  Say "But no weapons. You won't need 'em. Because the route is sooo safe. Got it?"
  IF paidForRat
    Say "Pretty good, huh? A secret like that is worth a little bit of spare change, huh?"
    ChangeChar rat campaign/rat-smile.png
    Say "And now if you'll excuse me, it's time for my dinner."
  ELSE
    ChangeChar rat campaign/rat-frown.png
    Say "And now if you'll excuse me, I need to find someone who will buy me a drink."
  END_OF_IF
  HideChar rat
  Delay 1300
  ChangeChar rat campaign/rat-peek.png
  ShowChar rat right
  Delay 700
  Say "Really. Don't bring any weapons. They'd just get in the way. Honest. Okay. Good luck! (Heh heh.)"
  Delay 200
  HideChar rat
  SET ratFinished
  RESET ratAvailable
  HideDlgScene
  RETURN
END_OF_SCRIPT

SCRIPT pirateDlg1

  ShowDlgScene
  ShowChar pirate left
  Say "Ho, men! Was your quarry as ripe for plucking as we hoped?"
  Say "Well, rust my thrusters! You aren't my men."
  Ask "Where are they? And who are you?"
  MENU
    OPT "I'm the one who captured your men. And now I'm going to capture you!"
      GO pirateArrestAttempt
    END_OF_OPT
    OPT "I'm your new pirate gang. Your old one was incompetent."
      Say "Humph! This is highly irregular ... but very well. Let me tell you how we operate:"
      Say "My agents will be luring gullible truckers into our trap. Your job is to attack them."
      Ask "I get 90 percent of your gross income, and you get to use my logo."
      MENU
        OPT_IFNOT pirateLogo "Uh ... what logo?"
          Ask "What logo? Haven't you ever seen the skull and crossbones? It is my registered trademark."
          SET pirateLogo
        END_OF_OPT
        OPT "That sounds like a good ending to this campaign. All right. I'll become a pirate."
          GO pirateChoice
        END_OF_OPT
        OPT "That doesn't sound like a very good deal to me."
          Say "Not to worry, my good [g(man|woman)]. The terms are negotiable."
          Say "Suppose instead that you give me 95 percent. Then you can use my trademarked slogans:"
          Say "Your money and your life! ... and ... Arr! Make them walk the Planck wavelength!"
          Ask "What say you to that, [g(sir|madam)]?"
          MENU
            OPT "I say you'd better come with me. I'm taking you in."
              GO pirateArrestAttempt
            END_OF_OPT
            OPT "Well, those are pretty good slogans. Okay. I'll give you 95 percent."
              GO pirateChoice
            END_OF_OPT
            OPT "You'll have to settle for 90 percent. I'll make up my own slogans."
              GO pirateChoice
            END_OF_OPT
          END_OF_MENU
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
    OPT_IFNOT pirateMeta "Uh ... wait a minute. This looks like an important decision point in the campaign."
      SET pirateMeta
      Say "Of course, [g(my good man|madam)]. Take your time."
      ChangeChar pirate campaign/pirate-hands-closed.png
      Delay 3500
      ChangeChar pirate campaign/pirate.png
      Ask "And in the meantime, tell me who you are."
    END_OF_OPT
  END_OF_MENU

  LABEL pirateChoice
  Say "Excellent! We have a deal, then."
  Delay 300
  ChangeChar pirate campaign/pirate-freeze.png
  Delay 600
  ShowChar programmer right
  Delay 600
  Say "Hi. Programmer here. I just wanted to be sure that everything is clear."
  Ask "If you decide to be a pirate, your trucking campaign will end here. Is that really what you want to do?"
  MENU
    OPT "Yeah. That sounds like fun."
      Say "Seriously?"
      Say "I told the developers that no one would try this branch of the program, so we didn't bother to design any missions for it."
      Say "Oh well. Guess I was wrong. Now I have to buy pizza for the entire office."
      Ask "And now please go back and choose the correct branch of the decision tree. Okay?"
      MENU
        OPT "Well, okay. If that's my only option, I guess I'll try to arrest this guy."
          Say "Thank you. I was hoping you would listen to reason."
          GO pirateInterventionAccepted
        END_OF_OPT
        OPT_IFNOT pirateLimits "If arresting this bad guy is my only option, why are you still letting me refuse?"
          Ask "Oh. Good point. Let me fix that."
          SET pirateLimits
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
    OPT "Hm. No, on second thought, I'll try to capture this guy and collect a nice reward."
      Say "Thank you."
      GO pirateInterventionAccepted
    END_OF_OPT
  END_OF_MENU

  LABEL pirateInterventionAccepted
  Delay 300
  HideChar programmer
  Delay 500
  ChangeChar pirate campaign/pirate.png
  Delay 500

  LABEL pirateArrestAttempt
  SayChar pirate "Oho! So you've heard about the reward for me back on Regula."
  Say "Very well. Come and take me, if you dare."
  Ask "But I warn you, no one has ever defeated me in single combat."
  MENU
    OPT "Hm. This may be harder than I thought."
      Say "Without a doubt, [g(sir|madam)]. Without a doubt."
      GO pirateEnd
    END_OF_OPT
    OPT "As far as I know, this game doesn't have any hand-to-hand combat."
      Say "But if it did, no one could defeat me. I assure you."
      GO pirateEnd
    END_OF_OPT
    OPT_IFNOT pirateBragging "You know, I've already defeated three ships full of bad guys."
      Ask "But that does not mean you can defeat me, [g(sir|madam)]. Not in single combat."
      SET pirateBragging
    END_OF_OPT
  END_OF_MENU

  LABEL pirateEnd
  Say "Now, since you cannot defeat me, you shall have to excuse me. I need to go and have a talk with the fool who bungled his assignment."
  Delay 400
  HideChar pirate
  Delay 300
  HideDlgScene
  HideButton bpirate1
  ShowButton bpirate2
  SetFocus bpirate2
END_OF_SCRIPT

SCRIPT pirateDlg2

  ShowDlgScene
  ShowChar pirate left
  IFNOT ropeCut
    ChangeChar rat campaign/rat-tied.png
    ShowChar rat right
  END_OF_IF
  IF pirateEscaped
    RESET pirateEscaped
    AskChar pirate "You and your vermin again?"
    MENU
      OPT "Yeah, and you're coming with me again."
        SayChar pirate "Very well. I shall escape again."
        Say "You'll land on some planet ... or slow down to collect a bounty for defeating an enemy ..."
        Say "Or perhaps you'll just take a moment to clean some stardust from an exposed connector ..."
        Say "And when you look up from your work ... I'll be gone!"
        GO pirateArrested
      END_OF_OPT
    END_OF_MENU
  END_OF_IF
  SayChar pirate "My dear boy, what part of your instructions did you fail to comprehend?"
  SayChar rat "Mmf. Mmmmf. Mmmmf mmmmf mmf."
  SayChar pirate "I instructed you to find gullible dupes. And instead, you send me this [g(modern-day Nelson|female Nelson)]."
  SayChar rat "Mmmmf mmf."
  SayChar pirate "[g(He|She)] was supposed to be helpless. Unarmed. No weapons. Didn't you tell [g(him|her)] no weapons?"
  AskChar rat "Mmf."
  MENU
    OPT "I hate to interrupt your little chat. But Nelson is back, along with [g(his|her)] crew."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  SayChar pirate "Egad! You again? I told you that I am without peer in single combat."
  SayChar rat "Mmf, mmmmmf!"
  AskChar pirate "Ah. Yes. Quite correct. Against an entire crew, I'm not certain how I would fare."
  MENU
    OPT "Right. And now, you're coming with me."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  SayChar pirate "Very well. I acknowledge your superior tactical position. For now."
  AskChar pirate "But do not count on getting me all the way to Regula. At the first opportunity, I shall escape, I assure you."
  MENU
    OPT_IFNOT pirateExplains "What? How could you escape? We'll be in space."
      SET pirateExplains
      SayChar pirate "Do not underestimate my foxlike cunning. You will stop on some planet ..."
      Say "Or perhaps you will encounter a strange ship. Or perhaps you'll stop just to clean some stardust out of an exposed connector ..."
      Ask "And when you look up from your work ... I'll be gone!"
    END_OF_OPT
    OPT "We'll see about that. Let's go."
      Say "Very well."
      GO pirateArrested
    END_OF_OPT
  END_OF_MENU

  LABEL pirateArrested
  Delay 300
  HideChar pirate
  HideButton bpirate2
  IFNOT ropeCut
    ShowButton bpirate3
    Delay 700
    SayChar rat "Mff, mmmmf? ... Mmf? .... MMMMMMFF!"
    Delay 500
    HideChar rat
  END_OF_IF
  Delay 300
  ShowPath U2-17x
  HideDlgScene
END_OF_SCRIPT

SCRIPT pirateDlg3

  ShowDlgScene
  ChangeChar rat campaign/rat-tied.png
  ShowChar rat right
  Ask "Mmf ... mmf mmmmmf?"
  MENU
    OPT "I suppose you want me to cut you loose, huh, sleazeball?"
      Say "Mmf."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  MENU
    OPT "Well, you don't deserve my help ... but all right."
      EXIT_MENU
    END_OF_OPT
    OPT "Not a chance."
      GO pirateEnd3
    END_OF_OPT
  END_OF_MENU
  Delay 500
  ChangeChar rat campaign/rat-tied-down.png
  Delay 800
  UnlockAchievement camp_cut_the_rope
  SET ropeCut
  Say "Mmf mmmmf."
  MENU
    OPT "Don't mention it."
    END_OF_OPT
    EXIT_MENU
  END_OF_MENU
  HideButton bpirate3

  LABEL pirateEnd3
  Delay 300
  HideChar rat
  Delay 300
  HideDlgScene
END_OF_SCRIPT

END_OF_SCRIPTS
