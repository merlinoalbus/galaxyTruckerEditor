SCRIPTS

SCRIPT vipCheck

  ShowDlgScene
  ShowChar securitybot left
  SET VIPTried
  IF VIPGranted
    Say "SOLICITANDO ACCESO AL SALÓN PRIV..."
    Say "¡OH, SI ERES TÚ!"
    IF richGirlPending
      Say "TE ESPERAN EN EL ALMACÉN."
      HideChar securitybot
      HideDlgScene
      RETURN
    ELSE
      Say "ACCESO CONCEDIDO."
    END_OF_IF
    Delay 200
    HideChar securitybot
    HideDlgScene
    Delay 1000
    SUB_SCRIPT richGirlDlg
    RETURN
  END_OF_IF
  Say "SOLICITANDO ACCESO AL SALÓN PRIVADO."
  Delay 750
  Say "IDENTIDAD DESCONOCIDA."
  Say "ACCESO DENEGADO."
  IF purpleAccepted
    Delay 200
    ChangeChar ambassador campaign/ambassador-peek.png
    ShowChar ambassador right
    Say "[g(Este caballero|Esta dama)] trabaja para nosotros. Puedes dejar que pase."
    IFNOT purpleEscortFinished
      Say "No se quedará mucho tiempo. Tiene una misión que cumplir."
    END_OF_IF
    Delay 200
    HideChar ambassador
    Delay 200
    SayChar securitybot "AY..."
    Say "ACCESO CONCEDIDO."
    SET VIPGranted
    Delay 200
    HideChar securitybot
    Delay 1000
    HideDlgScene
    SUB_SCRIPT richGirlDlg
    RETURN
  ELSE
    SET VIPfailed
  END_OF_IF
  HideChar securitybot
  HideDlgScene
END_OF_SCRIPT

SCRIPT richGirlDlg

  ShowDlgScene
  ChangeChar richgirl campaign/rich-bored.png
  ShowChar richgirl left
  Delay 750
  ChangeChar richgirl campaign/rich.png
  Delay 300
  IF richRefused
    Say "¡Hola! Si eres... ah... hm..."
    MENU
      OPT "[NAME]."
        Say "Lo tenía en la punta de la lengua."
        EXIT_MENU
      END_OF_OPT
    END_OF_MENU
    Ask "¿Qué? ¿Tienes ganas de volar hasta Régula a través de una tormenta de meteoritos?"
    GO richQuery
  END_OF_IF
  Ask "Hola. ¿Quién eres tú?"
  MENU
    OPT_IFNOT richgirlintro "[g(Un camionero|Una camionera)] del montón."
      Delay 200
      ChangeChar richgirl campaign/rich-bored.png
      Delay 200
      Ask "Vale. ¿Qué tal, camioner[g(o|a)] del montón?"
      SET richgirlintro
    END_OF_OPT
    OPT_IFNOT richgirlintro "Un diplomático en una misión de suma importancia."
      Delay 200
      ChangeChar richgirl campaign/rich-bored.png
      Delay 200
      Ask "Genial. [g(Otro camionero|Otra camionera)] del montón."
      SET richgirlintro
    END_OF_OPT
    OPT "Me llamo [NAME]. ¿Y tú?"
      ChangeChar richgirl campaign/rich-smile.png
      Delay 300
      Say "Eres [g(encantador|encantadora)], pero no hace falta que finjas. Sé que soy la camionera más famosa de la galaxia."
      Say "Aun así, te agradezco que hagas como si no me conocieras. Siéntate y bebe algo."
      Delay 200
      ChangeChar richgirl campaign/rich-smile.png
      Delay 400
      ChangeChar richgirl campaign/rich-smile-cheer.png
      Delay 400
      ChangeChar richgirl campaign/rich-smile.png
      GO richCont
    END_OF_OPT
  END_OF_MENU

  LABEL richCont
  Delay 1000
  ChangeChar richgirl campaign/rich-bored.png
  Delay 500
  Say "Un día aburrido en la estación espacial, ¿eh?"
  Say "¿Te apetece hacer algo?"
  MENU
    OPT "¿Algo de qué?"
      ChangeChar richgirl campaign/rich.png
      Say "Algo más divertido que quedarnos aquí [g(sentados|sentadas)]. ¿Qué tal un vuelo?"
      Ask "Dicen que la ruta hacia Régula está cerrada a causa de una tormenta de meteoritos. ¿Te apetece probarla?"
      GO richQuery
    END_OF_OPT
  END_OF_MENU

  LABEL richQuery
  MENU
    OPT_IFNOT rgDangerous "¿No será peligroso?"
      Ask "Claro, por eso es tan divertido."
      SET rgDangerous
    END_OF_OPT
    OPT_IFNOT rgForbidden "Pero... ¿no has dicho que está cerrada?"
      Ask "Si nos dejaran volar por ella, ¿dónde estaría la gracia?"
      SET rgForbidden
    END_OF_OPT
    OPT "De acuerdo. No es la primera vez que me enfrento a una tormenta de meteoritos."
      GO richCont2
    END_OF_OPT
    OPT_IF rgForbidden "No... lo sé. Mejor otro día."
      GO richRefused
    END_OF_OPT
  END_OF_MENU

  LABEL richCont2
  Say "¿En serio? ¿Incluso con meteoritos enormes que se acercan de costado?"
  Ask "Si no tienes cañones apuntando hacia los lados, te aplastarán como a una lata de refresco."
  MENU
    OPT "No hay problema. Soy un genio."
      Say "Me alegra saberlo."
      GO richCont3
    END_OF_OPT
    OPT "Me las arreglaré. ¿Y tú?"
      Say "Puedes contar con ello, [g(chico|chica)]."
      GO richCont3
    END_OF_OPT
    OPT "Oh. Hm. Entonces... mejor otro día."
      GO richRefused
    END_OF_OPT
  END_OF_MENU

  LABEL richCont3
  Say "Tengo una nave de Clase I personalizada. Elegante y deportiva. Es perfecta para este tipo de vuelos."
  ChangeChar richgirl campaign/rich-smile.png
  Ask "No terminará tan mal como la tuya."
  MENU
    OPT "Eso ya lo veremos."
      Ask "Por supuesto que sí. De hecho, tengo una idea para que el vuelo sea un poco más interesante. Si pierdes menos componentes que yo, te permitiré ver los detalles del diseño. ¿Qué me dices?"
      MENU
        OPT "Trato hecho."
          Say "Perfecto, nos vemos en el hangar."
          Delay 200
          ChangeChar richgirl campaign/rich-smile.png
          Delay 400
          ChangeChar richgirl campaign/rich-smile-cheer.png
          Delay 400
          ChangeChar richgirl campaign/rich-smile.png
          Delay 500
          SET richGirlAccepted
          SET richGirlPending
          HidePath "R1-4a"
          ShowPath "U1-4a"
          GO richEnd
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL richRefused
  ChangeChar richgirl campaign/rich-bored.png
  SET richRefused
  Say "Qué lástima. Parecías más divertid[g(o|a)] a primera vista."
  Say "En fin... Voy a buscar otra copa."
  Delay 300

  LABEL richEnd
  HideChar richgirl
  HideDlgScene
END_OF_SCRIPT

SCRIPT richGirlDlg2

  ShowDlgScene
  ChangeChar richgirl campaign/rich-bored.png
  ShowChar richgirl left
  Delay 750
  ChangeChar richgirl campaign/rich.png
  Delay 300
  IF richGirl2Accepted
    IF designSport2Unlocked
      Ask "Hola. ¿Quieres volar conmigo por los viejos tiempos? Esta vez no te dejaré ganar."
    ELSE
      Say "Hola. ¿Quieres volver a probar esa ruta desconocida? Será un placer demostrarte lo buena que soy."
      Ask "Y no lo olvides. Si pierdes menos componentes que yo, te permitiré copiar los detalles de mi modelo deportivo de Clase II."
    END_OF_IF
    MENU
      OPT "Me parece bien."
        ChangeChar richgirl campaign/rich-smile.png
        Say "Fantástico."
        Delay 300
        ChangeChar richgirl campaign/rich-smile-cheer.png
        Delay 600
        ChangeChar richgirl campaign/rich-smile.png
        Delay 300
        ShowPath U2-21a
        GO richGirl2End
      END_OF_OPT
      OPT "En otro momento."
        ChangeChar richgirl campaign/rich-bored.png
        Say "De acuerdo. Ya nos veremos."
        GO richGirl2End
      END_OF_OPT
    END_OF_MENU
  END_OF_IF
  IF richGirl2Refused
    Ask "Hola. ¿Has cambiado de opinión? ¿Te animas a volar conmigo por esa ruta desconocida?"
    GO richGirl2Querry
  END_OF_IF
  Say "¡Hola! Si eres... ah... hm..."
  MENU
    OPT "[NAME]."
      Say "Lo tenía en la punta de la lengua."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  Say "¿Recuerdas aquella vez en la que atravesamos una tormenta de meteoritos?"
  Ask "Sí, [g(hombre|mujer)], cuando te dejé ganar y copiaste los detalles de mi nave."
  MENU
    OPT "Ya me acuerdo."
      EXIT_MENU
    END_OF_OPT
    OPT "Ya me acuerdo, excepto de esa parte en la que me dejaste ganar."
      Delay 200
      ChangeChar richgirl campaign/rich-smile.png
      Delay 200
      Say "Eres muy gracios[g(o|a)]."
      Delay 300
      ChangeChar richgirl campaign/rich-smile-blink.png
      Delay 300
      ChangeChar richgirl campaign/rich-smile.png
      Delay 300
      ChangeChar richgirl campaign/rich-smile-blink.png
      Delay 300
      ChangeChar richgirl campaign/rich-smile.png
      Delay 300
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  Say "Si te apetece repetir la ruta, dímelo. Por los viejos tiempos."
  Say "Aunque tengo que reconocer que la primera vez es la más emocionante."
  Ask "¿Sabes? He oído hablar de otra ruta. Es tan peligrosa que ni siquiera sale en los mapas."

  LABEL richGirl2Querry
  MENU
    OPT_IFNOT richGirlHopeless "Sabes que no tienes remedio, ¿verdad?"
      SET richGirlHopeless
      Ask "Lo sé, lo sé, es el aburrimiento. Es muy duro ser la mejor en todo lo que hago."
    END_OF_OPT
    OPT "Suena bien. ¿Sabes algo más de esa ruta?"
      EXIT_MENU
    END_OF_OPT
    OPT "No cuentes conmigo."
      SET richGirl2Refused
      ChangeChar richgirl campaign/rich-bored.png
      Say "No te recordaba tan sos[g(o|a)]. Ya nos veremos."
      GO richGirl2End
    END_OF_OPT
  END_OF_MENU
  SET richGirl2Accepted
  Say "Atraviesa una densa nube de meteoritos, con pedruscos que se precipitan sobre tu nave por ambos lados."
  Say "Estoy deseando probar mi diseño deportivo de Clase II."
  MENU
    OPT "¿Puedo copiarlo si te gano?"
      Say "Je. Trato hecho, aunque esta vez no tendrás tanta suerte."
      Say "Ya veremos si pierdes menos componentes que yo."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  ShowPath U2-21a
  HideButton brichgirl2

  LABEL richGirl2End
  HideChar richGirl
  HideDlgScene
END_OF_SCRIPT

SCRIPT merchantLaughA

  ChangeChar merchant campaign/merch-laughA1.png
  Delay 150
  ChangeChar merchant campaign/merch-laughA2.png
  Delay 200
  ChangeChar merchant campaign/merch-laughA1.png
  Delay 250
  ChangeChar merchant campaign/merch-laughA2.png
  Delay 300
  ChangeChar merchant campaign/merch-sitting.png
END_OF_SCRIPT

SCRIPT merchantLaughB

  ChangeChar merchant campaign/merch-laughB1.png
  Delay 150
  ChangeChar merchant campaign/merch-laughB2.png
  Delay 200
  ChangeChar merchant campaign/merch-laughB1.png
  Delay 250
  ChangeChar merchant campaign/merch-laughB2.png
  Delay 300
  ChangeChar merchant campaign/merch-laughB1.png
  Delay 350
  ChangeChar merchant campaign/merch-laughB2.png
  Delay 400
  ChangeChar merchant campaign/merch-laughB1.png
  Delay 450
  ChangeChar merchant campaign/merch-laughB2.png
  Delay 500
  ChangeChar merchant campaign/merch-sitting.png
END_OF_SCRIPT

SCRIPT merchantLaughX

  ChangeChar merchant campaign/merch-laughB1.png
  Delay 150
  ChangeChar merchant campaign/merch-laughB2.png
  Delay 200
  ChangeChar merchant campaign/merch-laughA2.png
  Delay 300
  ChangeChar merchant campaign/merch-laughA1.png
  Delay 700
  ChangeChar merchant campaign/merch-disappointed.png
  Delay 400
END_OF_SCRIPT

SCRIPT purpleHomeDlg

  ShowDlgScene
  IF_PROB 33
    ShowChar purple left
  ELSE
    ShowChar purple right
  END_OF_IF
  IF_PROB 33
    Say "Khrr's ghzzz. Sssrgh wq ghhh-h."
    Ask "¿¡Zzrrgh qhhh-w-gz!?"
  ELSE
    IF_PROB 50
      Ask "¿Sggh'kh qt Th'kzw-r?"
    ELSE
      Ask "Sgh. Sgh wzwg'h. Sgh wzwg'h ¡K'wwfrg!"
    END_OF_IF
  END_OF_IF
  IF purpleLicenseApproval
    OPT "Solo quería darte las gracias por el permiso."
      GO purpleHomeAnswer
    END_OF_OPT
  ELSE
    IF class3Asked
      IF warriorTestUnlocked
        MENU
          OPT "Sí, por supuesto, me presentaré a la prueba muy pronto."
            GO purpleHomeAnswer
          END_OF_OPT
        END_OF_MENU
      ELSE
        MENU
          OPT "Estoy intentando conseguir una licencia de Clase III. ¿Podrías darme un permiso en nombre de tu especie?"
            EXIT_MENU
          END_OF_OPT
        END_OF_MENU
        Ask "Sgggk-h. Wghkkk'r kh gzzw. ¿Khz wrrzk-hrrg krz'h?"
        MENU
          OPT "Sí, claro que soy [g(un gran guerrero|una gran guerrera)]."
            EXIT_MENU
          END_OF_OPT
          OPT "Sí, claro. Puedo decir que soy [g(un gran guerrero|una gran guerrera)]. ¿Con eso servirá?"
            EXIT_MENU
          END_OF_OPT
          OPT "Hm... Ni siquiera me considero [g(un guerrero|una guerrera)]."
            Say "Kh krz'h wgz-h k."
            GO purpleHomeEnd
          END_OF_OPT
        END_OF_MENU
        Ask "¡Sgghr'g! Kzz ghrr zgg. Qwrg wrrzk-hrrg krz'h..."
        MENU
          OPT "Será un placer demostrar mi valía."
            EXIT_MENU
          END_OF_OPT
          OPT "¿Quieres que te lo demuestre? Vale... como tú digas."
            EXIT_MENU
          END_OF_OPT
          OPT "Hm. ¿Sabes qué? Mejor olvídalo."
            GO purpleHomeEnd
          END_OF_OPT
        END_OF_MENU
        Ask "Shkrrr zh-zh frrg'h'hr. Zrgw grrh gk'gkzzzr. ¡Ghrr wrrzk-hrrg krz'h zh-zh hrrrg!"
        MENU
          OPT "Sí, claro que puedo seguir esa ruta."
            EXIT_MENU
          END_OF_OPT
        END_OF_MENU
        Ask "¡¡¡Ghrr'wrrzk ssrr zh-zh, sgg gh krz'h zzwg-hrg!!!"
        MENU
          OPT "Entendido. El que sobreviva gana."
            ResetFocus bpurple
            Delay 500
            ShowNode cyanHome
            CenterMapByNode cyanHome
            Delay 500
            ShowPath U2-20a
            Say "Zrgh hr'w ghrrz..."
            SET warriorTestUnlocked
            GO purpleHomeEnd
          END_OF_OPT
          OPT "Hm... No me gusta cómo suena. Mejor lo olvidamos, ¿vale?"
            GO purpleHomeEnd
          END_OF_OPT
        END_OF_MENU
      END_OF_IF
    END_OF_IF
  END_OF_IF
  MENU
    OPT "Sí, por supuesto."
      EXIT_MENU
    END_OF_OPT
    OPT "No, no creo."
      EXIT_MENU
    END_OF_OPT
    OPT "¿Puedes repetirme la última parte?"
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL purpleHomeAnswer
  IF_PROB 33
    Say "¿W'z'ghrw gz? Thgr…"
  ELSE
    IF_PROB 50
      Say "Kkk'z hrgh wzr-g-g-gh."
    ELSE
      Say "¡Kh!"
    END_OF_IF
  END_OF_IF

  LABEL purpleHomeEnd
  Delay 300
  HideChar purple
  HideDlgScene
END_OF_SCRIPT

SCRIPT merchantDlg

  ShowDlgScene
  ShowChar merchant left
  Delay 300
  IF  merchantMet
    IF merchantAccepted
      ChangeChar merchant campaign/merch-laughA1.png
      IF merchantFailed
        RESET merchantFailed
        RESET merchantLeftAtPurple
        Ask "Hola, chic[g(o|a)]. Nuestro último viaje resultó ser un poco accidentado. ¿Quieres probar de nuevo?"
      ELSE
        IF merchantLeftAtPurple
          RESET merchantLeftAtPurple
          Ask "¿Has vuelto? ¡Genial! ¿List[g(o|a)] para volar conmigo hasta el planeta de los alienígenas marrones?"
        ELSE
          Ask "¿Todo a punto para despegar?"
        END_OF_IF
      END_OF_IF
      MENU
        OPT "No es un buen momento. Tendremos que posponerlo."
          Delay 200
          ChangeChar merchant campaign/merch-disappointed.png
          Delay 200
          Say "Oh... De acuerdo."
          GO merchEnd
        END_OF_OPT
        OPT "¡Todo a punto!"
          ShowPath U1-9a
          Say "¡Excelente!"
          GO merchEnd
        END_OF_OPT
      END_OF_MENU
    ELSE
      IF merchantOffer
        Ask "¿Has cambiado de opinión? Todavía busco a alguien que vuele conmigo hasta el planeta de los alienígenas marrones. La ruta es peligrosa."
        GO merchSecondQuery
      ELSE
        Ask "¿Tienes tiempo de escuchar una propuesta de negocios?"
        GO merchFirstQuery
      END_OF_IF
    END_OF_IF
  ELSE
    Say "¡Qué coincidencia!"
    Say "Estaba deseando ver a otro humano en este planeta."
    Ask "¿Tienes un momento?"
    SET merchantMet
  END_OF_IF

  LABEL merchFirstQuery
  MENU
    OPT "Claro, ¿qué puedo hacer por ti?"
      EXIT_MENU
    END_OF_OPT
    OPT "Ya. “Un momento”. Tú quieres darme una misión que tardaré una semana en cumplir."
      SUB_SCRIPT merchantLaughB
      Ask "¡Ja, ja! Me has pillado. Es justo lo que iba a hacer."
      MENU
        OPT "Venga, dispara."
          GO merchCont
        END_OF_OPT
        OPT "No me interesa."
          ChangeChar merchant campaign/merch-disappointed.png
          Say "Qué pena. Creía que podríamos hacer negocios juntos."
          GO merchEnd
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
    OPT "Kzwrrr... Brbl, brbl... ¡Chrch'chrch!"
      Ask "¿Sabes hablar su idioma? ¿¡En serio!? ¡Yo no he conseguido aprenderlo!"
      MENU
        OPT "Era una prueba. Tiene su gracia, pero no estoy segur[g(o|a)] de lo que he dicho."
          SUB_SCRIPT merchantLaughB
          GO merchCont
        END_OF_OPT
        OPT "Por supuesto. Tengo un talento innato para los idiomas. Was ist das Problem?"
          GO merchCont
        END_OF_OPT
        OPT "Significa: “Ahora mismo no tengo tiempo para aceptar misiones interminables”."
          ChangeChar merchant campaign/merch-disappointed.png
          Say "Oh. Vale. Lo dejamos para otro día."
          GO merchEnd
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL merchCont
  ChangeChar merchant campaign/merch.png
  SET merchantOffer
  Say "Verás, en estos momentos tengo en marcha un lucrativo negocio intercambiando mercancías entre los alienígenas violetas y los marrones."
  Say "Es decir, la gente de Tzkz... Thk'z... Bueno, esos tipos y los de Silencio."
  Say "El problema es que la ruta entre ambos mundos es peligrosa y yo no estoy hecho para grandes hazañas."
  Ask "¿Te gustaría ser mi escolta hasta que lleguemos a Silencio?"

  LABEL merchSecondQuery
  MENU
    OPT_IFNOT merchAlienQuestion "¿Por qué no contratas a alguien de por aquí? Son buenos guerreros."
      SET merchAlienQuestion
      Say "Sus silbidos me ponen nervioso."
      Say "Prefiero volar con alienígenas marrones. Es más tranquilo."
    END_OF_OPT
    OPT "Si es una ruta tan peligrosa, preferiría no ir."
      Say "Lo entiendo, a mí también me da miedo."
      GO merchEnd
    END_OF_OPT
    OPT "¿Qué me ofreces a cambio?"
      Ask "¿Qué tal un 25 por ciento de los beneficios?"
      MENU
        OPT "Me gusta la idea."
          Say "¡Fantástico! Te llevarás una buena comisión y además podrás conocer a los alienígenas marrones. Son buena gente."
          GO merchAccept
        END_OF_OPT
        OPT "Quiero el 50 por ciento."
          Ask "Lo siento, pero no puedo subir del 25. Además, podrás conocer a los alienígenas marrones."
          MENU
            OPT "De acuerdo."
              Say "¡Excelente!"
              GO merchAccept
            END_OF_OPT
            OPT "No creo que sea suficiente."
              Delay 300
              ChangeChar merchant campaign/merch-disappointed.png
              Say "Oh. Como quieras. Seguro que encuentro a algún interesado… un día de estos."
              GO merchEnd
            END_OF_OPT
          END_OF_MENU
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL merchAccept
  Say "Este podría ser el comienzo de una hermosa relación de negocios."
  SUB_SCRIPT merchantLaughA
  ShowNode brownHome
  ShowPath U1-9a
  ShowPath R1-9b
  ResetFocus bmerchant
  Say "Te veo en el muelle."
  SET merchantAccepted
  RESET merchantLeftAtPurple

  LABEL merchEnd
  Delay 200
  HideChar merchant
  Delay 300
  HideDlgScene
END_OF_SCRIPT

SCRIPT mechDlg

  ShowDlgScene
  IFNOT outpostInitialized
    HideButton bmech
    ShowButton bambassador
    SetFocus bambassador
    Delay 300
    CenterMapByNode outpost
    Delay 500
    Announce "Te llegan rumores de que hay una crisis diplomática en la estación de la embajada."
    Delay 300
    CenterMapByNode regula
    Delay 300
    HideDlgScene
    RETURN
  END_OF_IF
  ShowChar mech left
  IF mechOffered
    ChangeChar mech campaign/mech-explaining.png
    Say "Hola. ¿Has venido a aceptar el encargo?"
    MENU
      OPT "Sí."
        GO mechAccept
      END_OF_OPT
      OPT "No, yo solo estaba..."
        ChangeChar mech campaign/mech-f.png
        Say "Entonces no me hagas perder el tiempo. Tengo que encontrar a alguien dispuesto a hacerlo."
        GO mechEnd
      END_OF_OPT
    END_OF_MENU
  END_OF_IF
  Say "Hola, camioner[g(o|a)]."
  ChangeChar mech campaign/mech-explaining.png
  Ask "Tengo prisa, así que me saltaré las formalidades... Tengo un trabajo para ti."
  MENU
    OPT "Las formalidades también son importantes."
      ChangeChar mech campaign/mech-explaining-f.png
      Say "Vale. Yo me presento, tú te presentas, pregunto a qué te dedicas, respondes que al transporte de mercancías, bla, bla, bla."
      Ask "Después de unos minutos de cháchara, me dices que parezco preocupada y entonces te cuento lo del trabajo. ¿Mejor así?"
      MENU
        OPT "Sí, mucho mejor."
          GO mechCont
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
    OPT "Me parece bien. Puedes seguir."
      Say "Genial."
      GO mechCont
    END_OF_OPT
  END_OF_MENU

  LABEL mechCont
  SET mechOffered
  ChangeChar mech campaign/mech.png
  Say "Estamos intentando abrir una fábrica espacial."
  Say "Por eso he venido a Régula en busca de trabajadores... Y, bueno, he encontrado a varios. A un montón, en realidad."
  ChangeChar mech campaign/mech-explaining.png
  Ask "El problema es que no tengo naves para llevarlos hasta la fábrica."
  MENU
    OPT_IFNOT mechWhy "¿Qué quieres que haga?"
      ChangeChar mech campaign/mech-explaining-f.png
      Ask "¡Madre mía, cuánto te gusta hablar! Necesito que los lleves a la fábrica. ¿Puedes hacerlo o no?"
      SET mechWhy
    END_OF_OPT
    OPT_IFNOT mechHowMuch "¿Cuántos trabajadores son?"
      SET mechHowMuch
      SET mechWhy
      Ask "Muchos. Tendré que buscar a otros cuantos camioneros, pero espero que al menos pueda contar contigo."
    END_OF_OPT
    OPT_IF mechHowMuch "Por supuesto. Estaré encantad[g(o|a)] de ayudar."
      GO mechAccept
    END_OF_OPT
    OPT "No creo que pueda aceptar la misión ahora mismo."
      ChangeChar mech campaign/mech-f.png
      Say "Lo que pasa es que quieres probar todas las opciones del diálogo, ¿verdad? Vale, tarde o temprano aceptarás."
      GO mechEnd
    END_OF_OPT
  END_OF_MENU

  LABEL mechAccept
  SET mechAccepted
  RESET mechLeftAtRegula
  Delay 250
  ChangeChar mech campaign/mech-thumbup.png
  Delay 250
  Say "Genial."
  Say "Te veo en el almacén."
  Delay 250
  HideChar mech
  Delay 250
  AddNode workshop
  Delay 250
  ShowPath U1-7a
  HideButton bmech
  HideDlgScene
  RETURN

  LABEL mechEnd
  HideChar mech
  Delay 300
  HideDlgScene
END_OF_SCRIPT

SCRIPT brownDlg

  ShowDlgScene
  ShowChar brown left
  SET brownTried
  Delay 500
  Ask "..."
  MENU
    OPT "Hola."
      EXIT_MENU
    END_OF_OPT
    OPT "Vengo en son de paz."
      EXIT_MENU
    END_OF_OPT
    OPT "¿Puedes decirme cómo se llega al aeropuerto / a la estación de tren / a la tienda de disfraces / al hotel?"
      EXIT_MENU
    END_OF_OPT
    OPT_IF brownInfo "…"
      GO brownSuccess
    END_OF_OPT
  END_OF_MENU
  Delay 700
  Ask "…"
  MENU
    OPT "Entendido."
      EXIT_MENU
    END_OF_OPT
    OPT "Lo siento, no hablo tu idioma."
      EXIT_MENU
    END_OF_OPT
    OPT "Tu planeta / ciudad / estación espacial / jardín hidropónico es precioso en verano."
      EXIT_MENU
    END_OF_OPT
    OPT_IF brownInfo "…"
      GO brownSuccess
    END_OF_OPT
  END_OF_MENU
  Delay 900
  Ask "¿...?"
  MENU
    OPT "¿Por qué no hablas?"
      GO brownFail
    END_OF_OPT
    OPT "Traigo saludos del planeta Tierra."
      Delay 300
      Ask "¡...!"
      EXIT_MENU
    END_OF_OPT
    OPT "Vives en un sitio muy tranquilo."
      Ask "…"
      Delay 900
      EXIT_MENU
    END_OF_OPT
    OPT_IF brownInfo "…"
      GO brownSuccess
    END_OF_OPT
  END_OF_MENU
  MENU
    OPT "Hm... No sé qué más decir."
      Delay 500
      Ask "…"
    END_OF_OPT
    OPT "Ha sido un placer hablar contigo, pero tengo que irme."
      GO brownEnd
    END_OF_OPT
    OPT "¡Di algo, por lo que más quieras!"
      GO brownFail
    END_OF_OPT
    OPT_IF brownInfo "…"
      GO brownSuccess
    END_OF_OPT
  END_OF_MENU

  LABEL brownSuccess
  Delay 1500
  Say "¡...!"
  Delay 400
  ChangeChar brown campaign/brown-purr.png
  Say "… …"
  Delay 200
  Say "…"
  ChangeChar brown campaign/brown.png
  Delay 300
  HideChar brown
  SET brownUnlocked
  Announce "¡Has desbloqueado los alienígenas marrones!"
  Delay 500
  SetAchievementProgress camp_aliens 2
  Delay 500
  ShowChar merchant right
  Delay 300
  Say "Qué bonito, veo que habéis empezado a conoceros."
  Say "Felicidades, estos chicos son buena gente. ¡Y unos fantásticos ingenieros!"
  Say "Incluye a uno en tu tripulación y quedarás maravillado con lo rápido que va tu nave."
  Say "Huy, perdona. Es hora de que vuelva a la reunión."
  Delay 200
  HideChar merchant
  HideButton bbrown
  HideButton bmerchhelp
  IF class3Asked
    ShowButton bmerch2
    SetFocus bmerch2
    SET merch2immediatelly
  END_OF_IF
  HideChar brown
  HideDlgScene
  RETURN

  LABEL brownFail
  Delay 700
  ChangeChar brown campaign/brown-frown.png
  Say "¡...!"
  Delay 500

  LABEL brownEnd
  HideChar brown
  IF merchHelpAsked
    Delay 750
    ShowChar merchant right
    Say "Siento haber tardado. La reunión se está alargando."
    Ask "¿Cómo te va? ¿Ya sois amigos del alma?"
    MENU
      OPT "Esto marcha como la seda."
        Ask "¿De verdad? Cualquiera juraría que no sabes qué hacer."
      END_OF_OPT
      OPT "Verás, tenemos un pequeño problema de comunicación."
        EXIT_MENU
      END_OF_OPT
      OPT "¿Tú qué crees? Es imposible hablar con ellos."
        EXIT_MENU
      END_OF_OPT
    END_OF_MENU
    Ask "¿Podrías repetirme lo que te han dicho?"
    MENU
      OPT "Claro. Lo recuerdo palabra por palabra: …"
        EXIT_MENU
      END_OF_OPT
    END_OF_MENU
    Say "Oh, interesante."
    Say "La respuesta habitual es: …"
    Say "Perdona, tengo que volver a la reunión. A veces es muy difícil negociar con esta gente."
    SET brownInfo
    Delay 300
    HideChar merchant
  END_OF_IF
  HideDlgScene
END_OF_SCRIPT

SCRIPT merchHelpDlg

  ShowDlgScene
  ShowChar merchant left
  IF merchHelpAsked
    ShowChar brown right
    Delay 300
    SayChar merchant "¡Ssssh! ¿No ves que estoy en una reunión?"
    Delay 900
    SayChar brown "..."
    Delay 800
    HideChar merchant
    HideChar brown
    HideDlgScene
    RETURN
  END_OF_IF
  IFNOT merchHelpIntro
    Say "Hola, espero que estés disfrutando de tu estancia."
    Say "Siento no poder enseñarte el planeta, pero tengo que ir a una reunión."
    SET merchHelpIntro
  ELSE
    Say "La reunión está a punto de empezar."
  END_OF_IF
  Ask "¿Necesitas algo?"
  MENU
    OPT "No, solo quería decir hola."
      Say "Oh. Hola."
      GO merchHelpEnd
    END_OF_OPT
    OPT_IF brownTried "Venía a pedirte ayuda. Tengo un pequeño problema con esta gente."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  Say "Me lo imagino, hablar con ellos puede ser complicado."
  Ask "No es por presumir, pero te lo dice un experto en su cultura."
  MENU
    OPT_IFNOT qTelepat "¿Se comunican por telepatía?"
      Say "¿Qué? Claro que no."
      Say "Pero pertenecen a una raza muy antigua. Creen que todo lo que puede decirse ya está dicho."
      SET qTelepat
    END_OF_OPT
    OPT_IFNOT qBrownSkill "¿Hay alguna razón por la que debería hablar con ellos?"
      Say "Por supuesto. Son buenísimos con los motores."
      Say "Yo siempre intento llevar uno a bordo. No te imaginas lo rápido que irá tu nave con ellos."
      Say "Y además son muy tranquilos."
      SET qBrownSkill
    END_OF_OPT
    OPT "No conseguimos entendernos."
      Say "Ya veo... En cuanto termine la reunión, me reuniré con vosotros y haré de intérprete."
      SET merchHelpAsked
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  MENU
    OPT "Gracias."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL merchHelpEnd
  Delay 300
  HideChar merchant
  HideDlgScene
END_OF_SCRIPT

SCRIPT merchDlg2

  ShowDlgScene
  ChangeChar merchant campaign/merch-sitting.png
  ShowChar merchant left
  IF merch2Refused
    Say "Vaya, me alegro de volver a verte."
    ChangeChar merchant campaign/merch-inviting.png
    Ask "Lo has pensado mejor y has decidido acompañarme a ese planeta alienígena, ¿verdad?"
    SET merchFriendly
    GO merchDlg2Querry
  END_OF_IF
  IF merch2immediatelly
    Say "Me alegro de que los nativos te hayan puesto las cosas fáciles."
    Ask "¿Necesitas algo más de mí?"
    MENU
      OPT "No estaría mal que me dieran un permiso para mi licencia de Clase III."
        GO merchDlg2cont
      END_OF_OPT
      OPT "No, pero me gustaría darte las gracias de nuevo."
        Say "¡Eres muy amable!"
        SUB_SCRIPT merchantLaughA
        Say "En fin... de vuelta al trabajo."
        GO merchDlg2End
      END_OF_OPT
    END_OF_MENU
  ELSE
    Say "¡Eh, me acuerdo de ti! Me escoltaste en un viaje de negocios hace un tiempo."
    SUB_SCRIPT merchantLaughA
    Ask "¿Qué te trae hasta aquí?"
    MENU
      OPT "Necesito que los alienígenas marrones me firmen un permiso para conseguir una licencia de Clase III."
        GO merchDlg2cont
      END_OF_OPT
      OPT "Solo pasaba a saludar."
        Say "¡Eres muy amable!"
        SUB_SCRIPT merchantLaughA
        Say "En fin... de vuelta al trabajo."
        GO merchDlg2End
      END_OF_OPT
    END_OF_MENU
  END_OF_IF

  LABEL merchDlg2cont
  Say "Hm. Esos permisos son difíciles de conseguir, pero creo que podría interceder en tu favor."
  Say "De hecho, esta puede ser una gran oportunidad para los dos."
  Say "Tú necesitas mi ayuda y yo necesito a [g(un camionero|una camionera)]."
  SUB_SCRIPT merchantLaughA
  Delay 200
  ChangeChar merchant campaign/merch-inviting.png
  Delay 200
  Ask "¿Te gustaría acompañarme en otro viaje de negocios?"

  LABEL merchDlg2Querry
  MENU
    OPT "De acuerdo. Acepto la oferta."
      Say "¡Fantástico! Formamos un gran equipo saqueando planetas."
      SUB_SCRIPT merchantLaughB
      Say "Las mismas condiciones que la última vez. Tú me mantienes a salvo y yo te doy el 25 por ciento de los beneficios."
      Say "Este viaje va a ser largo, así que necesitarás una nave más grande."
      Say "Te veo en el almacén."
      Delay 200
      HideChar merchant
      Delay 500
      ShowNode cyanHome
      CenterMapByNode cyanHome
      Delay 500
      ShowPath U2-19a
      CenterMapByPath U2-19a
      SET merchant2Unlocked
      HideButton bmerch2
      HideDlgScene
      RETURN
    END_OF_OPT
    OPT_IFNOT merchFriendly "¿Qué pasaría si declinara amablemente tu oferta y te pidiera ayuda con el permiso de los alienígenas marrones?"
      SUB_SCRIPT merchantLaughA
      Ask "Te pediría amablemente ayuda una vez más."
      SET merchFriendly
    END_OF_OPT
    OPT "Olvídalo, he tenido suficiente."
      Delay 200
      ChangeChar merchant campaign/merch-disappointed.png
      Delay 200
      Say "Entendido. Total, ¿quién quiere pilotar una nave de Clase III?"
      Say "Avísame si cambias de opinión."
      SET merch2Refused
      GO merchDlg2End
    END_OF_OPT
  END_OF_MENU

  LABEL merchDlg2End
  HideChar merchant
  HideDlgScene
END_OF_SCRIPT

SCRIPT cyanDlg

  ShowDlgScene
  ShowChar cyan left
  Ask "Buenos días, [g(caballero|señorita)]. Sea bienvenid[g(o|a)] a nuestro planeta."
  IF wasAtCyan
    MENU
      OPT "Gracias. Solo venía a decir hola."
        Ask "Oh, ¡exquisito! Dígalo."
        MENU
          OPT "Hm.. ¿Hola?"
            GO cyanEnd
          END_OF_OPT
        END_OF_MENU
      END_OF_OPT
      OPT_IF class3 "Gracias. Tengo una licencia de Clase III. ¿Me podrías dar algún diseño?"
        Say "Será un placer."
        GO cyanDesignUnlock
      END_OF_OPT
    END_OF_MENU
  END_OF_IF
  SET wasAtCyan
  MENU
    OPT "Es fantástico que habléis mi idioma."
      Say "Oh, no. Tenemos nuestra propio lengua."
      Ask "Cada miembro de la especie es especialista en algún tema. El mío son las lenguas extranjeras."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  MENU
    OPT "¿En serio? Dime algo en otro idioma."
      Delay 300
      Say "Kwwrh'ghrr kzz gwwr-h."
      IF brownHomeInitialized
        GO cyanLangMore1
      ELSE
        GO cyanLangMore2
      END_OF_IF
    END_OF_OPT
    OPT "Ya. Impresionante..."
      Say "No todo el mundo sabe apreciar la calidad."
      GO cyanCont
    END_OF_OPT
  END_OF_MENU

  LABEL cyanLangMore1
  MENU
    OPT "¿¡De verdad!? Venga, ahora en otro."
      Delay 400
      Say "…"
      GO cyanLangMore2
    END_OF_OPT
    OPT "Hm. Ya he tenido suficiente por hoy, gracias."
      GO cyanLangEnd
    END_OF_OPT
  END_OF_MENU

  LABEL cyanLangMore2
  MENU
    OPT "¡Qué dicción tan elegante! ¿Sabes más idiomas?"
      Delay 300
      Say "¡TlhIngan maH!"
      MENU
        OPT "Chico, ha sido impresionante. Gracias por la demostración."
          GO cyanLangEnd
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
    OPT "Ajá. Guay. Gracias por la demostración."
      GO cyanLangEnd
    END_OF_OPT
  END_OF_MENU

  LABEL cyanLangEnd
  Say "En absoluto, gracias a usted."

  LABEL cyanCont
  Say "Nuestra especie ha desarrollado talentos extraordinarios."
  Say "Cada niño escoge un ámbito en el que especializarse y trabaja en él hasta alcanzar la perfección."
  MENU
    OPT_IFNOT cyanProblem1 "Ya, pero... ¿Por qué vivís en chabolas?"
      Say "No disponemos de expertos en construcción."
      Say "Pero nuestros libros de texto sobre hipergrupos autocontradictorios son obras punteras en la materia."
      SET cyanProblem1
    END_OF_OPT
    OPT_IFNOT cyanProblem2 "Ya, pero... ¿Por qué hay basura por todas partes?"
      Say "No disponemos de expertos en recogida selectiva."
      Say "Pero nuestras esculturas ultrafractales de infinitas dimensiones son verdaderas obras de arte."
      SET cyanProblem2
    END_OF_OPT
    OPT "Ya, pero... Déjalo, no tiene importancia."
      Say "Imagino lo que está pensando, pero no hay ninguna razón por la que deba sentirse como una criatura inferior. No todas las especies pueden alcanzar la perfección."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  MENU
    OPT_IFNOT cyanAskIntro "Viendo que hablas mi idioma, debería ser fácil llegar a un acuerdo."
      Say "Si usted lo dice..."
      SET cyanAskIntro
    END_OF_OPT
    OPT_IFNOT cyanOffer1 "¿Puedo añadir alienígenas de color turquesa a mi tripulación?"
      Say "No."
      SET cyanOffer1
      SET cyanAskIntro
      ADD cyanOffers 1
      IF_MIN cyanOffers 3
        SET cyanSadEnd
      END_OF_IF
    END_OF_OPT
    OPT_IFNOT cyanOffer2 "¿Puedes darme un permiso firmado para naves de Clase III?"
      Say "No."
      SET cyanOffer2
      SET cyanAskIntro
      ADD cyanOffers 1
      IF_MIN cyanOffers 3
        SET cyanSadEnd
      END_OF_IF
    END_OF_OPT
    OPT_IFNOT cyanOffer3 "¿Puedes darme un diseño de los que hacen historia?"
      Say "Por supuesto, [g(caballero|señorita)]."
      SET cyanAskIntro
      IFNOT class3
        Say "Ah... Disculpe. Me temo que todos nuestros diseños son para naves de Clase III."
        Say "Así que… No."
        SET cyanOffer3
        ADD cyanOffers 1
        IF_MIN cyanOffers 3
          SET cyanSadEnd
        END_OF_IF
      ELSE
        GO cyanDesignUnlock
      END_OF_IF
    END_OF_OPT
    OPT_IF cyanSadEnd "Hm. No ha ido tan bien como pensaba."
      Say "Lo sé... Y lo lamento profundamente, pero nosotros solo salimos en una expansión."
      Say "Si le soy sincero, ni siquiera deberíamos aparecer en la campaña del juego básico."
      Ask "Es hora de que me vaya. Antes de despedirme, le daré la oportunidad de decir en voz alta lo maravillosa que es mi especie."
      MENU
        OPT "Me faltan palabras para decir lo que pienso de tu especie."
          EXIT_MENU
        END_OF_OPT
        OPT "Sí, sí, sois todos maravillosos."
          EXIT_MENU
        END_OF_OPT
        OPT "Creo que debería guardarme lo que pienso de vosotros."
          EXIT_MENU
        END_OF_OPT
        OPT "Diga lo que diga, no va a servir de nada, ¿verdad?"
          EXIT_MENU
        END_OF_OPT
      END_OF_MENU
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  Delay 300
  HideButton bcyan

  LABEL cyanEnd
  Delay 500
  HideChar cyanAlien
  HideDlgScene
  RETURN

  LABEL cyanDesignUnlock
  Say "Mire por donde, tengo aquí un precioso diseño."
  Say "Es mucho mejor que los de su especie. No solo dispone de espacio para más componentes, sino que además tiene un mayor valor estético."
  Delay 300
  UnlockShipPlan "III-amoeba"
  Announce "Has desbloqueado un nuevo modelo de Clase III."
  Delay 500
  Say "Estoy seguro de que le gustará."
  HideButton bcyan
  HideChar cyanAlien
  HideDlgScene
END_OF_SCRIPT

SCRIPT workDlg

  ShowDlgScene
  ShowChar mech left
  IF designDouble1Unlocked
    IF designDouble2Unlocked
      IF designDouble3Unlocked
        Say "¡Hola! Espero que estés aprovechando bien mis diseños."
        ChangeChar mech campaign/mech-f.png
        Say "Tengo que volver al trabajo. Me alegro de verte."
        GO workEnd
      ELSE
        IF lab1Done
          GO workThirdDesign
        ELSE
          Say "Lo siento. Ahora mismo estoy ocupada."
          GO workEnd
        END_OF_IF
      END_OF_IF
    ELSE
      IF designDouble2Ready
        GO workSecondDesign
      ELSE
        Say "Lo siento. Ahora mismo estoy ocupada."
        GO workEnd
      END_OF_IF
    END_OF_IF
  END_OF_IF
  IF doubleDesignOffered
    Say "Hola. ¿Has venido a probar mi nuevo diseño de Clase I?"
    GO doubleDesignOffer
  END_OF_IF
  SET doubleDesignOffered
  Say "Hola. Gracias de nuevo por tu ayuda."
  ChangeChar mech campaign/mech-explaining.png
  Say "Estoy trabajando en un nuevo diseño de Clase I."
  Say "La parte delantera tiene dos salientes para colocar cañones."
  ChangeChar mech campaign/mech.png
  Ask "¿Te gustaría probarlo?"

  LABEL doubleDesignOffer
  MENU
    OPT "Sí, por favor."
      Delay 300
      ChangeChar mech campaign/mech-explaining.png
      Delay 300
      Say "Aquí lo tienes."
      Delay 300
      UnlockShipPlan "I-double"
      Announce "Has desbloqueado un nuevo modelo de Clase I."
      Delay 500
      SetAchievementProgress camp_design_factory 1
      Delay 300
      ChangeChar mech campaign/mech.png
      Say "Tengo que volver al trabajo. Nos vemos luego."
      Delay 500
      ChangeChar mech campaign/mech-f.png
      ResetFocus bworkshop
      SET designDouble1Unlocked
      EXIT_MENU
    END_OF_OPT
    OPT "No, gracias."
      Delay 300
      ChangeChar mech campaign/mech-f.png
      Delay 300
      Say "De acuerdo. Si cambias de opinión, ya sabes dónde encontrarme."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  ChangeChar mech campaign/mech.png
  IFNOT workUniversalInfo
    Delay 300
    HideChar mech
    Delay 500
    ShowChar mech right
    ChangeChar mech campaign/mech-explaining.png
    Say "Si te interesa, en el viaje de vuelta puedes probar algunos avances en los que estamos trabajando."
    Ask "Tecnología punta, encanto. No la encontrarás en ninguna otra parte."
    MENU
      OPT "¿Qué tipo de tecnología punta?"
        Say "Ya lo verás."
        EXIT_MENU
      END_OF_OPT
      OPT "Ajá, lo que tú digas."
        EXIT_MENU
      END_OF_OPT
    END_OF_MENU
    SET workUniversalInfo
  END_OF_IF

  LABEL workEnd
  Delay 500
  HideChar mech
  HideDlgScene
  RETURN

  LABEL workSecondDesign
  IF designDouble2Refused
    Say "¡Hola! ¿Has cambiado de opinión? ¿Llevarás mi dinero hasta la Taberna Espacial?"
    GO workSecondDesignQuerry
  END_OF_IF
  IF designDouble2Offered
    Say "¡Hola! ¿Quieres ver mi diseño de Clase II? Y... ya de paso... ¿puedo pedirte un favor?"
    MENU
      OPT "¿Qué tipo de favor?"
        GO workSecondDesignQuest
      END_OF_OPT
      OPT "Ah... no es un buen momento. Quizás más tarde."
        GO workEnd
      END_OF_OPT
    END_OF_MENU
  END_OF_IF
  Say "¡Hola! Me alegro de volver a verte. Llevo tiempo queriendo preguntarte algo..."
  Ask "¿Has probado ya el diseño que te regalé? ¿Funciona bien?"
  MENU
    OPT "Claro, es muy bueno."
      Say "¡Genial! Le gusta a mucha gente."
      EXIT_MENU
    END_OF_OPT
    OPT "Si te soy sincer[g(o|a)], no le he encontrado mucha utilidad."
      Say "Oh, cuánto lo siento. Hay gente a la que le gusta."
      EXIT_MENU
    END_OF_OPT
    OPT "No estoy segur[g(o|a)] de haber explorado todas sus posibilidades."
      Say "Ya, intentas ser amable. Sé que no es nada del otro mundo, pero hay gente a la que le gusta."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  SET designDouble2Offered
  Ask "Incluso me han pedido que diseñe una nave de Clase II."
  MENU
    OPT "¿Podrías enseñarme el diseño?"
      EXIT_MENU
    END_OF_OPT
    OPT "Vaya, ¡qué interesante! Tengo que irme."
      GO workEnd
    END_OF_OPT
  END_OF_MENU
  Say "Claro, me encantaría que lo probaras. Por cierto, ¿podrías hacerme un favor?"
  MENU
    OPT "Dispara, ¿qué necesitas?"
      EXIT_MENU
    END_OF_OPT
    OPT "Sabía que esto tendría truco."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL workSecondDesignQuest
  ChangeChar mech campaign/mech.png
  Say "Unos amigos y yo celebramos una fiesta en la Taberna Espacial, ¿sabes?"
  Say "Nos lo pasamos muy bien, pero... Gastamos más dinero del que teníamos en ese momento."
  Delay 300
  ChangeChar mech campaign/mech-explaining.png
  Delay 700
  ChangeChar mech campaign/mech.png
  Delay 300
  Say "El camarero fue muy amable y le prometí que saldaría la cuenta en cuanto pudiera."
  ChangeChar mech campaign/mech-f.png
  Ask "Ahora que por fin tengo el dinero, resulta que no puedo salir de la fábrica ni un segundo. ¿Serías tan amable de entregar el dinero por mí?"

  LABEL workSecondDesignQuerry
  MENU
    OPT "Claro, ¿cuánto es?"
      EXIT_MENU
    END_OF_OPT
    OPT "Ahora mismo no me apetece hacer de chic[g(o|a)] de los recados."
      SET designDouble2Refused
      GO workEnd
    END_OF_OPT
  END_OF_MENU
  Say "Unos 20 créditos. Aquí tienes."
  AddCredits 20
  Announce "Recibes 20 créditos."
  ChangeChar mech campaign/mech.png
  Ask "Gracias. Llevo tiempo sintiéndome culpable por esa fiesta."
  MENU
    OPT "Hm. Es mucho dinero... ¿Cómo sabes que no me quedaré con él?"
      ChangeChar mech campaign/mech-explaining.png
      Say "Simplemente lo sé. Sé juzgar a la gente."
      Say "Aquí tienes el nuevo diseño."
      EXIT_MENU
    END_OF_OPT
    OPT_IFNOT designDouble2Unlocked "Supongo que me darás el diseño en cuanto entregue el dinero, ¿no?"
      ChangeChar mech campaign/mech-explaining.png
      Say "Qué va. Puedes echarle un vistazo ahora mismo."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  SET mechDebtPending
  SET designDouble2Unlocked
  UnlockShipPlan "II-double"
  Delay 500
  Announce "Has desbloqueado un nuevo modelo de Clase II."
  Delay 500
  SetAchievementProgress camp_design_factory 2
  Delay 300
  IFNOT lab1Done
    ResetFocus bworkshop
  END_OF_IF
  SetFocus bbartender
  ChangeChar mech campaign/mech-thumbup.png
  Say "Gracias y buena suerte."
  GO workEnd

  LABEL workThirdDesign
  IF designDouble3Ready
    Say "¡Hola!"
    MENU
      OPT "¿Has tenido tiempo de terminar ese diseño de Clase III?"
        Say "¡Oh!"
        Say "Perdona, estamos tan ocupados que se me había olvidado."
        Say "Espera aquí un momento y enseguida te lo traigo.."
        HideChar mech
        Delay 5000
        ShowChar mech left
        Delay 200
        ChangeChar mech campaign/mech-explaining.png
        Say "Toma, pruébalo."
        SET designDouble3Unlocked
        UnlockShipPlan "III-double"
        Delay 300
        ChangeChar mech campaign/mech.png
        Delay 300
        Announce "Has desbloqueado un nuevo modelo de Clase III."
        Delay 500
        ResetFocus bworkshop
        SetAchievementProgress camp_design_factory 3
        Delay 300
        Say "Espero que funcione. Lo he hecho en cinco minutos."
        ChangeChar mech campaign/mech-explaining-f.png
        Say "Diseñar naves espaciales no es tan difícil como algunos se piensan."
        ChangeChar mech campaign/mech-explaining.png
        Ask "Solo he borrado una casilla y le he puesto otra aquí."
        MENU
          OPT "¡Genial! Gracias."
            ChangeChar mech campaign/mech.png
            Say "De nada. ¡Que tengas un buen viaje!"
            GO workEnd
          END_OF_OPT
          OPT "Eres demasiado modesta. Se te da muy bien hacer estas cosas."
            Delay 300
            ChangeChar mech campaign/mech.png
            Delay 500
            ChangeChar mech campaign/mech-blush.png
            Delay 300
            Say "Sí... bueno... te lo agradezco. Eres un encanto. Espero que estas naves te faciliten un poco el vuelo."
            GO workEnd
          END_OF_OPT
        END_OF_MENU
      END_OF_OPT
    END_OF_MENU
  END_OF_IF
  IF designDouble3Asked
    Say "Hola de nuevo."
    ChangeChar mech campaign/mech-explaining-f.png
    Say "Sigo pensando en ese diseño de Clase III. Aún no he tenido tiempo de terminarlo."
    GO workEnd
  END_OF_IF
  Ask "¿Qué tal? ¿Te gusta trabajar para CoMoSEA?"
  MENU
    OPT "No está mal."
      Say "Me alegra saberlo."
      EXIT_MENU
    END_OF_OPT
    OPT "No lo soporto."
      Say "No me extraña. Debe de ser duro."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  Ask "Será mejor que vuelva al trabajo. Encantada de hablar contigo."
  MENU
    OPT "Oye, antes de irte... ¿Has tenido tiempo de pensar en esa nave de Clase III con un saliente adicional para cañones?"
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  Say "¿Eh? El diseño estándar de Clase III ya tiene tres salientes. No te olvides de los laterales."
  MENU
    OPT "¿Y no se podría añadir otro más en la parte delantera?"
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  ChangeChar mech campaign/mech-f.png
  Say "Hm... Sí, ya entiendo. En principio debería funcionar."
  ChangeChar mech campaign/mech-explaining.png
  Say "Haremos una cosa. Habla conmigo la próxima vez que pases por la fábrica y te regalaré un diseño como el que me has pedido."
  SET designDouble3Asked
  ResetFocus bworkshop
  GO workEnd
END_OF_SCRIPT

SCRIPT shortcutDlg

  ShowDlgScene
  ShowChar ambassador left
  Delay 500
  ChangeChar ambassador campaign/ambassador-happy.png
  Say "Vaya, vaya, ¿qué tal? Me alegro de verl[g(o|a)], [g(caballero|señorita)]."
  ChangeChar ambassador campaign/ambassador.png
  Ask "Le estoy muy agradecido por su ayuda con aquella... situación tan delicada. Y por su discreción."
  MENU
    OPT_IFNOT shortcutPleasure "En absoluto, fue un placer."
      SET shortcutPleasure
      Ask "No sabe lo difícil que es encontrar a una persona responsable, discreta y capaz."
    END_OF_OPT
    OPT "¿Estás en otra... situación delicada?"
      Ask "Oh, no. Qué va. He terminado mi trabajo en este planeta. De hecho, me dirigía a la lanzadera para regresar a la estación de la embajada."
      MENU
        OPT "Yo también he terminado mi trabajo aquí."
          Say "¿Necesita que [g(lo|la)] lleve? Puede venir conmigo hasta la embajada espacial."
          Ask "Será un placer ayudarle."
          MENU
            OPT "Claro. Ir en una lanzadera diplomática suena bien."
              GO shortcutAccepted
            END_OF_OPT
            OPT "No, gracias. Iré en mi nave."
              Say "De acuerdo. Que tenga un buen viaje."
              GO shortcutEnd
            END_OF_OPT
          END_OF_MENU
        END_OF_OPT
        OPT "Ya veo. Que tenga un buen viaje."
          Say "Usted también, [g(caballero|señorita)]."
          ChangeChar ambassador campaign/ambassador-happy.png
          Delay 700
          ChangeChar ambassador campaign/ambassador.png
          GO shortcutEnd
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL shortcutAccepted
  Say "Maravilloso, nos veremos en el hangar."
  Delay 300
  HideChar ambassador
  Delay 500
  ShowChar ambassador right
  Delay 500
  ChangeChar ambassador campaign/ambassador-happy.png
  Delay 100
  Say "¿Qué le parece? Esta es la lanzadera."
  ChangeChar ambassador campaign/ambassador.png
  Ask "Suba a bordo y siga el oscuro pasadizo."
  MENU
    OPT "Hm. De acuerdo."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  Delay 300
  HideChar ambassador
  Delay 800
  Announce "La lanzadera diplomática ha resultado ser una nave más elegante de lo que parecía. Hay sillones con orejas por todas partes y apenas se ve alguna tubería."
  Announce "¡Esto sí que ha sido un gran vuelo! Tanto que te planteas ser diplomátic[g(o|a)] en tu próxima vida."
  Announce "Solo has tenido que tomar una decisión táctica en todo el trayecto."
  MENU
    OPT "Comeré pollo."
      EXIT_MENU
    END_OF_OPT
    OPT "Probaré la pasta."
      EXIT_MENU
    END_OF_OPT
    OPT "No, gracias. No tengo hambre."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  Delay 600
  CenterMapByNode outpost
  Delay 1000
  MovePlayerToNode outpost
  Delay 500
  ChangeChar ambassador campaign/ambassador-peek.png
  ShowChar ambassador right
  Say "¡De vuelta al trabajo! Ha sido un placer viajar con usted, [NAME]."
  MENU
    OPT "Lo mismo digo. Gracias por el viaje."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL shortcutEnd
  HideButton bcyanshortcut
  RESET shortcutEnabled
  Delay 300
  HideChar ambassador
  Delay 300
  HideDlgScene
END_OF_SCRIPT

END_OF_SCRIPTS
