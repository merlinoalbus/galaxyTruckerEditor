SCRIPTS

SCRIPT tutorialPurple

  ShowDlgScene
  ShowHelpImage 40 50 70 campaign/tutorial-purple.png
  HideDlgScene
END_OF_SCRIPT

SCRIPT ms_purpleAlien

  ACT_MISSION purpleAlienEscort
  IF_MISSION_WON
    SET purpleEscortFinished
    SET purpleUnlocked
    SetFocus bambassador
    HidePath U1-5purple
    ShowDlgScene
    ShowChar purpleDipl left
    Ask "Fz'z'w kh gwrzzt. Kwfff-ght! K'hrg gzt-gzt k?"
    MENU
      OPT "Yes."
        Say "K'hrg!"
        GO whatever
      END_OF_OPT
      OPT "No."
        Say "Gzt-gzt, k'hrg."
        GO whatever
      END_OF_OPT
      OPT "Whatever."
        Say "Dhw'g'k kkw..."
        GO whatever
      END_OF_OPT
    END_OF_MENU
  ELSE
    ShowDlgScene
    ShowChar ambassador right
    Say "I don't know what happened, but Mr. K'wwfrg has returned. He is angry, hungry, and he has two bruised tentacles."
    Say "Next time, please be sure he makes it to Th'kzw-r. You don't want to cause a diplomatic crisis, do you?"
    HideDlgScene
    RETURN
  END_OF_IF

  LABEL whatever
  Delay 500
  HideChar purpleDipl
  SET finished
  Announce "Purple aliens unlocked!"
  Delay 500
  SetAchievementProgress camp_aliens 1
  Delay 500
  HideDlgScene
  RETURN
END_OF_SCRIPT

SCRIPT ms_first_mission

  IFNOT firstMissionTaken
    SET firstMissionTaken
    ShowDlgScene
    ShowChar tutor right
    Say "Good luck!"
    HideChar tutor
    HideDlgScene
  END_OF_IF
  ACT_MISSION firstMission
END_OF_SCRIPT

SCRIPT ms_rough_mission

  SET firstMissionTaken
  ShowDlgScene
  ShowChar roughtrucker right
  IF wasChanceForBet
    Say "Hey[g(, pal|, sister)], how's it goin'?"
    IF wasBet
      Ask "Do you want to bet again? 3 cosmic credits for the first [g(man|trucker)] to finish?"
      GO bet_question
    ELSE
      Ask "Have you changed your mind? 3 cosmic credits for the first [g(man|trucker)] to finish?"
      GO bet_question
    END_OF_IF
  END_OF_IF
  Ask "Hey[g(, pal|, sister)], joining me on this miserable route?"
  MENU
    OPT_IFNOT roughintroduced "Hello. What are you doing here?"
      SET roughintroduced
      Say "I'm trucking this route. When I started, my instructor told me I wasn't good enough for it."
      Say "And see? Here I am, getting blasted by those meteors over and over, for 10 years."
      Ask "So who was right, huh?"
    END_OF_OPT
    OPT "Sounds fun. I'll fly with you."
      GO r2a_mission
    END_OF_OPT
    OPT "I think I'll look for an easier mission."
      HideChar roughtrucker
      HideDlgScene
      RETURN
    END_OF_OPT
  END_OF_MENU

  LABEL r2a_mission
  Say "Great[g(, pal|, sister)]. Those meteors get boring after a few years."
  Ask "Speaking of boredom, how about a bet? Whoever finishes first gets 3 cosmic credits from the other."
  SET wasChanceForBet

  LABEL bet_question
  RESET triedRaise
  MENU
    OPT "Okay, why not?"
      Say "Right! Let's go!"
      SET wasBet
      GO after_bet
    END_OF_OPT
    OPT_IFNOT triedRaise "Why so low? Let's make it 30 credits."
      SET triedRaise
      Say "Hey, do you know how hard it is to make 30 bucks?"
      Ask "Let's stay at 3[g(, pal|, sister)]!"
    END_OF_OPT
    OPT_IFNOT triedRaise "I never gamble."
      RESET wasBet
      Say "That's a shame. Oh well. Let's go."
      GO after_bet
    END_OF_OPT
    OPT_IF triedRaise "Bah! Then forget it."
      RESET wasBet
      RESET triedRaise
      Say "Okay, let's fly without a bet."
      Say "(Oof! That's one crazy profiteer.)"
      GO after_bet
    END_OF_OPT
  END_OF_MENU

  LABEL after_bet
  HideChar roughtrucker
  HideDlgScene
  ACT_MISSION roughMission
END_OF_SCRIPT

SCRIPT ms_booze_1

  ACT_MISSION booze1
  IF_MISSION_WON
    SET boozeQuestTried
    HidePath "R1-3a1"
    ShowPath "R1-3a"
  END_OF_IF
END_OF_SCRIPT

SCRIPT ms_booze_2

  ACT_MISSION booze2
  IF_MISSION_WON
    SET boozeQuestTried
    HidePath "R1-3a2"
    ShowPath "R1-3a"
  END_OF_IF
END_OF_SCRIPT

SCRIPT ms_road_closed

  ShowDlgScene
  Delay 300
  ShowChar securitybot2 left
  Say "SECURITY PROTOCOL 3-421."
  Say "THIS ROUTE IS CLOSED."
  Delay 1500
  Say "GO AWAY!"
  Delay 300
  HideChar securitybot2
  HideDlgScene
END_OF_SCRIPT

SCRIPT ms_meteors_side

  ShowDlgScene
  Delay 300
  ShowChar securitybot2 left
  Say "SECURITY PROTOCOL 3-421."
  Say "THIS ROUTE IS CLOSED."
  MENU
    OPT "Ah. Sorry, then."
      Delay 300
      HideChar securitybot2
      Delay 300
      HideDlgScene
      RETURN
    END_OF_OPT
    OPT "I know. And I don't care."
      Delay 300
      Say "*SIGH*"
      Say "APPLYING SPECIAL CASE PROTOCOL 3-421-F."
      Say "YOU CANNOT GO ALONE. I MUST ACCOMPANY YOU."
      Say "PLEASE CHECK THE ADVENTURE FORECAST DURING BUILDING."
      Say "IF YOU GET BLASTED, I WILL BE IN TROUBLE."
      Delay 300
      HideChar securitybot2
      Delay 300
      HideDlgScene
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  ACT_MISSION meteorsSideWithBot
END_OF_SCRIPT

SCRIPT ms_rich_girl

  ShowDlgScene
  Delay 300
  IFNOT wentToClosed
    ShowChar securitybot2 left
    Delay 300
    Say "SECURITY PROTOCOL 3-421."
    Delay 300
    Say "THIS ROUTE IS CLOSED."
    Delay 500
    ShowChar richgirl right
    Say "It's okay. [g(He|She|It)]'s here with me."
    Delay 300
    SayChar securitybot2 "*BEEP* *BEEP* SECURITY PROTOCOL OVERRIDDEN."
    Delay 500
    Say "AT YOUR OWN RISK, MISS."
    Say "METEORS REPORTED TO BE COMING FROM ONE SIDE."
    SayChar richgirl "I know. Don't worry about it."
    AskChar securitybot2 "IT IS MY DUTY TO WORRY, MISS."
    MENU
      OPT "Hey. The lady said not to worry."
        EXIT_MENU
      END_OF_OPT
      OPT "Wait a minute. Are these meteors coming from the left or from the right?"
        SayChar securitybot2 "SEARCHING DATABASE ..."
        Delay 1500
        Say "..."
        Delay 1500
        Say "..."
        Delay 1500
        Say "ERROR 404. CHECK YOUR CONNECTION AND TRY AGAIN LATER."
        Delay 250
        SayChar richgirl "It doesn't matter. We'll just check the adventure forecast while we're building."
        EXIT_MENU
      END_OF_OPT
    END_OF_MENU
    HideChar securitybot2
    SayChar richgirl "So let's go!"
    Say "If you finish the flight in better shape than me, I'll let you use my ship design."
    HideButton bvip2
    HideButton bvip
    SET wentToClosed
  ELSE
    ShowChar richgirl right
    Say "Hmm ... I know you. You're ... right on the tip of my tongue ..."
    MENU
      OPT "[NAME]."
        Say "Yeah. Right. That's it. Nice to see you again."
        EXIT_MENU
      END_OF_OPT
    END_OF_MENU
    Say "That was fun last time."
    Say "Let's go try it again!"
    Delay 300
    ShowChar securitybot2 left
    Delay 300
    Say "SECURITY PROTOCOL 3-421."
    Say "THIS ROUTE IS ... OH, IT IS YOU TWO AGAIN."
    Say "I HOPE YOU KNOW WHAT YOU ARE DOING."
    Delay 300
    SayChar richgirl "Don't worry. We'll check the adventure forecast to see which side the meteors are coming from."
    Delay 300
    HideChar securitybot2
    Delay 200
    SayChar richgirl "Let's go!"
    IFNOT designSport1Unlocked
      Say "If you lose fewer components than I do, I'll let you copy my ship design."
    ELSE
      Say "We'll see who loses fewer components this time!"
    END_OF_IF
  END_OF_IF
  HidePath "R1-4b1"
  ShowPath "R1-4b2"
  HideChar richgirl
  HideDlgScene
  ACT_MISSION meteorsSide
END_OF_SCRIPT

SCRIPT ms_rich_girl2

  ShowDlgScene
  Delay 300
  ShowChar richgirl right
  Say "All right. Let's get to it."
  Say "And quickly, before someone figures out what we're up to."
  HideChar richgirl
  HideDlgScene
  ACT_MISSION meteorsSide2
  ShowButton brichgirl2
  IF_MISSION_WON
    ShowPath U2-21b
    ShowDlgScene
    ShowChar richgirl right
    Say "Oh, I nearly forgot. Let's delete the route from our maps. We'll keep this between ourselves."
    Delay 500
    HidePath U2-21a
    HidePath U2-21b
    Delay 500
    IFNOT firstClassJoke
      Say "Anyway, it was fun."
      Say "I'm taking a shuttle back. Do you want to join me?"
      Delay 800
      ChangeChar richgirl campaign/rich-smile.png
      Delay 700
      Say "Heh. Just kidding. It's a luxury flight. Private, of course."
      Delay 200
      ChangeChar richgirl campaign/rich-smile-blink.png
      Delay 400
      ChangeChar richgirl campaign/rich-smile.png
      Delay 200
      Say "See ya."
      SET firstClassJoke
    ELSE
      Say "Anyway, it was fun. See ya."
    END_OF_IF
    HideChar richgirl
    HideDlgScene
  ELSE
    HidePath U2-21a
  END_OF_IF
END_OF_SCRIPT

SCRIPT ms_leavingPurple

  IF merchantAccepted
    SET merchantLeftAtPurple
  END_OF_IF
  ACT_MISSION adv1mediumRand
END_OF_SCRIPT

SCRIPT ms_merchant

  ShowDlgScene
  Delay 200
  ShowChar merchant left
  Delay 400
  ChangeChar merchant campaign/merch-laughA1.png
  Say "Hi there! Thanks for taking me up on my offer."
  ChangeChar merchant campaign/merch.png
  Say "I think your job is pretty clear? I'll take care of business and you take care of me."
  Ask "I need to have at least 10 credits' worth of goods on my ship. Otherwise, I can't even cover the port tariff."
  MENU
    OPT_IFNOT merchEnoughGoods "What if you don't pick up enough goods?"
      SET merchEnoughGoods
      Say "Well, then we'll have to come back."
    END_OF_OPT
    OPT_IFNOT merchGiveUp "What if one of us has to give up on the flight?"
      SET merchGiveUp
      Say "Well, then, we'll both come back here. We're traveling as a team. An all-star team!"
      SUB_SCRIPT merchantLaughA
    END_OF_OPT
    OPT "What happens if we succeed?"
      Say "Then we'll earn a lot of money and get rich!"
      Say "Of course, I'm already rich. But a little bit more can't hurt."
      SUB_SCRIPT merchantLaughB
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  Say "All right. Let's get down to business."
  Delay 300
  HideChar merchant
  HideDlgScene
  ACT_MISSION merchantProtection
END_OF_SCRIPT

SCRIPT ms_merchant2

  ShowDlgScene
  Delay 200
  ChangeChar merchant campaign/merch-sitting.png
  ShowChar merchant left
  Delay 400
  ChangeChar merchant campaign/merch-laughA1.png
  Say "Hello! Glad to be flying with you again."
  ChangeChar merchant campaign/merch-sitting.png
  Say "This time I need to get at least 18 credits' worth of goods on my ship."
  ChangeChar merchant campaign/merch-inviting.png
  Ask "Ready to go?"
  MENU
    OPT "Uh ... just a minute. I need to ... do something first."
      Say "Sure. Take your time."
      Delay 300
      HideChar merchant
      HideDlgScene
      RETURN
    END_OF_OPT
    OPT "Ready!"
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  Delay 300
  HideChar merchant
  HideDlgScene
  ACT_MISSION merchantProtection2
END_OF_SCRIPT

SCRIPT ms_merchant3

  ShowDlgScene
  Delay 200
  ChangeChar merchant campaign/merch-sitting.png
  ShowChar merchant left
  Delay 400
  IFNOT merchant3Failed
    Say "All right. Let's get to it. This time, we don't need any goods."
    Say "We just need to survive the flight. Of course, it never hurts to make money along the way, right?"
  ELSE
    Say "I hope we make it this time. This planet isn't exactly the easiest place for a guy like me to hide."
  END_OF_IF
  SUB_SCRIPT merchantLaughX
  Delay 500
  Say "Well, I don't feel like laughing. Let's go."
  HideChar merchant
  HideDlgScene
  ACT_MISSION merchantProtection3
END_OF_SCRIPT

SCRIPT ms_people_delivery

  ShowDlgScene
  Delay 200
  ShowChar mech left
  Delay 300
  IF peopleDeliveryFailed
    Say "Oh, it's you? To tell the truth, I was hoping for someone else this time. But no one else wants the job."
    Say "So I can give you another chance."
    Say "Don't blow it. I'll see you at the factory."
    HideChar mech
    HideDlgScene
    RESET peopleDeliveryFailed
    ACT_MISSION peopleDelivery
    RETURN
  END_OF_IF
  IFNOT mechLeftAtRegula
    ChangeChar mech campaign/mech-thumbup.png
    Say "Glad to see you!"
  ELSE
    ChangeChar mech campaign/mech-f.png
    Say "You sure took your time. But I am glad you're finally here."
    RESET mechLeftAtRegula
  END_OF_IF
  ChangeChar mech campaign/mech-explaining.png
  Say "We'll keep it simple. You take as many people as you want."
  Say "They'll be part of your crew along the way."
  Say "And I'll pay you 1 credit per person when you get there."
  Ask "But they ALL have to get there, got it? If even one person gets hurt, I'm not paying."
  MENU
    OPT "Okay."
      ChangeChar mech campaign/mech-thumbup.png
      Say "Great. Take good care of them for me."
      GO peopleDeliveryCont
    END_OF_OPT
    OPT "No, I think you should pay 1 credit for each person that arrives, regardless."
      Say "Hey, these aren't sewer pipes, bud. These are my people."
      Ask "I'm not going to send them out with a trucker who's expecting to lose a few along the way."
      MENU
        OPT "Oh. Okay."
          ChangeChar mech campaign/mech-thumbup.png
          Say "Good. I'm glad we're clear."
          GO peopleDeliveryCont
        END_OF_OPT
        OPT "Right. I'm sorry. You know what? Let me do this one for free."
          ChangeChar mech campaign/mech-explaining.png
          Say "That's nice of you, but I'm happy to pay. The boss has given me a good budget for this project. Might as well use it."
          GO peopleDeliveryCont
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL peopleDeliveryCont
  ChangeChar mech campaign/mech.png
  Say "Okay, I gotta go. My ship is getting ready for take off. See you there!"
  HideChar mech
  HideDlgScene
  ACT_MISSION peopleDelivery
  RETURN
END_OF_SCRIPT

SCRIPT ms_pirateEscort

  IFNOT pirateEscortTried
    ShowDlgScene
    ShowChar prisoner1 left
    Say "Thanks for defeating that robber baron. I've been imprisoned in his hideout for the last two months."
    Delay 500
    ShowChar prisoner2 right
    SayChar prisoner2 "I've been imprisoned for nearly three years. I was beginning to lose hope."
    SayChar prisoner1 "It seemed like such a good deal. Fabulous wealth. Baby-safe route."
    SayChar prisoner2 "Tell me about it. I didn't have a single cannon."
    AskChar prisoner1 "I took two cannons, just in case. Lost them both, though. Along with everything else."
    MENU
      OPT "Yeah, okay. Now let's get going. I've got to take this robber baron to justice."
        EXIT_MENU
      END_OF_OPT
    END_OF_MENU
    SayChar prisoner1 "Of course. Anyway ... thank you."
    Delay 500
    HideChar prisoner2
    Delay 300
    HideChar prisoner1
    HideDlgScene
  END_OF_IF
  ACT_MISSION pirateEscort
END_OF_SCRIPT

SCRIPT ms_finalRoute

  SaveState
  ShowDlgScene
  Delay 300
  ShowChar chairman left
  ShowChar crowd righttop
  SayChar chairman "Ladies and Gentlemen, welcome to the Transgalactic Trek!"
  Delay 300
  ShowChar crowd righttop
  Delay 500
  Announce "* The crowd applauds. *"
  Delay 300
  SayChar chairman "Four truckers, three flights, and millions of fans ready to watch intense trucking competition!"
  IF_MIN loadInstances 2
    MENU
      OPT "I feel like I've heard this before. Maybe I'll close my eyes for a minute ..."
        ChangeChar chairman campaign/chair-sorry.png
        Say "Now, for those of you watching for the first time ... blah blah blah blah ..."
        HideChar chairman
        HideChar crowd
        Delay 2000
        ChangeChar chairman campaign/chair-no.png
        ShowChar crowd righttop
        ShowChar chairman left
        Say "... and could someone wake [g(him|her)] up, please? There we go. Thank you."
        Delay 400
        ChangeChar chairman campaign/chair-smile.png
        Delay 400
        Say "And our fourth competitor is now awake and entering the warehouse which means I can finally say ..."
        GO raceStart
      END_OF_OPT
      OPT "Hm. This sounds important. I'd better listen carefully."
        EXIT_MENU
      END_OF_OPT
    END_OF_MENU
  END_OF_IF
  ChangeChar chairman campaign/chair-sorry.png
  Say "Now, for those of you watching for the first time, let me go over the rules:"
  Say "The competition consists of three flights, one for each ship class. It doesn't matter how many flights a trucker actually finishes."
  ChangeChar chairman campaign/chair.png
  Say "The winner is the one who earns the most credits."
  ChangeChar chairman campaign/chair-smile.png
  Say "Because that's what trucking is all about, right?"
  ChangeChar chairman campaign/chair-no-smile.png
  Say "And now, here are your transgalactic truckers:"
  Delay 1000
  ChangeChar chairman campaign/chair-smile.png
  Say "Our first competitor needs no introduction."
  Delay 400
  ShowChar tutor right
  Delay 600
  SayChar chairman "So many of the Galaxy's finest truckers have trained under this man. Let's have a warm round of applause for ..."
  Delay 300
  Announce "* The crowd applauds, drowning out the announcer. *"
  Delay 500
  HideChar tutor
  SayChar chairman "Our next trucker is a household name, known throughout the Galaxy!"
  Delay 400
  ShowChar richgirl right
  Delay 300
  Announce "* The crowd cheers. *"
  Delay 600
  ChangeChar richgirl campaign/rich-smile.png
  Delay 700
  ChangeChar richgirl campaign/rich-smile-cheer.png
  Delay 600
  ChangeChar richgirl campaign/rich-smile.png
  Delay 800
  Announce "* The crowd is still cheering. *"
  Delay 500
  ChangeChar richgirl campaign/rich-smile-blink.png
  Delay 350
  ChangeChar richgirl campaign/rich-smile.png
  Delay 350
  ChangeChar richgirl campaign/rich-smile-blink.png
  Delay 350
  ChangeChar richgirl campaign/rich-smile.png
  Delay 600
  Announce "* The crowd can't stop cheering. *"
  Delay 1000
  SayChar chairman "Please ... please, we need to introduce the other competitors."
  Delay 800
  HideChar richgirl
  SayChar chairman "Our third competitor comes from the distant reaches of the Galaxy."
  Delay 300
  ShowChar halfbreed right
  Delay 300
  SayChar chairman "The only trucker from Th'kzw-r and Silence, he has the best features of both species."
  Delay 300
  Announce "* The crowd is absolutely silent. *"
  Delay 200
  SayChar chairman "And already the crowd is chanting his nickname, which is:                                                 ..."
  Delay 500
  HideChar halfbreed
  SayChar chairman "And finally, the fresh face in our venerable competition ..."
  Say "In the upper left corner of your screen ... please give a big round of applause for ... [NAME]!"
  Delay 300
  Announce "* A few people clap politely. *"
  Delay 1200
  SayChar chairman "And now the competitors are entering the warehouse, which means it's time for me to say ..."

  LABEL raceStart
  ChangeChar chairman campaign/chair-welcome.png
  Say "... let the Transgalactic Trek begin!"
  HideChar chairman
  HideChar crowd
  HideDlgScene
  ACT_MISSION finalRoute
  IFNOT finalRaceFinished
    RETURN
  END_OF_IF
  HidePath U123
  ShowDlgScene
  ShowChar chairman left
  Say "And the competitors are entering the clubhouse now."
  Say "They all flew well, but the Transgalactic Trek can have only one victor."
  ChangeChar chairman campaign/chair-welcome.png
  Say "This year's winner is ..."
  IF_ORDER 0 1
    Say "... [NAME]!"
    MENU
      OPT "Thank you!"
        ChangeChar chairman campaign/chair-no-smile.png
        Say "That was a well-earned victory. Congratulations."
        Delay 300
        UnlockAchievement camp_victory
        Delay 300
        GO campVictory
      END_OF_OPT
    END_OF_MENU
  END_OF_IF
  IF_ORDER 1 1
    ShowChar tutor right
    SayChar chairman "... this stalwart flight instructor!"
    Say "Congratulations! Now everyone knows that our newbies are in the best hands when they are training under you!"
    HideChar tutor
  END_OF_IF
  IF_ORDER 2 1
    ShowChar richgirl right
    SayChar chairman "... this amazing lady!"
    Say "Congratulations! Once again, you have proven that trucking is not just for grizzled old men."
    HideChar richgirl
  END_OF_IF
  IF_ORDER 3 1
    ShowChar halfbreed right
    SayChar chairman "... this exceptional alien!"
    Say "Congratulations! May cooperation between the Galaxy's species live long and prosper!"
    HideChar halfbreed
  END_OF_IF
  HideChar chairman

  LABEL timeLoop
  Delay 1500
  ShowChar designer left
  ChangeChar designer campaign/designer-hi.png
  Ask "Hello. You look a little down."
  MENU
    OPT "No, I'm fine."
      ChangeChar designer campaign/designer.png
      Say "I know how it is. You played through such a long campaign and in the end, you didn't win."
      EXIT_MENU
    END_OF_OPT
    OPT "It was such a long campaign. And I didn't win!"
      ChangeChar designer campaign/designer.png
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  Say "Well, cheer up!"
  Say "You can always restart the campaign and try again!"
  MENU
    OPT "You've got to be kidding me!"
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  Say "Hm. Well, it would take a long time to get back to the Transgalactic Trek. And you might even lose again."
  ChangeChar designer campaign/designer-point.png
  Say "Maybe there's a better way!"
  IF loadInstances
    SET dejavu
  END_OF_IF
  MENU
    OPT "Go on ..."
      ChangeChar designer campaign/designer.png
      EXIT_MENU
    END_OF_OPT
    OPT_IF dejavu "I feel like this has happened before."
      Say "Oh, that's just deja vu. It just means someone has been playing around with the flow of time."
      ChangeChar designer campaign/designer.png
      EXIT_MENU
    END_OF_OPT
    OPT "Forget it. It's done."
      ChangeChar designer campaign/designer.png
      Say "Well, yes. But listen to me anyway:"
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  ChangeChar designer campaign/designer-expl.png
  Say "Remember when I told you that my lab was working on time travel?"
  ChangeChar designer campaign/designer.png
  Ask "Well, that might help you out. You could travel back in time to the start of the competition."
  RESET loadButterfly
  RESET loadDangers
  MENU
    OPT_IFNOT loadDangers "That sounds dangerous."
      SET loadDangers
      Say "No, it should be safe ... at least in theory ..."
      ChangeChar designer campaign/designer-down.png
      Say "Okay, so the technology hasn't been tested very thoroughly. There's a slight chance that reality will cease to exist."
      ChangeChar designer campaign/designer.png
      Ask "But I think it's worth the risk."
    END_OF_OPT
    OPT_IFNOT loadButterfly "But if I go back to the same time, won't everything happen the same way?"
      SET loadButterfly
      Say "No. Because we have advanced technology:"
      Delay 300
      ChangeChar designer campaign/designer-butterfly.png
      Delay 300
      Say "We'll just put this butterfly in the time machine with you. And we have to make sure it flaps its wings."
      Delay 300
      ChangeChar designer campaign/designer.png
      Delay 300
      Ask "So what do you say?"
    END_OF_OPT
    OPT "Okay, lets try it."
      GO loadTime
    END_OF_OPT
    OPT "You know what? Let's forget it."
      Say "Very well."
      Delay 400
      ChangeChar designer campaign/designer-down.png
      Delay 300
      Say "(I guess I wasn't very convincing.)"
      Say "(What did I do wrong? Was it my tone of voice?)"
      Say "(I should probably try again. That's what time machines are for, right?)"
      Delay 300
      ChangeChar designer campaign/designer.png
      Delay 400
      ChangeChar designer campaign/designer-butterfly.png
      Delay 300
      Say "Excuse me a moment ..."
      Delay 300
      HideChar designer
      SET dejavu
      GO timeLoop
    END_OF_OPT
  END_OF_MENU

  LABEL campVictory
  HideChar chairman
  Delay 800
  ShowChar merchant right
  Say "Would you believe that this amazing trucker started [g(his|her)] career as my business partner?"
  SUB_SCRIPT merchantLaughA
  Delay 800
  ShowChar mech left
  Delay 500
  Say "That was great. Just great. Congratulations."
  Delay 300
  ChangeChar mech campaign/mech-blush.png
  Delay 800
  HideChar merchant
  Delay 300
  ShowChar designer right
  Delay 500
  Say "Yes, that was quite an impressive performance. It's hard to believe that was your first competition!"
  Delay 300
  ChangeChar designer campaign/designer-hi.png
  Delay 500
  ChangeChar designer campaign/designer.png
  Delay 300
  HideChar designer
  Delay 200
  HideChar mech
  Delay 200
  ShowChar guru left
  Delay 500
  Say "You have reached the ultimate level in your quest for excellence, fame, and riches."
  Delay 300
  Say "But remember: success is not like the mouth of the sympathetic frog gaping hungrily toward the celestial equator at tea time."
  Delay 500
  HideChar guru
  Delay 300
  ShowChar foreman right
  Delay 600
  ChangeChar foreman campaign/foreman-hello.png
  Say "Ho boy! What a trucker!"
  Delay 300
  ChangeChar foreman campaign/foreman-smile.png
  Delay 200
  Say "Anyway, hope you enjoyed your vacation. I have a lot of shipments piling up at the forge."
  HideChar mech
  Delay 300
  ShowChar richgirl left
  Delay 700
  ChangeChar richgirl campaign/rich-bored.png
  Delay 300
  Say "Oh come now. Jobs like that are too boring for celebrities like me. And [g(him|her)]."
  Delay 300
  ChangeChar richgirl campaign/rich-smile.png
  Delay 700
  ChangeChar richgirl campaign/rich-smile-cheer.png
  Delay 400
  ChangeChar richgirl campaign/rich-smile.png
  Delay 300
  HideChar foreman
  Delay 300
  ShowChar tutor right
  Say "Let's give the winner a little room, lady."
  Delay 300
  HideChar richgirl
  Delay 300
  Ask "Nice work, newbie! Hm. Maybe I should stop calling you that."
  MENU
    OPT_IFNOT finalTutorOk "It's okay. You helped me a lot at the beginning."
      SET finalTutorOk
      Say "Of course I did! That's my job, newbie!"
    END_OF_OPT
    OPT "So, what's next?"
      Say "Whatever you want, newbie. Whatever you want."
      Say "You accomplished the ultimate goal of this campaign."
      Say "You can go back and try any missions that didn't go as well as you hoped."
      Say "But once you've unlocked all the campaign achievements, you've seen everything this campaign has to offer."
      Say "Of course, you can always challenge robots to a custom game, even a game in the same format as the Transgalactic Trek."
      Say "And you can play against other truckers online. Turn-based building or real-time. Short games or long games."
      Say "Do whatever seems fun. Your training is done."
      Delay 300
      Say "Good luck, ..."
      Delay 300
      ChangeChar tutor campaign/tutor-smile.png
      Delay 300
      Say "... trucker!"
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  Delay 500
  HideChar tutor
  HideDlgScene
  RETURN

  LABEL loadTime
  Say "Excellent! You are very brave."
  Delay 500
  ChangeChar designer campaign/designer-butterfly.png
  Delay 300
  Say "One moment ..."
  Say "Okay. Get ready ..."
  CenterMapByNode newbie
  Delay 300
  HideChar designer
  HideDlgScene
  Delay 1500
  LoadState
END_OF_SCRIPT

END_OF_SCRIPTS
