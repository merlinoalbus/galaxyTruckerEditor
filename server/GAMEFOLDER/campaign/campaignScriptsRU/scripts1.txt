SCRIPTS

SCRIPT newbieDlg

  IF class3
    SUB_SCRIPT tutorClass3Dlg
    RETURN
  END_OF_IF
  IF tutor_moremissions
    Delay 200
    ShowDlgScene
    ChangeChar tutor campaign/tutor-hidden.png
    ShowChar tutor left
    Delay 1500
    ChangeChar tutor campaign/tutor-pizza.png
    Delay 500
    SayChar tutor "Ты что, не видишь, что я занят? Исчезни!"
    Delay 500
    ChangeChar tutor campaign/tutor-hidden.png
    Delay 500
    HideChar tutor
    Delay 500
    HideDlgScene
    RETURN
  END_OF_IF
  Delay 200
  ShowDlgScene
  ShowChar tutor left
  Delay 200
  IF tutor_somemissions
    Say "Привет еще раз. Надеюсь, ты [g(доволен|довольна)] этим обычным скучным маршрутом."
    MENU
      OPT "Да, я просто [g(зашел|зашла)] поздороваться."
        HideDlgScene
        RETURN
      END_OF_OPT
      OPT "Можете порекомендовать другой маршрут?"
        GO more_missions
      END_OF_OPT
    END_OF_MENU
  END_OF_IF
  IF tutorialRefused
    IF playerIsCocky
      Ask "А, [g(мистер|мисс)] Всезнайка! Что, передумал[g(|a)]?"
    ELSE
      Ask "А, [g(мистер|мисс)] Я-ненавижу-обучалки! Что, передумал[g(|a)]?"
    END_OF_IF
    MENU
      OPT "Нет. Просто покажите мне, как убраться отсюда."
        Say "Хорошо. Можно выбрать обычный маршрут до Космобара и обратно."
        Delay 500
        ShowNode "bar"
        CenterMapByNode bar
        Delay 750
        ShowPath "R1-1a"
        ShowPath "R1-1b"
        Delay 500
        CenterMapByPath R1-1a
        Delay 1000
        SET tutor_somemissions
        Ask "Это безопасный и скучный маршрут. Можешь потратить остаток своей никчемной жизни, гоняя туда и обратно."
        MENU
          OPT "Звучит неплохо."
            GO basic_route_agreed
          END_OF_OPT
          OPT "Не, дай мне нормальное задание!"
            GO more_missions
          END_OF_OPT
        END_OF_MENU
      END_OF_OPT
    END_OF_MENU
  ELSE
    Ask "О, это же наш новичок! Твоя подготовка завершена. Что тебе еще нужно?"
    MENU
      OPT "Вы не сказали мне, что делать дальше."
        Say "Да, действительно. Ну, можно выбрать обычный маршрут до Космобара."
        Delay 250
        ShowNode "bar"
        CenterMapByNode bar
        Delay 750
        ShowPath "R1-1a"
        ShowPath "R1-1b"
        Delay 500
        CenterMapByPath R1-1a
        Delay 1000
        SET tutor_somemissions
        Ask "Это безопасный и скучный маршрут. Можешь потратить остаток своей никчемной жизни, гоняя туда и обратно."
        MENU
          OPT "Звучит неплохо. Спасибо."
            Say "Отлично. Теперь просто нажми на эту мигающую ракету и можешь отправляться."
            GO basic_route_agreed
          END_OF_OPT
          OPT "А есть еще варианты?"
            GO more_missions
          END_OF_OPT
        END_OF_MENU
      END_OF_OPT
    END_OF_MENU
  END_OF_IF

  LABEL basic_route_agreed
  HideDlgScene
  RETURN

  LABEL more_missions
  Ask "Думаю, это пока все, с чем ты сможешь справиться."
  MENU
    OPT "Хм. Возможно, вы правы. Полечу по стандартному маршруту."
      GO newbie_end
    END_OF_OPT
    OPT "Нет, я способ[g(ен|нa)] на большее."
      Say "Дерзковато, не находишь? Впрочем, ты сам[g(|a)] напросил[g(ся|ась)]."
      Say "Есть другой маршрут, на дальний аванпост."
      Delay 1000
      ChangeChar tutor campaign/tutor-scary.png
      Delay 500
      AskChar tutor "Полон опасностей он и вещей ужасных."
      MENU
        OPT "Продолжайте."
          Say "Ужасные невыговариваемые опасности."
          Say "Больше отвечать на вопросы твои не буду я, юный падаван."
          Ask "Сказал я уже много слишком..."
          MENU
            OPT "Эй, да ладно!"
              Delay 250
              ChangeChar tutor campaign/tutor.png
              Delay 250
              Say "Ну, есть тут один занюханный астероид. Довол[g(ен|ьна)]?"
              Say "А теперь исчезни. Мне вот-вот должны принести пиццу."
              GO more_missions1
            END_OF_OPT
          END_OF_MENU
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL more_missions1
  HideChar tutor
  ResetFocus btutor
  Delay 250
  ShowNode outpost
  CenterMapByNode outpost
  Delay 500
  ShowPath "R1-2a"
  ShowPath "R1-2b"
  CenterMapByPath "R1-2a"
  Delay 500
  SET tutor_moremissions

  LABEL newbie_end
  HideDlgScene
  RETURN
END_OF_SCRIPT

SCRIPT embassyDlg

  ShowDlgScene
  IF purpleEscortFinished
    ShowChar ambassador left
    Delay 250
    ChangeChar ambassador campaign/ambassador-happy.png
    Delay 100
    Say "Благодарим за вашу работу. Вы нам очень помогли."
    Say "Вот ваша награда от нашего Фонда Непредвиденных Расходов на Непонятных Незнакомцев в размере 10 кредитов."
    AddCredits 10
    Announce "Ты получаешь 10 кредитов."
    Delay 250
    HideButton bambassador
    HideChar ambasador
    HideDlgScene
    RETURN
  END_OF_IF
  IF purpleAccepted
    ShowChar ambassador left
    Delay 250
    Say "Пожалуйста, нам надо торопиться."
    Say "Мы не можем допустить осложнений с инопланетянами с Тх'кзв-р. Это очень воинственная раса."
    Delay 250
    HideChar ambassador
    HideDlgScene
    RETURN
  END_OF_IF
  IFNOT wasAtEmbassy
    SET wasAtEmbassy
    ChangeChar ambassador campaign/ambassador-peek.png
    ShowChar ambassador right
    Delay 250
    Ask "Эй! Не могли бы вы пройти со мной в этот темный закоулок?"
    MENU
      OPT "Конечно. Почему нет?"
        GO corridor
      END_OF_OPT
      OPT "Мама запрещает мне ходить с незнакомцами по темным закоулкам."
        Ask "Прошу вас. У меня очень деликатная просьба. Нас не должны услышать."
        MENU
          OPT "Ох, ну ладно."
            GO corridor
          END_OF_OPT
          OPT "Нет, нет и нет!"
            SET corridorRefused
            GO refused
          END_OF_OPT
        END_OF_MENU
      END_OF_OPT
    END_OF_MENU
  ELSE
    IF purpleOfferMade
      ChangeChar ambassador campaign/ambassador.png
      ShowChar ambassador left
      Delay 250
      Ask "Да?"
      MENU
        OPT "Вам все еще требуется доставить мистера сами-знаете-кого сами-знаете-куда?"
          Ask "Да. Вы согласны?"
          MENU
            OPT "Да, я в деле."
              GO acceptPurple
            END_OF_OPT
            OPT "Не, просто спросил[g(|а)]."
              GO refused
            END_OF_OPT
          END_OF_MENU
        END_OF_OPT
      END_OF_MENU
    ELSE
      ChangeChar ambassador campaign/ambassador-peek.png
      ShowChar ambassador  right
      Delay 250
      Say "Да?"
      MENU
        OPT "Я передумал[g(|а)]. Ведите меня в ваш темный закоулок."
          GO corridor
        END_OF_OPT
        OPT "Просто осматриваюсь."
          GO refused
        END_OF_OPT
      END_OF_MENU
    END_OF_IF
  END_OF_IF

  LABEL corridor
  Say "Отлично, спасибо."
  Delay 250
  HideChar ambassador
  Delay 1500
  ChangeChar ambassador campaign/ambassador.png
  ShowChar ambassador left
  Delay 250
  Say "Итак... у нас тут деликатная ситуация. Недавно мы принимали делегацию с Тх'кзв-р. После того, как они уехали, мы обнаружили, что один делегат... все еще здесь."
  Ask "Не будете ли вы столь любезны, что сопроводите г-на К'ввфрг на его родную планету?"
  SET purpleOfferMade
  MENU
    OPT "Конечно. Я отвезу г-на К'ввфрг на Тх'кзв-р, без проблем."
      GO acceptPurple
    END_OF_OPT
    OPT "А почему бы вам не посадить его на один из ваших  прекрасных дипломатических кораблей?"
      Ask "Дело в том, что эта ситуация несколько смущает г-на К'ввфрг. Мы бы предпочли сделать все деликатно, не предавая дело огласке."
      MENU
        OPT "Хорошо, можете на меня положиться."
          GO acceptPurple
        END_OF_OPT
        OPT "Вам лучше найти кого-то другого."
          GO refused
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL acceptPurple
  Say "О, замечательно. Спасибо вам."
  Delay 250
  ChangeChar ambassador campaign/ambassador-happy.png
  Delay 250
  Say "Господин К'ввфрг?"
  Delay 1000
  ShowChar purpleDipl right
  Delay 250
  SayChar ambassador "[g(Этот джентельмен|Эта леди)] сопроводит вас домой."
  Delay 250
  AskChar purpleDipl "Взрр кх'гвх квт Тх'кзв-р!"
  MENU
    OPT "Всегда пожалуйста."
      Delay 250
      HideChar purpleDipl
      Delay 500
      ChangeChar ambassador campaign/ambassador.png
      Delay 250
      SayChar ambassador "Большое вам спасибо. Мы вознаградим вас сразу по возвращении."
      SET purpleAccepted
      ShowNode "purpleHome"
      ShowPath "U1-5purple"
      SET purpleAccepted
      ResetFocus bambassador
      IF VIPTried
        HideButton bvip
        ShowButton bvip2
      END_OF_IF
      HideChar ambassador
      HideDlgScene
      RETURN
    END_OF_OPT
  END_OF_MENU

  LABEL refused
  Say "Что ж... Тогда хорошего вам дня."

  LABEL sceneEnd
  Delay 1000
  HideChar ambassador
  HideDlgScene
  RETURN
END_OF_SCRIPT

SCRIPT barDlg

  ShowDlgScene
  ShowChar bartender left
  Ask "Здравствуйте. Чего изволите?"
  IF boozeQuestHeard
    MENU
      OPT_IF mechDebtPending "Меня прислала сюда одна механик."
        GO bar_debt
      END_OF_OPT
      OPT_IF boozeQuestTried "Может, хотите, чтобы я доставил[g(|a)] что-то еще?"
        GO booze_quest
      END_OF_OPT
      OPT_IF boozeQuestRefused "Вам все еще нужно что-то доставить?"
        GO booze_quest
      END_OF_OPT
      OPT "Сегодня я хочу потратить немного денег. Принесите мне ваш лучший коктейль!"
        GO drinking
      END_OF_OPT
      OPT "О, простите. Я [g(зашел|зашла)] в бар совершенно случайно."
        Say "Все в порядке. Хорошего дня."
        GO leaving_bar
      END_OF_OPT
    END_OF_MENU
  END_OF_IF
  MENU
    OPT_IF mechDebtPending "Меня прислала механик."
      GO bar_debt
    END_OF_OPT
    OPT "Сделайте мне ваш лучший коктейль!"
      Ask "Конечно. 1 кредит, пожалуйста."
      IF_HAS_CREDITS 1
        MENU
          OPT "Без проблем. Я кое-что заработал[g(|a)] по пути сюда. Вот, возьмите."
            Say "Благодарю."
            AddCredits -1
            Announce "Ты отдаешь 1 кредит."
            GO anything_else
          END_OF_OPT
          OPT "Ну, это слишком. Пожалуй, я пойду."
            Say "Ладно. Хорошего дня."
            GO leaving_bar
          END_OF_OPT
        END_OF_MENU
      ELSE
        MENU
          OPT "А, забудь. Я сейчас немного на мели."
            Say "Ничего страшного. Всего хорошего."
            GO leaving_bar
          END_OF_OPT
        END_OF_MENU
      END_OF_IF
    END_OF_OPT
    OPT "Просто осматриваюсь. Отличный бар, кстати."
      GO nice_bar
    END_OF_OPT
    OPT "Не место, а какая-то дыра. Я сваливаю."
      Say "Как вам будет угодно. Всего хорошего."
      GO leaving_bar
    END_OF_OPT
  END_OF_MENU
  HideChar programmer
  HideDlgScene

  LABEL anything_else
  Say "Что-нибудь еще?"
  MENU
    OPT "Это я [g(должен|должна)] спросить: «Что-нибудь еще»? Миссии, задания, где это все?"
      Say "Прошу прощения?"
      MENU
        OPT "Я только что потратил[g(|a)] виртуальные деньги на виртуальную выпивку. Обычно после этого запускается какое-нибудь задание или типа того!"
          Delay 200
          ChangeChar bartender campaign/bartender-close.png
          Delay 200
          Say "А, вот вы о чем. К сожалению, вы выбрали неправильную ветку диалога."
          Say "К заданию вела та, которая «Просто осматриваюсь». И там, к слову, не надо было платить."
          Delay 200
          ChangeChar bartender campaign/bartender.png
          Delay 200
          Say "Что-нибудь еще?"
          MENU
            OPT_IFNOT bar_quest_insist "Миссию давай."
              SET bar_quest_insist
              Ask "Искренне сожалею, но здесь мы можем дать только напитки. Что-нибудь еще?"
            END_OF_OPT
            OPT "Еще одну порцию, пожалуйста!"
              GO drinking
            END_OF_OPT
            OPT "Не, все норм. Просто осматриваюсь. Отличный бар, кстати."
              GO nice_bar
            END_OF_OPT
          END_OF_MENU
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL drinking
  IF_HAS_CREDITS 1
    Say "Один момент!.. Пожалуйста. С вас 1 кредит."
    AddCredits -1
    Announce "Ты отдаешь 1 кредит."
    MENU
      OPT "*Бульк*"
        Ask "Что-нибудь еще?"
        MENU
          OPT "Эх, гулять так гулять. Налей еще!"
            GO drinking
          END_OF_OPT
          OPT "*Ик* ...Похоже, мне уже хватит. Пожалуй, пойду."
            Say "Спасибо за визит. Хорошего дня."
            GO leaving_bar_in_shame
          END_OF_OPT
        END_OF_MENU
      END_OF_OPT
    END_OF_MENU
  ELSE
    Say "Один кредит, пожалуйста... О, вижу, у вас закончились деньги."
    Say "Можете взять эту конфетку. За счет заведения. А теперь, пожалуйста, уходите без лишнего шума."
    GO leaving_bar_in_shame
  END_OF_IF

  LABEL nice_bar
  Say "Благодарю. Но это всего лишь тень былого великолепия."
  MENU
    OPT "Былого?"
      Say "Последнее время дела идут не очень хорошо. Боюсь, скоро вообще придется закрыться."
      Say "У меня отличный выбор элитного алкоголя. Но сейчас дальнобойщики предпочитают что-нибудь подешевле."
      Ask "Лучше бы я открыл бар на Регуле. Уж там-то люди понимают толк в качественных напитках."
      MENU
        OPT "Так почему бы вам не продать часть запасов на Регуле?"
          SET boozeQuestHeard
          Say "Это не так просто. Маршрут до Регулы довольно опасен для такого хрупкого груза."
          Ask "Сомневаюсь, что смогу найти дальнобойщика, который за это возьмется."
          MENU
            OPT "Вы только что его нашли!"
              GO booze_quest
            END_OF_OPT
            OPT "Похоже на то. Что ж, мне пора. Удачи."
              SET boozeQuestRefused
              Say "Спасибо. Хорошего дня."
              GO leaving_bar
            END_OF_OPT
          END_OF_MENU
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL leaving_bar_in_shame
  Delay 200
  ChangeChar bartender campaign/bartender-close.png
  Delay 200
  Say "О... И не могли бы вы нас покинуть, воспользовавшись задней дверью? Спасибо."

  LABEL leaving_bar
  Delay 300
  HideChar bartender
  HideDlgScene
  RETURN

  LABEL booze_quest
  Delay 200
  ChangeChar bartender campaign/bartender-close.png
  Delay 200
  Say "О, это было бы очень любезно с вашей стороны."
  Ask "Сколько контейнеров груза вы хотели бы доставить?"
  MENU
    OPT "Дайте два. Я с удовольствием их доставлю."
      HidePath "R1-3a"
      ShowPath "R1-3a2"
      Say "Вы очень добры!"
      Say "Контейнеры будут ждать вас на складе компонентов."
      GO quest_agreed
    END_OF_OPT
    OPT "Ну, мой корабль довольно мал. Пожалуй, одного контейнера будет достаточно."
      HidePath "R1-3a"
      ShowPath "R1-3a1"
      Say "Вы очень добры."
      Say "Контейнер будет ждать вас на складе компонентов."
      GO quest_agreed
    END_OF_OPT
    OPT "Я передумал[g(|a)]. Эта поездка слишком опасна для такого хрупкого товара."
      SET boozeQuestRefused
      RESET boozeQuestTried
      Say "*Вздох* ...Наверное, вы правы. Хорошего дня."
      GO leaving_bar
    END_OF_OPT
  END_OF_MENU

  LABEL quest_agreed
  ResetFocus bbartender
  SET boozeQuestAccepted
  RESET boozeQuestRefused
  Delay 300
  ChangeChar bartender campaign/bartender.png
  Say "Спасибо и хорошего дня."
  GO leaving_bar

  LABEL bar_debt
  IF debtRefused
    ChangeChar bartender campaign/bartender-close.png
    Ask "Вы по поводу долга, не так ли?"
  ELSE
    Say "А. Я, кажется, знаю, кого вы имеете в виду. У нее еще такое запоминающееся имя."
    Say "И вечеринка, которую она здесь устроила, тоже была довольно запоминающейся."
    Say "К сожалению, я также запомнил, сколько денег она потратила."
    ChangeChar bartender campaign/bartender-close.png
    Ask "Полагаю, вас послали разобраться с долгом?"
  END_OF_IF
  MENU
    OPT "Ага. Именно."
      IF_HAS_CREDITS 21
        SET hasEnoughForDebt
      END_OF_IF
      Ask "О, чудно. Речь идет о 21 кредите."
      MENU
        OPT_IFNOT hasEnoughForDebt "Кхм... Я, похоже, забыл[g(|a)] свой бумажник на корабле."
          GO bar_debt_refused
        END_OF_OPT
        OPT_IF hasEnoughForDebt "Вот, пожалуйста"
          RESET mechDebtPending
          Delay 300
          AddCredits -21
          Announce "Ты отдаешь 21 кредит."
          ChangeChar bartender campaign/bartender.png
          ResetFocus bbartender
          Ask "Спасибо вам большое, [g(сэр|мисс)]. Хорошего дня."
          MENU
            OPT "Ага, бывай."
              UnlockAchievement camp_honesty
              GO leaving_bar
            END_OF_OPT
            OPT "Погоди. И это все? Просто «спасибо»?!"
              EXIT_MENU
            END_OF_OPT
          END_OF_MENU
          Ask "Долг погашен. Проблема урегулирована. Все в порядке. А чего вы ожидали?"
          MENU
            OPT "Ну, не знаю... Какой-нибудь награды... Или хотя бы новой миссии..."
              Say "Извините. У меня нет для вас новых миссий."
              Say "Как насчет достижения?"
              UnlockAchievement camp_honesty
              Delay 1500
              Say "Вот. Надеюсь, вас устроит."
              Say "Хорошего дня."
              GO leaving_bar
            END_OF_OPT
          END_OF_MENU
        END_OF_OPT
        OPT_IFNOT debtConfusion "Хм. Она говорила, что речь шла о 20 кредитах."
          SET debtConfusion
          ChangeChar bartender campaign/bartender.png
          Ask "Нет. 21, я абсолютно уверен. У меня даже где-то был счет."
        END_OF_OPT
        OPT "Хм. Похоже, мы говорим о разных механиках. Оставлю-ка я деньги при себе."
          GO bar_debt_refused
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
    OPT "Хм. Похоже, мы говорим о разных механиках. Оставлю-ка я деньги при себе."
      GO bar_debt_refused
    END_OF_OPT
  END_OF_MENU

  LABEL bar_debt_refused
  SET debtRefused
  Delay 500
  Say "О. Понятно."
  ChangeChar bartender campaign/bartender.png
  Say "Что ж. Хорошего дня."
  GO leaving_bar
END_OF_SCRIPT

SCRIPT regulaDlg

  ShowDlgScene
  ShowChar clerk left
  Delay 1200
  ChangeChar clerk campaign/clerk-frown.png
  Delay 500
  IF class3
    Ask "Снова вы? Что теперь?"
    MENU
      OPT "Вообще-то, ничего."
        GO leaveClerk
      END_OF_OPT
      OPT "Как насчет лицензии на корабли класса IV?"
        ChangeChar clerk campaign/clerk.png
        Ask "Такого класса не существует. Как это вам только в голову пришло?"
        MENU
          OPT_IFNOT gtabe "А, например, в Galaxy Trucker: Another Big Expansion..."
            SET gtabe
            Ask "Понятия не имею, о чем вы говорите. Не бывает кораблей IV класса."
          END_OF_OPT
          OPT "И что же мне теперь делать?"
            Say "Это не моя забота. Я не ваша нянька."
            Delay 300
            ChangeChar clerk campaign/clerk-frown.png
            Say "Пойдите спросите у своего инструктора, если не можете думать самостоятельно."
            GO leaveClerk
          END_OF_OPT
        END_OF_MENU
      END_OF_OPT
    END_OF_MENU
  END_OF_IF
  IF class3Asked
    Say "Снова вы? Я же сказал, корабли класса III не для вас."
    ChangeChar clerk campaign/clerk.png
    Ask "Или... может, у вас есть 450 кредитов? И разрешения от двух инопланетных рас?"
    RESET oneApproval
    RESET noApproval
    IF brownLicenseApproval
      IF purpleLicenseApproval
        SET twoApprovals
      ELSE
        SET oneApproval
      END_OF_IF
    ELSE
      IF purpleLicenseApproval
        SET oneApproval
      ELSE
        SET noApproval
      END_OF_IF
    END_OF_IF
    IF_HAS_CREDITS 450
      MENU
        OPT_IF twoApprovals "Конечно, есть. А вы что думали?"
          ChangeChar clerk campaign/clerk.png
          Delay 200
          Say "В самом деле? Позвольте взглянуть..."
          Delay 500
          ChangeChar clerk campaign/clerk-frown.png
          Delay 1500
          ChangeChar clerk campaign/clerk.png
          Delay 300
          Say "Хм... бумаги, похоже, в порядке... и банковский счет выглядит неплохо."
          Delay 300
          Say "Кхм..."
          Delay 300
          Say "Очень хорошо. Позвольте мне поздравить вас с получением престижной лицензии на корабли класса III."
          AddCredits -450
          Announce "Ты отдаешь 450 кредитов. Доступны корабли класса III."
          UnlockAchievement camp_class3
          Delay 500
          SUB_SCRIPT class3_inits
          ResetFocus bclerk
          GO afterUnlock
        END_OF_OPT
        OPT_IF oneApproval "Деньги не проблема. И одно разрешение у меня уже есть."
          Say "Если у вас чего-то нет, то и говорить не о чем."
          Say "А теперь прошу меня извинить... мне нужно работать."
          GO leaveClerk
        END_OF_OPT
        OPT_IF noApproval "Ну... у меня есть деньги."
          Say "Очень рад за вас. Но пока у вас не будет разрешений, здесь вы на них ничего не купите."
          Say "А теперь прошу меня извинить... мне нужно работать."
          GO leaveClerk
        END_OF_OPT
      END_OF_MENU
    ELSE
      MENU
        OPT_IF twoApprovals "Ну... у меня есть бумаги, о которых вы говорили..."
          Say "Бумаги бесполезны, если у вас нет денег."
          Say "А теперь прошу меня извинить... мне нужно работать."
          GO leaveClerk
        END_OF_OPT
        OPT_IF oneApproval "У меня есть разрешение."
          Say "Одно разрешение? И вы решили, что стоит оторвать меня от работы, чтобы сообщить об этом? Серьезно?!"
          GO leaveClerk
        END_OF_OPT
        OPT_IF noApproval "Ну, пока нет. Но я работаю над этим."
          Say "Нет, не работаете. Мало того, вы еще отвлекаете от работы меня!"
          GO leaveClerk
        END_OF_OPT
      END_OF_MENU
    END_OF_IF
  END_OF_IF
  IF class2
    Ask "А, это опять вы. У вас уже есть лицензия на корабли класса II. Что вам еще нужно?"
    MENU
      OPT "Вообще-то, ничего."
        GO leaveClerk
      END_OF_OPT
      OPT "Как насчет лицензии на корабли класса III?"
        Say "Класса III? Нет, не думаю."
        ChangeChar clerk campaign/clerk.png
        Say "У этого класса очень серьезные требования к соискателям."
        Say "Сама лицензия стоит 450 кредитов, плюс необходимы разрешения от двух инопланетных рас."
        Say "При всем уважении, [g(сэр|мадам)], мне кажется, у вас не хватит дипломатического такта, чтобы получить эти разрешения."
        SET class3Asked
        ResetFocus bclerk
        IF purpleUnlocked
          SetFocus bpurple
        END_OF_IF
        IF brownUnlocked
          ShowButton bmerch2
          SetFocus bmerch2
        END_OF_IF
        Say "А теперь прошу меня извинить... мне нужно работать."
        GO leaveClerk
      END_OF_OPT
    END_OF_MENU
  END_OF_IF
  Ask "Что вам нужно?"
  MENU
    OPT "Эм... ничего."
      GO leaveClerk
    END_OF_OPT
    OPT "Я хочу получить лицензию на корабли класса II."
      ResetFocus bclerk
      SetFocusIfCredits bclerk 100
      Ask "Стоимость лицензии – 100 кредитов!"
      RESET tooMuchFor2
      RESET whereToGetSoMuch
      RESET hasFor2
      RESET hasFewer
      IF_HAS_CREDITS 100
        SET hasFor2
      ELSE
        SET tooMuchFor2
      END_OF_IF
      IF_HAS_CREDITS 50
        SET hasFewer
        IF clerkBribe
          IF clerkDiscount
            RESET hasFewer
          END_OF_IF
        END_OF_IF
      END_OF_IF
      MENU
        OPT_IF hasFor2 "Без проблем. Вот деньги. Могу я получить лицензию?"
          Delay 100
          ChangeChar clerk campaign/clerk.png
          Delay 300
          Say "О, у вас действительно есть эти деньги?"
          Say "В таком случае, вот ваша лицензия[g(, сэр.|.)]"
          Delay 300
          AddCredits -100
          Announce "Ты отдаешь 100 кредитов. Доступны корабли класса II."
          UnlockAchievement camp_class2
          Delay 500
          SUB_SCRIPT class2_inits
          HideButton bclerk
          GO afterUnlock
        END_OF_OPT
        OPT_IF tooMuchFor2 "Это слишком дорого!"
          RESET tooMuchFor2
          SET whereToGetSoMuch
          Delay 300
          ChangeChar clerk campaign/clerk.png
          Say "Уверяю вас, стоимость именно такая, какая должна быть."
          Delay 500
          ChangeChar clerk campaign/clerk-frown.png
          Ask "Мы не можем допустить, чтобы на высококачественных кораблях летал всякий сброд."
        END_OF_OPT
        OPT_IF whereToGetSoMuch "И откуда мне взять такие деньжищи?"
          RESET whereToGetSoMuch
          Say "Это ваше дело. Попробуйте заработать. Я, например, зарабатываю деньги тяжким трудом."
          Say "Мне приходится сидеть в этом кабинете по четыре часа в день (со всего одним часовым перерывом не обед), причем целых три дня в неделю!"
          Say "Насколько я знаю, дальнобойщики просто летают туда-сюда, пока судьба не смилостивится и не пошлет им кучу денег."
          Ask "А потом они приносят их все сюда, в обмен на мою подпись."
        END_OF_OPT
        OPT_IF hasFewer "Я дам 50, по рукам?"
          Delay 900
          Ask "Вы пытаетесь меня подкупить?"
          MENU
            OPT_IFNOT clerkDiscount "Нет, я пытаюсь получить скидку."
              Delay 300
              ChangeChar clerk campaign/clerk.png
              Say "[g(Вы на Регуле, молодой человек|Девушка, это Регула)]."
              Say "За последние 200 лет на Регуле не было зафиксировано ни одной скидки."
              Delay 200
              ChangeChar clerk campaign/clerk-frown.png
              Delay 300
              Say "Но если хотите, я могу уточнить, возможно, на ваш случай распространяются какие-нибудь надбавки или дополнительные сборы..."
              Ask "Или вы можете просто заплатить 100 кредитов."
              SET clerkDiscount
              EXIT_MENU
            END_OF_OPT
            OPT_IFNOT clerkBribe "И что будет, если я скажу «да»?"
              Delay 300
              ChangeChar clerk campaign/clerk.png
              Say "Мы же на Регуле, так? Вам нужно будет заполнить форму B-29, Взятки и прочие Нестандартные Вознаграждения."
              Say "Причем я сомневаюсь, что у вас получится заполнить ее правильно."
              Say "А нормативы, регламентирующие взяточничество, довольно строги и сложны."
              Say "Поэтому предлагаю вам просто забыть об этом. Искусство коррупции было доведено до совершенства много веков назад."
              Delay 200
              ChangeChar clerk campaign/clerk-frown.png
              Delay 300
              Ask "Поэтому любителю, вроде вас, лучше просто заплатить 100 кредитов."
              SET clerkBribe
              EXIT_MENU
            END_OF_OPT
            OPT "О как... тогда забудьте все, что я говорил[g(|a)]."
              Ask "Разумеется. Так вы заплатите 100 кредитов?"
              EXIT_MENU
            END_OF_OPT
          END_OF_MENU
        END_OF_OPT
        OPT "Ну, мне нужно все как следует обдумать. До свидания."
          GO leaveClerk
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL afterUnlock
  MENU
    OPT "Хорошо. Я пойду."
      GO leaveClerk
    END_OF_OPT
    OPT "Спасибо, вы очень добры."
      GO leaveClerk
    END_OF_OPT
  END_OF_MENU

  LABEL leaveClerk
  Delay 500
  ChangeChar clerk campaign/clerk-frown.png
  Delay 500
  HideChar clerk
  HideDlgScene
  RETURN
END_OF_SCRIPT

END_OF_SCRIPTS
