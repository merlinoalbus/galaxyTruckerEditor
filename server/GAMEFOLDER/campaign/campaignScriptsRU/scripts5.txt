SCRIPTS

SCRIPT tutorClass3Dlg

  ShowDlgScene
  ShowChar tutor left
  RESET tutorCanAskForRec
  IF clubTalk
    IFNOT recommendationTutor
      SET tutorCanAskForRec
    END_OF_IF
  END_OF_IF
  Delay 500
  IF tutorProgressTold
    IF tutorWhatDoNext
      IF tutorWhatBuyNext
        IFNOT shuttlesRecommended
          IFNOT tutorCanAskForRec
            Say "Ha! It's the newbie! You're doing pretty good."
            GO tutorClass3End
          END_OF_IF
        END_OF_IF
      END_OF_IF
    END_OF_IF
    Ask "Ha! It's the newbie! You're doing pretty good."
    GO tutorClass3MainQuerry
  END_OF_IF
  SET tutorProgressTold
  Say "Ha! It's my newbie!"
  Ask "I don't see you around much anymore. It's like you don't care about trucking between here and the Space Bar."
  MENU
    OPT_IFNOT tutorTooEasy "That route was just too easy."
      Say "Yeah, that's what all the newbies say. Then BLAM! They get obliterated by the first meteor."
      Ask "You can't learn to be an ace trucker overnight."
      SET tutorTooEasy
    END_OF_OPT
    OPT "Yeah, well, I have a class III license now."
      Say "You kidding me?"
      Ask "It takes a long time and a lot of work to get that license."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL tutorClass3MainQuerry
  IF mine3Initialized
    SET tutorWhatDoNext
  END_OF_IF
  IF shuttlesRecommended
    SET tutorWhatBuyNext
  END_OF_IF
  MENU
    OPT_IFNOT tutorLicensionShown "Look. Here's my license. I even have vouchers from two alien species."
      Say "Wow! That's the real thing. I didn't know you had it in you, newbie."
      SET tutorLicensionShown
      SET tutorNoNewbie
    END_OF_OPT
    OPT_IF tutorNoNewbie "Isn't it about time you stopped calling me newbie?"
      Say "No. Even with your class III license, you'll always be my little newbie."
      RESET tutorNoNewbie
    END_OF_OPT
    OPT_IFNOT tutorWhatBuyNext "I'm wondering if there is anything left to buy."
      SET tutorWhatBuyNext
      Say "Not really. You already have the highest license a trucker can get."
      Say "Now you can just sit back and enjoy your money. Spend it on whatever."
      Ask "And, of course, you can take shuttles more often."
      MENU
        OPT "Shuttles? What are you talking about?"
          EXIT_MENU
        END_OF_OPT
      END_OF_MENU
      Ask "You know. Shuttles. I told you about them when you first got here."
      SUB_SCRIPT tutorShuttleSubdlg
    END_OF_OPT
    OPT_IF shuttlesRecommended "I heard that it's possible to travel by shuttle."
      RESET shuttlesRecommended
      Ask "Of course. I told you all about shuttles when you first got here, didn't I?"
      SUB_SCRIPT tutorShuttleSubdlg
    END_OF_OPT
    OPT_IFNOT tutorWhatDoNext "I'm wondering what I'm supposed to do next."
      SET tutorWhatDoNext
      Say "Oh, well, you have to figure that out yourself, newbie. No one can tell you what to do with your life."
      Say "Well ... maybe one person can."
      Say "I've heard there is a wise robotic guru who can help you find your purpose in life. They say he lives on one of the mining asteroids."
      Say "But those are just rumors. What do robots know about life, eh newbie?"
      IF mine3shown
        ShowButton bmine3
        SetFocus bmine3
      END_OF_IF
    END_OF_OPT
    OPT_IF tutorCanAskForRec "I'm planning to join Club Gold really soon."
      Say "Really? That went fast."
      IF_TUTORIAL_SEEN
        Say "I still remember you as the [g(guy|girl)] who was trying to figure out how to attach a cabin to a ship."
      END_OF_IF
      Say "But to tell you the truth, I always suspected you might have potential, newbie."
      Say "Do you need my recommendation to get into Club Gold?"
      MENU
        OPT "Sorry. A recommendation only counts if it comes from a club member."
          Say "Oh. I see. And you think your old instructor couldn't possibly be an elite trucker."
          EXIT_MENU
        END_OF_OPT
        OPT "Are you a member?"
          Say "Of course I am!"
          EXIT_MENU
        END_OF_OPT
      END_OF_MENU
      Say "What do you think I do when there aren't any newbies to teach? Sit around and eat pizza?"
      Say "I even fly in the Transgalactic Trek. I almost won it, once. Competition is a little stiff."
      MENU
        OPT_IFNOT tutorKicksAss "Well, maybe we'll be competing against each other this year."
          Say "Ha! I'd kick your ... I'd beat you, newbie. Beat you soundly."
          SET tutorKicksAss
        END_OF_OPT
        OPT "So ... would you recommend me?"
          Say "Sure, why not. It's been a while since I've trained such a good newbie."
          Delay 500
          Announce "Recommendation from flight instructor obtained."
          Delay 500
          SET recommendationTutor
          SET recommendationTutorUntold
          SUB_SCRIPT recommendationCheck
          EXIT_MENU
        END_OF_OPT
      END_OF_MENU
      RESET tutorCanAskForRec
    END_OF_OPT
    OPT "I just wanted you to see what's become of your student."
      Say "Good work, newbie. You've done well for yourself."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  IF tutorWhatDoNext
    IF tutorWhatBuyNext
      ResetFocus btutor
    END_OF_IF
  END_OF_IF
  IF tutorCanAskForRec
    SetFocus btutor
  END_OF_IF

  LABEL tutorClass3End
  Delay 300
  HideChar tutor
  HideDlgScene
END_OF_SCRIPT

SCRIPT tutorShuttleSubdlg

  MENU
    OPT "No, you didn't!"
      EXIT_MENU
    END_OF_OPT
    OPT "Hm. I don't recall any such conversation."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  Say "Oh come on! I always tell every newbie:"
  Say "And if you have enough spare change, you can take a shuttle between certain stations instead of trucking."
  UnlockShuttles
  SET shuttlesUnlocked
  Delay 500
  CenterMapByNode forge
  Delay 1000
  CenterMapByNode outpost
  Delay 1000
  CenterMapByNode bar
  Delay 1000
  CenterMapByNode regula
  Delay 500
  Say "See the shuttle icons?"
  Say "They mark other stations with public shuttleports."
  MENU
    OPT "Oh. Thanks."
      Say "No problem."
      EXIT_MENU
    END_OF_OPT
    OPT "Hm. I'm pretty sure those icons weren't there before."
      Say "Oh come on! They've been there the whole time."
      Say "They're icons. They can't just appear and disappear on their own."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  Say "Anyway, if you're at a station with a shuttleport, you can buy a ticket to any other shuttleport."
  Say "Just [v(tap|click)] on the destination and say you want to go there. Simple as pie, newbie."
END_OF_SCRIPT

SCRIPT guruDlg

  ShowDlgScene
  ShowChar guru left
  Say "Welcome, traveler."
  Ask "Your journey has been long. To arrive here, you have overcome many obstacles."
  MENU
    OPT "You speak sooth, O wise one."
      Say "Wisdom is but an illusion, like the tears of a songbird trapped in the cage of its own shadow."
      EXIT_MENU
    END_OF_OPT
    OPT "That's right. This better be worth it."
      Say "What is the worth of life without challenges, or of challenges without life? Only the fullness of time can discern whether the journey is the goal or whether the goal is the journey."
      EXIT_MENU
    END_OF_OPT
    OPT "Huh! You have no idea how much work this was."
      Say "No, but I like the way the words sound: Your journey has been long. To arrive here, you have overcome many obstacles."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  Say "And now tell me, traveler, why have you come to my humble chunk of interstellar rock?"
  MENU
    OPT "To drink from the well of your wisdom, O great guru."
      Say "If a thought is a drop in the ocean of knowledge, if inspiration is a light morning dew, if wise advice is a gentle spring rain, what, then, is a high-pressure fire hydrant?"
      EXIT_MENU
    END_OF_OPT
    OPT "I was hoping you could explain the meaning of life."
      Say "But what if the meaning of life is in fact the search for meaning, so that, in the instant you find it, you have lost it forever?"
      EXIT_MENU
    END_OF_OPT
    OPT "I want to know how to win the campaign."
      Say "Hey, did it occur to you that you could find out even if you spoke politely and called me flattering names?"
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  Say "Let us abandon frivolous whims and consider the question more deeply."
  Say "Look within and seek your greatest desire."
  Ask "Be not hasty, for the answer you find will change your life forever."
  MENU
    OPT_IFNOT guruRich "I want to be the richest trucker in the Galaxy."
      Say "Ah, riches. They bring comfort. They give the illusion of wishes fulfilled."
      Say "But riches lead the mind to apathetic unconsciousness. Why walk the thorny path of material wealth when one could achieve the same end with a lobotomy?"
      Ask "No, I don't think you wish to erase your mind. And so I judge that riches are not your true desire."
      SET guruRich
      ADD guruOptions 1
      IF_IS guruOptions 3
        SET guruFinal
      END_OF_IF
    END_OF_OPT
    OPT_IFNOT guruPerfect "I want to be the best trucker in the Galaxy."
      Say "Excellence is a worthy pursuit, but is it a worthy goal?"
      Say "Excellence has not one aspect, but many. Who has the insight to judge and say what is truly best?"
      Say "Only the one who is best is fit to measure the excellence of others."
      Say "And yet how can we find the best without first establishing the measure?"
      Ask "I doubt you wish to spend your life chasing the tail of this paradox. And so I judge that this is not your true desire."
      SET guruPerfect
      ADD guruOptions 1
      IF_IS guruOptions 3
        SET guruFinal
      END_OF_IF
    END_OF_OPT
    OPT_IFNOT guruFamous "I want to be the most famous trucker in the Galaxy."
      Say "Oh, fame. A treacherous thing."
      Say "You can be famous for one outstanding ability ... or for one outstanding flaw."
      Say "Some gain fame for their life's work, while others become famous in an instant."
      Say "The mirror of fame creates grotesque images. The murderer of the famous also gains much fame."
      Say "And now the paths of fate wind and twist. Would you rather die for fame or kill for it?"
      Ask "I see confusion in your eyes. And so I judge that fame is not your true desire."
      SET guruFamous
      ADD guruOptions 1
      IF_IS guruOptions 3
        SET guruFinal
      END_OF_IF
    END_OF_OPT
    OPT_IF guruFinal "Um ... it looks like I'm out of choices."
      Say "Ah, but what do your choices tell you about yourself? Fame ... excellence ... riches ..."
      Say "You seek respect. You seek the respect of those who are respected."
      Say "You need to join Club Gold, the club with the most famous, wealthiest, and best truckers in the Galaxy."
      Say "And your true desire is to earn their respect by winning the Transgalactic Trek, the most prestigious trucking competition."
      Ask "Here, I've a got a Club Gold brochure somewhere ..."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  MENU
    OPT "I thank you, O great guru."
      Say "It has been my pleasure to lead you along the rocky paths of your own thoughts."
      EXIT_MENU
    END_OF_OPT
    OPT "You could have told me that up front."
      Say "Of what use is the search for knowledge if you do not find it yourself?"
      EXIT_MENU
    END_OF_OPT
    OPT "So all this mystical babble is just a recruiting pitch for some club?"
      Say "I will not say that they pay me, traveler. Yet I will not say that they do not pay me."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  Say "Anyway, Club Gold is right here:"
  Delay 500
  ShowNode club
  CenterMapByNode club
  Delay 1000
  ShowPath U3-25a
  Delay 1000
  ResetFocus bmine3
  Say "And yet, to reach Club Gold, you must pass your most difficult test:"
  Ask "To reach me, you had to defeat many other truckers. But now you must discover this: Can you defeat yourself?"
  MENU
    OPT_IFNOT guruRhetoric "I assume that's a rhetorical question."
      Say "Of course. But it is also the question that your mission will answer."
      SET guruRhetoric
    END_OF_OPT
    OPT_IFNOT guruRhetoric "I'm sorry. That just sounds like more mystical babbling."
      Say "Perhaps. But only the future will reveal whether the overturned jug holds red wine, or whether the five-legged cricket will hesitate to leap into the wind."
      SET guruRhetoric
    END_OF_OPT
    OPT "What exactly do you mean?"
      Say "Can you. Defeat. Yourself? Can you build a better ship than you build?"
      Say "And can you earn more money than your alter ego?"
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  HideButton bmine3
  HideChar guru
  HideDlgScene
END_OF_SCRIPT

SCRIPT clubDlg

  ShowDlgScene
  ShowChar chairman left
  Delay 500
  ChangeChar chairman campaign/chair-smile.png
  Delay 500
  IF clubTalk
    Say "Oh! It's [NAME], our up-and-coming trucker. Welcome back."
    IF recommendationsTold
      Ask "So, how many new recommendations do you have?"
    ELSE
      Ask "So, how many recommendations do you have?"
    END_OF_IF
    SET_TO untold 0
    IF recommendationDesignerUntold
      ADD untold 1
    END_OF_IF
    IF recommendationMerchantUntold
      ADD untold 1
    END_OF_IF
    IF recommendationTutorUntold
      ADD untold 1
    END_OF_IF
    IFNOT untold
      MENU
        OPT_IF recommendationsTold "I don't have any new ones."
          Say "Ah, then go out and get them, [g(my friend|my dear)]. You are only a small step away from receiving the great honor of Club Gold membership."
          GO clubEnd
        END_OF_OPT
        OPT_IFNOT recommendationsTold "I don't have any recommendations yet."
          Say "Then go out and get them. I know you have it in you."
          GO clubEnd
        END_OF_OPT
      END_OF_MENU
    END_OF_IF
    MENU
      OPT_IF recommendationDesignerUntold "I got a recommendation from the designer at Corp Inc Lab."
        Say "Wonderful. He's an old friend of mine."
        Say "Back in the day, he and I worked together to get the Transgalactic Trek started."
        RESET recommendationDesignerUntold
        ADD untold -1
        ADD recommendationsTold 1
      END_OF_OPT
      OPT_IF recommendationMerchantUntold "I got a recommendation from some businessman."
        ChangeChar chairman campaign/chair.png
        Say "Fine, but do show a little respect. He's a good man, and he has a name, you know."
        ChangeChar chairman campaign/chair-sorry.png
        Say "He's not exactly a trucker, but much of the opulence you see around here is thanks to his generosity."
        Delay 300
        ChangeChar chairman campaign/chair.png
        RESET recommendationMerchantUntold
        ADD untold -1
        ADD recommendationsTold 1
      END_OF_OPT
      OPT_IF recommendationTutorUntold "I got a recommendation from my flight instructor."
        ChangeChar chairman campaign/chair-no.png
        Say "Wait. Among our members we have only one flight instructor."
        ChangeChar chairman campaign/chair-no-smile.png
        Say "You don't mean to say that your flight instructor was ... Ah! But of course!"
        ChangeChar chairman campaign/chair-smile.png
        Say "That explains how you got to be so good so fast."
        RESET recommendationTutorUntold
        ADD untold -1
        ADD recommendationsTold 1
      END_OF_OPT
      OPT_IFNOT untold "And that's it."
        EXIT_MENU
      END_OF_OPT
    END_OF_MENU
  END_OF_IF
  IF_IS recommendationsTold 1
    Say "Well, that's a good start. I'm sure you'll be able to get the other two recommendations."
    GO clubEnd
  END_OF_IF
  IF_IS recommendationsTold 2
    Say "Wonderful! Soon you shall earn the third and I can admit you into Club Gold."
    GO clubEnd
  END_OF_IF
  IF_MIN recommendationsTold 3
    Say "Hm. That makes three recommendations, if I am not mistaken. And that means ..."
    Delay 700
    ChangeChar chairman campaign/chair-welcome.png
    Delay 300
    Say "I welcome you as a member of Club Gold!"
    Delay 600
    ChangeChar chairman campaign/chair-smile.png
    Say "You're just in time, you know."
    Say "This year's Transgalactic Trek is about to start."
    ChangeChar chairman campaign/chair-no-smile.png
    Ask "If you intend to compete, you really should take off for the starting line."
    MENU
      OPT "And where does it start?"
        Say "Why, where does everything start?"
        Delay 300
        ShowButton brace
        SetFocus brace
        CenterMapByNode newbie
        Delay 300
        Say "At Port Newbie."
        Delay 300
        CenterMapByNode club
        ChangeChar chairman campaign/chair-welcome.png
        Say "I shall see you there."
        HideButton bclub
        HideButton btutor
        UnlockShuttles
        GO clubEnd
      END_OF_OPT
    END_OF_MENU
  END_OF_IF
  Ask "Ah! You must be [NAME]. Welcome to the headquarters of Club Gold. I have the honor of being the club's president."
  MENU
    OPT "You've heard of me?"
      ChangeChar chairman campaign/chair-welcome.png
      Say "Of course. We follow the careers of all promising truckers."
      ChangeChar chairman campaign/chair-smile.png
      Say "And your recent exploits have been hard to overlook in any case."
      ChangeChar chairman campaign/chair.png
      IF pirateEscorted
        Say "For example, you brought that robber baron to justice. Brilliant!"
      ELSE
        Say "Your escort missions in alien territory, for example, have been quite impressive."
      END_OF_IF
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  MENU
    OPT_IFNOT chairmanRace "I heard that you sponsor a trucking competition."
      ChangeChar chairman campaign/chair-smile.png
      Say "Yes, the Transgalactic Trek."
      ChangeChar chairman campaign/chair.png
      Ask "Three flights, spanning the Galaxy."
      MENU
        OPT "I'd like to sign up for it."
          Say "Of course you would. So would everyone."
          ChangeChar chairman campaign/chair-no.png
          Say "That's why entry is limited to only those truckers who are members of our club."
          ChangeChar chairman campaign/chair.png
          EXIT_MENU
        END_OF_OPT
      END_OF_MENU
      SET chairmanRace
      IF chairmanMembership
        EXIT_MENU
      END_OF_IF
    END_OF_OPT
    OPT_IFNOT chairmanMembership "I'd like to join your club."
      ChangeChar chairman campaign/chair.png
      Say "Oho! That will not be so simple, [g(my friend|my dear)]. True, you are a good trucker ..."
      ChangeChar chairman campaign/chair-no.png
      Say "But our club does not accept members who are merely good. We take only the best."
      ChangeChar chairman campaign/chair-sorry.png
      Ask "Ask yourself, [g(my friend|my dear)], are you truly among the best in the Galaxy?"
      MENU
        OPT "You better believe it!"
          ChangeChar chairman campaign/chair-welcome.png
          Say "Good answer! Self-confidence is our first requirement. But not our only requirement."
          EXIT_MENU
        END_OF_OPT
        OPT "Probably. Or at least, I must be pretty close."
          ChangeChar chairman campaign/chair.png
          Say "A realistic assessment, I'm sure. But for admittance to our club, that does not suffice."
          EXIT_MENU
        END_OF_OPT
        OPT "Well, I'm above average, at least."
          ChangeChar chairman campaign/chair-welcome.png
          Say "Such modesty. Surely your trial by combat among the aliens of Th'kzw-r showed you what you are truly made of."
          ChangeChar chairman campaign/chair-sorry.png
          Say "But to join our club, even such a test is not sufficient."
          EXIT_MENU
        END_OF_OPT
      END_OF_MENU
      SET chairmanMembership
      IF chairmanRace
        EXIT_MENU
      END_OF_IF
    END_OF_OPT
  END_OF_MENU
  ChangeChar chairman campaign/chair.png
  Say "According to our club charter ..."
  Delay 500
  Say "A new member may be accepted only on the recommendation of three current members."
  ChangeChar chairman campaign/chair-smile.png
  Say "It's quite straightforward, isn't it?"
  Ask "If you gain the respect of three members, you can become one of us."
  MENU
    OPT_IFNOT chairmanDirect "What if I asked you for a recommendation?"
      SET chairmanDirect
      IF chairmanMap
        SET chairmanBye
      END_OF_IF
      ChangeChar chairman campaign/chair-sorry.png
      Ask "Well, then I would probably think the request was bordering on inappropriate."
      MENU
        OPT "Hm. So it's a good thing I didn't ask."
          Delay 200
          ChangeChar chairman campaign/chair.png
          Delay 200
          EXIT_MENU
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
    OPT_IFNOT chairmanMap "So where can I find members of Club Gold?"
      SET chairmanMap
      IF chairmanDirect
        SET chairmanBye
      END_OF_IF
      ChangeChar chairman campaign/chair.png
      Say "Our members can be found in all corners of the Galaxy."
      ChangeChar chairman campaign/chair-smile.png
      Ask "Here. I'll label a few locations for you. Does your map allow me to make notes?"
      MENU
        OPT "No."
          EXIT_MENU
        END_OF_OPT
        OPT "If it does, I don't know about it."
          EXIT_MENU
        END_OF_OPT
        OPT_IF tutorWhatBuyNext "No ... unless that's another thing my instructor forgot to tell me."
          EXIT_MENU
        END_OF_OPT
      END_OF_MENU
      Delay 800
      Say "Ah! It's an older model with exclamation points."
      ChangeChar chairman campaign/chair-no-smile.png
      Say "One moment ..."
      HideChar chairman
      Delay 300
      ShowNode lab
      ShowPath R3-27a
      ShowPath R3-27b
      IF workshopInitialized
        ShowPath R3-26a
        ShowPath R3-26b
      END_OF_IF
      CenterMapByNode lab
      Delay 300
      ShowButton blab
      SetFocus blab
      Delay 500
      CenterMapByNode cyanHome
      Delay 300
      ShowButton bcyanmerch
      SetFocus bcyanmerch
      Delay 500
      CenterMapByNode newbie
      Delay 300
      SetFocus btutor
      Delay 500
      CenterMapByNode club
      Delay 500
      ChangeChar chairman campaign/chair.png
      ShowChar chairman left
      Say "There. I've put exclamation points near all members whose current locations I know."
      SET clubTalk
    END_OF_OPT
    OPT_IF chairmanBye "Well, thanks. See you later."
      Delay 300
      ChangeChar chairman campaign/chair-smile.png
      Delay 300
      Say "I suspect we shall meet again, yes."
      EXIT_MENU
    END_OF_OPT
    OPT_IF chairmanBye "Oh man! I thought I was at the end of the campaign."
      Delay 300
      ChangeChar chairman campaign/chair-smile.png
      Delay 300
      Say "To tell the truth, you are fairly close to the end."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  Delay 300
  ChangeChar chairman campaign/chair-welcome.png
  Delay 300
  Say "Good luck!"
  ResetFocus bclub

  LABEL clubEnd
  Delay 300
  HideChar chairman
  HideDlgScene
END_OF_SCRIPT

SCRIPT merchCyanDlg

  IFNOT wasAtCyan
    ShowButton bcyan
  END_OF_IF
  ShowDlgScene
  ChangeChar merchant campaign/merch-sitting.png
  ShowChar merchant left
  IFNOT clubTalk
    Say "Thanks again for all your help."
    ChangeChar merchant campaign/merch-laughA1.png
    Say "Between you and me, these alien species are a gold mine."
    Say "If they ever talked to each other, they'd discover just what kind of ... deals ... I'm giving them."
    SUB_SCRIPT merchantLaughA
    Say "But that won't happen. They've been ignoring each other for centuries. It's a businessman's dream."
    ChangeChar merchant campaign/merch-inviting.png
    Say "And now that you have my back, I'm not worried about anything."
    Say "It is truly a pleasure doing business with you."
    SET merchCyanTalked
    HideButton bcyanmerch
    GO merchCyanEnd
  END_OF_IF
  IF merchCyanRefused
    Say "Hello! I'm glad you came back!"
    ChangeChar merchant campaign/merch-inviting.png
    Ask "You've changed your mind and you're ready to do business with me, right?"
    MENU
      OPT "Yeah, I guess."
        GO merchCyanAccepted
      END_OF_OPT
      OPT "No way."
        ChangeChar merchant campaign/merch-disappointed.png
        Say "Oh. That's ... too bad."
        GO merchCyanEnd
      END_OF_OPT
    END_OF_MENU
  END_OF_IF
  Ask "Oh, it's you! You don't know how glad I am to see you."
  MENU
    OPT "They told me there's a member of Club Gold on this planet."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  Say "Ah. They were probably thinking of me."
  MENU
    OPT "You? You're an elite trucker?"
      Say "Of course. I'm the best. Didn't you know?"
      SUB_SCRIPT merchantLaughB
      Say "I'm joking of course. But when a man has enough money, he can buy fame, glory, and honor and still have enough left over for dinner at a fancy restaurant."
      EXIT_MENU
    END_OF_OPT
    OPT "And what an honor it is to know such an elite trucker."
      Say "Oh come now. You know I'm not really an elite trucker. I bought my way into the club."
      Say "You see, with enough money, a man can buy fame, glory, and honor and still have enough left over for dinner at a fancy restaurant!"
      SUB_SCRIPT merchantLaughA
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  Say "I suppose you're looking for recommendations so you can join Club Gold, too, huh?"
  ChangeChar merchant campaign/merch-inviting.png
  Say "And I have to say, you could not have picked a more advantageous time to ask."
  Say "You see, I also need a little favor."
  Say "We've done this before, haven't we? It's practically a tradition!"
  SUB_SCRIPT merchantLaughA
  IF merchCyanTalked
    Say "Remember when I told you how profitable it is to do business in this part of the Galaxy?"
    Say "Especially because the aliens don't talk to each other?"
    ChangeChar merchant campaign/merch-disappointed.png
    Say "Well ... they started talking to each other."
  ELSE
    Say "You see, the success of my enterprises was somewhat dependent on the fact that the alien species didn't talk to each other."
    Say "They had no way to find out that certain deals were ... how should I put this? ... not to their best advantage."
    ChangeChar merchant campaign/merch-disappointed.png
    Say "Well ... they started talking to each other."
  END_OF_IF
  Say "Did you know that these cyan aliens have a linguistics expert?"
  Say "To make a long story short, I've been banned from Silence and Tzw ... Trrk ... that purple planet."
  Say "So ... I'm kind of stuck here."
  Say "I tried to escape along an unmarked route ... but you know, I'm not the greatest trucker."
  ChangeChar merchant campaign/merch-sitting.png
  Say "But with your help, I'm sure we can do it!"
  ChangeChar merchant campaign/merch-inviting.png
  Ask "So do we have a deal? Same terms as before ... although technically, this isn't a commercial route."
  MENU
    OPT_IFNOT merchCyanPercent "Hm. I think this is a good time to discuss my percentage."
      SET merchCyanPercent
      Say "Why fix it if it ain't broken?"
      SUB_SCRIPT merchantLaughA
      Say "I still think 25 percent is very advantageous for a trucker in your position."
      Say "And I'll throw in a recommendation to Club Gold."
      ChangeChar merchant campaign/merch-inviting.png
      Ask "Do we have a deal?"
    END_OF_OPT
    OPT "Deal."
      GO merchCyanAccepted
    END_OF_OPT
    OPT "Not right now."
      ChangeChar merchant campaign/merch-disappointed.png
      Say "I understand. But please think it over. I really need your help this time."
      SET merchCyanRefused
      GO merchCyanEnd
    END_OF_OPT
  END_OF_MENU

  LABEL merchCyanAccepted
  ChangeChar merchant campaign/merch-laughA1.png
  Say "Wonderful! I knew I could count on you."
  ChangeChar merchant campaign/merch-sitting.png
  Say "I look forward to another adventure."
  ChangeChar merchant campaign/merch-disappointed.png
  Say "(And to getting off this miserable planet.)"
  ShowPath U3-29a
  HideButton bcyanmerch

  LABEL merchCyanEnd
  HideChar merchant
  HideDlgScene
END_OF_SCRIPT

SCRIPT labDlg

  ShowDlgScene
  ShowChar designer left
  Delay 500
  ChangeChar designer campaign/designer-hi.png
  Delay 700
  ChangeChar designer campaign/designer.png
  Delay 300
  RESET labHello
  IFNOT wasInLab
    Ask "Welcome to the Corporation Incorporated Laboratory."
    SET wasInLab
  ELSE
    Ask "Welcome back to our laboratory."
  END_OF_IF
  IF lab3fullEnd
    SET lab3Finished
    RESET lab3fullEnd
  END_OF_IF
  MENU
    OPT_IFNOT labHello "Hello."
      Say "Hello."
      Delay 300
      ChangeChar designer campaign/designer.png
      SET labHello
    END_OF_OPT
    OPT_IFNOT labResearch "What does this lab do?"
      Say "Oh, you know. Just basic research."
      Say "Space-time anomalies, time travel, faster-than-light cosmology, biocybernetics ..."
      Say "And a little bit of psychohistory, socioeconomics, and extraterrestrial bryology ..."
      Say "But mostly we study ways to build and install sewer systems."
      SET labResearch
      SET labHello
      SET labCanAskSpeed
      SET labCanAskSound
    END_OF_OPT
    OPT_IF labCanAskSpeed "So can you explain how Corp Inc ships manage to travel such long distances?"
      Say "Oh, that's actually pretty simple. It's 20th-century technology, really."
      ChangeChar designer campaign/designer-expl.png
      Say "Have you ever heard of Hollywood?"
      Say "It was the most famous laboratory of its day, and the leader in the study of plot-sensitive time compression."
      Say "They discovered a way to shorten hours, days, years, even centuries in which nothing important happens."
      ChangeChar designer campaign/designer.png
      Say "Today, all space travel works on this ancient principle."
      Say "Most of a flight is skipped over using quantum plot-time shortcuts, which you experience as a vision of the spaceship against an aesthetically pleasing background of flowing stars."
      Say "But if anything important happens, time is allowed to run normally."
      Say "It's called the adventure card method of space travel."
      Say "But you probably learned all about that in grade school."
      RESET labCanAskSpeed
    END_OF_OPT
    OPT_IF labCanAskSound "Can you tell me why I can hear explosions, meteors, and engines in space?"
      Say "Oh, that took a lot of work. As you know, sound does not travel in a vacuum."
      ChangeChar designer campaign/designer-expl.png
      Say "But our ships looked kind of stupid when they exploded soundlessly."
      Say "Our stock-holders hated it. So Corp Inc invested a lot of money into developing a special particle field that can transmit sound in space."
      ChangeChar designer campaign/designer.png
      Say "Impressive, isn't it? Too bad they had to pull all the funding from the team that was working on superdurable armor."
      SET labHello
      RESET labCanAskSound
    END_OF_OPT
    OPT_IFNOT labBigRace "Have you ever heard of Club Gold or the Transgalactic Trek?"
      SET labBigRace
      GO labRace
    END_OF_OPT
    OPT_IF labRefused "Hey. I'm ready to hear about that mission now."
      Say "Great!"
      RESET labRefused
      GO labQuest1
    END_OF_OPT
    OPT_IF labWorkshopSolved "Okay, the space factory is now fully operational. Tell me about that mission."
      RESET labWorkshopSolved
      Ask "Wonderful! Here's a list of things I need taken to the space factory. What do you think you can handle?"
      GO labQuerry1
    END_OF_OPT
    OPT_IF lab1Failed "That delivery didn't go so well. I'd like to try another."
      RESET lab1Failed
      SET lab1objections
      Ask "No problem. What do you think you can deliver?"
      GO labQuerry1
    END_OF_OPT
    OPT_IF lab2Failed "That delivery didn't work out. I guess I'll try again."
      RESET lab2Failed
      SET lab2objections
      Ask "No problem. Here's the list of things that need to be delivered. Pick one."
      GO labQuerry2
    END_OF_OPT
    OPT_IF lab3Failed "That last delivery didn't work out. Let me try again."
      RESET lab3Failed
      SET lab3FailedOnce
      Ask "No problem. Will you also take 2 crates of beverage for me?"
      GO labQuerry3
    END_OF_OPT
    OPT_IF lab1Success "I had a little trouble. But I managed to make a partial delivery."
      RESET lab1Success
      GO labQuest2
    END_OF_OPT
    OPT_IF lab1FullSuccess "I completed that mission for you."
      RESET lab1FullSuccess
      GO labQuest2
    END_OF_OPT
    OPT_IF lab2Success "Well, I had a little trouble, but I managed to deliver something."
      RESET lab2Success
      GO labQuest3
    END_OF_OPT
    OPT_IF lab2FullSuccess "I completed that second mission for you."
      RESET lab2FullSuccess
      GO labQuest3
    END_OF_OPT
    OPT_IF lab3Success "I had some trouble. But I did manage to deliver something. Again."
      RESET lab3Success
      GO labFinal
    END_OF_OPT
    OPT_IF lab3FullSuccess "Okay. I completed that third ... and final ... mission."
      RESET lab3FullSuccess
      GO labFinal
    END_OF_OPT
    OPT_IF lab3prematureEnd "Can I try making that final delivery?"
      RESET lab3prematureEnd
      Ask "Interesting. Why, if I may ask?"
      MENU
        OPT "It's part of an achievement."
          Say "Oh, that's right. In that case, you probably need 4 different campaign components, don't you?"
          IFNOT lab3tried
            Say "Hm. Maybe you could help me send a gift to the mechanic who's been helping with the experiment."
            GO labQuerry3boozeIntro
          ELSE
            GO labQuerry3
          END_OF_IF
        END_OF_OPT
        OPT "Just for fun."
          Say "Wonderful! Now just one more thing ..."
          Say "Could you deliver a small gift to the mechanic who has been helping me with this experiment?"
          GO labQuerry3boozeIntro
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
    OPT_IF lab3Finished "I'd like to try that final delivery again."
      RESET lab3FailedOnce
      RESET lab3Finished
      IF labAchievement
        Ask "Just for fun? Sure. Will you also take 2 crates of beverage?"
      ELSE
        Ask "Looking for an achievement, huh? In that case, you'll also be wanting 2 crates of beverage, right?"
      END_OF_IF
      GO labQuerry3
    END_OF_OPT
    OPT_IF labHello "Good bye."
      Say "Nice talking with you."
      GO labEnd
    END_OF_OPT
  END_OF_MENU

  LABEL labRace
  ChangeChar designer campaign/designer.png
  Say "What? Of course I've heard of the Transgalactic Trek."
  ChangeChar designer campaign/designer-point.png
  Say "I helped writing the rules for very first competition."
  ChangeChar designer campaign/designer.png
  Say "I don't fly much anymore, but I watch the Transgalactic Trek every year."
  Say "I even designed a board game based on it."
  Delay 400
  ChangeChar designer campaign/designer-game.png
  Delay 200
  Ask "Want to play?"
  MENU
    OPT "Oh, I would. But right now I'm playing the digital version."
      EXIT_MENU
    END_OF_OPT
    OPT "Why would I pretend to be a galaxy trucker when I am one in real life?"
      EXIT_MENU
    END_OF_OPT
    OPT "Hm. Maybe some other time."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  Say "I understand."
  Delay 300
  ChangeChar designer campaign/designer.png
  Delay 300
  Say "It's a shame, really. My board game is a very good simulation of the Transgalactic Trek."
  Say "It has three flights, three ship classes ..."
  ChangeChar designer campaign/designer-point.png
  Say "Even two alien species."
  ChangeChar designer campaign/designer.png
  Say "Unfortunately, we live in an era where people don't play board games very often."
  Delay 500
  ChangeChar designer campaign/designer-down.png
  Delay 300
  Say "I'm thinking of traveling back in time so I can publish the game in the past."
  Delay 400
  Say "Maybe I'll go back to the turn of the millennium. That was a golden era for board games."
  ChangeChar designer campaign/designer-point.png
  Say "Our time-travel technology is becoming quite advanced."
  Delay 500
  ChangeChar designer campaign/designer.png
  Delay 200
  Say "Ah, forgive me. I'm sure you didn't come here because of my game."
  Ask "What can I do for you?"
  MENU
    OPT "Are you a member of Club Gold?"
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  ChangeChar designer campaign/designer-point.png
  Say "Yes. A founding member."
  ChangeChar designer campaign/designer.png
  MENU
    OPT "Can you give me a recommendation?"
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  Say "Hey, look. I like you."
  Say "If I ever made a videogame with a campaign, the main character would be a lot like you."
  Say "But I can't just hand out recommendations. This is an exclusive club."
  ChangeChar designer campaign/designer-expl.png
  Say "How about this? I'm working on a very important experiment right now."
  Ask "If you help me, I'll give you a recommendation."
  MENU
    OPT "Sure. No problem. What do I need to do?"
      ChangeChar designer campaign/designer.png
      Say "That's the spirit!"
      GO labQuest1
    END_OF_OPT
    OPT "Eh, I'm not really interested. Even though I know I'll have to fly the mission eventually."
      Ask "Right. So, we'll end the dialogue here, and the next time you trigger it, I'll give you the option to take the mission."
      MENU
        OPT "Yeah. That sounds good."
          SET labRefused
          GO labEnd
        END_OF_OPT
        OPT "Ah, forget it. Tell me about the mission now."
          Say "Great!"
          GO labQuest1
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL labQuest1
  Say "For this experiment, I need to send some items to the space factory."
  Say "There's a clever mechanic there who's helping me out with this experiment."
  IFNOT workshopInitialized
    Delay 300
    ChangeChar designer campaign/designer-note.png
    Delay 200
    Say "Oh no! I just got a message saying she's not at the space factory yet. She's still on Regula looking for a trucker who can transport workers."
    Delay 200
    ChangeChar designer campaign/designer.png
    Say "I guess I'll have to postpone my experiment. That's too bad."
    Ask "Unless ... do you think you could go to Regula and ask around the spaceport? Maybe you can help her speed things up a bit."
    MENU
      OPT "I'll think about it."
        EXIT_MENU
      END_OF_OPT
    END_OF_MENU
    Say "Good. Thank you. Come back after you've helped her out."
    SET labWorkshopPending
    GO labEnd
  END_OF_IF
  Ask "Here's a list of things I need delivered. Which job would you like? They all pay the same."

  LABEL labQuerry1
  RESET labTotal
  MENU
    OPT "I think I can deliver 3 packages of radioactive isotopes."
      SET labRadio3
      SET_TO labTotal 3
      EXIT_MENU
    END_OF_OPT
    OPT "I'll try delivering 6 bundles of explosives."
      SET labExpl6
      SET_TO labTotal 6
      EXIT_MENU
    END_OF_OPT
    OPT "How about if I deliver the two size-4 ingots?"
      SET labIngots2_4
      SET_TO labTotal 2
      EXIT_MENU
    END_OF_OPT
    OPT_IFNOT lab1objections "Oof! I haven't delivered any of that stuff in a long time."
      SET lab1objections
      Say "Interesting."
      Ask "But what if you had to choose one? What would you say?"
    END_OF_OPT
  END_OF_MENU
  Delay 200
  ChangeChar designer campaign/designer-note.png
  Say "Interesting choice. Let me write that down."
  Delay 200
  ChangeChar designer campaign/designer.png
  SET_TO labQuestLevel 1
  ShowPath U3-26a
  CenterMapByPath U3-26a
  ResetFocus blab
  IFNOT labQuestDetails
    SET labQuestDetails
    Say "The space factory will pay you 25 credits. But only if you deliver the entire shipment."
    IF shuttlesUnlocked
      Say "Once you've made your delivery, come back here. It's a long way. You might want to take a shuttle."
    ELSE
      Ask "Once you've made your delivery, come back here. It's a long way. You might want to take a shuttle."
      MENU
        OPT "A shuttle?"
          Say "Yes. A shuttle. Every trucker takes a shuttle now and then, when [g(he|he ... or she ...)] doesn't feel like trucking."
          EXIT_MENU
        END_OF_OPT
      END_OF_MENU
      MENU
        OPT "I've never heard of such a thing."
          Say "How did you get so far in the campaign without learning about shuttles? You must be a hardcore trucker."
          Say "I think you should talk to your flight instructor. Evidently, he forgot to tell you something."
          SET shuttlesRecommended
          EXIT_MENU
        END_OF_OPT
      END_OF_MENU
    END_OF_IF
    Say "All right. I think that's everything. I suppose I don't need to tell you that you'll find the components in the warehouse."
    GO labEnd
  ELSE
    Say "Okay. Good luck."
    GO labEnd
  END_OF_IF

  LABEL labQuest2
  ChangeChar designer campaign/designer.png
  Say "Wonderful! That will really help with our experiment. But now I've got another job for you:"
  Ask "Here's the list of things that need to be delivered. Pick one."

  LABEL labQuerry2
  RESET labTotal
  MENU
    OPT "Um ... 2 radioactive thingies and 3 size-two ingots."
      SET labRadio2Ingots3_2
      SET_TO labTotal 5
      EXIT_MENU
    END_OF_OPT
    OPT "Well ... 2 size-three ingots and 4 bundles of explosives."
      SET labExpl4Ingots2_3
      SET_TO labTotal 5
      EXIT_MENU
    END_OF_OPT
    OPT "(Oh brother.) Gimme 4 bundles of explosives and 2 packages of isotopes."
      SET labExpl4Radio2
      SET_TO labTotal 6
      EXIT_MENU
    END_OF_OPT
    OPT_IFNOT lab2objections "And I suppose there's no point in trying to back out now?"
      SET lab2objections
      Say "You're catching on."
    END_OF_OPT
  END_OF_MENU
  Delay 200
  ChangeChar designer campaign/designer-note.png
  Say "Interesting choice. Let me write that down."
  Delay 300
  ChangeChar designer campaign/designer.png
  SET_TO labQuestLevel 2
  ShowPath U3-26a
  CenterMapByPath U3-26a
  ResetFocus blab
  Say "As before, you will be paid 25 credits, but only for a complete delivery. Come back here when you are done."
  GO labEnd

  LABEL labQuest3
  Say "Wonderful! Thank you. Our experiment is going really well thanks to you."
  Say "Here's your final delivery:"
  Ask "2 packages of radioactive isotopes, 1 size-five ingot, and 3 bundles of explosives. What do you say?"
  MENU
    OPT "No problem. I'm a trucker."
      Say "Wonderful!"
      GO labQuest3Accepted
    END_OF_OPT
    OPT "I'm not delivering anything until you tell me what this experiment's about."
      Delay 400
      ChangeChar designer campaign/designer-note.png
      Say "Interesting. Let me write that down."
      Delay 200
      ChangeChar designer campaign/designer.png
      Ask "So, are you refusing to make this delivery?"
      MENU
        OPT "Yes. I'm refusing."
          SET lab3prematureEnd
          GO labQuest3Refused
        END_OF_OPT
        OPT "No. I guess I'll try it."
          Say "Interesting ..."
          GO labQuest3Accepted
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
    OPT "I'm not delivering anything unless you give me a recommendation."
      Delay 400
      ChangeChar designer campaign/designer-note.png
      Say "Interesting. Let me write that down."
      Delay 200
      ChangeChar designer campaign/designer.png
      Ask "So, are you refusing to make this delivery?"
      MENU
        OPT "Yes. I'm refusing."
          SET lab3prematureEnd
          GO labQuest3Refused
        END_OF_OPT
        OPT "No, I guess not. I'll give it a try."
          Say "Interesting ..."
          GO labQuest3Accepted
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL labQuest3Accepted
  Say "Now I've got one more request."
  Say "Because this is our final experiment together, I'd like to send the mechanic a little gift."

  LABEL labQuerry3boozeIntro
  IF_MIN totalBooze 2
    Say "I have 2 crates of high-quality beverage that I purchased on Regula. Apparently a trucker shipped them from the Space Bar."
  ELSE
    Say "I have 2 crates of high-quality beverage that I purchased from a somewhat-depressed bartender."
  END_OF_IF
  Ask "Could you deliver them to the mechanic for me?"

  LABEL labQuerry3
  MENU
    OPT "Yeah, sure. I'll take everything plus two crates of beverage."
      SET labExpl3Radio2Ingots1_5booze2
      SET_TO labTotal 6
      EXIT_MENU
    END_OF_OPT
    OPT "I think that's a little bit much. I'll take the rest of it, but not the 2 crates."
      SET labExpl3Radio2Ingots1_5
      SET_TO labTotal 6
      EXIT_MENU
    END_OF_OPT
    OPT_IF lab3FailedOnce "Actually, now that I think about it, I'm fed up with this. Forget the whole thing."
      SET lab3Finished
      IFNOT experimentExplained
        GO labQuest3Refused
      ELSE
        Say "Fair enough."
        GO labEnd
      END_OF_IF
    END_OF_OPT
  END_OF_MENU
  SET lab3tried
  SET_TO labQuestLevel 3
  ShowPath U3-26a
  CenterMapByPath U3-26a
  ResetFocus blab
  IF experimentExplained
    Say "All right. Good luck."
  ELSE
    Delay 300
    ChangeChar designer campaign/designer-note.png
    Say "Interesting. Let me write that down."
    Delay 300
    ChangeChar designer campaign/designer.png
  END_OF_IF
  GO labEnd

  LABEL labQuest3Refused
  ChangeChar designer campaign/designer-hi.png
  Ask "Okay. You have fulfilled your role in this experiment. Thank you very much."
  GO lab3ExperimentExplained

  LABEL labFinal
  SET lab3Finished
  IF experimentExplained
    Say "Great job!"
    GO labEnd
  END_OF_IF
  Ask "Well done! Thank you for your help with this experiment."

  LABEL lab3ExperimentExplained
  SET experimentExplained
  MENU
    OPT "But ... what was the experiment?"
      ChangeChar designer campaign/designer-expl.png
      Say "Oh, didn't I tell you?"
      Say "We're trying to find out what a trucker will do for 25 credits."
      ChangeChar designer campaign/designer.png
      Say "We got a grant from Regula, you know."
      Say "Thank you for your help with this important research."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  MENU
    OPT "I really don't like being jerked around like this."
      EXIT_MENU
    END_OF_OPT
    OPT "Nice experiment."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  Delay 300
  ChangeChar designer campaign/designer-note.png
  Say "Interesting response. Let me write that down."
  Delay 400
  ChangeChar designer campaign/designer.png
  MENU
    OPT "So what about that recommendation?"
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  Delay 300
  ChangeChar designer campaign/designer-point.png
  Delay 200
  Say "Oh. Of course. You've definitely earned it."
  Delay 200
  ChangeChar designer campaign/designer.png
  Delay 300
  Announce "Recommendation from researcher obtained."
  Delay 500
  ResetFocus blab
  SET recommendationDesigner
  SET recommendationDesignerUntold
  SUB_SCRIPT recommendationCheck
  GO labEnd

  LABEL labEnd
  ChangeChar designer campaign/designer.png
  Delay 500
  ChangeChar designer campaign/designer-hi.png
  Delay 900
  ChangeChar designer campaign/designer.png
  Delay 300
  HideChar designer
  HideDlgScene
END_OF_SCRIPT

SCRIPT raceDlg

  ShowDlgScene
  ShowChar chairman left
  Delay 300
  ChangeChar chairman campaign/chair-welcome.png
  Delay 500
  Say "Ah, [NAME]! My newest colleague in Club Gold! Good to see you, [g(my friend|my dear)]."
  ChangeChar chairman campaign/chair-smile.png
  Say "I'm afraid that introductions and formalities will have to wait for now. The Transgalactic Trek is about to begin."
  Say "The competition crosses the Galaxy. From here, all the way to our club headquarters."
  ChangeChar chairman campaign/chair-welcome.png
  Say "Best of luck to you."
  ShowPath U123
  HideButton brace
  HideChar chairman
  HideDlgScene
END_OF_SCRIPT

SCRIPT FinalRaceDlg

  ShowDlgScene
  ADD frRound 1
  IF_IS frRound 3
    GO frRound3
  END_OF_IF
  IF_IS frRound 2
    GO frRound2
  END_OF_IF
  IF_ORDER 0 1
    IF_ORDER 1 2
      ShowChar tutor left
      Say "Hey, nice going. For a newbie."
      Say "I let you have a head start, but now you better watch your back."
      ShowChar richgirl right
      Say "Tch. You two. In the next two flights I will blow you away."
      SayChar tutor "Talk is cheap, lady. We'll see you in space."
      HideChar tutor
      HideChar richgirl
    ELSE
      IF_ORDER 2 2
        ShowChar richgirl left
        Say "Just so you know, holding back in second place is part of my plan."
        Say "It's a long race. No sense in wasting energy early."
        ShowChar tutor right
        Say "Yeah, whatever. Let's see what happens in round two."
        HideChar tutor
        HideChar richgirl
      ELSE
        IF_ORDER 3 2
          ShowChar tutor left
          ShowChar richgirl right
          SayChar tutor "Will you look at that! The newbie and the stripey are in the lead!"
          SayChar richgirl "All part of my plan. I like to attack from behind."
          SayChar tutor "Yeah, whatever. Let's see what happens in round two."
          HideChar tutor
          HideChar richgirl
        END_OF_IF
      END_OF_IF
    END_OF_IF
  ELSE
    IF_ORDER 1 1
      ShowChar tutor left
      Say "Yeah. Great start! Now I just need to extend my lead."
      IF_ORDER 0 2
        Say "You're not doing too bad, either, newbie!"
      END_OF_IF
      IF_ORDER 2 2
        ShowChar richgirl right
        Say "You call that a lead? I'll make that up in round two before you even get out of the warehouse."
        HideChar richgirl
      END_OF_IF
      IF_ORDER 3 2
        ShowChar halfbreed right
        Say "Kh."
        HideChar halfbreed
      END_OF_IF
      HideChar tutor
    ELSE
      IF_ORDER 2 1
        ShowChar richgirl left
        Say "Looks like I'm in front. As usual."
        IF_ORDER 0 2
          ShowChar tutor right
          Say "Don't get overconfident, lady. My newbie is right behind you."
          HideChar tutor
        END_OF_IF
        IF_ORDER 1 2
          ShowChar tutor right
          Say "That was just round 1, lady. We'll see what things look like after round 2."
          SayChar richgirl "And what about round 3? That's what counts. We all know how this is going to end."
          HideChar tutor
        END_OF_IF
        IF_ORDER 3 2
          ShowChar halfbreed right
          Say "Kh. Fwr'z."
          HideChar halfbreed
        END_OF_IF
        HideChar richgirl
      ELSE
        IF_ORDER 3 1
          ShowChar halfbreed left
          Say "...!"
          IF_ORDER 0 2
            ShowChar tutor right
            Say "Nice work, stripey! But my newbie is right on your tentacles."
            HideChar tutor
          END_OF_IF
          IF_ORDER 1 2
            ShowChar tutor right
            Say "Nice work, stripey, but the race isn't over yet."
            HideChar tutor
          END_OF_IF
          IF_ORDER 2 2
            ShowChar richgirl right
            Say "Strong language. Don't get so excited. You need to save something for the later rounds."
            HideChar richgirl
          END_OF_IF
          HideChar halfbreed
        END_OF_IF
      END_OF_IF
    END_OF_IF
  END_OF_IF
  HideDlgScene
  RETURN

  LABEL frRound2
  IF_ORDER 0 1
    IF_ORDER 1 2
      ShowChar tutor left
      Say "You're having a good day, newbie."
      Say "But I'm right behind you. Don't expect me to take it easy on you in the final round."
      ShowChar richgirl right
      Say "Hm. Really. I just let you two have a lead to make the third round interesting."
      SayChar tutor "Ha! We'll see!"
      HideChar tutor
      HideChar richgirl
    ELSE
      IF_ORDER 2 2
        ShowChar richgirl left
        Say "All according to plan. Sit comfortably in second place, then rocket ahead at the finish."
        ShowChar tutor right
        Say "Don't listen to her, newbie! You're trucking great today!"
        HideChar tutor
        HideChar richgirl
      ELSE
        IF_ORDER 3 2
          ShowChar tutor left
          ShowChar richgirl right
          SayChar tutor "My newbie is doing great. And stripey's not too bad, either."
          SayChar richgirl "Well, they're both doing okay, but they won't stand a chance against me in round 3."
          SayChar tutor "Yeah? This I gotta see, lady."
          HideChar tutor
          HideChar richgirl
        END_OF_IF
      END_OF_IF
    END_OF_IF
  ELSE
    IF_ORDER 1 1
      ShowChar tutor left
      Say "Ha! This is my big chance!"
      IF_ORDER 0 2
        Say "I hope you like second place, newbie!"
      END_OF_IF
      IF_ORDER 2 2
        ShowChar richgirl right
        Say "Yes. You might get second place if those two in the back don't get going."
        Say "Well, I let you have a lead to make the third round interesting, but I doubt this will be much of a challenge."
        HideChar richgirl
      END_OF_IF
      IF_ORDER 3 2
        ShowChar halfbreed right
        Say "Kh."
        HideChar halfbreed
      END_OF_IF
      HideChar tutor
    ELSE
      IF_ORDER 2 1
        ShowChar richgirl left
        Say "Hm. I'm winning without even trying. Is that all you guys got?"
        IF_ORDER 0 2
          ShowChar tutor right
          Say "Hey, newbie, you're not too far behind. Go catch her!"
          HideChar tutor
        END_OF_IF
        IF_ORDER 1 2
          ShowChar tutor right
          Say "Don't push me lady. I think you're scared."
          SayChar richgirl "Yes, I'm afraid of being bored to death. Oh well. Let's go fly round three."
          HideChar tutor
        END_OF_IF
        IF_ORDER 3 2
          ShowChar halfbreed right
          Say "Kh. Kh fwr'z."
          HideChar halfbreed
        END_OF_IF
        HideChar richgirl
      ELSE
        IF_ORDER 3 1
          ShowChar halfbreed left
          Say "...!"
          IF_ORDER 0 2
            ShowChar tutor right
            Say "Aw, stripey is getting away. At least my newbie is hanging in there."
            HideChar tutor
            ShowChar richgirl right
            Say "Getting away from you, maybe. I'll pass them both in round three."
            HideChar richgirl
          END_OF_IF
          IF_ORDER 1 2
            ShowChar tutor right
            Say "Maybe so, stripey, but you won't have the last word!"
            HideChar tutor
          END_OF_IF
          IF_ORDER 2 2
            ShowChar richgirl right
            Say "Let's not exaggerate, alien. I'll be red-shifting past you so fast, you won't know what color you are."
            HideChar richgirl
          END_OF_IF
          HideChar halfbreed
        END_OF_IF
      END_OF_IF
    END_OF_IF
  END_OF_IF
  HideDlgScene
  RETURN

  LABEL frRound3
  RESET frHBfirst
  RESET frRGfirst
  SET finalRaceFinished
  IF_ORDER 2 1
    SET frRGfirst
    ChangeChar richgirl campaign/rich-smile.png
    ShowChar richgirl left
    Say "Yesss! I did it!"
    Delay 500
    ChangeChar richgirl campaign/rich-bored.png
    Delay 200
    Say "I mean ... as expected, of course."
    Say "Not bad, guys. Practice for a few more years and you might even become a challenge."
    Delay 200
    ChangeChar richgirl campaign/rich-smile.png
    Delay 600
    ChangeChar richgirl campaign/rich-smile-cheer.png
    Delay 400
    ChangeChar richgirl campaign/rich-smile.png
  ELSE
    IF_ORDER 3 1
      SET frHBfirst
      ShowChar halfbreed left
      Say "... ... !"
      Say "H' khwk!"
      Say "I wins / is the victor / takes first prize!"
    END_OF_IF
  END_OF_IF
  ShowChar tutor right
  IF_ORDER 0 1
    IF_ORDER 1 2
      Say "Congratulations, newbie!"
      Say "We both had good runs, but yours was a little better."
    ELSE
      Say "Congratulations, newbie!"
      Say "Things didn't go so well for me, personally, but you showed us all!"
    END_OF_IF
    SET finalRaceWon
  ELSE
    IF_ORDER 0 2
      IF_ORDER 1 1
        Say "Yeah! I finally won!"
        Say "You did pretty good, too, newbie. But I guess I did a little better."
      ELSE
        Say "Yeah. Nice flying."
        Say "But my newbie did a good job, too. Second place. Not bad!"
      END_OF_IF
    ELSE
      IF_ORDER 1 1
        Say "Yeah! I finally won!"
        Say "Sorry you didn't do as well as you hoped, newbie. But it was still pretty good for your first try."
      ELSE
        IF_ORDER 1 2
          Say "Ahh! I was this close. Oh well. Congratulations."
          Say "Hey, newbie, sorry you didn't do as well as you hoped. But that was still pretty good for your first try."
        ELSE
          Say "Congratulations."
          Say "I guess the newbie and I had a pretty rough time of it."
        END_OF_IF
      END_OF_IF
    END_OF_IF
  END_OF_IF
  IF frHBfirst
    Delay 300
    HideChar halfbreed
    Delay 300
  END_OF_IF
  IFNOT frRGfirst
    ShowChar richgirl left
  END_OF_IF
  IF_ORDER 2 2
    Say "Yes. Congratulations. I thought that this time I'd settle for second place so someone else could see what winning is like."
  END_OF_IF
  IF_ORDER 2 3
    Say "Congratulations. I was just flying for fun this time."
    Say "Actually, I was aiming for last place, but I'm so good that even when I don't try, I still finish ahead of someone."
  END_OF_IF
  IF_ORDER 2 4
    Say "I decided to hold back this year so I could see how the three of you would do without my interference."
    Say "Of course, it turned out just as I expected. Congratulations."
  END_OF_IF
  IFNOT frHBfirst
    HideChar richgirl
    Delay 300
    ShowChar halfbreed left
    Say "K'ngrtl's!"
    Say "..."
  END_OF_IF
  Delay 800
  SayChar tutor "Well, that was a great race."
  SayChar tutor "I guess you're all pretty good truckers. In fact, you're so good that ..."
  SayChar tutor "I'm buying pizza for everyone!"
  Delay 300
  HideChar tutor
  Delay 300
  IFNOT frHBfirst
    HideChar halfbreed
  ELSE
    HideChar richgirl
  END_OF_IF
  HideDlgScene
END_OF_SCRIPT

END_OF_SCRIPTS
