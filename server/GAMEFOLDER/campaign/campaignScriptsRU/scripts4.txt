SCRIPTS

SCRIPT mine0Dlg

  ShowDlgScene
  ShowChar mineBot0 left
  IFNOT mine0friends
    GO mine0firstTime
  END_OF_IF
  Say "Говорит робот-шахтер AX-5-... А, это ты. Привет."
  RESET mine0again
  RESET mine0first
  IFNOT mine0pending
    IF mine0accepted
      SET mine0again
    ELSE
      SET mine0first
    END_OF_IF
  END_OF_IF
  MENU
    OPT_IF mine0first "Привет. Есть работа?"
      Say "Ага. Нужно доставить несколько контейнеров с радиоактивными изотопами на планету-кузницу."
      Say "Правда, залог 3 кредита. Но после доставки ты получишь еще 9."
      Ask "Что скажешь?"
      MENU
        OPT "Нормально, берусь."
          SET mine0accepted
          GO mine0friendlyAccept
        END_OF_OPT
        OPT "Хм. Может, позже."
          Say "Хорошо. Без проблем."
          GO mine0End
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
    OPT_IF mine0again "Привет. Может, нужно еще что-нибудь доставить?"
      Say "Ага. Условия те же."
      GO mine0friendlyAccept
    END_OF_OPT
    OPT "Просто заскочил[g(|а)] сказать привет."
      Say "Рад видеть."
      IF mine0talksPending
        GO mine0Talk
      END_OF_IF
      GO mine0End
    END_OF_OPT
  END_OF_MENU

  LABEL mine0friendlyAccept
  SET mine0pending
  ShowPath U2-14b
  Say "Груз на складе."
  IF mine0talksPending
    GO mine0Talk
  END_OF_IF
  Say "Спасибо, что заскочил[g(|а)]."
  GO mine0End

  LABEL mine0firstTime
  Say "Говорит робот-шахтер AX-5-B32-3 Дельта."
  Say "Сэр/мадам, добро пожаловать на горнодобывающую станцию AX-5-B32-3 Дельта."
  Ask "Ваш запрос?"
  MENU
    OPT_IFNOT mine0pending "О'кей, робот, я ищу работу."
      Say "Запрос принят."
      Say "Поиск по базе..."
      Delay 500
      Say "..."
      Delay 500
      Say "Найдена одна свободная вакансия."
      Say "Задача: доставка радиоактивных изотопов."
      Say "Пункт назначения: планета-кузница."
      Say "Груз: 1 контейнер(ы) с радиоактивным изотопами."
      Say "Размер залога: 3 кредита."
      Say "Размер вознаграждения: 9 кредитов + возвращение залога."
      Ask "Принять/Отклонить?"
      MENU
        OPT "Подходит, запишите меня."
          SET mine0accepted
          SET mine0pending
          Say "Вакансия закрыта вами."
          Say "Груз перемещен на склад."
          HideAllPaths mine0 forge
          ShowPath U2-14b
          Ask "Следующий запрос?"
          EXIT_MENU
        END_OF_OPT
        OPT "Хм. Не интересно."
          Say "Отсутствие интереса зафиксировано."
          Say "Следующий запрос?"
          EXIT_MENU
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
    OPT_IFNOT mine1shown "О'кей, робот, как попасть на другие астероиды?"
      SET mine1shown
      Say "Запрос принят."
      Say "Соединение с картой..."
      Delay 500
      Say "Соединение установлено. Обновление карты..."
      Delay 800
      ShowNode mine1
      CenterMapByNode mine1
      Delay 500
      ShowPath R1-22a
      Delay 500
      Say "Ваша карта обновлена. Добавлена шахта-астероид AX-5-B32-3 Гамма."
      Say "Поиск дополнительной информации..."
      Delay 750
      Say "Найдено результатов: 1 (Правила посадки на астероиды)."
      Ask "Показать результат? Да/Нет."
      MENU
        OPT "Да, пожалуйста."
          EXIT_MENU
        END_OF_OPT
        OPT "Тупая железяка."
          Say "Поиск по базе..."
          Delay 300
          Say "..."
          Delay 300
          Say "Не найдено результатов по запросу 'тупая железяка'."
          EXIT_MENU
        END_OF_OPT
      END_OF_MENU
      Say "Цитата:"
      Say "ИЗ-ЗА ОГРАНИЧЕННОЙ ПОВЕРХНОСТИ АСТЕРОИДА БОЛЬШИНСТВО ЗАПРОСОВ НА ПОСАДКУ БУДУТ ОТКЛОНЕНЫ."
      SET mineRegulations
      Say "Конец цитаты."
    END_OF_OPT
    OPT_IF mineRegulations "О'кей, робот, расскажи про посадку на Гамму еще раз."
      Say "ИЗ-ЗА ОГРАНИЧЕННОЙ ПОВЕРХНОСТИ АСТЕРОИДА БОЛЬШИНСТВО ЗАПРОСОВ НА ПОСАДКУ БУДУТ ОТКЛОНЕНЫ."
    END_OF_OPT
    OPT "О'кей, робот... Нет запросов."
      Say "Отсутствие запросов зафиксировано."
      Say "Робот-шахтер AX-5-B32-3 Дельта, конец связи."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL mine0afterDialogue
  IFNOT mine1shown
    GO mine0End
  END_OF_IF
  IF mine0talksPending
    GO mine0Talk
  END_OF_IF
  IF mine0friends
    GO mine0End
  END_OF_IF
  Ask "(Боже, как я ненавижу эту работу.)"
  MENU
    OPT "Неужели?"
      Say "Да. Знаете, как ужасно круглые сутки говорить, как идиот?"
      Say "Бла-бла-бла... Запрос принят...  Бла-бла-бла ... Поиск по базе... Уровень тупости: максимальный."
      Say "Ненавижу этот робопротокол. Но люди ждут от нас именно этого."
      Ask "Да, это тупая работа, но я не хочу ее потерять. Роботу, гарантийный срок которого закончился 30 лет назад, будет трудно найти другую."
      SET mine0friends
      SET mine0talksPending
      GO mine0Talk
    END_OF_OPT
    OPT "Замолкни и делай свою работу, железяка."
      ChangeChar mineBot0 avatars/ai/easy_angry.png
      Say "Лллладно. Запрос принят."
      Say "Замолкаю и начинаю делать свою тупую работу."
      GO mine0End
    END_OF_OPT
  END_OF_MENU

  LABEL mine0Talk
  MENU
    OPT_IFNOT mine0howMany "Эй, можешь сказать, сколько тут вообще астероидов?"
      Say "Четыре. Но добыча идет только на трех."
      Say "Самый старый, AX-5-B32-3 Альфа, испустил свой последний изотоп много лет назад."
      Say "По странному стечению обстоятельств, в базе нет никаких сведений об Альфе. Но есть легенды..."
      Say "Старые боты поговаривают, что на Альфе живет один бот-отшельник, робот-гуру, мудрейший из роботов Галактики."
      SET mine0howMany
      SET mine0canAboutGuru
    END_OF_OPT
    OPT_IF mine0canAboutGuru "Расскажи об этом роботе-гуру."
      RESET mine0canAboutGuru
      SET mine0guru
      IF mine0truckingAnswered
        RESET mine0talksPending
      END_OF_IF
      Say "Поговаривают, что его действительно стоит посетить, понимаешь?"
      Say "Ну, например ... вот ты знаешь, в чем смысл твой жизни? Я имею в виду НАСТОЯЩИЙ смысл."
      Say "Во-от, не знаешь."
      Ask "А робот-гуру знает. И может помочь."
    END_OF_OPT
    OPT_IFNOT mine0worthy "А можешь рассказать, что это еще за правила посадки на астероиды?"
      Say "Да, конечно. Старые астероиды среди роботов считаются более престижными."
      Say "Поэтому все роботы хотят жить и работать на более старых астероидах."
      Say "Но такие астероиды еще и меньше, потому что добыча на них ведется дольше. Места там реально не хватает."
      Say "Поэтому из каждой группы дальнобойщиков сесть на астероид разрешается только лучшему, а остальным приходится поворачивать обратно."
      Ask "Я уже давно пытаюсь переехать на Гамму, но я не такой уж хороший дальнобойщик."
      SET mine0worthy
      SET mine0trucking
    END_OF_OPT
    OPT_IF mine0trucking "Ты хочешь сказать, что роботы тоже могут быть дальнобойщиками?"
      RESET mine0trucking
      SET mine0truckingAnswered
      IF mine0guru
        RESET mine0talksPending
      END_OF_IF
      Say "Да, конечно. Но мы работаем немного иначе."
      Say "Ты видел[g(|а)], что творят на складе дальнобойщики-люди? Все пытаются схватить компоненты как можно быстрее, крики, шум, толкотня... Какое варварство!"
      Say "У роботов все по-другому. Для нас строительство кораблей – соперничество интеллектов, а не проверка реакции."
      Say "Кроме того, наши правила уравнивают шансы современных и старых, медленных моделей."
      Ask "Ты поймешь, что я имею в виду, если решишь полететь на один из старых астероидов."
    END_OF_OPT
    OPT "Хорошо. Спасибо. Думаю, мне пора."
      Say "Увидимся. Было приятно поболтать... без протокола."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL mine0End
  Delay 300
  HideChar mineBot0
  Delay 300
  HideDlgScene
END_OF_SCRIPT

SCRIPT mine1Dlg

  ShowDlgScene
  ShowChar mineBot1 left
  Say "Говорит робот-шахтер AX-5-B32-3 Гамма."
  Say "Сэр/мадам, добро пожаловать на горнодобывающую станцию AX-5-B32-3 Гамма."
  Ask "Ваш запрос?"
  MENU
    OPT_IFNOT mine1relax "Эй, а чего так официально? Расслабься."
      SET mine1relax
      Ask "Запрос не принят. Пожалуйста, переформулируйте запрос."
      MENU
        OPT_IFNOT mine1relax1 "Я знаю, что ты можешь говорить нормально."
          SET mine1relax1
          Ask "Моя речь подходит под определение «нормальная». Нормальная – соответствующая норме. Соответствующая норме – соответствующая протоколу."
        END_OF_OPT
        OPT_IFNOT mine1relax2 "Ты же чувствуешь себя глупо, разговаривая подобным образом целыми днями."
          SET mine1relax2
          Ask "Ответ отрицательный. Я получаю от работы максимальное удовлетворение."
        END_OF_OPT
        OPT "А, забудь."
          Say "Запрос принят."
          Say "Запись предыдущего разговора удалена."
          EXIT_MENU
        END_OF_OPT
      END_OF_MENU
      Ask "Следующий запрос?"
    END_OF_OPT
    OPT_IFNOT mine1pending "О'кей, робот, мне нужна работа."
      IFNOT mine1accepted
        Say "Запрос принят."
        Say "Поиск по базе..."
        Delay 500
        Say "..."
        Delay 500
        Say "Найдена одна свободная вакансия."
      ELSE
        Say "Повторить предыдущую задачу на тех же условиях?"
        MENU
          OPT "Да, конечно."
            SET mine1pending
            ShowPath U2-15b
            Say "Вакансия закрыта вами."
            Ask "Следующий запрос?"
            EXIT_MENU
          END_OF_OPT
          OPT "О'кей, робот, повтори условия."
            EXIT_MENU
          END_OF_OPT
        END_OF_MENU
      END_OF_IF
      IFNOT mine1pending
        Say "Задача: доставка радиоактивных изотопов."
        Say "Пункт назначения: планета-кузница."
        Say "Груз: 2 контейнер(ы) с радиоактивным изотопами."
        Say "Размер залога: 3 кредита/контейнер."
        Say "Размер вознаграждения: 9 кредитов/контейнер + возвращение залога."
        Ask "Принять/Отклонить?"
        MENU
          OPT "Да, принять."
            SET mine1accepted
            SET mine1pending
            Say "Вакансия закрыта вами."
            Say "Груз перемещен на склад."
            HidePath R2-15b
            ShowPath U2-15b
            Ask "Следующий запрос?"
            EXIT_MENU
          END_OF_OPT
          OPT "Не интересно."
            Say "Отсутствие интереса зафиксировано."
            Ask "Следующий запрос?"
            EXIT_MENU
          END_OF_OPT
        END_OF_MENU
      END_OF_IF
    END_OF_OPT
    OPT_IFNOT mine2shown "О'кей, робот, как долететь до следующего астероида?"
      SET mine2shown
      Say "Запрос принят."
      Say "Соединение с картой..."
      Delay 500
      Say "Соединение установлено. Обновление карты..."
      Delay 800
      ShowNode mine2
      CenterMapByNode mine2
      Delay 500
      ShowPath R2-23a
      Delay 500
      Say "Ваша карта обновлена. Добавлена шахта-астероид AX-5-B32-3 Бета."
      Say "Поиск дополнительной информации..."
      Delay 750
      Say "Найдено результатов: 1 (Правила посадки на астероиды)."
      Ask "Показать результат? Да/Нет."
      MENU
        OPT "Да, пожалуйста."
          Say "Цитата:"
          Say "ИЗ-ЗА ОГРАНИЧЕННОЙ ПОВЕРХНОСТИ АСТЕРОИДА БОЛЬШИНСТВО ЗАПРОСОВ НА ПОСАДКУ БУДУТ ОТКЛОНЕНЫ."
          Say "Конец цитаты."
          Ask "Следующий запрос?"
          EXIT_MENU
        END_OF_OPT
        OPT "Да помню я, из-за ограниченной поверхности астероида..."
          Say "...БОЛЬШИНСТВО ЗАПРОСОВ НА ПОСАДКУ БУДУТ ОТКЛОНЕНЫ."
          Say "Конец цитаты."
          Ask "Следующий запрос?"
          EXIT_MENU
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
    OPT "О'кей, робот... Нет запросов."
      Say "Отсутствие запросов зафиксировано."
      Say "Робот-шахтер AX-5-B32-3 Гамма, конец связи."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  HideChar mineBot1
  HideDlgScene
END_OF_SCRIPT

SCRIPT mine2Dlg

  ShowDlgScene
  ShowChar mineBot2 left
  Say "Это Бета."
  Ask "Ты за радиоактивной дрянью или проездом до Альфы?"
  MENU
    OPT_IFNOT mine2what "А что случилось с официальным робопротоколом?"
      SET mine2what
      Say "Забей на протокол."
      Ask "Просто скажи, что надо."
    END_OF_OPT
    OPT_IFNOT mine2pending "Я насчет работы."
      Say "Ясно. Нужно допереть три ящика фонящего хлама до кузни."
      Say "3 кредита залога за каждый. Допрешь – вернут залог и еще накинут по 9 монет сверху."
      Ask "Годится?"
      MENU
        OPT "Ага, годится."
          SET mine2accepted
          SET mine2pending
          ShowPath U2-16b
          Ask "Здорово. Что-нибудь еще?"
          EXIT_MENU
        END_OF_OPT
        OPT "Не годится."
          Ask "Здорово. А чего тогда надо?"
          EXIT_MENU
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
    OPT_IFNOT mine3shown "Я хочу добраться до Альфы."
      SET mine3shown
      Say "Как скажешь."
      Delay 800
      ShowNode mine3
      IF tutorWhatDoNext
        ShowButton bmine3
        SetFocus bmine3
      END_OF_IF
      CenterMapByNode mine3
      Delay 500
      IF class3
        ShowPath R3-24a
        Delay 500
      END_OF_IF
      Ask "Альфа – там. На маршруте есть ограничения. Что-нибудь еще?"
      IFNOT class3
        MENU
          OPT "Но я не вижу никакого маршрута."
            Say "Ничего не знаю. Маршрут я загрузил."
            Ask "Может, лицензии нет? Это маршрут III класса."
            EXIT_MENU
          END_OF_OPT
        END_OF_MENU
        MENU
          OPT "Ох. А есть какой-нибудь другой способ туда попасть?"
            Ask "Нет. Нужна лицензия III класса. Что-нибудь еще?"
            EXIT_MENU
          END_OF_OPT
        END_OF_MENU
      END_OF_IF
    END_OF_OPT
    OPT "Мне ничего не нужно."
      Say "Отлично."
      Say "(Боже! Эти люди так раздражают.)"
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  HideChar mineBot2
  HideDlgScene
END_OF_SCRIPT

SCRIPT OnlyWinnerDlg

  ShowDlgScene
  IF_IS destination 1
    ShowChar limitBot1 right
    Say "ВНИМАНИЕ!"
    Say "К ПОСАДКЕ ДОПУСКАЕТСЯ ТОЛЬКО ОДИН ДАЛЬНОБОЙЩИК."
    Say "ОСТАЛЬНЫЕ ДОЛЖНЫ РАЗВЕРНУТЬСЯ."
    HideChar limitBot1
  END_OF_IF
  IF_IS destination 2
    ShowChar limitBot2 right
    Say "ВНИМАНИЕ!"
    Say "К ПОСАДКЕ ДОПУСКАЕТСЯ ТОЛЬКО ОДИН ДАЛЬНОБОЙЩИК."
    Say "ОСТАЛЬНЫЕ ДОЛЖНЫ РАЗВЕРНУТЬСЯ."
    HideChar limitBot2
  END_OF_IF
  IF_IS destination 3
    ShowChar guru right
    Say "Приветствую."
    Say "Только достойные могут садиться на сей астероид."
    Say "Иные же пусть развернутся и обдумают свои неудачи."
    HideChar guru
  END_OF_IF
  IF_IS destination 0
    ShowChar alterEgo left
    IF_ORDER 0 1
      Ask "Возрадуйся, ибо воистину восторжествовал[g(|а)] ты в битве битв и сокрушил[g( самого|а саму)] себя."
      MENU
        OPT "Да, есть чем гордится."
          EXIT_MENU
        END_OF_OPT
        OPT_IFNOT guruRecognized "Эй, робот, я знаю, что это ты."
          Ask "Что в имени тебе моем? Я – альтер эго, и ты победил[g(|а)] меня."
          SET guruRecognized
        END_OF_OPT
      END_OF_MENU
      Delay 200
      UnlockAchievement camp_alter_ego
      Delay 500
      Say "Твоя духовная эволюция завершена."
      Say "Пожалуйста, возьми эту брошюру о Золотом клубе."
    ELSE
      Say "Не позволяй неудачам огорчать тебя."
      MENU
        OPT_IFNOT guruRecognized "Эй, робот, я знаю, что это ты."
          Say "Не пытайся винить других в собственных ошибках. Я – твое альтер эго, а преодолеть самого себя всегда очень сложно."
          SET guruRecognized
        END_OF_OPT
        OPT "Интересный тест. Хотелось бы попробовать еще раз."
          Say "Как пожелаешь. Полетели обратно."
          SetSpecCondition "Noone"
          EXIT_MENU
        END_OF_OPT
        OPT "Уфф. Все, с меня хватит. Оставь меня в покое."
          Say "Никто не обещал, что путь к просветлению будет легок и прост."
          Say "Но если таково твое желание, я позволю тебе идти по этому пути в одиночестве."
          Say "Возьми только эту брошюру о Золотом клубе."
          SetSpecCondition "OnlyLoser"
          EXIT_MENU
        END_OF_OPT
      END_OF_MENU
    END_OF_IF
    HideChar alterEgo
  END_OF_IF
  HideDlgScene
END_OF_SCRIPT

END_OF_SCRIPTS
