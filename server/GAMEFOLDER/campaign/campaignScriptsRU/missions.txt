SCRIPTS

MISSION roughMission
  AddOpponent "roughTrucker"
  SetDeckPreparationScript "meteor1Deck"
  SetSpecCondition "bet"
  INIT_BUILD
  BuildingHelpScript 500 "tutorialAdditionalConcepts"
  RESET shouldShowFlightTutorial
  ModifyOpponentsBuildSpeed 75
  SetShipType STI
  SUB_SCRIPT stdParts
  START_BUILDING
  END_BUILDING
  INIT_FLIGHT
  START_FLIGHT
  FlightHelpScript "tutorialAdditionalFlightConcepts"
  EVALUATE_FLIGHT
  ShowDlgScene
  ShowChar roughtrucker left
  Delay 500
  IF wasBet
    IfMissionResultIs 1
      Say "*Вздох*"
      Say "Эти азартные игры меня погубят."
      AddMissionCredits 3
      AddOpponentsCredits 1 -3
      Announce "Ты получаешь 3 кредита от водилы."
      UnlockAchievement camp_bet
    ELSE
      IfMissionResultIs 0
        Say "Я победил. Время платить [g(, братишка|, сестренка)]."
        AddMissionCredits -3
        AddOpponentsCredits 1 3
        Announce "Ты [g(должен|должна)] заплатить водиле 3 кредита."
      ELSE
        Say "О как. Мы оба влипли."
        Say "Полагаю, это значит, что никто не выиграл."
        Say "Может, в следующий раз."
      END_OF_IF
    END_OF_IF
  ELSE
    IfMissionResultIs 1
      Say "Уф. Я рад, что ты [g(решил|решила)] не делать ставки."
    ELSE
      IfMissionResultIs 0
        Say "Ха. Я победил. Жаль, что мы не делали ставок."
      ELSE
        Say "О как. Мы оба влипли."
      END_OF_IF
    END_OF_IF
  END_OF_IF
  Delay 300
  HideChar roughtrucker
  Delay 200
  HideDlgScene
  END_FLIGHT
  FINISH_MISSION
END_OF_MISSION

SCRIPT purpleSupportInfo

  ShowDlgScene
  ChangeChar ambassador campaign/ambassador-peek.png
  ShowChar ambassador right
  Delay 300
  Say "Пожалуйста, подберите господину К'ввфрг хорошую каюту. И не забудьте присоединить к ней модуль жизнеобеспечения!"
  HideDlgScene
  AddInfoWindow "tutorial/infoPurpleLifeSupport.png"
  ShowInfoWindow "tutorial/infoPurpleLifeSupport.png"
  ShowDlgScene
  ChangeChar ambassador campaign/ambassador-peek.png
  ShowChar ambassador right
  Say "Еще раз благодарю вас и желаю удачного и безопасного пути."
  Delay 300
  HideChar ambassador
  HideDlgScene
END_OF_SCRIPT

SCRIPT purpleAlienInfo

  AddInfoWindow "tutorial/infoPurpleAliens.png"
  ShowInfoWindow "tutorial/infoPurpleAliens.png"
END_OF_SCRIPT

SCRIPT brownInfo

  AddInfoWindow "tutorial/infoBrownLifeSupport.png"
  AddInfoWindow "tutorial/infoBrownAliens.png"
  ShowInfoWindow "tutorial/infoBrownLifeSupport.png"
  SET brownTutorial
END_OF_SCRIPT

MISSION purpleAlienEscort
  SUB_SCRIPT add2RandomOpponentsMedium
  SetDeckPreparationScript "originalDeck"
  SetSpecCondition "purplealien"
  INIT_BUILD
  BuildingHelpScript 1000 "purpleSupportInfo"
  SetAdvPile 1 3
  SetSecretAdvPile 1 3
  SetShipType STI
  SUB_SCRIPT stdParts
  AddPartToAsideSlot alienGun 2 1 2 0
  START_BUILDING
  END_BUILDING
  INIT_FLIGHT
  START_FLIGHT
  AlienHelpScript "purpleAlienInfo"
  EVALUATE_FLIGHT
  IF_MISSION_WON
    ShowDlgScene
    ShowChar purpleGuard left
    Ask "Кзвррр' кх! Хр гхз тррзк! ТРРЗК!"
    IfMissionResultMin 1
      MENU
        OPT "Расслабься, я привез[g(|ла)] вашего фиолетового парня."
          Delay 300
          Say "Кзвррр' кх! Хр гхз тррзк! ТРРЗК!"
          Delay 1000
          ShowChar purpleDipl right
          Delay 500
          Say "Зррг. Х-хрнг кхзз кв Тх'крв-р..."
          Delay 300
          SayChar purpleGuard "Кх гз кврр-г. Зхг."
          Delay 500
          HideChar purpleGuard
          Delay 300
          AskChar purpleDipl "Врвг'гг кврр-г. В'вг зхг-к."
          MENU
            OPT "Итак... мы можем садиться, верно?"
              EXIT_MENU
            END_OF_OPT
          END_OF_MENU
          SayChar purpleDipl "Кззх."
          Delay 300
          HideChar purpleDipl
          EXIT_MENU
        END_OF_OPT
      END_OF_MENU
    ELSE
      MENU
        OPT "Эй, я проделал[g(|а)] такой путь, а ты даже не дашь мне сесть?"
          Say "КХХХ'ВЗ ГХЗ ТРРЗК!"
          EXIT_MENU
        END_OF_OPT
      END_OF_MENU
      MENU
        OPT "Ну да. Кажется, ты зол, потому что я не [g(взял|взяла)] того фиолетового парня, так?"
          Say "ТРРЗК!"
          Delay 1000
          HideChar purpleGuard
          SetMissionAsFailed
          EXIT_MENU
        END_OF_OPT
      END_OF_MENU
    END_OF_IF
    HideDlgScene
  END_OF_IF
  END_FLIGHT
  FINISH_MISSION
END_OF_MISSION

SCRIPT tutorialSideMeteors

  AddInfoWindow "tutorial/infoBigSideMeteor.png"
  ShowInfoWindow "tutorial/infoBigSideMeteor.png"
  SET wasTutorialSideMeteors
END_OF_SCRIPT

MISSION meteorsSide
  AddOpponent richGirl
  IF_PROB 50
    SetDeckPreparationScript "meteor1LeftDeck"
  ELSE
    SetDeckPreparationScript "meteor1RightDeck"
  END_OF_IF
  SetSpecCondition "tilesBet"
  INIT_BUILD
  BuildingHelpScript 500 "tutorialSideMeteors"
  SetShipType STI
  SUB_SCRIPT stdParts
  START_BUILDING
  END_BUILDING
  INIT_FLIGHT
  START_FLIGHT
  EVALUATE_FLIGHT
  IF_MISSION_WON
    ShowDlgScene
    ShowChar richgirl left
    IfMissionResultMin 0
      Delay 300
      IfMissionResultMin 1
        Say "Хм, похоже, что в этот раз тебе повезло больше."
      ELSE
        Say "Хм. Ничья."
      END_OF_IF
      Say "Впечатляет. Ты почти так же хорош[g(|a)], как и я."
      IFNOT designSport1Unlocked
        Say "Вот. Можешь использовать дизайн моего корабля. Ты заслужил[g(|a)]."
        Delay 300
        HideChar richgirl
        Delay 200
        UnlockShipPlan "I-sport"
        SET designSport1Unlocked
        Announce "Доступен новый дизайн корабля класса I."
        Delay 500
        SetAchievementProgress camp_design_sporty 1
        Delay 500
        HidePath "U1-4a"
        ShowPath "R1-4a1"
        IF class2
          HideButton bvip
          HideButton bvip2
          ShowButton brichgirl2
        END_OF_IF
      END_OF_IF
    ELSE
      Delay 300
      Say "Похоже, я победила."
      Say "На самом деле, ничего удивительного. Надеюсь, это послужит тебе уроком."
      Delay 500
      HideChar richgirl
    END_OF_IF
    HideDlgScene
  END_OF_IF
  END_FLIGHT
  FINISH_MISSION
END_OF_MISSION

MISSION meteorsSide2
  AddOpponent richGirl
  SetDeckPreparationScript "meteor2SidesDeck"
  SetSpecCondition "tilesBet"
  INIT_BUILD
  SetShipType STII
  SUB_SCRIPT stdParts
  START_BUILDING
  END_BUILDING
  INIT_FLIGHT
  START_FLIGHT
  EVALUATE_FLIGHT
  IF_MISSION_WON
    ShowDlgScene
    ShowChar richgirl left
    IfMissionResultMin 0
      Delay 300
      IfMissionResultMin 1
        Say "Невероятно. Как кому-то может настолько везти?"
      ELSE
        Say "Неужели ничья?"
      END_OF_IF
      Say "А ты довольно хорош[g(|a)] для обычного дальнобойщика."
      IFNOT designSport2Unlocked
        Say "Вот. Можешь использовать и этот дизайн. Уверена, он попадает в хорошие руки."
        Delay 300
        HideChar richgirl
        Delay 200
        UnlockShipPlan "II-sport"
        SET designSport2Unlocked
        Announce "Доступен новый дизайн корабля класса II."
        Delay 500
        SetAchievementProgress camp_design_sporty 2
        Delay 500
      END_OF_IF
    ELSE
      Delay 300
      Say "Похоже, я победила."
      Say "Понятно, ведь тебе не могло повезти дважды. Но ты хотя бы [g(попытался|попыталась)]."
      Delay 500
    END_OF_IF
    HideChar richgirl
    HideDlgScene
  END_OF_IF
  END_FLIGHT
  FINISH_MISSION
END_OF_MISSION

MISSION meteorsSideWithBot
  AddOpponent medium_safe
  IF_PROB 50
    SetDeckPreparationScript "meteor1LeftDeck"
  ELSE
    SetDeckPreparationScript "meteor1RightDeck"
  END_OF_IF
  INIT_BUILD
  SetShipType STI
  SUB_SCRIPT stdParts
  START_BUILDING
  END_BUILDING
  INIT_FLIGHT
  START_FLIGHT
  EVALUATE_FLIGHT
  ShowDlgScene
  ShowChar securitybot2 left
  Delay 300
  IF_MISSION_WON
    Say "ЧТО Ж, У ВАС ПОЛУЧИЛОСЬ. К СЧАСТЬЮ."
  ELSE
    Say "А Я ПРЕДУПРЕЖДАЛ."
  END_OF_IF
  Delay 300
  HideChar securitybot2
  Delay 300
  HideDlgScene
  END_FLIGHT
  FINISH_MISSION
END_OF_MISSION

SCRIPT tutorialBooze

  ShowDlgScene
  ShowChar bartender left
  Say "Груз готов."
  Say "Разумеется, я хорошо заплачу, если ты доставишь его в целости и сохранности. Но если что-то разобьется, придется компенсировать."
  Delay 100
  HideDlgScene
  AddInfoWindow "tutorial/infoFragileCargo.png"
  ShowInfoWindow "tutorial/infoFragileCargo.png"
  ShowDlgScene
  ChangeChar bartender campaign/bartender-warehouse.png
  ShowChar bartender left
  Delay 200
  Say "Счастливого пути. И не экономь на экипаже!"
  Say "С некоторыми из моих клиентов происходили несчастные случаи, когда они пытались пролететь по этому маршруту с небольшим экипажем."
  HideChar bartender
  HideDlgScene
END_OF_SCRIPT

SCRIPT bartenderOverIntercom

  ChangeChar bartender campaign/bartender-overIntercom.png
  ShowChar bartender left
  Delay 100
  ChangeChar bartender campaign/bartender-overIntercom1.png
  Delay 100
  ChangeChar bartender campaign/bartender-overIntercom.png
  Delay 100
  ChangeChar bartender campaign/bartender-overIntercom1.png
  Delay 100
  ChangeChar bartender campaign/bartender-overIntercom.png
  Delay 100
  ChangeChar bartender campaign/bartender-overIntercom1.png
  Delay 200
  ChangeChar bartender campaign/bartender-overIntercom.png
  Delay 300
  ChangeChar bartender campaign/bartender-overIntercom1.png
  Delay 300
  ChangeChar bartender campaign/bartender-overIntercom.png
  Delay 300
END_OF_SCRIPT

MISSION booze2
  SUB_SCRIPT add3RandomOpponentsEasy
  SetDeckPreparationScript "boozeDeck"
  SetSpecCondition "booze"
  INIT_BUILD
  ModifyOpponentsBuildSpeed 75
  SetShipType STI
  SUB_SCRIPT stdParts
  AddPartToAsideSlot booze 2 1 2 0
  AddPartToAsideSlot booze 1 2 1 0
  BuildingHelpScript 500 "tutorialBooze"
  START_BUILDING
  END_BUILDING
  INIT_FLIGHT
  START_FLIGHT
  EVALUATE_FLIGHT
  ShowDlgScene
  IF_MISSION_WON
    SUB_SCRIPT bartenderOverIntercom
    IfMissionResultMin 2
      ADD totalBooze 2
      Say "Оба ящика? Удивительно! Плачу по 5 кредитов за каждый."
      AddMissionCredits 10
      Delay 500
      UnlockAchievement camp_booze
      Delay 500
    ELSE
      IfMissionResultMin 1
        ADD totalBooze 1
        Say "Ты разбил[g(|a)] ящик? Что ж, выгружай второй, и мы в расчете. 5 кредитов за доставку, 5 – компенсация за разбитый груз, все честно."
      ELSE
        Say "Ты все разбил[g(|a)]? Ну, мы же договорились о компенсации. 5 кредитов за ящик, ничего личного."
        AddMissionCredits -10
      END_OF_IF
    END_OF_IF
  ELSE
    ChangeChar bartender campaign/bartender-warehouse.png
    ShowChar bartender left
    Delay 300
    IfMissionResultMin 2
      Say "Снова ты? Что ж, по крайней мере, ты не разбил[g(|a)] мои ящики."
    ELSE
      IfMissionResultMin 1
        Say "Что? Опять ты? Да еще с разбитым ящиком? Придется заплатить. 5 кредитов."
        AddMissionCredits -5
      ELSE
        Say "Что ты наделал[g(|a)]? От тебя одни убытки. Гони 10 кредитов."
        AddMissionCredits -10
      END_OF_IF
    END_OF_IF
  END_OF_IF
  HideChar bartender
  HideDlgScene
  SET boozeQuestTried
  HidePath R1-3a2
  ShowPath R1-3a
  END_FLIGHT
  FINISH_MISSION
END_OF_MISSION

MISSION booze1
  SUB_SCRIPT add3RandomOpponentsEasy
  SetDeckPreparationScript "boozeDeck"
  SetSpecCondition "booze"
  INIT_BUILD
  ModifyOpponentsBuildSpeed 75
  SetShipType STI
  SUB_SCRIPT stdParts
  AddPartToAsideSlot booze 2 1 2 0
  BuildingHelpScript 500 "tutorialBooze"
  START_BUILDING
  END_BUILDING
  INIT_FLIGHT
  START_FLIGHT
  EVALUATE_FLIGHT
  ShowDlgScene
  IF_MISSION_WON
    SUB_SCRIPT bartenderOverIntercom
    IfMissionResultMin 1
      ADD totalBooze 1
      Say "Груз доставлен? Чудно. Перевожу 5 кредитов."
      AddMissionCredits 5
    ELSE
      Say "Ты разбил[g(|a)] груз? Что ж, значит, с тебя 5 кредитов."
      AddMissionCredits -5
    END_OF_IF
  ELSE
    ChangeChar bartender campaign/bartender-warehouse.png
    ShowChar bartender left
    Delay 300
    IfMissionResultMin 1
      Say "Опять ты? Что ж, по крайней мере, мой ящик цел."
    ELSE
      Say "Что ты сделал[g(|a)] с грузом? С тебя 5 кредитов."
      AddMissionCredits -5
    END_OF_IF
  END_OF_IF
  HideChar bartender
  HideDlgScene
  SET boozeQuestTried
  HidePath R1-3a1
  ShowPath R1-3a
  END_FLIGHT
  FINISH_MISSION
END_OF_MISSION

MISSION nobooze
  SUB_SCRIPT add3RandomOpponentsEasy
  SetDeckPreparationScript "boozeDeck"
  INIT_BUILD
  SetShipType STI
  SUB_SCRIPT stdParts
  START_BUILDING
  END_BUILDING
  INIT_FLIGHT
  START_FLIGHT
  EVALUATE_FLIGHT
  END_FLIGHT
  FINISH_MISSION
END_OF_MISSION

SCRIPT tutorialAdditionalFlightConcepts

  IFNOT shouldShowFlightTutorial
    RETURN
  END_OF_IF
  AddInfoWindow "tutorial/infoShipInfo.png"
  AddInfoWindow "tutorial/infoGivingUpFlight.png"
  AddInfoWindow "tutorial/infoGivingUpFlight2.png"
  ShowInfoWindow "tutorial/infoShipInfo.png"
END_OF_SCRIPT

SCRIPT tutorialAdditionalConcepts

  IF tutorialAdditional
    RETURN
  END_OF_IF
  IF_TUTORIAL_SEEN
    SET showAdditional
  ELSE
    IF tutorialRefused
      RESET showAdditional
    ELSE
      SET showAdditional
    END_OF_IF
  END_OF_IF
  IFNOT showAdditional
    RETURN
  END_OF_IF
  SET tutorialAdditional
  SET shouldShowFlightTutorial
  ShowDlgScene
  ShowChar tutor right
  Delay 300
  Say "Эй, новичок, вот еще кое-что о строительстве."
  Say "Профессор, запускай слайды."
  HideChar tutor
  HideDlgScene
  AddInfoWindow "tutorial/infoAsideSlots.png"
  AddInfoWindow "tutorial/infoLookingAtCards.png"
  ShowInfoWindow "tutorial/infoAsideSlots.png"
  ShowDlgScene
  ShowChar tutor right
  Delay 300
  Say "Хм. Похоже, тебе интересно узнать, где же найти те карты, на которые можно посмотреть."
  Say "Они появятся в верхней части экрана после того, как ты присоединишь к кораблю хотя бы один компонент."
  Say "Ну, давай, удачи."
  HideChar tutor
  HideDlgScene
END_OF_SCRIPT

MISSION firstMission
  SUB_SCRIPT add2RandomOpponentsEasy
  SetDeckPreparationScript "firstMissionDeck"
  INIT_BUILD
  BuildingHelpScript 500 "tutorialAdditionalConcepts"
  RESET shouldShowFlightTutorial
  ModifyOpponentsBuildSpeed 75
  SetShipType STI
  SUB_SCRIPT stdParts
  START_BUILDING
  END_BUILDING
  INIT_FLIGHT
  START_FLIGHT
  FlightHelpScript "tutorialAdditionalFlightConcepts"
  EVALUATE_FLIGHT
  END_FLIGHT
  FINISH_MISSION
END_OF_MISSION

SCRIPT merchantGivesUp

  IF merchForcedFail
    RETURN
  END_OF_IF
  ShowDlgScene
  ChangeChar merchant campaign/merch-disappointed.png
  ShowChar merchant left
  Say "Получилось не так хорошо, как я рассчитывал."
  Say "Ну, ничего. Давайте вернемся. Попробуем еще раз как-нибудь потом."
  SetMissionAsFailed
  SET merchForcedFail
  HideChar merchant
  HideDlgScene
END_OF_SCRIPT

MISSION merchantProtection
  AddOpponent merchant
  SetDeckPreparationScript "merchantDeck"
  RESET merchForcedFail
  INIT_BUILD
  SetAdvPile 1 3
  SetSecretAdvPile 1 3
  SetShipType STI
  SUB_SCRIPT stdParts
  SetSpecCondition "merchantProtect"
  START_BUILDING
  AddPartToShip 1 7 alienEngine 3333 0
  END_BUILDING
  INIT_FLIGHT
  START_FLIGHT
  EVALUATE_FLIGHT
  IF_MISSION_WON
    IfMissionResultMin 3
      ShowDlgScene
      ShowChar merchant left
      Say "О, да! У нас получилось. Мы живы, да еще и с кучей товаров впридачу."
      Say "С вами приятно иметь дело."
      Say "Вот ваша доля: [missionResult]$."
      AddMissionCreditsByResult
      SubOpponentCreditsByResult
      HideButton bmerchant
      Delay 500
      SetAchievementProgress camp_merchant 1
      Delay 300
      HidePath U1-9a
      ShowPath R1-9a
      ShowPath R1-9b
    ELSE
      ShowDlgScene
      ShowChar merchant left
      Say "Ну, мы это сделали. Но товаров получилось не так много."
      Say "Садиться нет смысла – выручка не покроет даже портовых сборов."
      Say "Давайте вернемся назад и попробуем в другой раз, ладно?"
      AllShipsGiveUp
      SET merchantFailed
      HidePath U1-9a
      SetFocus bmerchant
    END_OF_IF
    HideChar merchant
    HideDlgScene
  ELSE
    SET merchantFailed
    HidePath U1-9a
    SetFocus bmerchant
    IFNOT merchForcedFail
      ShowDlgScene
      ChangeChar merchant campaign/merch-disappointed.png
      ShowChar merchant left
      Say "Вы бросаете меня? Но я не хочу лететь остаток пути в одиночестве."
      Say "Лучше я вернусь с вами. Может, попробуем в следующий раз."
      AllShipsGiveUp
      HideChar merchant
      HideDlgScene
    END_OF_IF
  END_OF_IF
  END_FLIGHT
  FINISH_MISSION
END_OF_MISSION

MISSION merchantProtection2
  AddOpponent merchant
  SetDeckPreparationScript "merchantDeck2"
  RESET merchForcedFail
  INIT_BUILD
  SetAdvPile 1 1
  SetAdvPile 2 3
  SetSecretAdvPile 1 1
  SetSecretAdvPile 2 3
  SetShipType STII
  SUB_SCRIPT stdParts
  SetSpecCondition "merchantProtect"
  IF brownUnlocked
    IFNOT brownTutorial
      BuildingHelpScript 500 brownInfo
    END_OF_IF
  END_OF_IF
  START_BUILDING
  AddPartToShip 1 17 alienEngine 3333 0
  END_BUILDING
  INIT_FLIGHT
  START_FLIGHT
  EVALUATE_FLIGHT
  IF_MISSION_WON
    IfMissionResultMin 5
      ShowDlgScene
      ShowChar merchant left
      Say "Да! Мы снова сделали это, и снова у нас на борту куча товара."
      Say "С вами по-прежнему приятно иметь дело."
      Say "Вот ваша доля: [missionResult]$."
      AddMissionCreditsByResult
      SubOpponentCreditsByResult
      Delay 500
      SetAchievementProgress camp_merchant 2
      Delay 300
      HidePath U2-19a
      ShowPath R2-19a
      ShowPath R2-19b
      Say "О... а вот и разрешение от коричневых парней."
      Say "Оказывается, все это время оно лежало у меня в кармане."
      SUB_SCRIPT merchantLaughA
      Announce "Получено разрешение на корабли класса III от коричневых инопланетян."
      SET brownLicenseApproval
      SUB_SCRIPT licenseApprovalCheck
      ShowButton bcyanmerch
    ELSE
      ShowDlgScene
      ShowChar merchant left
      Say "Ну, мы это сделали. Но товаров получилось не так много."
      Say "Садиться нет смысла – выручка не покроет даже портовых сборов."
      Say "Давайте вернемся назад и попробуем в другой раз, ладно?"
      AllShipsGiveUp
      SET merchantFailed
    END_OF_IF
    HideChar merchant
    HideDlgScene
  ELSE
    SET merchantFailed
    IFNOT merchForcedFail
      ShowDlgScene
      ChangeChar merchant campaign/merch-disappointed.png
      ShowChar merchant left
      Say "Ты бросаешь меня? Я не полечу остаток маршрута в одиночестве."
      Say "Я лучше вернусь с тобой. Может быть, попробуем в следующий раз."
      AllShipsGiveUp
      HideChar merchant
      HideDlgScene
    END_OF_IF
  END_OF_IF
  END_FLIGHT
  FINISH_MISSION
END_OF_MISSION

MISSION merchantProtection3
  AddOpponent merchant
  SetDeckPreparationScript "merchantDeck3"
  RESET merchForcedFail
  INIT_BUILD
  SetAdvPile 1 1
  SetAdvPile 2 1
  SetAdvPile 3 3
  SetSecretAdvPile 1 1
  SetSecretAdvPile 2 1
  SetSecretAdvPile 3 3
  SetShipType STIII
  SUB_SCRIPT stdParts
  SetSpecCondition "merchantProtect"
  START_BUILDING
  AddPartToShip 1 22 alienEngine 3333 0
  END_BUILDING
  INIT_FLIGHT
  START_FLIGHT
  EVALUATE_FLIGHT
  IF_MISSION_WON
    ShowDlgScene
    ShowChar merchant left
    Say "О, да! Мы выбрались с территории инопланетян!"
    IfMissionResultIs 0
      Say "Я, правда, без груза, но кому какое дело? По крайней мере, мы живы."
    ELSE
      IfMissionResultMin 8
        Say "И что еще лучше, у меня куча товаров на продажу!"
      ELSE
        IfMissionResultMin 4
          Say "И у меня есть довольно много товаров на продажу!"
        ELSE
          Say "И у меня даже есть немного товаров на продажу."
        END_OF_IF
      END_OF_IF
      Say "Вот твоя доля: [missionResult]$."
      AddMissionCreditsByResult
      SubOpponentCreditsByResult
    END_OF_IF
    Delay 500
    SetAchievementProgress camp_merchant 3
    Delay 300
    HidePath U3-29a
    ChangeChar merchant campaign/merch-inviting.png
    Say "Вот мои рекомендации. Никто не заслуживает их больше, чем ты."
    SET recommendationMerchant
    SET recommendationMerchantUntold
    Announce "Получены рекомендации от коммерсанта."
    SUB_SCRIPT recommendationCheck
    Say "Спасибо за все. Это было настоящее приключение!"
    SUB_SCRIPT merchantLaughB
    Say "Береги себя."
    HideChar merchant
    HideDlgScene
  ELSE
    SET merchant3Failed
    IFNOT merchForcedFail
      ShowDlgScene
      ChangeChar merchant campaign/merch-disappointed.png
      ShowChar merchant left
      Say "Ты бросаешь меня? Я не полечу остаток маршрута в одиночестве."
      Say "Я лучше вернусь с тобой. Может быть, попробуем сбежать в следующий раз."
      AllShipsGiveUp
      HideChar merchant
      HideDlgScene
    END_OF_IF
  END_OF_IF
  END_FLIGHT
  FINISH_MISSION
END_OF_MISSION

MISSION alienPlanets
  IF_PROB 50
    AddOpponent purple2
  ELSE
    AddOpponent brown2
  END_OF_IF
  IF_PROB 75
    IF_PROB 50
      AddOpponent purple2
    ELSE
      AddOpponent brown2
    END_OF_IF
    IF_PROB 50
      IF_PROB 50
        AddOpponent purple2
      ELSE
        AddOpponent brown2
      END_OF_IF
    END_OF_IF
  END_OF_IF
  SetDeckPreparationScript "digitalDeck"
  INIT_BUILD
  SetShipType STI
  SUB_SCRIPT stdParts
  IF brownUnlocked
    IFNOT brownTutorial
      BuildingHelpScript 500 brownInfo
    END_OF_IF
  END_OF_IF
  START_BUILDING
  END_BUILDING
  INIT_FLIGHT
  START_FLIGHT
  EVALUATE_FLIGHT
  END_FLIGHT
  FINISH_MISSION
END_OF_MISSION

MISSION alienPlanets2brown
  IF_PROB 50
    AddOpponent cyan2
  ELSE
    AddOpponent brown2
  END_OF_IF
  IF_PROB 75
    IF_PROB 50
      AddOpponent cyan2
    ELSE
      AddOpponent brown2
    END_OF_IF
    IF_PROB 50
      IF_PROB 50
        AddOpponent cyan2
      ELSE
        AddOpponent brown2
      END_OF_IF
    END_OF_IF
  END_OF_IF
  SetDeckPreparationScript "digitalDeck"
  INIT_BUILD
  SetShipType STII
  SUB_SCRIPT stdParts
  START_BUILDING
  END_BUILDING
  INIT_FLIGHT
  START_FLIGHT
  EVALUATE_FLIGHT
  END_FLIGHT
  FINISH_MISSION
END_OF_MISSION

MISSION alienPlanets2purple
  IF_PROB 50
    AddOpponent cyan2
  ELSE
    AddOpponent purple2
  END_OF_IF
  IF_PROB 75
    IF_PROB 50
      AddOpponent cyan2
    ELSE
      AddOpponent purple2
    END_OF_IF
    IF_PROB 50
      IF_PROB 50
        AddOpponent cyan2
      ELSE
        AddOpponent purple2
      END_OF_IF
    END_OF_IF
  END_OF_IF
  SetDeckPreparationScript "digitalDeck"
  INIT_BUILD
  SetShipType STII
  SUB_SCRIPT stdParts
  START_BUILDING
  END_BUILDING
  INIT_FLIGHT
  START_FLIGHT
  EVALUATE_FLIGHT
  END_FLIGHT
  FINISH_MISSION
END_OF_MISSION

MISSION warriorTest
  AddOpponent purple2
  AddOpponent purple2
  AddOpponent purple2
  SetDeckPreparationScript "battleZones"
  INIT_BUILD
  SetShipType STII
  SUB_SCRIPT stdParts
  START_BUILDING
  END_BUILDING
  INIT_FLIGHT
  START_FLIGHT
  EVALUATE_FLIGHT
  IF_MISSION_WON
    ShowDlgScene
    ShowChar purple left
    Delay 300
    Say "Зх вррзк-хррг крз'х!"
    Delay 300
    ShowChar purple2 right
    Delay 300
    Say "Гхррк-зхр вв'гх вррзк-хррг крз'х!"
    Delay 300
    Announce "Получено разрешение на корабли класса III от фиолетовых инопланетян."
    SET purpleLicenseApproval
    SUB_SCRIPT licenseApprovalCheck
    HideChar purple2
    Delay 300
    HideChar purple
    HideDlgScene
    HidePath U2-20a
    ShowPath R2-20a
  END_OF_IF
  END_FLIGHT
  FINISH_MISSION
END_OF_MISSION

MISSION noMerchant
  SUB_SCRIPT add2RandomOpponentsMedium
  SetDeckPreparationScript "merchantDeck"
  INIT_BUILD
  SetShipType STI
  SUB_SCRIPT stdParts
  START_BUILDING
  END_BUILDING
  INIT_FLIGHT
  START_FLIGHT
  EVALUATE_FLIGHT
  END_FLIGHT
  FINISH_MISSION
END_OF_MISSION

SCRIPT tutorialEpidemy

  ShowDlgScene
  ShowChar mech right
  Delay 200
  Say "Привет. Это снова я."
  ChangeChar mech campaign/mech-explaining.png
  Say "Я просто слышал, что в последнее время участились случаи инфекционных заболеваний."
  Ask "Не строй каюты вплотную друг к другу, чтобы зараза не могла распространяться, хорошо?"
  MENU
    OPT "Понятно. Не переживай, я буду [g(осторожен|осторожна)]."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  Delay 300
  ChangeChar mech campaign/mech-thumbup.png
  Delay 300
  Say "Хорошо, еще раз спасибо. И удачи!"
  HideChar mech
  HideDlgScene
END_OF_SCRIPT

MISSION peopleDelivery
  SUB_SCRIPT add2RandomOpponentsMedium
  SetDeckPreparationScript "epidemyDeck"
  INIT_BUILD
  IFNOT wasTutorialEpidemy
    SET wasTutorialEpidemy
    BuildingHelpScript 1000 tutorialEpidemy
  END_OF_IF
  SetShipType STI
  SUB_SCRIPT stdParts
  SetSpecCondition "peopleDelivery"
  START_BUILDING
  END_BUILDING
  INIT_FLIGHT
  START_FLIGHT
  EVALUATE_FLIGHT
  IF_MISSION_WON
    ShowDlgScene
    ShowChar mech right
    IFNOT nextPeopleDelivery
      Say "Добро пожаловать на нашу фабрику. Ты [g(привез|привезла)] кого-нибудь для меня?"
    ELSE
      Say "Рада тебя видеть! Ты [g(привез|привезла)] еще людей?"
    END_OF_IF
    IfMissionResultMin 0
      ChangeChar mech campaign/mech-explaining.png
      IfMissionResultMin 10
        Say "Так много? И со всеми все в порядке?! Ты – [g(лучший|лучшая)]!!!"
        Delay 300
        ChangeChar mech campaign/mech-kiss.png
        Delay 300
        Say "*чмок!*"
        Delay 300
        ChangeChar mech campaign/mech.png
        Delay 600
        ChangeChar mech campaign/mech-blush.png
        Delay 300
        Say "Извини. Похоже, я немного увлеклась..."
        UnlockAchievement camp_kiss
        Delay 300
        ChangeChar mech campaign/mech.png
        SET wasKiss
      ELSE
        IfMissionResultMin 8
          Say "Довольно много. И со всеми все в порядке? Ты – [g(лучший|лучшая)]."
          Delay 200
          ChangeChar mech campaign/mech-blush.png
          Delay 200
          IFNOT wasKiss
            Say "Извини... Я чуть тебя не поцеловала. Так неловко."
          ELSE
            Say "Извини. Я, похоже, опять немного увлеклась..."
          END_OF_IF
          Delay 300
          ChangeChar mech campaign/mech.png
        ELSE
          IfMissionResultMin 6
            Say "Хорошая работа. Я рада, что все в порядке."
          ELSE
            Say "Это все? Что ж, по крайней мере, никто не пострадал. Хорошая работа."
          END_OF_IF
        END_OF_IF
      END_OF_IF
      IFNOT nextPeopleDelivery
        Say "Вот [missionResult]$. Спасибо тебе большое."
        AddMissionCreditsByResult
        Say "Когда в следующий раз соберешься сюда, загляни на Регулу и посмотри, может, кого нужно подбросить?"
        Say "Не могу обещать, что заплачу за них, но уверена, они отработают поездку."
        SET nextPeopleDelivery
        HidePath "U1-7a"
        ShowPath "R1-7a"
        ShowPath "R1-7b"
      ELSE
        Say "Спасибо большое. Я знаю, мы не договаривались о вознаграждении..."
        IfMissionResultMin 8
          Say "Но, думаю, смогу выделить тебе 3 кредита. Ты заслужил[g(|a)]."
          AddMissionCredits 3
        ELSE
          IfMissionResultMin 5
            Say "Но вот тебе 2 кредита, за беспокойство."
            AddMissionCredits 2
          ELSE
            Say "Но вот тебе 1 кредит. За попытку..."
            AddMissionCredits 1
          END_OF_IF
        END_OF_IF
        Say "И в следующий раз привози больше людей, хорошо?"
      END_OF_IF
    ELSE
      SET peopleDeliveryFailed
      IfMissionResultMin -2
        Say "Но... это же не все."
      ELSE
        IfMissionResultMin -4
          Say "О, нет. Что случилось с остальными?"
        ELSE
          Say "О, нет. Это похоже на массовое убийство!"
        END_OF_IF
      END_OF_IF
      IFNOT nextPeopleDelivery
        Say "Ужасно!"
        MENU
          OPT "Мне так жаль!"
            EXIT_MENU
          END_OF_OPT
        END_OF_MENU
        ChangeChar mech campaign/mech.png
        Say "Я знаю, ты сделал[g(|a)] все, что можно."
        ChangeChar mech campaign/mech-f.png
        Say "Ну, что тут скажешь. Нужно вернуться на Регулу и найти еще людей."
        ChangeChar mech campaign/mech.png
        Ask "Можно я полечу с тобой?"
        MENU
          OPT "Конечно. Если это поможет..."
            Say "Спасибо."
            SetMissionAsFailed
            EXIT_MENU
          END_OF_OPT
        END_OF_MENU
      ELSE
        Say "Я знаю, ты не хотел[g(|a)], чтобы так вышло."
        Say "Но в следующий раз, прошу, будь осторожнее!"
      END_OF_IF
    END_OF_IF
    HideChar mech
    HideDlgScene
  ELSE
    IFNOT nextPeopleDelivery
      ShowDlgScene
      ChangeChar mech campaign/mech-intercom.png
      ShowChar mech right
      Ask "Что случилось?"
      IfMissionResultMin 0
        MENU
          OPT "Мы поворачиваем назад. Не волнуйся. все в порядке."
            EXIT_MENU
          END_OF_OPT
        END_OF_MENU
      ELSE
        MENU
          OPT "Тебе лучше не знать."
            EXIT_MENU
          END_OF_OPT
        END_OF_MENU
      END_OF_IF
      Say "Ясно. Я поняла, что случилось. Я тоже возвращаюсь на Регулу."
      SET peopleDeliveryFailed
      HideChar mech
      HideDlgScene
    END_OF_IF
  END_OF_IF
  END_FLIGHT
  FINISH_MISSION
END_OF_MISSION

MISSION universalEpidemic
  SUB_SCRIPT addRandomOpponentsMedium
  SetDeckPreparationScript "epidemyDeck"
  INIT_BUILD
  SetShipType STI
  SUB_SCRIPT universalParts
  START_BUILDING
  END_BUILDING
  INIT_FLIGHT
  START_FLIGHT
  EVALUATE_FLIGHT
  END_FLIGHT
  FINISH_MISSION
END_OF_MISSION

MISSION universalSabotages
  SUB_SCRIPT addRandomOpponentsMedium
  SetDeckPreparationScript "boozeDeck"
  INIT_BUILD
  SetShipType STI
  SUB_SCRIPT universalParts
  START_BUILDING
  END_BUILDING
  INIT_FLIGHT
  START_FLIGHT
  EVALUATE_FLIGHT
  END_FLIGHT
  FINISH_MISSION
END_OF_MISSION

MISSION universal3long
  SUB_SCRIPT addRandomOpponentsMedium
  SetDeckPreparationScript "digitalDeck"
  INIT_BUILD
  SetAdvPile 1 1
  SetAdvPile 2 1
  SetAdvPile 3 3
  SetSecretAdvPile 1 1
  SetSecretAdvPile 2 1
  SetSecretAdvPile 3 3
  SetShipType STIII
  SUB_SCRIPT universalParts
  START_BUILDING
  END_BUILDING
  INIT_FLIGHT
  START_FLIGHT
  EVALUATE_FLIGHT
  END_FLIGHT
  FINISH_MISSION
END_OF_MISSION

SCRIPT ingotsIntro

  ShowDlgScene
  ShowChar foreman left
  Delay 750
  IF ingotsFailedDelivery
    ChangeChar foreman campaign/foreman-no.png
    Say "Эй, слышал, в прошлый раз у тебя не вышло."
    ChangeChar foreman campaign/foreman.png
    Say "Не переживай, я дам тебе еще один шанс. Вот такой хороший я парень."
    Say "(К тому же, больше никто не хочет этим заниматься.)"
    RESET ingotsFailedDelivery
  ELSE
    ChangeChar foreman campaign/foreman-whatsup.png
    IF oneingot
      IF ing3
        Say "По рукам. Вот, держи. Идеальный слиток из чистейшей космической стали."
        ChangeChar foreman campaign/foreman-smile.png
        Say "Не забудь прицепить его к кораблю."
      ELSE
        Say "По рукам. Вот, держи. Еще один слиток. Шикарно выглядит, согласись?"
      END_OF_IF
    END_OF_IF
    IF twoingots
      Say "По рукам. Вот, держи. Два отличных слитка. Не поцарапай."
    END_OF_IF
    IF threeingots
      Say "Е-мое! Три слитка. Представляешь, три!"
      ChangeChar foreman campaign/foreman-smile.png
      Say "Мне нужно прикрепить один к твоему кораблю. Надеюсь, ты не против."
    END_OF_IF
  END_OF_IF
  HideDlgScene
  AddInfoWindow "tutorial/infoHeavyCargo.png"
  ShowInfoWindow "tutorial/infoHeavyCargo.png"
  ShowDlgScene
  ShowChar foreman left
  Delay 500
  ChangeChar foreman campaign/foreman-hello.png
  Say "Удачи на дорогах."
  HideChar foreman
  HideDlgScene
END_OF_SCRIPT

MISSION ingotDelivery
  SUB_SCRIPT add2RandomOpponentsMedium
  SetDeckPreparationScript "ingotDeck"
  INIT_BUILD
  SetSpecCondition "ingots"
  BuildingHelpScript 1000 ingotsIntro
  SetShipType STII
  SUB_SCRIPT stdParts
  START_BUILDING
  IF ing2
    AddPartToAsideSlot ingot2 2 1 0 0
  END_OF_IF
  IF ing3
    AddPartToAsideSlot ingot3 3 2 0 0
  END_OF_IF
  IF ing4
    AddPartToAsideSlot ingot4 2 3 2 0
  END_OF_IF
  IF ing5
    AddPartToAsideSlot ingot5 1 3 1 0
  END_OF_IF
  IF ing6
    AddPartToAsideSlot ingot4 1 3 2 3
  END_OF_IF
  IF ing8
    AddPartToAsideSlot ingot4 2 1 2 0
  END_OF_IF
  IF twoingots
    IF ing2
      AddPartToAsideSlot ingot2 3 0 3 0
    END_OF_IF
    IF ing3
      AddPartToAsideSlot ingot3 1 2 1 0
    END_OF_IF
    IF ing4
      AddPartToAsideSlot ingot4  3 0 3 0
    END_OF_IF
  END_OF_IF
  IF threeingots
    IF ing2
      AddPartToAsideSlot ingot2 1 3 2 0
      AddPartToShip 0 17 ingot2 2323 0
    END_OF_IF
    IF ing3
      AddPartToAsideSlot ingot3 3 2 3 0
      AddPartToShip 0 17 ingot3 1212 0
    END_OF_IF
  END_OF_IF
  END_BUILDING
  INIT_FLIGHT
  START_FLIGHT
  EVALUATE_FLIGHT
  RESET allok
  RESET somemissing
  RESET allmissing
  IF_MISSION_WON
    IfMissionResultMin 1
      IfMissionResultMin 2
        IfMissionResultMin 3
          SET allok
        ELSE
          IF threeingots
            SET somemissing
          ELSE
            SET allok
          END_OF_IF
        END_OF_IF
      ELSE
        IF oneingot
          SET allok
        ELSE
          SET allmissing
        END_OF_IF
      END_OF_IF
    END_OF_IF
    ShowDlgScene
    ShowChar mech right
    Delay 500
    IFNOT ingotDeliveryAttempt
      Say "Ты, должно быть, дальнобойщик. Меня зовут... О! Это ты!"
      ChangeChar mech campaign/mech-explaining.png
      Say "Рада встрече. Я здесь заведую приемкой груза."
      SET ingotDeliveryAttempt
    ELSE
      ChangeChar mech campaign/mech-explaining.png
      Say "Привет! Рада встрече."
    END_OF_IF
    IF allok
      ChangeChar mech campaign/mech-thumbup.png
      Say "Ага. Вижу, груз в порядке."
      Say "Это – тебе."
      HideAllPaths forge workshop
      ShowPath R2-13a
      RESET ingotsPending
      SET ingotsFulfilledOnce
      IF oneingot
        IF ing3
          AddMissionCredits 9
          Announce "Ты получаешь 9 кредитов."
          SetAchievementAttempt camp_ingots 9
        END_OF_IF
        IF ing5
          AddMissionCredits 12
          Announce "Ты получаешь 12 кредитов."
          SetAchievementAttempt camp_ingots 12
        END_OF_IF
        IF ing6
          AddMissionCredits 15
          Announce "Ты получаешь 15 кредитов."
          SetAchievementAttempt camp_ingots 15
        END_OF_IF
        IF ing8
          AddMissionCredits 18
          Announce "Ты получаешь 18 кредитов."
          SetAchievementAttempt camp_ingots 18
        END_OF_IF
      END_OF_IF
      IF twoingots
        IF ing2
          AddMissionCredits 12
          Announce "Ты получаешь 12 кредитов."
          SetAchievementAttempt camp_ingots 12
        END_OF_IF
        IF ing3
          AddMissionCredits 18
          Announce "Ты получаешь 18 кредитов."
          SetAchievementAttempt camp_ingots 18
        END_OF_IF
        IF ing4
          AddMissionCredits 22
          Announce "Ты получаешь 22 кредита."
          SetAchievementAttempt camp_ingots 22
        END_OF_IF
      END_OF_IF
      IF threeingots
        IF ing2
          AddMissionCredits 21
          Announce "Ты получаешь 21 кредит."
          SetAchievementAttempt camp_ingots 21
        END_OF_IF
        IF ing3
          AddMissionCredits 30
          Announce "Ты получаешь 30 кредитов."
          SetAchievementProgress camp_ingots 30
        END_OF_IF
      END_OF_IF
    ELSE
      IF somemissing
        ChangeChar mech campaign/mech-f.png
        Say "Хм. Это же не все слитки."
        Say "Извини, но такую доставку я принять не могу."
        ChangeChar mech campaign/mech-explaining-f.png
        Ask "Таковы правила. Прямиком из Регулы, кстати."
      ELSE
        ChangeChar mech campaign/mech-f.png
        Say "Погоди... а где груз?"
        ChangeChar mech campaign/mech-explaining-f.png
        Ask "Слушай, нам действительно нужны эти слитки. Без них производство попросту встанет."
      END_OF_IF
      MENU
        SET ingotsFailedDelivery
        OPT "Понимаю. Я вернусь и попробую снова."
          SetMissionAsFailed
          ChangeChar mech campaign/mech-thumbup.png
          Say "Спасибо. Я ценю это. Я знаю, перевозка слитков – работа не из легких."
          EXIT_MENU
        END_OF_OPT
      END_OF_MENU
    END_OF_IF
    Delay 300
    HideChar mech
    HideDlgScene
  ELSE
    SET ingotsFailed
  END_OF_IF
  END_FLIGHT
  FINISH_MISSION
END_OF_MISSION

END_OF_SCRIPTS
