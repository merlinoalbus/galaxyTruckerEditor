SCRIPTS

SCRIPT vipCheck

  ShowDlgScene
  ShowChar securitybot left
  SET VIPTried
  IF VIPGranted
    Say "OVERUJI V.I.P. STA..."
    Say "AH, TO JSTE ZASE VY!"
    IF richGirlPending
      Say "DAMA VAS OCEKAVA V HANGARU."
      HideChar securitybot
      HideDlgScene
      RETURN
    ELSE
      Say "MUZETE VEJIT."
    END_OF_IF
    Delay 200
    HideChar securitybot
    HideDlgScene
    Delay 1000
    SUB_SCRIPT richGirlDlg
    RETURN
  END_OF_IF
  Say "OVERUJI V.I.P. STATUS ..."
  Delay 750
  Say "V.I.P. STATUS NEPOTVRZEN."
  Say "PRISTUP ZAMITNUT."
  IF purpleAccepted
    Delay 200
    ChangeChar ambassador campaign/ambassador-peek.png
    ShowChar ambassador right
    Say "Ach, to je v pořádku. [g(Tento džentlmen|Tato dáma)] má pro nás velký význam. Můžete [g(jej|ji)] pustit dovnitř."
    IFNOT purpleEscortFinished
      Say "Určitě se nezdrží dlouho, neboť [g(jeho|její)] mise stále není dokončena."
    END_OF_IF
    Delay 200
    HideChar ambassador
    Delay 200
    SayChar securitybot "*CHJO*"
    Say "PRISTUP POVOLEN."
    SET VIPGranted
    Delay 200
    HideChar securitybot
    Delay 1000
    HideDlgScene
    SUB_SCRIPT richGirlDlg
    RETURN
  ELSE
    SET VIPfailed
  END_OF_IF
  HideChar securitybot
  HideDlgScene
END_OF_SCRIPT

SCRIPT richGirlDlg

  ShowDlgScene
  ChangeChar richgirl campaign/rich-bored.png
  ShowChar richgirl left
  Delay 750
  ChangeChar richgirl campaign/rich.png
  Delay 300
  IF richRefused
    Say "Ach, ty jsi přece... No... Mám to na jazyku..."
    MENU
      OPT "[NAME]."
        Say "Jo, [g(ten|ta)]..."
        EXIT_MENU
      END_OF_OPT
    END_OF_MENU
    Ask "Tak co, poletíš se mnou zakázanou trasu na Regulu? Tu s těma meteorama?"
    GO richQuery
  END_OF_IF
  Ask "Ahojky. Kdopak jsi?"
  MENU
    OPT_IFNOT richgirlintro "Prostě [g(nějaký trucker|nějaká truckerka)]."
      Delay 200
      ChangeChar richgirl campaign/rich-bored.png
      Delay 200
      Ask "Ach, tak tedy ahojky, [g(nějaký truckere|nějaká truckerko)]."
      SET richgirlintro
    END_OF_OPT
    OPT_IFNOT richgirlintro "V.I.P. diplomat[g(|ka)], plnící mise nejvyšší důležitosti."
      Delay 200
      ChangeChar richgirl campaign/rich-bored.png
      Delay 200
      Ask "Ach, rozumím, prostě [g(nějaký trucker|nějaká truckerka)]."
      SET richgirlintro
    END_OF_OPT
    OPT "Jmenuji se [NAME]. A ty?"
      ChangeChar richgirl campaign/rich-smile.png
      Delay 300
      Say "Heh, roztomilé: předstíráš, že neznáš nejslavnější truckerku v galaxii."
      Say "Pojď a posaď se, [g(vtipálku|holka)]. A dej si drink."
      Delay 200
      ChangeChar richgirl campaign/rich-smile.png
      Delay 400
      ChangeChar richgirl campaign/rich-smile-cheer.png
      Delay 400
      ChangeChar richgirl campaign/rich-smile.png
      GO richCont
    END_OF_OPT
  END_OF_MENU

  LABEL richCont
  Delay 1000
  ChangeChar richgirl campaign/rich-bored.png
  Delay 500
  Say "Tahle díra je pěkně nudná, nemyslíš?"
  Say "Co takhle něco podniknout?"
  MENU
    OPT "A co přesně máš na mysli?"
      ChangeChar richgirl campaign/rich.png
      Say "Nějaké vzrušení. Co takhle někam letět?"
      Ask "Teď zrovna hlásili, že trasa na Regulu je zavřená. Prý kvůli zvýšenému výskytu meteorických rojů."
      GO richQuery
    END_OF_OPT
  END_OF_MENU

  LABEL richQuery
  MENU
    OPT_IFNOT rgDangerous "A není to nebezpečné?"
      Ask "Jasně že je. Ale kdyby nebylo, tak to není žádná zábava, ne?"
      SET rgDangerous
    END_OF_OPT
    OPT_IFNOT rgForbidden "No jo, ale když je to zakázané..."
      Ask "No právě. Kdyby se to smělo, tak to není žádná zábava, ne?"
      SET rgForbidden
    END_OF_OPT
    OPT "Okay, já se meteorických rojů nebojím, už jsem jich pár proletěl[g(|a)]."
      GO richCont2
    END_OF_OPT
    OPT_IF rgForbidden "No, já nevím. Radši někdy jindy."
      GO richRefused
    END_OF_OPT
  END_OF_MENU

  LABEL richCont2
  Say "Skutečně? I velký meteory, co lítaj z boku? Jako zleva a zprava?"
  Ask "Na ty potřebuješ mít děla do stran, jinak z tebe udělaj fašírku."
  MENU
    OPT "Není problém. Jsem [g(zkušenej trucker|zkušená truckerka)]."
      Say "Fajn, to ráda slyším. Já se taky nebojím."
      GO richCont3
    END_OF_OPT
    OPT "O mě strach neměj. Ale co ty, zvládneš to?"
      Say "O tom vůbec nepochybuj."
      GO richCont3
    END_OF_OPT
    OPT "Uhm... Hele, víš co? Necháme to na jindy."
      GO richRefused
    END_OF_OPT
  END_OF_MENU

  LABEL richCont3
  Say "Moje loď má sportovní úpravu. Sníženej profil a tak."
  ChangeChar richgirl campaign/rich-smile.png
  Ask "Vsadím se, že skončí míň rozmlácená, než tvoje."
  MENU
    OPT "To se ještě uvidí."
      Ask "Tak schválně. Jestli ztratím víc kusů lodi, než ty, nechám ti svůj design lodi zkopírovat. Co ty na to?"
      MENU
        OPT "Platí!"
          Say "Fajn, uvidíme se v hangáru!"
          Delay 200
          ChangeChar richgirl campaign/rich-smile.png
          Delay 400
          ChangeChar richgirl campaign/rich-smile-cheer.png
          Delay 400
          ChangeChar richgirl campaign/rich-smile.png
          Delay 500
          SET richGirlAccepted
          SET richGirlPending
          HidePath "R1-4a"
          ShowPath "U1-4a"
          GO richEnd
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL richRefused
  ChangeChar richgirl campaign/rich-bored.png
  SET richRefused
  Say "Tak to je škoda. Myslela jsem, že s tebou bude zábava."
  Say "No nic. Jdu si pro další drink."
  Delay 300

  LABEL richEnd
  HideChar richgirl
  HideDlgScene
END_OF_SCRIPT

SCRIPT richGirlDlg2

  ShowDlgScene
  ChangeChar richgirl campaign/rich-bored.png
  ShowChar richgirl left
  Delay 750
  ChangeChar richgirl campaign/rich.png
  Delay 300
  IF richGirl2Accepted
    IF designSport2Unlocked
      Ask "Ahoj, chceš to zkusit znovu? Jen tak? Tentokrát samozřejmě vyhraju já!"
    ELSE
      Say "Ahoj. Chceš to zkusit znovu? Jsem připravená předvést ti své dokonalé schopnosti, kolikrát chceš."
      Ask "A ta nabídka s mým designem lodi platí. Ztrať méně dílků než já – a je tvůj."
    END_OF_IF
    MENU
      OPT "Tak jo."
        ChangeChar richgirl campaign/rich-smile.png
        Say "Bezva!"
        Delay 300
        ChangeChar richgirl campaign/rich-smile-cheer.png
        Delay 600
        ChangeChar richgirl campaign/rich-smile.png
        Delay 300
        ShowPath U2-21a
        GO richGirl2End
      END_OF_OPT
      OPT "Dneska ne."
        ChangeChar richgirl campaign/rich-bored.png
        Say "Hmm... tak se měj."
        GO richGirl2End
      END_OF_OPT
    END_OF_MENU
  END_OF_IF
  IF richGirl2Refused
    Ask "Ahoj. Nechceš přece jen zkusit tu nezmapovanou trať?"
    GO richGirl2Querry
  END_OF_IF
  Say "Ach, to je přece zase... No... Mám to na jazyku..."
  MENU
    OPT "[NAME]."
      Say "Jo, [g(ten|ta)]..."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  Say "Pamatuješ si, jak jsme letěli skrz meteory?"
  Ask "A jak jsem tě nechala vyhrát design své lodě?"
  MENU
    OPT "Jo, pamatuju."
      EXIT_MENU
    END_OF_OPT
    OPT "Jo, až na tu část 'nechala' vyhrát."
      Delay 200
      ChangeChar richgirl campaign/rich-smile.png
      Delay 200
      Say "He he, jsi vtipn[g(ý|á)]."
      Delay 300
      ChangeChar richgirl campaign/rich-smile-blink.png
      Delay 300
      ChangeChar richgirl campaign/rich-smile.png
      Delay 300
      ChangeChar richgirl campaign/rich-smile-blink.png
      Delay 300
      ChangeChar richgirl campaign/rich-smile.png
      Delay 300
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  Say "Kdybys chtěl[g(|a)] někdy letět tu zakázanou trasu znovu, dej mi vědět. Jen tak, pro zábavu."
  Say "I když musím přiznat, že už to není tak vzrušující jako poprvé."
  Ask "Slyšela jsem o jiné trati. Je tak nebezpečná, že ani není na mapách."

  LABEL richGirl2Querry
  MENU
    OPT_IFNOT richGirlHopeless "Víš o tom, že jsi beznadějný případ, že?"
      SET richGirlHopeless
      Ask "Jen se beznadějně nudím. Ty nevíš, jak je těžké být nejlepší ve všem, co děláš?"
    END_OF_OPT
    OPT "Ta trasa zní zajímavě. Pokračuj."
      EXIT_MENU
    END_OF_OPT
    OPT "Se mnou nepočítej."
      SET richGirl2Refused
      ChangeChar richgirl campaign/rich-bored.png
      Say "Jsi tak nudn[g(ý|á)][g(ý|á)][g(ý|á)]... Čau."
      GO richGirl2End
    END_OF_OPT
  END_OF_MENU
  SET richGirl2Accepted
  Say "Říká se, že meteory tam lítají velmi hustě, z obou stran."
  Say "Ráda bych na té trati vyzkoušela svůj modifikovaný sportovní model lodi třídy II."
  MENU
    OPT "A můžu si zkopírovat ten model, když tě zase porazím?"
      Say "Heh... Není šance, že bys měl znovu takové štěstí. Ale klidně."
      Say "Když neztratíš víc dílků, než já, máš ho mít."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  ShowPath U2-21a
  HideButton brichgirl2

  LABEL richGirl2End
  HideChar richGirl
  HideDlgScene
END_OF_SCRIPT

SCRIPT merchantLaughA

  ChangeChar merchant campaign/merch-laughA1.png
  Delay 150
  ChangeChar merchant campaign/merch-laughA2.png
  Delay 200
  ChangeChar merchant campaign/merch-laughA1.png
  Delay 250
  ChangeChar merchant campaign/merch-laughA2.png
  Delay 300
  ChangeChar merchant campaign/merch-sitting.png
END_OF_SCRIPT

SCRIPT merchantLaughB

  ChangeChar merchant campaign/merch-laughB1.png
  Delay 150
  ChangeChar merchant campaign/merch-laughB2.png
  Delay 200
  ChangeChar merchant campaign/merch-laughB1.png
  Delay 250
  ChangeChar merchant campaign/merch-laughB2.png
  Delay 300
  ChangeChar merchant campaign/merch-laughB1.png
  Delay 350
  ChangeChar merchant campaign/merch-laughB2.png
  Delay 400
  ChangeChar merchant campaign/merch-laughB1.png
  Delay 450
  ChangeChar merchant campaign/merch-laughB2.png
  Delay 500
  ChangeChar merchant campaign/merch-sitting.png
END_OF_SCRIPT

SCRIPT merchantLaughX

  ChangeChar merchant campaign/merch-laughB1.png
  Delay 150
  ChangeChar merchant campaign/merch-laughB2.png
  Delay 200
  ChangeChar merchant campaign/merch-laughA2.png
  Delay 300
  ChangeChar merchant campaign/merch-laughA1.png
  Delay 700
  ChangeChar merchant campaign/merch-disappointed.png
  Delay 400
END_OF_SCRIPT

SCRIPT purpleHomeDlg

  ShowDlgScene
  IF_PROB 33
    ShowChar purple left
  ELSE
    ShowChar purple right
  END_OF_IF
  IF_PROB 33
    Say "Khrr's ghzzz. Sssrgh wq ghhh-h."
    Ask "Zzrrgh qhhh-w-gz!?!"
  ELSE
    IF_PROB 50
      Ask "Sggh'kh qt Th'kzw-r?"
    ELSE
      Ask "Sgh. Sgh wzwg'h. Sgh wzwg'h K'wwfrg!"
    END_OF_IF
  END_OF_IF
  IF purpleLicenseApproval
    OPT "Jen jsem vám chtěl[g(|a)] poděkovat za souhlas k licenci."
      GO purpleHomeAnswer
    END_OF_OPT
  ELSE
    IF class3Asked
      IF warriorTestUnlocked
        MENU
          OPT "Jo jo... Už brzy projdu tím vaším testem."
            GO purpleHomeAnswer
          END_OF_OPT
        END_OF_MENU
      ELSE
        MENU
          OPT "Jen jsem vás chtěl[g(|a)] požádat o svolení k licenci na lodě třídy III."
            EXIT_MENU
          END_OF_OPT
        END_OF_MENU
        Ask "Sgggk-h. Wghkkk'r kh gzzw. Khz wrrzk-hrrg krz'h?"
        MENU
          OPT "Ovšem že jsem [g(mocný bojovník|mocná bojovnice)]."
            EXIT_MENU
          END_OF_OPT
          OPT "Jasně, cokoliv. Klidně i [g(bojovník|bojovnice)], když to pomůže."
            EXIT_MENU
          END_OF_OPT
          OPT "Je mi líto. Nejsem zrovna bojovný typ."
            Say "Kh krz'h wgz-h k."
            GO purpleHomeEnd
          END_OF_OPT
        END_OF_MENU
        Ask "Sgghr'g! Kzz ghrr zgg. Qwrg wrrzk-hrrg krz'h..."
        MENU
          OPT "Jsem úplně nažhaven[g(ej|á)] vám to dokázat."
            EXIT_MENU
          END_OF_OPT
          OPT "Pokud je to nezbytné, můžu to zkusit dokázat."
            EXIT_MENU
          END_OF_OPT
          OPT "Hele... Víte co? Radši na to zapomeňme."
            GO purpleHomeEnd
          END_OF_OPT
        END_OF_MENU
        Ask "Shkrrr zh-zh frrg'h'hr. Zrgw grrh gk'gkzzzr. Ghrr wrrzk-hrrg krz'h zh-zh hrrrg!"
        MENU
          OPT "Dobrá, zúčastním se té cesty."
            EXIT_MENU
          END_OF_OPT
        END_OF_MENU
        Ask "Ghrr'wrrzk ssrr zh-zh, sgg gh krz'h zzwg-hrg!!!"
        MENU
          OPT "Rozumím. Kdo přežije, ten vyhraje."
            ResetFocus bpurple
            Delay 500
            ShowNode cyanHome
            CenterMapByNode cyanHome
            Delay 500
            ShowPath U2-20a
            Say "Zrgh hr'w ghrrz..."
            SET warriorTestUnlocked
            GO purpleHomeEnd
          END_OF_OPT
          OPT "Ale tohle nezní moc dobře. Raději se mnou nepočítejte."
            GO purpleHomeEnd
          END_OF_OPT
        END_OF_MENU
      END_OF_IF
    END_OF_IF
  END_OF_IF
  MENU
    OPT "Jasně. Samozřejmě."
      EXIT_MENU
    END_OF_OPT
    OPT "To bych neřekl[g(|a)]."
      EXIT_MENU
    END_OF_OPT
    OPT "Obávám se, že jsem špatně rozuměl[g(|a)] tomu poslednímu slovu."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL purpleHomeAnswer
  IF_PROB 33
    Say "W'z'ghrw gz? Thgr..."
  ELSE
    IF_PROB 50
      Say "Kkk'z hrgh wzr-g-g-gh."
    ELSE
      Say "Kh!"
    END_OF_IF
  END_OF_IF

  LABEL purpleHomeEnd
  Delay 300
  HideChar purple
  HideDlgScene
END_OF_SCRIPT

SCRIPT merchantDlg

  ShowDlgScene
  ShowChar merchant left
  Delay 300
  IF merchantMet
    IF merchantAccepted
      ChangeChar merchant campaign/merch-laughA1.png
      IF merchantFailed
        RESET merchantFailed
        RESET merchantLeftAtPurple
        Ask "Ah, to jste vy. Posledně nám to moc nevyšlo, co... Nezkusíte to se mnou ještě jednou?"
      ELSE
        IF merchantLeftAtPurple
          RESET merchantLeftAtPurple
          Ask "Vy jste se vrátil[g(|a)]? To je milé. Doprovodíte mne tedy?"
        ELSE
          Ask "Tak co, kdy se vydáme na cestu?"
        END_OF_IF
      END_OF_IF
      MENU
        OPT "Teď se to nehodí. Někdy jindy."
          Delay 200
          ChangeChar merchant campaign/merch-disappointed.png
          Delay 200
          Say "To je ale škoda. No, co se dá dělat."
          GO merchEnd
        END_OF_OPT
        OPT "Připravte se, za chvíli vyrážíme!"
          ShowPath U1-9a
          Say "Výborně!"
          GO merchEnd
        END_OF_OPT
      END_OF_MENU
    ELSE
      IF merchantOffer
        Ask "Tak co, nezměnil[g(|a)] jste názor? Doprovodil[g(|a)] byste mě na nebezpečné cestě na Hvězdné Ticho?"
        GO merchSecondQuery
      ELSE
        Ask "Tak co, už byste na mě měl[g(|a)] chvíli čas?"
        GO merchFirstQuery
      END_OF_IF
    END_OF_IF
  ELSE
    Say "Vida, to je ale náhodička."
    Say "Zrovna jsem si říkal, jaká je škoda, že na téhle bohem zapomenuté planetě nejsou žádný truckeři."
    Ask "Měl[g(|a)] byste na mě chviličku?"
    SET merchantMet
  END_OF_IF

  LABEL merchFirstQuery
  MENU
    OPT "Jistě, k vašim službám."
      EXIT_MENU
    END_OF_OPT
    OPT "Aha, jasně, 'neměl[g(|a)] byste chviličku', a pak je z toho quest nejmíň na týden."
      SUB_SCRIPT merchantLaughB
      Ask "Ha ha, no, abych se přiznal... Měl jsem na mysli něco takového."
      MENU
        OPT "No tak to vyklopte!"
          GO merchCont
        END_OF_OPT
        OPT "Tak to nemám zájem."
          ChangeChar merchant campaign/merch-disappointed.png
          Say "Tak to je ale opravdu škoda."
          GO merchEnd
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
    OPT "Kzwrrr... Brbl, brbl... Chrch'chrch!"
      Ask "Oh, vy hovoříte místním jazykem? Úžasné. Nikdy jsem se ho nenaučil."
      MENU
        OPT "Jen tak to zkouším. Docela mi to jde. Ani nevím, co to znamenalo."
          SUB_SCRIPT merchantLaughB
          GO merchCont
        END_OF_OPT
        OPT "Jasně, umím všechny řeči. Nu, što vam nado?"
          GO merchCont
        END_OF_OPT
        OPT "To znamenalo něco jako: Nemám zrovna náladu na divnýho chlápka s questem."
          ChangeChar merchant campaign/merch-disappointed.png
          Say "Ah, rozumím. Snad tedy někdy jindy."
          GO merchEnd
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL merchCont
  ChangeChar merchant campaign/merch.png
  SET merchantOffer
  Say "Inu, věc se má tak. Mám tu rozjetý docela výnosný obchod mezi fialovýma a hnědýma."
  Say "Tedy pardon, chci říci mezi Tzkz... Thk'z... vítekterýma a obyvateli Hvězdného Ticha."
  Say "Jenže cesta mezi oběma planetami je nebezpečná, a já nejsem žádný akční hrdina."
  Ask "Neměl[g(|a)] byste zájem nechat se najmout jako ozbrojený doprovod na cestu na planetu Hvězdné Ticho?"

  LABEL merchSecondQuery
  MENU
    OPT_IFNOT merchAlienQuestion "Proč nenajmete někoho z Th'kzw-r, to jsou prý rození bojovníci."
      SET merchAlienQuestion
      Say "Upřímně, jsem z toho jejich chrčení nervózní."
      Say "Raději mám ty z Ticha. Mlčí a makají."
    END_OF_OPT
    OPT "Nebezpečná říkáte? Tak to se mnou nepočítejte."
      Say "Ah, co se dá dělat. Ale rozumím vám. Také jsem sám strašpytel."
      GO merchEnd
    END_OF_OPT
    OPT "A co nabízíte?"
      Ask "Co byste řekl[g(|a)] na dvacet pět procent ze zisku?"
      MENU
        OPT "To zní dobře."
          Say "Ah, výborně. Dobře vyděláte. A navíc se setkáte s hnědými mimozemšťany. Moc příjemní chlapíci."
          GO merchAccept
        END_OF_OPT
        OPT "Co takhle padesát procent?"
          Ask "Víc si nemohu dovolit. Ale seznámím vás s hnědými mimozemšťany. To není k zahození."
          MENU
            OPT "Dobrá, platí."
              Say "Ach, výborně."
              GO merchAccept
            END_OF_OPT
            OPT "Pětadvacet procent je málo!"
              Delay 300
              ChangeChar merchant campaign/merch-disappointed.png
              Say "V tom případě, co se dá dělat. Třeba najdu někoho jiného... časem."
              GO merchEnd
            END_OF_OPT
          END_OF_MENU
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL merchAccept
  Say "Myslím, že spolu uděláme úžasný obchod!"
  SUB_SCRIPT merchantLaughA
  ShowNode brownHome
  ShowPath U1-9a
  ShowPath R1-9b
  ResetFocus bmerchant
  Say "Uvidíme se v docích."
  SET merchantAccepted
  RESET merchantLeftAtPurple

  LABEL merchEnd
  Delay 200
  HideChar merchant
  Delay 300
  HideDlgScene
END_OF_SCRIPT

SCRIPT mechDlg

  ShowDlgScene
  IFNOT outpostInitialized
    HideButton bmech
    ShowButton bambassador
    SetFocus bambassador
    Delay 300
    CenterMapByNode outpost
    Delay 500
    Announce "V přístavu se povídá, že na Hraniční stanici mají nějakou delikátní diplomatickou krizi."
    Delay 300
    CenterMapByNode regula
    Delay 300
    HideDlgScene
    RETURN
  END_OF_IF
  ShowChar mech left
  IF mechOffered
    ChangeChar mech campaign/mech-explaining.png
    Say "Ahoj. Přiš[g(el|la)] jsi přijmout ten job?"
    MENU
      OPT "Ano."
        GO mechAccept
      END_OF_OPT
      OPT "Ne. Já jen..."
        ChangeChar mech campaign/mech-f.png
        Say "V tom případě mě nezdržuj, musím sehnat někoho, kdo to udělá."
        GO mechEnd
      END_OF_OPT
    END_OF_MENU
  END_OF_IF
  Say "Ahoj [g(truckere|truckerko)]."
  ChangeChar mech campaign/mech-explaining.png
  Ask "Spěchám, takže přeskočme formality – mám pro tebe job."
  MENU
    OPT "Ale formality jsou důležité..."
      ChangeChar mech campaign/mech-explaining-f.png
      Say "No jo, dobrá. Já se představím, ty se představíš, já se zeptám, co děláš, ty mi řekneš, že jsi trucker[g(|ka)], ..."
      Ask "... chvíli si budeme nezávazně povídat, ty zmíníš, že vypadám ustaraně, a já ti řeknu o tom jobu. Okay?"
      MENU
        OPT "Um, dobrá."
          GO mechCont
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
    OPT "V pohodě. Sem s ním."
      Say "Fajn."
      GO mechCont
    END_OF_OPT
  END_OF_MENU

  LABEL mechCont
  SET mechOffered
  ChangeChar mech campaign/mech.png
  Say "Takže: Nedaleko uvádíme do provozu novou tovární stanici."
  Say "Poslali mě na Regulu najmout nějaké dělníky. To by problém nebyl, lidí, co chtěj práci, je tu spousta."
  ChangeChar mech campaign/mech-explaining.png
  Ask "Jenže jsem nesehnala žádnou loď, která by mi je tam dovezla."
  MENU
    OPT_IFNOT mechWhy "A proč mi to říkáš?"
      ChangeChar mech campaign/mech-explaining-f.png
      Ask "Ach jo, vidím, že ty si ty rozhovory fakt užíváš. Potřebuju, abys mi na tovární stanici hodil[g(|a)] nějaké lidi."
      SET mechWhy
    END_OF_OPT
    OPT_IFNOT mechHowMuch "Chápu. Kolik vás je?"
      SET mechHowMuch
      SET mechWhy
      Ask "Hodně. Budu muset sehnat i další truckery. Ale můžu počítat s tebou?"
    END_OF_OPT
    OPT_IF mechHowMuch "Jasně, počítej se mnou"
      GO mechAccept
    END_OF_OPT
    OPT "Hm, se mnou nepočítej."
      ChangeChar mech campaign/mech-f.png
      Say "Jasně. Stejně nakonec přijdeš, že ten úkol chceš, ale proč nevyzkoušet všechny možnosti rozhovoru, že?"
      GO mechEnd
    END_OF_OPT
  END_OF_MENU

  LABEL mechAccept
  SET mechAccepted
  RESET mechLeftAtRegula
  Delay 250
  ChangeChar mech campaign/mech-thumbup.png
  Delay 250
  Say "Fajn."
  Say "Uvidíme se ve skladišti."
  Delay 250
  HideChar mech
  Delay 250
  AddNode workshop
  Delay 250
  ShowPath U1-7a
  HideButton bmech
  HideDlgScene
  RETURN

  LABEL mechEnd
  HideChar mech
  Delay 300
  HideDlgScene
END_OF_SCRIPT

SCRIPT brownDlg

  ShowDlgScene
  ShowChar brown left
  SET brownTried
  Delay 500
  Ask "..."
  MENU
    OPT "Dobrý den."
      EXIT_MENU
    END_OF_OPT
    OPT "Přicházím v míru."
      EXIT_MENU
    END_OF_OPT
    OPT "Můžete mi říct, kde je tu hotel/letiště/půjčovna kostýmů/kadeřník?"
      EXIT_MENU
    END_OF_OPT
    OPT_IF brownInfo "..."
      GO brownSuccess
    END_OF_OPT
  END_OF_MENU
  Delay 700
  Ask "..."
  MENU
    OPT "Rozumím."
      EXIT_MENU
    END_OF_OPT
    OPT "Nerozumím."
      EXIT_MENU
    END_OF_OPT
    OPT "Vaše planeta/soustava/sektor je v létě moc krásná."
      EXIT_MENU
    END_OF_OPT
    OPT_IF brownInfo "..."
      GO brownSuccess
    END_OF_OPT
  END_OF_MENU
  Delay 900
  Ask "... ?"
  MENU
    OPT "Proč sakra vůbec nemluvíte?"
      GO brownFail
    END_OF_OPT
    OPT "Přináším pozdravy od pozemských bratrů."
      Delay 300
      Ask "... !"
      EXIT_MENU
    END_OF_OPT
    OPT "Máte tu hezky tichoučko."
      Ask "..."
      Delay 900
      EXIT_MENU
    END_OF_OPT
    OPT_IF brownInfo "..."
      GO brownSuccess
    END_OF_OPT
  END_OF_MENU
  MENU
    OPT "Tak nějak už nevím, co bych řekl[g(|a)]."
      Delay 500
      Ask "..."
    END_OF_OPT
    OPT "Nic, hezky jsem si popovídal[g(|a)] a musím už jít."
      GO brownEnd
    END_OF_OPT
    OPT "Začínáte mě štvát, není s vámi rozumná řeč."
      GO brownFail
    END_OF_OPT
    OPT_IF brownInfo "..."
      GO brownSuccess
    END_OF_OPT
  END_OF_MENU

  LABEL brownSuccess
  Delay 1500
  Say "... !"
  Delay 400
  ChangeChar brown campaign/brown-purr.png
  Say "... ..."
  Delay 200
  Say "..."
  ChangeChar brown campaign/brown.png
  Delay 300
  HideChar brown
  SET brownUnlocked
  Announce "Odemčeni hnědí mimozemšťané!"
  Delay 500
  SetAchievementProgress camp_aliens 2
  Delay 500
  ShowChar merchant right
  Delay 300
  Say "Ach, vidím, že jste se s nimi domluvil[g(|a)]!"
  Say "Tak to vám blahopřeji! Jsou to nejlepší lodní inženýři."
  Say "Zkuste nějakého najmout na vaši loď. Uvidíte, jaké rychlosti s nimi dosáhnete."
  Say "Och, a já se teď musím se vrátit na jednání."
  Delay 200
  HideChar merchant
  HideButton bbrown
  HideButton bmerchhelp
  IF class3Asked
    ShowButton bmerch2
    SetFocus bmerch2
    SET merch2immediatelly
  END_OF_IF
  HideChar brown
  HideDlgScene
  RETURN

  LABEL brownFail
  Delay 700
  ChangeChar brown campaign/brown-frown.png
  Say "... !"
  Delay 500

  LABEL brownEnd
  HideChar brown
  IF merchHelpAsked
    Delay 750
    ShowChar merchant right
    Say "Ach, omlouvám se, že jdu pozdě."
    Ask "Tak co, už se vám podařilo s místními domluvit?"
    MENU
      OPT "Jasně, jde to skvěle."
        Ask "Nezdálo se mi. Opravdu to zvládáte?"
      END_OF_OPT
      OPT "No, trochu potíže s tím mám."
        EXIT_MENU
      END_OF_OPT
      OPT "Děláte si legraci? S nima se domluvit nejde."
        EXIT_MENU
      END_OF_OPT
    END_OF_MENU
    Ask "Můžete mi říct, co vám řekli?"
    MENU
      OPT "Jo. To vím naprosto přesně. Pořád říkají:  ..."
        EXIT_MENU
      END_OF_OPT
    END_OF_MENU
    Say "Ach, tak to je zajímavé."
    Say "Tradiční odpověď zní zhruba takhle:                                    ..."
    Say "A teď mě omluvte, musím se vrátit na jednání. Smlouvání s místními není jednoduché."
    SET brownInfo
    Delay 300
    HideChar merchant
  END_OF_IF
  HideDlgScene
END_OF_SCRIPT

SCRIPT merchHelpDlg

  ShowDlgScene
  ShowChar merchant left
  IF merchHelpAsked
    ShowChar brown right
    Delay 300
    SayChar merchant "Psst... Nevidíte, že mám jednání?"
    Delay 900
    SayChar brown "..."
    Delay 800
    HideChar merchant
    HideChar brown
    HideDlgScene
    RETURN
  END_OF_IF
  IFNOT merchHelpIntro
    Say "Ah, to jste vy. Doufám, že si pobyt užíváte."
    Say "Promiňte, že se vám nemohu věnovat, mám za chvíli důležité obchodní jednání."
    SET merchHelpIntro
  ELSE
    Say "Promiňte, stále čekám na důležité jednání."
  END_OF_IF
  Ask "Potřebujete něco?"
  MENU
    OPT "Ani ne. Jen jsem vás chtěl[g(|a)] pozdravit."
      Say "Ach. Také vám přeji dobrého dne."
      GO merchHelpEnd
    END_OF_OPT
    OPT_IF brownTried "Upřímně, potřeboval[g(|a)] bych pomoc, moc se tu nedomluvím."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  Say "Ach, rozumím. Chvíli vám mohu věnovat."
  Ask "Nechci se chlubit, ale považuji se za odborníka na místní kulturu."
  MENU
    OPT_IFNOT qTelepat "Jsou místní obyvatelé telepaté?"
      Say "Cože? Samozřejmě že ne."
      Say "Ale je to prastará rasa. Dospěli k názoru, že vše, co mělo být řečeno, už řečeno bylo."
      SET qTelepat
    END_OF_OPT
    OPT_IFNOT qBrownSkill "Je k něčemu dobré snažit se s nimi domluvit?"
      Say "O tom nepochybujte. Jsou to nejlepší lodní inženýři."
      Say "Sám se většinou snažím mít aspoň jednoho na své lodi. Nevěřil[g(|a)] byste, jaké rychlosti s nimi dosáhnu."
      Say "Navíc nekecají a makají."
      SET qBrownSkill
    END_OF_OPT
    OPT "Já jim prostě nerozumím."
      Say "Dobrá, víte co? Já tu dořeším to jednání, a pak se za vámi stavím a budu vám tlumočit."
      SET merchHelpAsked
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  MENU
    OPT "Děkuji. Budu se těšit."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL merchHelpEnd
  Delay 300
  HideChar merchant
  HideDlgScene
END_OF_SCRIPT

SCRIPT merchDlg2

  ShowDlgScene
  ChangeChar merchant campaign/merch-sitting.png
  ShowChar merchant left
  IF merch2Refused
    Say "Ach, rád vás zase vidím."
    ChangeChar merchant campaign/merch-inviting.png
    Ask "Změnil[g(|a)] jste názor a chcete mě doprovodit na mé cestě, že?"
    SET merchFriendly
    GO merchDlg2Querry
  END_OF_IF
  IF merch2immediatelly
    Say "Ach, to jste opět vy. Jsem rád, že místní vstoupili do vašich služeb. Blahopřeji."
    Ask "Potřebujete ještě něco?"
    MENU
      OPT "No, potřeboval[g(|a)] bych ještě jejich souhlas pro licenci na lodě třídy III."
        GO merchDlg2cont
      END_OF_OPT
      OPT "Ani ne, jen jsem vám chtěl[g(|a)] znovu poděkovat."
        Say "To je ale velice milé."
        SUB_SCRIPT merchantLaughA
        Say "Tak zatím."
        GO merchDlg2End
      END_OF_OPT
    END_OF_MENU
  ELSE
    Say "Ach, vás si pamatuju. Před časem jsme spolu vydělali spoustu peněz!"
    SUB_SCRIPT merchantLaughA
    Ask "Co vás sem zase přivádí?"
    MENU
      OPT "Potřebuji souhlas místních pro licenci na lodě třídy III."
        GO merchDlg2cont
      END_OF_OPT
      OPT "Jen jsem se stavil[g(|a)], abych vás pozdravil[g(|a)]."
        Say "To je ale velice milé."
        SUB_SCRIPT merchantLaughA
        Say "Tak zatím."
        GO merchDlg2End
      END_OF_OPT
    END_OF_MENU
  END_OF_IF

  LABEL merchDlg2cont
  Say "Rozumím. To nebude lehké získat, ale mohu se o to pro vás pokusit."
  Say "Ach, zrovna jsem si uvědomil, jak příhodná situace."
  Say "Potřebujete pomoc, a já zrovna také hledám pomoc."
  SUB_SCRIPT merchantLaughA
  Delay 200
  ChangeChar merchant campaign/merch-inviting.png
  Delay 200
  Ask "Nedoprovodil[g(|a)] byste mne na další obchodní cestě?"

  LABEL merchDlg2Querry
  MENU
    OPT "Dobrá, uděláme spolu další kšeft."
      Say "No nejsme my povedený párek obchodních partnerů?"
      SUB_SCRIPT merchantLaughB
      Say "Stejné podmínky jako posledně. Vy mne ochráníte, a já vám dám 25 procent zisku ze zboží."
      Say "Jen teď poletíme s většími loděmi, a na jinou ufonskou planetu."
      Say "Očekávám vás ve skladišti."
      Delay 200
      HideChar merchant
      Delay 500
      ShowNode cyanHome
      CenterMapByNode cyanHome
      Delay 500
      ShowPath U2-19a
      CenterMapByPath U2-19a
      SET merchant2Unlocked
      HideButton bmerch2
      HideDlgScene
      RETURN
    END_OF_OPT
    OPT_IFNOT merchFriendly "Co když přátelsky odmítnu a jen požádám o ten souhlas?"
      SUB_SCRIPT merchantLaughA
      Ask "Tak vás znovu přátelsky požádám o pomoc."
      SET merchFriendly
    END_OF_OPT
    OPT "Na to zapomeňte. Jednou to stačilo."
      Delay 200
      ChangeChar merchant campaign/merch-disappointed.png
      Delay 200
      Say "Ach, rozumím. Konec konců, kdo potřebuje licenci na lodě třídy III, že?"
      Say "Dejte mi prosím vědět, kdybyste změnil[g(|a)] názor."
      SET merch2Refused
      GO merchDlg2End
    END_OF_OPT
  END_OF_MENU

  LABEL merchDlg2End
  HideChar merchant
  HideDlgScene
END_OF_SCRIPT

SCRIPT cyanDlg

  ShowDlgScene
  ShowChar cyan left
  Ask "Dobrý den, [g(pane|slečno)]. Vítejte na naší planetě."
  IF wasAtCyan
    MENU
      OPT "Jen jsem vás chtěl[g(|a)] znovu pozdravit."
        Ask "Skvělé. Tak tedy pozdravte."
        MENU
          OPT "Ehm... Zdravím."
            GO cyanEnd
          END_OF_OPT
        END_OF_MENU
      END_OF_OPT
      OPT_IF class3 "Tak už mám trojkovou licenci. Co takhle nějaký nový design lodě?"
        Say "Dobrá."
        GO cyanDesignUnlock
      END_OF_OPT
    END_OF_MENU
  END_OF_IF
  SET wasAtCyan
  MENU
    OPT "Vaše rasa mluví naším jazykem? To je vítaná změna!"
      Say "Ne, máme svůj vlastní jazyk."
      Ask "Ale každý z nás je odborníkem v některé oblasti poznání. A mým oborem jsou jazyky."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  MENU
    OPT "Bezva! Pěkně prosím, řekněte něco v jiných jazycích!"
      Delay 300
      Say "Kwwrh'ghrr kzz gwwr-h."
      IF brownHomeInitialized
        GO cyanLangMore1
      ELSE
        GO cyanLangMore2
      END_OF_IF
    END_OF_OPT
    OPT "No jo, pro mě za mě..."
      Say "Zdá se, že nedokážete ocenit opravdovou odbornost."
      GO cyanCont
    END_OF_OPT
  END_OF_MENU

  LABEL cyanLangMore1
  MENU
    OPT "Úžasné! Ještě něco, prosím."
      Delay 400
      Say "..."
      GO cyanLangMore2
    END_OF_OPT
    OPT "Díky. To stačilo."
      GO cyanLangEnd
    END_OF_OPT
  END_OF_MENU

  LABEL cyanLangMore2
  MENU
    OPT "Jak dokonalá výslovnost! Ještě naposled, prosím."
      Delay 300
      Say "TlhIngan maH!"
      MENU
        OPT "Skvělé. Jste opravdový odborník na jazyky."
          GO cyanLangEnd
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
    OPT "Díky. To stačilo."
      GO cyanLangEnd
    END_OF_OPT
  END_OF_MENU

  LABEL cyanLangEnd
  Say "Bylo mi potěšením."

  LABEL cyanCont
  Say "Víte, moje rasa je velmi talentovaná."
  Say "Každé dítě si zvolí v útlém věku specializaci, a v té se pak cvičí k naprosté dokonalosti."
  MENU
    OPT_IFNOT cyanProblem1 "Ale všiml[g(|a)] jsem si, že žijete v docela jednoduchých chatrčích."
      Say "Evidentně si nikdo nevybral stavbu domů jako svou specializaci."
      Say "Ale měl[g(|a)] byste si přečíst naše knihy o samorozporných umělých hyperalgebrách."
      SET cyanProblem1
    END_OF_OPT
    OPT_IFNOT cyanProblem2 "Ale všiml[g(|a)] jsem si, že tu máte všude odpadky."
      Say "Zdá se, že si nikdo nevybral úklid jako svou specializaci."
      Say "Ale měl[g(|a)] byste vidět naše umění nekonečněrozměrných ultrafraktálů."
      SET cyanProblem2
    END_OF_OPT
    OPT "Ale... No... Radši nic."
      Say "Vím, co jste chtěl[g(|a)] říci. Nemusíte se za lidskou rasu stydět. Každý nemůže být dokonalý."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  MENU
    OPT_IFNOT cyanAskIntro "Každopádně, jsem rád[g(|a)], že mluvíte naším jazykem. Snadno se dohodneme."
      Say "Když myslíte..."
      SET cyanAskIntro
    END_OF_OPT
    OPT_IFNOT cyanOffer1 "Mohli byste se vy azuroví chlapíci přidat k mé posádce?"
      Say "Ne."
      SET cyanOffer1
      SET cyanAskIntro
      ADD cyanOffers 1
      IF_MIN cyanOffers 3
        SET cyanSadEnd
      END_OF_IF
    END_OF_OPT
    OPT_IFNOT cyanOffer2 "Mohl[g(|a)] bych prosím dostat vaše svolení pro licenci na lodě třídy III?"
      Say "Ne."
      SET cyanOffer2
      SET cyanAskIntro
      ADD cyanOffers 1
      IF_MIN cyanOffers 3
        SET cyanSadEnd
      END_OF_IF
    END_OF_OPT
    OPT_IFNOT cyanOffer3 "Mohli byste mi dát nějaký nový design lodě?"
      Say "Dobře."
      SET cyanAskIntro
      IFNOT class3
        Say "Aha, moment. Naše designy jsou jen na lodě třídy III. Na ty nemáte licenci."
        Say "Takže: ne."
        SET cyanOffer3
        ADD cyanOffers 1
        IF_MIN cyanOffers 3
          SET cyanSadEnd
        END_OF_IF
      ELSE
        GO cyanDesignUnlock
      END_OF_IF
    END_OF_OPT
    OPT_IF cyanSadEnd "Hmmm... To tedy nešlo moc dobře."
      Say "Je mi líto. Ale my jsme mimozemská rasa z rozšíření."
      Say "V základní hře bychom ani neměli být!"
      Ask "Myslím, že už půjdu. Klidně ještě řekněte něco o tom, jak jsme úžasná rasa, a pak sbohem."
      MENU
        OPT "Co se týče vaší rasy, nedostává se mi slov..."
          EXIT_MENU
        END_OF_OPT
        OPT "Jasně. 'Úžasná' rasa."
          EXIT_MENU
        END_OF_OPT
        OPT "Radši vám ani neřeknu, co si o vás myslím."
          EXIT_MENU
        END_OF_OPT
        OPT "Předpokládám, že stejně nezáleží na tom, co odpovím."
          EXIT_MENU
        END_OF_OPT
      END_OF_MENU
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  Delay 300
  HideButton bcyan

  LABEL cyanEnd
  Delay 500
  HideChar cyanAlien
  HideDlgScene
  RETURN

  LABEL cyanDesignUnlock
  Say "Můžu vám nechat zkopírovat tuto."
  Say "Má místo pro více komponent, než vaše lidská... A je to opravdové abstraktní umělecké dílo."
  Delay 300
  UnlockShipPlan "III-amoeba"
  Announce "Nový design lodi třídy III odemčen."
  Delay 500
  Say "Doufám, že se vám bude líbit."
  HideButton bcyan
  HideChar cyanAlien
  HideDlgScene
END_OF_SCRIPT

SCRIPT workDlg

  ShowDlgScene
  ShowChar mech left
  IF designDouble1Unlocked
    IF designDouble2Unlocked
      IF designDouble3Unlocked
        Say "Ahoj. Doufám, že si užíváš moje lodě s dvojitými děly."
        ChangeChar mech campaign/mech-f.png
        Say "Musím se vrátit ke své práci. Zatím..."
        GO workEnd
      ELSE
        IF lab1Done
          GO workThirdDesign
        ELSE
          Say "Promiň, teď mám moc práce."
          GO workEnd
        END_OF_IF
      END_OF_IF
    ELSE
      IF designDouble2Ready
        GO workSecondDesign
      ELSE
        Say "Promiň, teď mám moc práce."
        GO workEnd
      END_OF_IF
    END_OF_IF
  END_OF_IF
  IF doubleDesignOffered
    Say "Ahoj. Jdeš si pro ten nový design lodě se dvěma dělama, že?"
    GO doubleDesignOffer
  END_OF_IF
  SET doubleDesignOffered
  Say "Ahoj. Ještě jednou díky za pomoc."
  ChangeChar mech campaign/mech-explaining.png
  Say "Podívej, zrovna pracuju na novém designu lodě."
  Say "Má dva vepředu dvě pozice pro děla místo jedné."
  ChangeChar mech campaign/mech.png
  Ask "Chceš ho vyzkoušet?"

  LABEL doubleDesignOffer
  MENU
    OPT "Jasně, díky."
      Delay 300
      ChangeChar mech campaign/mech-explaining.png
      Delay 300
      Say "Tak jo, tady ho máš."
      Delay 300
      UnlockShipPlan "I-double"
      Announce "Nový design lodi třídy I odemčen."
      Delay 500
      SetAchievementProgress camp_design_factory 1
      Delay 300
      ChangeChar mech campaign/mech.png
      Say "Tak zatím, musím se vrátit k práci."
      Delay 500
      ChangeChar mech campaign/mech-f.png
      ResetFocus bworkshop
      SET designDouble1Unlocked
      EXIT_MENU
    END_OF_OPT
    OPT "Ani ne."
      Delay 300
      ChangeChar mech campaign/mech-f.png
      Delay 300
      Say "Jak myslíš. Kdybys změnil[g(|a)] názor, stav se."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  ChangeChar mech campaign/mech.png
  IFNOT workUniversalInfo
    Delay 300
    HideChar mech
    Delay 500
    ShowChar mech right
    ChangeChar mech campaign/mech-explaining.png
    Say "Jo, a mimochodem, až budeš odlétat, můžeš vykoušet novou vychytávku, kterou tu vyvíjíme."
    Ask "Je to suprová věc, žádné jiné skladiště ji nemá."
    MENU
      OPT "Jakou vychytávku?"
        Say "To je překvapení. Ale určitě si toho všimneš."
        EXIT_MENU
      END_OF_OPT
      OPT "Když říkáš..."
        EXIT_MENU
      END_OF_OPT
    END_OF_MENU
    SET workUniversalInfo
  END_OF_IF

  LABEL workEnd
  Delay 500
  HideChar mech
  HideDlgScene
  RETURN

  LABEL workSecondDesign
  IF designDouble2Refused
    Say "Ahoj. Rozmyslel[g(|a)] sis to? Doručíš moje peníze do Hvězdného baru?"
    GO workSecondDesignQuerry
  END_OF_IF
  IF designDouble2Offered
    Say "Ahoj. Chtěl bys vidět můj nový design pro lodě třídy II? Ale na oplátku bych potřebovala službu."
    MENU
      OPT "O co jde?"
        GO workSecondDesignQuest
      END_OF_OPT
      OPT "Nemám zájem."
        GO workEnd
      END_OF_OPT
    END_OF_MENU
  END_OF_IF
  Say "Ahoj. Chtěla jsem se zeptat..."
  Ask "Zkoušel[g(|a)] jsi ten nový design lodě, co jsem ti kdysi dala? Funguje?"
  MENU
    OPT "Jo, ten je bezva."
      Say "Díky. Spousta truckerů si ho pochvaluje."
      EXIT_MENU
    END_OF_OPT
    OPT "Hele, upřímně, není nic moc."
      Say "Škoda, že se ti nelíbí. Některým jiným truckerům se líbí."
      EXIT_MENU
    END_OF_OPT
    OPT "Ještě jsem neprozkoumal[g(|a)] všechny jeho možnosti."
      Say "Jasně. Zdvořilé naznačení, že se ti nelíbí. Vím, že to není to nic revolučního, ale některým lidem se líbí."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  SET designDouble2Offered
  Ask "Dokonce mě už požádali, abych navrhla podobné úpravy pro třídu II."
  MENU
    OPT "A můžeš mi ten design pro dvojkové lodě ukázat?"
      EXIT_MENU
    END_OF_OPT
    OPT "Dobře. Tak zatím..."
      GO workEnd
    END_OF_OPT
  END_OF_MENU
  Say "Jasně. Můžeš ho otestovat. Ale na oplátku bych potřebovala laskavost."
  MENU
    OPT "O co jde?"
      EXIT_MENU
    END_OF_OPT
    OPT "Věděl[g(|a)] jsem, že v tom bude háček."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL workSecondDesignQuest
  ChangeChar mech campaign/mech.png
  Say "Před nějakou dobou jsem měla s přáteli bezva večírek ve Hvězdném baru."
  Say "Bylo to fajn... Ale... Tak nějak jsme tam utratili víc, než jsme tehdy měli."
  Delay 300
  ChangeChar mech campaign/mech-explaining.png
  Delay 700
  ChangeChar mech campaign/mech.png
  Delay 300
  Say "Ten smutný chlapík, co tam dělá barmana, byl velmi hodný. A já mu slíbila, že to zaplatím co nejdříve."
  ChangeChar mech campaign/mech-f.png
  Ask "Ale teď tady musím trčet v práci, už týdny. Nedoručil[g(|a)] bys mu ty peníze?"

  LABEL workSecondDesignQuerry
  MENU
    OPT "Jasně. Kolik to bylo?"
      EXIT_MENU
    END_OF_OPT
    OPT "Na to zapomeň. Nebudu ti dělat poslíčka."
      SET designDouble2Refused
      GO workEnd
    END_OF_OPT
  END_OF_MENU
  Say "Bylo to kolem dvaceti kreditů. Tady je máš."
  AddCredits 20
  Announce "Dostal[g(|a)] jsi 20 kreditů."
  ChangeChar mech campaign/mech.png
  Ask "A díky, jsi moc hodn[g(ý|á)]. Tenhle nesplacený dluh mě docela trápil."
  MENU
    OPT "To je slušná částka. Jak víš, že si ji prostě nenechám?"
      ChangeChar mech campaign/mech-explaining.png
      Say "Prostě vím. V lidech se docela vyznám. A ty mi přijdeš férov[g(ý|á)]."
      Say "Mimochodem, tady máš ten design."
      EXIT_MENU
    END_OF_OPT
    OPT_IFNOT designDouble2Unlocked "A až to udělám, tak mi dáš ten design?"
      ChangeChar mech campaign/mech-explaining.png
      Say "Ale ne, ten ti dám klidně hned. Věřím ti."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  SET mechDebtPending
  SET designDouble2Unlocked
  UnlockShipPlan "II-double"
  Delay 500
  Announce "Nový design lodi třídy II odemčen."
  Delay 500
  SetAchievementProgress camp_design_factory 2
  Delay 300
  IFNOT lab1Done
    ResetFocus bworkshop
  END_OF_IF
  SetFocus bbartender
  ChangeChar mech campaign/mech-thumbup.png
  Say "Tak díky. A hodně štěstí."
  GO workEnd

  LABEL workThirdDesign
  IF designDouble3Ready
    Say "Ahoj."
    MENU
      OPT "Ahoj. Měla jsi čas kouknout se na ten trojkovej design se dvěma dělama?"
        Say "Sakryš, promiň..."
        Say "Máme tady teď takovej frmol, že jsem na to úplně zapomněla."
        Say "Ale víš co, počkej chvíli... Hned jsem zpátky..."
        HideChar mech
        Delay 5000
        ShowChar mech left
        Delay 200
        ChangeChar mech campaign/mech-explaining.png
        Say "Tak jo, tady to je..."
        SET designDouble3Unlocked
        UnlockShipPlan "III-double"
        Delay 300
        ChangeChar mech campaign/mech.png
        Delay 300
        Announce "Nový design lodi třídy III odemčen."
        Delay 500
        ResetFocus bworkshop
        SetAchievementProgress camp_design_factory 3
        Delay 300
        Say "Jsem to teď narychlo udělala. Doufejme, že to bude fungovat."
        ChangeChar mech campaign/mech-explaining-f.png
        Say "Víš, ono to není tak složité, jak si lidí myslí."
        ChangeChar mech campaign/mech-explaining.png
        Ask "Prostě jsem jen smazala tenhle čtvereček a přidala dva sem a sem."
        MENU
          OPT "Dobře, díky."
            ChangeChar mech campaign/mech.png
            Say "Není zač. Měj se..."
            GO workEnd
          END_OF_OPT
          OPT "Díky, jsi ta nejlepší návrhářka lodí!"
            Delay 300
            ChangeChar mech campaign/mech.png
            Delay 500
            ChangeChar mech campaign/mech-blush.png
            Delay 300
            Say "No... to trochu přeháníš... Ale díky. Zatím se měj."
            GO workEnd
          END_OF_OPT
        END_OF_MENU
      END_OF_OPT
    END_OF_MENU
  END_OF_IF
  IF designDouble3Asked
    Say "Ahoj."
    ChangeChar mech campaign/mech-explaining-f.png
    Say "Jak už jsem ti říkala, teď nemám čas. Ale zkusím se na ty plány lodi podívat, než se sem znovu vrátíš."
    GO workEnd
  END_OF_IF
  Ask "Ahoj. Jak se ti líbí tvá nová zakázka pro laboratoře Společnosti?"
  MENU
    OPT "Je skvělá."
      Say "To ráda slyším."
      EXIT_MENU
    END_OF_OPT
    OPT "Nenávidím ji."
      Say "Ani se nedivím. Musí to být těžké."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  Ask "Ale promiň, teď si s tebou nemůžu povídat. Máme tu teď fakt hodně práce."
  MENU
    OPT "Než odejdu – nepřemýšlela jsi o designu trojkové lodě se dvěma dělovými sloty?"
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  Say "Ech, trojkové lodě přece mají tři dělové sloty. Nezapomeň na ty dva na křídlech."
  MENU
    OPT "Ale stejně by se dalo to hlavní dělo zdvojit, ne?"
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  ChangeChar mech campaign/mech-f.png
  Say "Hm... To je zajímavá myšlenka. Nikdy jsem o tom nepřemýšlela."
  ChangeChar mech campaign/mech-explaining.png
  Say "Víš co? Zamyslím se nad tím. Až se sem znovu vrátíš, mělo by to být připraveno."
  SET designDouble3Asked
  ResetFocus bworkshop
  GO workEnd
END_OF_SCRIPT

SCRIPT shortcutDlg

  ShowDlgScene
  ShowChar ambassador left
  Delay 500
  ChangeChar ambassador campaign/ambassador-happy.png
  Say "Ach, to jste vy. Rád vás zase potkávám, [g(pane|slečno)]."
  ChangeChar ambassador campaign/ambassador.png
  Ask "Velmi jste nám tehdy pomohl[g(|a)]... A děkuji, že se o tom nikde nešíříte..."
  MENU
    OPT_IFNOT shortcutPleasure "Jsem rád[g(|a)], že jsem mohl[g(|a)] pomoci."
      SET shortcutPleasure
      Ask "Nevěřil[g(|a)] byste, jak je dnes obtížné najít zkušenou, zodpovědnou a diskrétní osobu."
    END_OF_OPT
    OPT "Potřebujete vyřešit další problém?"
      Ask "Kdepak. Má práce zde je ve skutečnosti už hotova, a zrovna se vracím na Hraniční stanici."
      MENU
        OPT "No vida, já zde právě také skončil[g(|a)]."
          Say "Ach, a nepotřebujete svézt? Mohu vás vzít na Hraniční stanici."
          Ask "Aspoň vám oplatím laskavost..."
          MENU
            OPT "Proč ne, rád[g(|a)] si zkusím, jak se cestuje diplomatickou lodí."
              GO shortcutAccepted
            END_OF_OPT
            OPT "Díky, ale raději zůstanu u tradiční truckeřiny."
              Say "Dobrá. Nashledanou tedy."
              GO shortcutEnd
            END_OF_OPT
          END_OF_MENU
        END_OF_OPT
        OPT "Dobře. Tak nashle, a příjemnou cestu."
          Say "Díky, vám taky!"
          ChangeChar ambassador campaign/ambassador-happy.png
          Delay 700
          ChangeChar ambassador campaign/ambassador.png
          GO shortcutEnd
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL shortcutAccepted
  Say "Výborně, budeme cestovat společně."
  Delay 300
  HideChar ambassador
  Delay 500
  ShowChar ambassador right
  Delay 500
  ChangeChar ambassador campaign/ambassador-happy.png
  Delay 100
  Say "Toto je moje loď."
  ChangeChar ambassador campaign/ambassador.png
  Ask "Prosím nastupte si a pokračujet dál tou temnou chodbou."
  MENU
    OPT "Uh. Dobrá."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  Delay 300
  HideChar ambassador
  Delay 800
  Announce "Velvyslancova loď se nakonec ukázala velmi pohodlná. Spousta čalounění a skoro žádné trubky."
  Announce "Byla to příjemná cesta. Ve svém příštím životě bys mohl[g(|a)] zvážit spíš diplomatickou kariéru."
  Announce "Jediné taktické rozhodnutí, které jsi cestou musel[g(|a)] udělat, bylo z těch příjemnějších:"
  MENU
    OPT "Dal[g(|a)] bych si kuřecí maso."
      EXIT_MENU
    END_OF_OPT
    OPT "Zkusím těstoviny."
      EXIT_MENU
    END_OF_OPT
    OPT "Nemám hlad, děkuji."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  Delay 600
  CenterMapByNode outpost
  Delay 1000
  MovePlayerToNode outpost
  Delay 500
  ChangeChar ambassador campaign/ambassador-peek.png
  ShowChar ambassador right
  Say "A jsme tady. Byla to příjemná cesta."
  MENU
    OPT "To rozhodně byla. Díky moc. A nashledanou."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL shortcutEnd
  HideButton bcyanshortcut
  RESET shortcutEnabled
  Delay 300
  HideChar ambassador
  Delay 300
  HideDlgScene
END_OF_SCRIPT

END_OF_SCRIPTS
