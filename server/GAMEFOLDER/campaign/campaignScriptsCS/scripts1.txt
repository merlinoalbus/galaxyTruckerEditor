SCRIPTS

SCRIPT newbieDlg

  IF class3
    SUB_SCRIPT tutorClass3Dlg
    RETURN
  END_OF_IF
  IF tutor_moremissions
    Delay 200
    ShowDlgScene
    ChangeChar tutor campaign/tutor-hidden.png
    ShowChar tutor left
    Delay 1500
    ChangeChar tutor campaign/tutor-pizza.png
    Delay 500
    SayChar tutor "Nevidíš, že mám práci? Vypadni..."
    Delay 500
    ChangeChar tutor campaign/tutor-hidden.png
    Delay 500
    HideChar tutor
    Delay 500
    HideDlgScene
    RETURN
  END_OF_IF
  Delay 200
  ShowDlgScene
  ShowChar tutor left
  Delay 200
  IF tutor_somemissions
    Say "Nazdar. Doufám že jsi spokojen[g(ý|á)] se svou běžnou nudnou trasou."
    MENU
      OPT "Jasně, jen jsem si říkal[g(|a)], že vás pozdravím."
        HideDlgScene
        RETURN
      END_OF_OPT
      OPT "Mohl byste mi doporučit i nějakou jinou trasu?"
        GO more_missions
      END_OF_OPT
    END_OF_MENU
  END_OF_IF
  IF tutorialRefused
    IF playerIsCocky
      Ask "Ha, [g(pan|slečna)] 'já všechno vím'. Rozmyslel[g(|a)] sis to, co?"
    ELSE
      Ask "Ha, [g(pan|slečna)] 'nemám rád výukové mise'. Rozmyslel[g(|a)] jste si to, co?"
    END_OF_IF
    MENU
      OPT "Ale kdeže, jen jste mi zapomněl odemknout nějakou trasu."
        Say "Aha, no jasně. Tak začni lítat mezi touto stanicí a hvězdným barem."
        Delay 500
        ShowNode "bar"
        CenterMapByNode bar
        Delay 750
        ShowPath "R1-1a"
        ShowPath "R1-1b"
        Delay 500
        CenterMapByPath R1-1a
        Delay 1000
        SET tutor_somemissions
        Ask "Je to bezpečná a nudná trasa. Můžeš klidně strávit zbytek svýho mizernýho života pendlováním tam a zpátky."
        MENU
          OPT "To zní dobře."
            GO basic_route_agreed
          END_OF_OPT
          OPT "Hele, dejte mi pořádnou štreku."
            GO more_missions
          END_OF_OPT
        END_OF_MENU
      END_OF_OPT
    END_OF_MENU
  ELSE
    Ask "A hele, [g(náš nováček|to nové děvče)]. Tvůj trénink skončil. Co ještě potřebuješ?"
    MENU
      OPT "Neřekl jste mi, co dál."
        Say "Aha, pravda. Můžeš začít lítat mezi touto stanicí a hvězdným barem."
        Delay 250
        ShowNode "bar"
        CenterMapByNode bar
        Delay 750
        ShowPath "R1-1a"
        ShowPath "R1-1b"
        Delay 500
        CenterMapByPath R1-1a
        Delay 1000
        SET tutor_somemissions
        Ask "Je to bezpečná a nudná trasa. Můžeš klidně strávit zbytek svýho mizernýho života pendlováním tam a zpátky."
        MENU
          OPT "Dík, rád[g(|a)] se do toho pustím."
            Say "Bezva. Teď prostě jen [v(ťap|klik)]ni na tu pulzující raketu a můžeš letět."
            GO basic_route_agreed
          END_OF_OPT
          OPT "A nějaké jiné možnosti?"
            GO more_missions
          END_OF_OPT
        END_OF_MENU
      END_OF_OPT
    END_OF_MENU
  END_OF_IF

  LABEL basic_route_agreed
  HideDlgScene
  RETURN

  LABEL more_missions
  Ask "Heh, nemyslím si, že bys měl[g(|a)] na víc."
  MENU
    OPT "Hmmm, asi máte pravdu. Budu lítat tu běžnou trasu."
      GO newbie_end
    END_OF_OPT
    OPT "Jsem si dost jist[g(ý|á)], že mám na víc."
      Say "Nějak[g(ý|á)] namyšlen[g(ý|á)], ne? Ale dobře, řekl[g(|a)] sis o to."
      Say "Je tu jiná cesta, do vzdálené hlídkové stanice."
      Delay 1000
      ChangeChar tutor campaign/tutor-scary.png
      Delay 500
      AskChar tutor "Plná nebezpečí a strašlivých věcí ta cesta jest."
      MENU
        OPT "Řekněte mi něco o těch nebezpečích."
          Say "Prostě nebezpečí a strašlivé věci jsou to."
          Say "Otázek více já nezodpovím ti, [g(mladý padawane|mladá padawanko)]."
          Ask "Příliš mnoho řekl jsem již."
          MENU
            OPT "Ale no tak!"
              Delay 250
              ChangeChar tutor campaign/tutor.png
              Delay 250
              Say "Dobře, jsou tam nějaký ošklivý meteory. Spokojen[g(ej|á)]?"
              Say "A teď zmiz. Za chvíli mi dovezou pizzu."
              GO more_missions1
            END_OF_OPT
          END_OF_MENU
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL more_missions1
  HideChar tutor
  ResetFocus btutor
  Delay 250
  ShowNode outpost
  CenterMapByNode outpost
  Delay 500
  ShowPath "R1-2a"
  ShowPath "R1-2b"
  CenterMapByPath "R1-2a"
  Delay 500
  SET tutor_moremissions

  LABEL newbie_end
  HideDlgScene
  RETURN
END_OF_SCRIPT

SCRIPT embassyDlg

  ShowDlgScene
  IF purpleEscortFinished
    ShowChar ambassador left
    Delay 250
    ChangeChar ambassador campaign/ambassador-happy.png
    Delay 100
    Say "Děkujeme vám za vaši ochotu. Velmi nám to pomohlo."
    Say "Přijměte prosím odměnu 10 kreditů. Je to z našeho fondu pro neočekávané výdaje neznámým [g(cizincům|cizinkám)]."
    AddCredits 10
    Announce "Získal[g(|a)] jsi 10 kreditů."
    Delay 250
    HideButton bambassador
    HideChar ambasador
    HideDlgScene
    RETURN
  END_OF_IF
  IF purpleAccepted
    ShowChar ambassador left
    Delay 250
    Say "Promiňte, ale ta mise spěchá."
    Say "Nemůžeme si dovolit žádné problémy s mimozemšťanama z Th'kzw. Je to velmi bojovná rasa."
    Delay 250
    HideChar ambassador
    HideDlgScene
    RETURN
  END_OF_IF
  IFNOT wasAtEmbassy
    SET wasAtEmbassy
    ChangeChar ambassador campaign/ambassador-peek.png
    ShowChar ambassador right
    Delay 250
    Ask "Pššt, [g(pane|slečno)]. Mohl[g(|a)] byste mě prosím následovat do této tmavé chodby?"
    MENU
      OPT "Jasně, proč ne?"
        GO corridor
      END_OF_OPT
      OPT "Maminka mi vždycky říkala, ať nechodím s neznámýma lidma do tmavých chodeb."
        Ask "Promiňte, ale je to velmi delikátní záležitost. Nemůžeme o ní mluvit na veřejnosti."
        MENU
          OPT "No tak dobře."
            GO corridor
          END_OF_OPT
          OPT "Ne a ne a ne!"
            SET corridorRefused
            GO refused
          END_OF_OPT
        END_OF_MENU
      END_OF_OPT
    END_OF_MENU
  ELSE
    IF purpleOfferMade
      ChangeChar ambassador campaign/ambassador.png
      ShowChar ambassador left
      Delay 250
      Ask "Ano?"
      MENU
        OPT "Pořád potřebujete doprovodit pana... víte koho... víte kam?"
          Ask "Ano. Nabízíte se, že to uděláte?"
          MENU
            OPT "Jasně, počítejte se mnou."
              GO acceptPurple
            END_OF_OPT
            OPT "Ani ne, jen mě to zajímalo."
              GO refused
            END_OF_OPT
          END_OF_MENU
        END_OF_OPT
      END_OF_MENU
    ELSE
      ChangeChar ambassador campaign/ambassador-peek.png
      ShowChar ambassador right
      Delay 250
      Say "Ano?"
      MENU
        OPT "Rozmyslel[g(|a)] jsem si to. Pojďme do té temné chodby."
          GO corridor
        END_OF_OPT
        OPT "Jen se procházím, nevšímejte si mě."
          GO refused
        END_OF_OPT
      END_OF_MENU
    END_OF_IF
  END_OF_IF

  LABEL corridor
  Say "Výborně, děkuji vám."
  Delay 250
  HideChar ambassador
  Delay 1500
  ChangeChar ambassador campaign/ambassador.png
  ShowChar ambassador left
  Delay 250
  Say "Takže... Máme tu takovou delikátní situaci. Nedávno tu byla delegace z Th'kzw-r. Když odlétali, omylem zde zapomněli jednoho ze svých čelních představitelů."
  Ask "Byl[g(|a)] byste tak laskav[g(|a)] a dopravil[g(|a)] pana K'wwfrg na jeho domovskou planetu?"
  SET purpleOfferMade
  MENU
    OPT "Jasně, vezmu pana K'wwfrg na Th'kzw-r, nevidím v tom problém."
      GO acceptPurple
    END_OF_OPT
    OPT "Proč si na to nenajdete nějakou úžasnou diplomatickou loď?"
      Ask "No, on se pan K'wwfrg cítí celou situací trochu zahanben. Raději bychom to udělali tiše a diskrétně."
      MENU
        OPT "Dobrá, počítejte se mnou."
          GO acceptPurple
        END_OF_OPT
        OPT "Asi byste raději měli na tu práci najít někoho jiného."
          GO refused
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL acceptPurple
  Say "Oh, to je úžasné. Děkuji vám."
  Delay 250
  ChangeChar ambassador campaign/ambassador-happy.png
  Delay 250
  Say "Pane K'wwfrg?"
  Delay 1000
  ShowChar purpleDipl right
  Delay 250
  SayChar ambassador "[g(Tento džentlmen|Tato milá dáma)] vás doprovodí domů."
  Delay 250
  AskChar purpleDipl "Wzrr kh'gwh qt Th'kzw-r!"
  MENU
    OPT "Není zač."
      Delay 250
      HideChar purpleDipl
      Delay 500
      ChangeChar ambassador campaign/ambassador.png
      Delay 250
      SayChar ambassador "Ještě jednou vám velmi děkuji. Jakmile se vrátíte, odměníme se vám."
      SET purpleAccepted
      ShowNode "purpleHome"
      ShowPath "U1-5purple"
      SET purpleAccepted
      ResetFocus bambassador
      IF VIPTried
        HideButton bvip
        ShowButton bvip2
      END_OF_IF
      HideChar ambassador
      HideDlgScene
      RETURN
    END_OF_OPT
  END_OF_MENU

  LABEL refused
  Say "No... co se dá dělat. Pak tedy příjemný den[g(, pane|)]."

  LABEL sceneEnd
  Delay 1000
  HideChar ambassador
  HideDlgScene
  RETURN
END_OF_SCRIPT

SCRIPT barDlg

  ShowDlgScene
  ShowChar bartender left
  Ask "Dobrý den. Jak mohu sloužit?"
  IF boozeQuestHeard
    MENU
      OPT_IF mechDebtPending "Jedna mechanička mě sem posílá."
        GO bar_debt
      END_OF_OPT
      OPT_IF boozeQuestTried "Nepotřebujete doručit další zásilku?"
        GO booze_quest
      END_OF_OPT
      OPT_IF boozeQuestRefused "Stále potřebujete doručit zásilku?"
        GO booze_quest
      END_OF_OPT
      OPT "Dneska tady chci roztočit nějaké peníze! Váš nejlepší drink, prosím."
        GO drinking
      END_OF_OPT
      OPT "Ach, pardon. Vlezl[g(|a)] jsem do vašeho baru omylem."
        Say "To je v pořádku. Přeji hezký den."
        GO leaving_bar
      END_OF_OPT
    END_OF_MENU
  END_OF_IF
  MENU
    OPT_IF mechDebtPending "Jedna mechanička mě sem posílá."
      GO bar_debt
    END_OF_OPT
    OPT "Namíchejte mi váš nejlepší koktejl."
      Ask "Ovšem. Bude to 1 kredit."
      IF_HAS_CREDITS 1
        MENU
          OPT "Můžu si to dovolit. Vydělal[g(|a)] jsem cestou nějaký prachy."
            Say "Děkuji vám."
            AddCredits -1
            Announce "Zaplatil[g(|a)] jsi 1 kredit."
            GO anything_else
          END_OF_OPT
          OPT "To je dost drahé. Raději mizím."
            Say "V pořádku. Přeji hezký den."
            GO leaving_bar
          END_OF_OPT
        END_OF_MENU
      ELSE
        MENU
          OPT "Ééé... Zapomeňte na to. Zrovna u sebe nemám peníze."
            Say "Jistě. Přeji hezký den."
            GO leaving_bar
          END_OF_OPT
        END_OF_MENU
      END_OF_IF
    END_OF_OPT
    OPT "Jen tak se rozhlížím. Máte hezký bar."
      GO nice_bar
    END_OF_OPT
    OPT "Tohle je ale špinavá díra! Raději vypadnu."
      Say "V pořádku. Přeji hezký den."
      GO leaving_bar
    END_OF_OPT
  END_OF_MENU
  HideChar programmer
  HideDlgScene

  LABEL anything_else
  Say "Něco dalšího?"
  MENU
    OPT "To se chci zeptat já vás. Nic dalšího? Nějaký úkol nebo tak něco?"
      Say "Promiňte?"
      MENU
        OPT "Právě jsem utratil[g(|a)] herní peníze za virtuální drink. To by mělo vést k úkolu, ne?"
          Delay 200
          ChangeChar bartender campaign/bartender-close.png
          Delay 200
          Say "Ach, už rozumím. To jste si vybral[g(|a)] špatnou větev dialogu."
          Say "K úkolu vedla ta 'jen tak se rozhlížím'. A byla zadarmo."
          Delay 200
          ChangeChar bartender campaign/bartender.png
          Delay 200
          Say "Ještě něco?"
          MENU
            OPT_IFNOT bar_quest_insist "Já chci úkol!"
              SET bar_quest_insist
              Ask "Je mi velmi líto, ale vedeme tu jen nápoje. Přejete si ještě něco?"
            END_OF_OPT
            OPT "Ještě jeden drink!"
              GO drinking
            END_OF_OPT
            OPT "Ani ne, to je dobrý. Jen tak se rozhlížím. Máte hezký bar."
              GO nice_bar
            END_OF_OPT
          END_OF_MENU
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL drinking
  IF_HAS_CREDITS 1
    Say "Už se to nese... Jeden kredit, prosím."
    AddCredits -1
    Announce "Zaplatil[g(|a)] jsi jeden kredit."
    MENU
      OPT "*Polk*"
        Ask "Ještě něco?"
        MENU
          OPT "Hele, víš ty co? Nalej mi další..."
            GO drinking
          END_OF_OPT
          OPT "*Škyt* ... Myslím, že už jsem měl dost. Raději půjdu."
            Say "Děkujeme za návštěvu. Hezký den."
            GO leaving_bar_in_shame
          END_OF_OPT
        END_OF_MENU
      END_OF_OPT
    END_OF_MENU
  ELSE
    Say "Jeden kredit, prosím... Ach, vidím, že vám došly peníze!"
    Say "Můžete si vzít toto lízátko. Na účet podniku. A nyní prosím tiše odejděte."
    GO leaving_bar_in_shame
  END_OF_IF

  LABEL nice_bar
  Say "Děkuji vám. Je to jen odlesk bývalé slávy tohoto podniku."
  MENU
    OPT "Bývalé?"
      Say "Obchody v poslední době moc nejdou. Obávám se, že to tu budeme muset brzy zavřít."
      Say "Mám velké zásoby těch nejvybranějších nápojů. Ale truckeři dnes dávají přednost levnému pití."
      Ask "Měl jsem si otevřít bar raději na Regule. Tamní lidé dokážou ocenit kvalitu."
      MENU
        OPT "Proč neprodáte něco ze svých zásob na Regulu?"
          SET boozeQuestHeard
          Say "To není tak jednoduché. Víte, je to dost nebezpečná cesta pro tak křehké zboží."
          Ask "Pochybuju, že bych našel truckera ochotného doručit je na Regulu."
          MENU
            OPT "Zrovna jste [g(jednoho|jednu truckerku)] našel!"
              GO booze_quest
            END_OF_OPT
            OPT "Také pochybuju. Tak hodně štěstí, já mizím."
              SET boozeQuestRefused
              Say "Děkuji. Hezký den."
              GO leaving_bar
            END_OF_OPT
          END_OF_MENU
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL leaving_bar_in_shame
  Delay 200
  ChangeChar bartender campaign/bartender-close.png
  Delay 200
  Say "Ach... A byl[g(|a)] byste tak hodn[g(ý|á)] a odeš[g(el|la)] zadním vchodem? Děkuji..."

  LABEL leaving_bar
  Delay 300
  HideChar bartender
  HideDlgScene
  RETURN

  LABEL booze_quest
  Delay 200
  ChangeChar bartender campaign/bartender-close.png
  Delay 200
  Say "Ach, vy byste byl[g(|a)] tak laskav[g(|a)]?"
  Ask "Kolik beden nápojů byste byl[g(|a)] ochot[g(en|na)] doručit?"
  MENU
    OPT "Dejte mi dvě bedny. Bez problému je doručím."
      HidePath "R1-3a"
      ShowPath "R1-3a2"
      Say "Jste neobyčejně laskav[g(|a)]."
      Say "Bedny budou připraveny ve skladišti."
      GO quest_agreed
    END_OF_OPT
    OPT "No, mám jen malou loď. Víc jak jednu bednu bych nebral[g(|a)]."
      HidePath "R1-3a"
      ShowPath "R1-3a1"
      Say "Jste velmi laskav[g(|a)]."
      Say "Bedna bude připravena ve skladišti."
      GO quest_agreed
    END_OF_OPT
    OPT "Hele, víte co? Zapomeňme na to. To zboží je moc křehké."
      SET boozeQuestRefused
      RESET boozeQuestTried
      Say "Hmm. Asi máte pravdu. Přeji hezký den."
      GO leaving_bar
    END_OF_OPT
  END_OF_MENU

  LABEL quest_agreed
  ResetFocus bbartender
  SET boozeQuestAccepted
  RESET boozeQuestRefused
  Delay 300
  ChangeChar bartender campaign/bartender.png
  Say "Ještě jednou vám děkuji a přeji hezký den!"
  GO leaving_bar

  LABEL bar_debt
  IF debtRefused
    ChangeChar bartender campaign/bartender-close.png
    Ask "Přiš[g(el|la)] jste vyrovnat její dluh, že?"
  ELSE
    Say "Ach, tu si vybavuji. Měla velmi pěkné a zapamatovatelné jméno."
    Say "Také ta oslava, kterou zde uspořádala, byla nezapomenutelná."
    Say "Bohužel, stejně tak i částka, kterou zde utratila."
    ChangeChar bartender campaign/bartender-close.png
    Ask "Předpokládám, že vás poslala vyrovnat ten dluh."
  END_OF_IF
  MENU
    OPT "Ano, proto tu jsem."
      IF_HAS_CREDITS 21
        SET hasEnoughForDebt
      END_OF_IF
      Ask "Výborně. Pokud si dobře pamatuju, bylo to 21 kreditů."
      MENU
        OPT_IFNOT hasEnoughForDebt "Sakryš. Zdá se, že jsem si dnes zapomněl[g(|a)] peněženku."
          GO bar_debt_refused
        END_OF_OPT
        OPT_IF hasEnoughForDebt "Tady je máte."
          RESET mechDebtPending
          Delay 300
          AddCredits -21
          Announce "Zaplatil[g(|a)] jsi 21 kreditů."
          ChangeChar bartender campaign/bartender.png
          ResetFocus bbartender
          Ask "Děkuji. Přeji vám hezký den."
          MENU
            OPT "Já vám taky. Nashledanou."
              UnlockAchievement camp_honesty
              GO leaving_bar
            END_OF_OPT
            OPT "Počkat – to je všechno? Jen děkuji?"
              EXIT_MENU
            END_OF_OPT
          END_OF_MENU
          Ask "Co jste čekal[g(|a)], že se stane? Dluh je splacen, vše je v pořádku."
          MENU
            OPT "Nevím... Nějakou odměnu. Nebo aspoň nový úkol."
              Say "Ach, tak to je smutné. O nic takového tu nešlo."
              Say "Ale podívám se, co pro vás mohu udělat. Co takhle achievement?"
              UnlockAchievement camp_honesty
              Delay 1500
              Say "Doufám, že takto vám to stačí."
              Say "Přeji hezký den."
              GO leaving_bar
            END_OF_OPT
          END_OF_MENU
        END_OF_OPT
        OPT_IFNOT debtConfusion "Počkat! Říkala 20 kreditů."
          SET debtConfusion
          ChangeChar bartender campaign/bartender.png
          Ask "Ne, zcela jistě to bylo 21. Pořád tu někde mám ten účet."
        END_OF_OPT
        OPT "Hm, když o tom tak přemýšlím, musel to být někdo jiný. (Ty prachy si nechám.)"
          GO bar_debt_refused
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
    OPT "Hm, když o tom tak přemýšlím, musel to být někdo jiný. (Ty prachy si nechám.)"
      GO bar_debt_refused
    END_OF_OPT
  END_OF_MENU

  LABEL bar_debt_refused
  SET debtRefused
  Delay 500
  Say "Ach tak. Rozumím. Co se dá dělat."
  ChangeChar bartender campaign/bartender.png
  Say "Přeji vám hezký den."
  GO leaving_bar
END_OF_SCRIPT

SCRIPT regulaDlg

  ShowDlgScene
  ShowChar clerk left
  Delay 1200
  ChangeChar clerk campaign/clerk-frown.png
  Delay 500
  IF class3
    Ask "Zase vy? Co chcete teď?"
    MENU
      OPT "Um, radši nic."
        GO leaveClerk
      END_OF_OPT
      OPT "A jak je to s licencí pro lodě třídy IV?"
        ChangeChar clerk campaign/clerk.png
        Ask "Taková třída neexistuje. Jak jste na to vůbec přiš[g(el|la)]?"
        MENU
          OPT_IFNOT gtabe "Hmm, omlouvám se. Asi z Dalšího velkého rozšíření Galaxy Truckera."
            SET gtabe
            Ask "Netuším, o čem mluvíte. Třída IV není."
          END_OF_OPT
          OPT "No dobře. Ale co mám jako teď dělat?"
            Say "To není moje starost. Nejsem vaše chůva."
            Delay 300
            ChangeChar clerk campaign/clerk-frown.png
            Say "Jděte se zeptat svého učitele, když si neumíte poradit [g(sám|sama)]..."
            GO leaveClerk
          END_OF_OPT
        END_OF_MENU
      END_OF_OPT
    END_OF_MENU
  END_OF_IF
  IF class3Asked
    Say "Zase vy? Už jsem vám řekl, že třída III není pro vás!"
    ChangeChar clerk campaign/clerk.png
    Ask "Nebo snad máte 450 kreditů? Máte souhlas dvou mimozemských ras?"
    RESET oneApproval
    RESET noApproval
    IF brownLicenseApproval
      IF purpleLicenseApproval
        SET twoApprovals
      ELSE
        SET oneApproval
      END_OF_IF
    ELSE
      IF purpleLicenseApproval
        SET oneApproval
      ELSE
        SET noApproval
      END_OF_IF
    END_OF_IF
    IF_HAS_CREDITS 450
      MENU
        OPT_IF twoApprovals "Jasně, že mám. Co jste si myslel?"
          ChangeChar clerk campaign/clerk.png
          Delay 200
          Say "Opravdu? Ukažte..."
          Delay 500
          ChangeChar clerk campaign/clerk-frown.png
          Delay 1500
          ChangeChar clerk campaign/clerk.png
          Delay 300
          Say "Ty dokumenty vypadají pravé. A skutečně máte i ty peníze."
          Delay 300
          Say "Ehm..."
          Delay 300
          Say "V tom případě mi prosím dovolte udělit vám tuto prestižní licenci na lodě třídy III, [g(pane|slečno)]."
          AddCredits -450
          Announce "Zaplatil[g(|a)] jsi 450 kreditů. Odemčena licence na lodě třídy III."
          UnlockAchievement camp_class3
          Delay 500
          SUB_SCRIPT class3_inits
          ResetFocus bclerk
          GO afterUnlock
        END_OF_OPT
        OPT_IF oneApproval "Peníze nejsou problém. Ale pořád mi chybí jedno povolení."
          Say "Dokud nemáte všechno v pořádku, je to, jako byste neměl[g(|a)] nic."
          Say "A teď běžte pryč. Mám hromadu práce."
          GO leaveClerk
        END_OF_OPT
        OPT_IF noApproval "Mám ty peníze!"
          Say "To se máte. Užijte si je. Ale tady si za ně nic nekoupíte, když nemáte souhlas mimozemských ras."
          Say "A teď běžte pryč. Mám hromadu práce."
          GO leaveClerk
        END_OF_OPT
      END_OF_MENU
    ELSE
      MENU
        OPT_IF twoApprovals "Mám obě povolení."
          Say "Jasně, ale je to jen pár cárů papíru, pokud nemáte peníze."
          Say "A teď běžte pryč. Mám hromadu práce."
          GO leaveClerk
        END_OF_OPT
        OPT_IF oneApproval "Zatím nemám nic než jedno povolení."
          Say "A cítíte potřebu mi to oznámit? Že jako nemám lepší práci."
          GO leaveClerk
        END_OF_OPT
        OPT_IF noApproval "Hm, pracuju na tom."
          Say "Ne, nepracujete na tom. Teď právě jste zde a plýtváte mým časem."
          GO leaveClerk
        END_OF_OPT
      END_OF_MENU
    END_OF_IF
  END_OF_IF
  IF class2
    Ask "Ach, to jste vy. Už máte licenci na lodě třídy II, tak co tu zase chcete?"
    MENU
      OPT "Um, radši nic."
        GO leaveClerk
      END_OF_OPT
      OPT "A jak je to s licencí pro lodě třídy III?"
        Say "Lodě třídy III? Tak na to zapomeňte."
        ChangeChar clerk campaign/clerk.png
        Say "Na lodě třídy III jsme zavedli opravdu přísné regulace."
        Say "Nejen, že licence stojí 450 kreditů – navíc potřebujete podepsaný souhlas dvou různých mimozemských ras."
        Say "Promiňte, že to říkám tak otevřeně, [g(pane|slečno)], ale k jejich získání vám prostě chybí diplomatické schopnosti."
        SET class3Asked
        ResetFocus bclerk
        IF purpleUnlocked
          SetFocus bpurple
        END_OF_IF
        IF brownUnlocked
          ShowButton bmerch2
          SetFocus bmerch2
        END_OF_IF
        Say "A teď prosím odejděte. Mám práci."
        GO leaveClerk
      END_OF_OPT
    END_OF_MENU
  END_OF_IF
  Ask "Co chcete?"
  MENU
    OPT "Um, radši nic."
      GO leaveClerk
    END_OF_OPT
    OPT "Rád[g(|a)] bych dostal[g(|a)] licenci na lodi třídy II."
      ResetFocus bclerk
      SetFocusIfCredits bclerk 100
      Ask "Licence stojí 100 kreditů."
      RESET tooMuchFor2
      RESET whereToGetSoMuch
      RESET hasFor2
      RESET hasFewer
      IF_HAS_CREDITS 100
        SET hasFor2
      ELSE
        SET tooMuchFor2
      END_OF_IF
      IF_HAS_CREDITS 50
        SET hasFewer
        IF clerkBribe
          IF clerkDiscount
            RESET hasFewer
          END_OF_IF
        END_OF_IF
      END_OF_IF
      MENU
        OPT_IF hasFor2 "Jasně. Tady jsou peníze a dejte mi tu licenci."
          Delay 100
          ChangeChar clerk campaign/clerk.png
          Delay 300
          Say "Ach, vy opravdu máte tolik peněz?"
          Say "V tom případě, tady je vaše licence, [g(pane|milostivá)]."
          Delay 300
          AddCredits -100
          Announce "Zaplatil[g(|a)] jsi 100 kreditů. Odemčena licence na lodě třídy II."
          UnlockAchievement camp_class2
          Delay 500
          SUB_SCRIPT class2_inits
          HideButton bclerk
          GO afterUnlock
        END_OF_OPT
        OPT_IF tooMuchFor2 "Och, ale... Ale... To je hrozně moc!"
          RESET tooMuchFor2
          SET whereToGetSoMuch
          Delay 300
          ChangeChar clerk campaign/clerk.png
          Say "Je to přesně tolik, kolik to má být."
          Delay 500
          ChangeChar clerk campaign/clerk-frown.png
          Ask "Nepotřebujeme, aby se nám po galaxii proháněl kdejakej žebrák."
        END_OF_OPT
        OPT_IF whereToGetSoMuch "Kde mám jako sehnat tolik peněz?"
          RESET whereToGetSoMuch
          Say "To je vaše věc. Například já si vydělávám peníze velmi tvrdou prací."
          Say "Sedím v této kanceláři. Čtyři hodiny denně (jen s hodinovou přestávkou na oběd), tři dny v týdnu."
          Say "Pokud vím, truckeři prostě jen poletují kolem, znovu a znovu, dokud se štěstěna nesmiluje a nedá jim vydělat dost peněz."
          Ask "A pak je všechny zaplatí zde, za můj podpis na tomto formuláři."
        END_OF_OPT
        OPT_IF hasFewer "Dám vám 50 a bude to vyřízeno, dobře?"
          Delay 900
          Ask "Snažíte se mě podplatit?"
          MENU
            OPT_IFNOT clerkDiscount "Ne, snažím se vyjednat slevu."
              Delay 300
              ChangeChar clerk campaign/clerk.png
              Say "Podívejte, jsme na Regule."
              Say "Za posledních 200 let zde nebyl zaznamenán jediný případ slevy."
              Delay 200
              ChangeChar clerk campaign/clerk-frown.png
              Delay 300
              Say "Můžu se ale podívat, jestli se na váš případ nevztahují nějaké příplatky, pokud chcete."
              Ask "Nebo prostě zaplaťte těch 100 kreditů."
              SET clerkDiscount
              EXIT_MENU
            END_OF_OPT
            OPT_IFNOT clerkBribe "A když řeknu, že ano?"
              Delay 300
              ChangeChar clerk campaign/clerk.png
              Say "Jsme na Regule, víte?"
              Say "Uplácení tu má velmi přísná pravidla a směrnice."
              Say "Pochybuju, že vůbec víte, které formuláře musíte pro standardní pokus o uplácení vyplnit."
              Say "Na to prostě zapomeňte. Umění korupce zde bylo během staletí vybroušeno k dokonalosti."
              Delay 200
              ChangeChar clerk campaign/clerk-frown.png
              Delay 300
              Ask "Amatér nemá šanci. Musíte zaplatit 100 kreditů."
              SET clerkBribe
              EXIT_MENU
            END_OF_OPT
            OPT "Ehm, zapomeňte prosím, že jsem to řekl[g(|a)]."
              Ask "Dobře. A máte těch 100 kreditů?"
              EXIT_MENU
            END_OF_OPT
          END_OF_MENU
        END_OF_OPT
        OPT "Dobrá, zvážím to a případně se vrátím. Nashledanou."
          GO leaveClerk
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL afterUnlock
  MENU
    OPT "Tak jo. Mizím."
      GO leaveClerk
    END_OF_OPT
    OPT "Děkuji, jste velice milý."
      GO leaveClerk
    END_OF_OPT
  END_OF_MENU

  LABEL leaveClerk
  Delay 500
  ChangeChar clerk campaign/clerk-frown.png
  Delay 500
  HideChar clerk
  HideDlgScene
  RETURN
END_OF_SCRIPT

END_OF_SCRIPTS
