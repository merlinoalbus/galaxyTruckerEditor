SCRIPTS

SCRIPT forgeDlg

  ShowDlgScene
  ShowChar foreman left
  Delay 750
  IF ingotsPending
    Say "Co tu ještě děláš, [g(chlape|ženská)]?"
    ChangeChar foreman campaign/foreman-no.png
    Say "V kosmické továrně čekají na ty ingoty!"
    GO forgeEnd
  END_OF_IF
  IF explosivesPending
    Say "Ty jsi ještě tady, [g(chlape|ženská)]?"
    ChangeChar foreman campaign/foreman-no.png
    Say "Na tom asteroidu stále čekají na své trhaviny!"
    IFNOT explosivesFailed
      GO forgeEnd
    END_OF_IF
    IF toMine0
      GO forgeEnd
    END_OF_IF
    MENU
      OPT "Jasně. Měl[g(|a)] jsem s tím nějaké problémy. Jdu to zkusit znova."
        ChangeChar foreman campaign/foreman.png
        Say "Dobrá."
        GO forgeEnd
      END_OF_OPT
      OPT "No, je to dost trhavin. Co nějakou jednodušší zakázku?"
        ChangeChar foreman campaign/foreman.png
        RESET explosivesFailed
        IF toMine2
          RESET toMine2
          HidePath U2-16a
          Ask "A co takhle doručit čtyři balení trhavin na gamu?"
          MENU
            OPT "Dobrá."
              SET toMine1
              ShowPath U2-15a
              EXIT_MENU
            END_OF_OPT
            OPT "No, pořád je to trochu moc."
              SET toMine0
              ShowPath U2-14a
              Say "Tak dovezeš dvě balení na deltu. A už nechci slyšet žádné výmluvy."
              EXIT_MENU
            END_OF_OPT
          END_OF_MENU
          EXIT_MENU
        ELSE
          RESET toMine1
          HidePath U2-15a
          SET toMine0
          ShowPath U2-14a
          Say "Dobrá. Tak tedy dovez dvě balení na deltu. Jednoduší práci tu nemám."
          EXIT_MENU
        END_OF_IF
      END_OF_OPT
    END_OF_MENU
    GO forgeEnd
  END_OF_IF
  IF forgeAccepted
    ChangeChar foreman campaign/foreman-hello.png
    Say "Ha, [g(můj trucker|moje truckerka)]. Výborně."
    ChangeChar foreman campaign/foreman-smile.png
    Ask "Připraven[g(|a)] na další štreku?"
    MENU
      OPT_IFNOT ingotsAccepted "Můžu se vypravit do kosmické továrny."
        GO forgeIngots
      END_OF_OPT
      OPT_IF ingotsAccepted "Můžu dovézt další ingoty do kosmické továrny."
        GO forgeIngotsCont
      END_OF_OPT
      OPT_IFNOT explosivesAccepted "Můžu se vypravit na důlní asteroidy."
        GO forgeExplosives
      END_OF_OPT
      OPT_IF explosivesAccepted "Můžu dovézt další trhaviny na důlní asteroidy."
        GO forgeExplosivesCont
      END_OF_OPT
      OPT "Teď se mi to nehodí, někdy příště."
        ChangeChar foreman campaign/foreman-whatsup.png
        Say "Jasně. I tak díky."
        GO forgeEnd
      END_OF_OPT
    END_OF_MENU
  END_OF_IF
  IF forgeOffered
    Ask "To jsi ty! Rozmyslel[g(|a)] sis to?"
    MENU
      OPT "Ano. Poletím do kosmické továrny."
        Say "Výborně."
        GO forgeIngots
      END_OF_OPT
      OPT "Ano. Poletím na důlní asteroidy."
        Say "Výborně."
        GO forgeExplosives
      END_OF_OPT
      OPT_IFNOT mineUnlocked "Ne. Ale rád[g(|a)] bych znal[g(|a)] cestu na důlní asteroidy."
        SET mineUnlocked
        Say "Ach. Hmmm. Jasně."
        Say "Pomoci nechce."
        Say "Ale požádat o radu se neostýchá."
        Say "Ale jak chceš. Tudy."
        Delay 500
        ShowNode mine0
        Delay 500
        ShowPath R2-14a
        Delay 500
        Say "Ale budeš se divit. He he."
        Say "A teď zmiz."
        GO forgeEnd
      END_OF_OPT
      OPT "Ne. Jen tak prudím."
        ChangeChar foreman campaign/foreman-no.png
        Say "Zmiz, [g(chlape|ženská)]."
        Say "Nebo se neznám."
        GO forgeEnd
      END_OF_OPT
    END_OF_MENU
  END_OF_IF
  Ask "No to je dost, že jsi tady!"
  MENU
    OPT "To mluvíte na mne?"
      Say "Na koho asi..."
      Say "Jsi [g(trucker|truckerka)], že?"
      GO forgeExpl
    END_OF_OPT
    OPT "Vy mě tu očekáváte?"
      Say "No samozřejmě!"
      GO forgeExpl
    END_OF_OPT
    OPT "Taky si říkám."
      Ask "Dobře. Tak dost řečí a do práce."
      MENU
        OPT "Ehm, do práce?"
          Say "Jasně že do práce."
          GO forgeExpl
        END_OF_OPT
        OPT "Jasně, jdeme na to..."
          ChangeChar foreman campaign/foreman-smile.png
          Say "Tak se mi to líbí!"
          GO forgeExpl
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL forgeExpl
  SET forgeOffered
  Say "Je nejvyšší čas. Žádal jsem na Regule o truckery už před čtrnácti dny."
  Say "Zásilky se mi tu hromadí a na důlních asteroidech i v kosmické továrně na ně marně čekají."
  ChangeChar foreman campaign/foreman-smile.png
  Ask "Tak co, kam prvně?"
  MENU
    OPT "Třeba do kosmické továrny..."
      Say "Fajn."
      GO forgeIngots
    END_OF_OPT
    OPT "Třeba na důlní asteroidy..."
      Say "Fajn."
      GO forgeExplosives
    END_OF_OPT
    OPT "Nerad[g(|a)] vás zklamu, ale nejsem tu kvůli vašim zásilkám."
      Ask "Ach, ty nejsi ten můj trucker? A kdo teda jsi?"
      MENU
        OPT "Jsem [g(trucker|truckerka)], co tudy jen tak projíždí."
          ChangeChar foreman campaign/foreman-whatsup.png
          Ask "Hmmm... A nemohl[g(|a)] bys tu práci stejně vzít?"
          MENU
            OPT "To tedy v žádném případě!"
              ChangeChar foreman campaign/foreman.png
              Say "Hmmm... Tak co se dá dělat. Až se vrátíš na Regulu, připomeň jim, že čekám na truckery."
              Say "Na truckery, co se nebojí práce!"
              GO forgeEnd
            END_OF_OPT
            OPT "Tak jo. Kosmická továrna, říkáte?"
              ChangeChar foreman campaign/foreman-smile.png
              Say "Díky. Jsi [g(správnej chlap|správná ženská)]!"
              GO forgeIngots
            END_OF_OPT
            OPT "Tak jo. Důlní asteroidy, říkáte?"
              ChangeChar foreman campaign/foreman-smile.png
              Say "Díky. Jsi [g(správnej chlap|správná ženská)]!"
              GO forgeExplosives
            END_OF_OPT
          END_OF_MENU
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL forgeIngots
  IFNOT workshopInitialized
    Say "Vyrobili jsme megatěžké ingoty pro kosmickou továrnu a... Moment..."
    ChangeChar foreman campaign/foreman-list.png
    Say "Aha, tak nic."
    Say "Tady píšou, že kosmická továrna ještě není v provozu. Chybí lidi."
    Say "Prý mám poslat svoje truckery na Regulu na kosmodrom, aby pomohli dovézt lidi do kosmické továrny."
    ChangeChar foreman campaign/foreman.png
    Ask "A to jsem původně žádal o truckery já je. Ach jo."
    MENU
      OPT "Tak jo, poletím zpátky na Regulu."
        GO forgeEnd
      END_OF_OPT
      OPT "A co ty důlní asteroidy?"
        GO forgeExplosives
      END_OF_OPT
    END_OF_MENU
  END_OF_IF
  Say "Vyrobili jsme megatěžké ingoty."
  Say "Je potřeba je připojit k nějaké lodi a dovléct je ke kosmické továrně."
  ChangeChar foreman campaign/foreman-whatsup.png
  Ask "Musíš ale postavit loď, která má pořádný tah motorů!"
  MENU
    OPT "Dobrá. Jdu na to."
      ChangeChar foreman campaign/foreman-smile.png
      Say "Výborně, [g(chlape|ženská)]."
      SET ingotsAccepted
      SET ingotsPending
      SET oneingot
      SET ing3
      HideAllPaths forge workshop
      ShowPath U2-13a1
      GO forgeAcceptEnd
    END_OF_OPT
    OPT "Do toho se mi nechce."
      ChangeChar foreman campaign/foreman-smile.png
      Say "Hm... No dobře."
      Delay 300
      ChangeChar foreman campaign/foreman.png
      Delay 300
      Say "(Dnešní trukeři jsou pakáž líná!)"
      GO forgeEnd
    END_OF_OPT
  END_OF_MENU

  LABEL forgeIngotsCont
  IF threeingots
    IF ing2
      RESET ingots3small
      SET ingots3medium
    END_OF_IF
  ELSE
    IF twoingots
      IF ing2
        RESET ingots2small
        SET ingots2medium
        SET ingots3small
      END_OF_IF
      IF ing3
        RESET ingots2medium
        SET ingots2big
      END_OF_IF
    ELSE
      IF ing3
        SET ingots2small
        SET ingot1big
      END_OF_IF
      IF ing5
        RESET ingot1big
        SET ingot1bigger
      END_OF_IF
      IF ing6
        RESET ingot1bigger
        SET ingot1biggest
      END_OF_IF
    END_OF_IF
  END_OF_IF
  RESET ing2
  RESET ing3
  RESET ing4
  RESET ing5
  RESET ing6
  RESET ing7
  RESET ing8
  RESET oneingot
  RESET twoingots
  RESET threeingots
  ChangeChar foreman campaign/foreman-list.png
  Ask "Tak jo. Tady vidíš, co je potřeba doručit."
  MENU
    OPT_IF ingots2small "Vezmu dva menší ingoty."
      SET twoingots
      SET ing2
      EXIT_MENU
    END_OF_OPT
    OPT_IF ingots2medium "Vezmu dva střední ingoty."
      SET twoingots
      SET ing3
      EXIT_MENU
    END_OF_OPT
    OPT_IF ingots2big "Vezmu dva velké ingoty."
      SET twoingots
      SET ing4
      EXIT_MENU
    END_OF_OPT
    OPT_IF ingots3small "Vezmu tři menší ingoty."
      SET threeingots
      SET ing2
      EXIT_MENU
    END_OF_OPT
    OPT_IF ingots3medium "Vezmu tři střední ingoty."
      SET threeingots
      SET ing3
      EXIT_MENU
    END_OF_OPT
    OPT_IF ingot1big "Vezmu jeden větší ingot."
      SET oneingot
      SET ing5
      EXIT_MENU
    END_OF_OPT
    OPT_IF ingot1bigger "Vezmu ještě větší ingot."
      SET oneingot
      SET ing6
      EXIT_MENU
    END_OF_OPT
    OPT_IF ingot1biggest "Vezmu támhleten obří ingot."
      SET oneingot
      SET ing8
      EXIT_MENU
    END_OF_OPT
    OPT "Dneska asi spíš nic."
      GO forgeEnd
    END_OF_OPT
  END_OF_MENU
  ChangeChar foreman campaign/foreman-smile.png
  Say "Skvěle. Bude to nachystáno ve skladišti."
  HideAllPaths forge workshop
  IF oneingot
    ShowPath U2-13a1
  END_OF_IF
  IF twoingots
    ShowPath U2-13a2
  END_OF_IF
  IF threeingots
    ShowPath U2-13a3
  END_OF_IF
  SET ingotsPending
  GO forgeAcceptEnd

  LABEL forgeExplosives
  ChangeChar foreman campaign/foreman-smile.png
  Delay 300
  Say "Asi víš, že důlní asteroidy jsou plně automatizované?"
  Say "Potkáš tam výhradně roboty."
  Delay 300
  ChangeChar foreman campaign/foreman-list.png
  Delay 700
  Ask "Mám tu od nich objednávku na dva balíky trhavin."
  MENU
    OPT "Dobře, jdu do toho."
      IF mineUnlocked
        HideAllPaths forge mine0
        ShowPath U2-14a
        CenterMapByPath U2-14a
        Say "Skvěle. Cestu už znáš."
      ELSE
        Say "Skvěle. Vyznačím ti cestu."
        Delay 500
        ShowNode mine0
        Delay 500
        ShowPath U2-14a
        CenterMapByPath U2-14a
      END_OF_IF
      Delay 500
      Say "Balíky s trhavinami budeš mít připraveny ve skladišti."
      SET explosivesAccepted
      SET explosivesPending
      SET toMine0
      GO forgeAcceptEnd
    END_OF_OPT
    OPT_IFNOT forgeExcuseExpl "Ale já se bojím trhavin."
      Say "Však je můžeš dát na opačnej konec lodi než posádku, ne?"
      SET forgeExcuseExpl
      IF forgeExcuseBots
        SET forgeBothExcuses
      END_OF_IF
    END_OF_OPT
    OPT_IFNOT forgeExcuseBots "Ale já se bojím robotů."
      Say "To nemusíš. Neslyšel[g(|a)] jsi o sedmadvaceti zákonech robotiky?"
      Say "Hmmm... Původně vlastně byly jen tři ..."
      Say "Od té doby, co jsou mezi roboty i právníci, se to trochu zkomplikovalo."
      SET forgeExcuseBots
      IF forgeExcuseExpl
        SET forgeBothExcuses
      END_OF_IF
    END_OF_OPT
    OPT_IF forgeBothExcuses "Už mi došly výmluvy... Prostě se mi nechce."
      Say "Jasně. To jsem si myslel, další [g(povaleč|povalečka)]."
      Say "Tak zmiz."
      RESET forgeBothExcuses
      GO forgeEnd
    END_OF_OPT
  END_OF_MENU

  LABEL forgeExplosivesCont
  Ask "Skvěle. Tak co, na který asteroid se vydáš?"

  LABEL forgeQuerry
  RESET canAsk3
  IF mine3Initialized
    IFNOT smallestAsked
      SET canAsk3
    END_OF_IF
  END_OF_IF
  RESET toMine0
  RESET toMine1
  RESET toMine2
  MENU
    OPT_IFNOT mine1Initialized "Znám jenom jeden."
      Delay 300
      Say "Jasně, na deltu... Moment..."
      Delay 300
      ChangeChar foreman campaign/foreman-list.png
      Delay 500
      Say "Tam chtějí dvě bedny trhavin."
      SET toMine0
      EXIT_MENU
    END_OF_OPT
    OPT_IF mine1Initialized "Na ten velký."
      Say "Aha, delta... Moment..."
      Delay 300
      ChangeChar foreman campaign/foreman-list.png
      Delay 500
      Say "Tam chtějí dvě bedny trhavin."
      SET toMine0
      EXIT_MENU
    END_OF_OPT
    OPT_IF mine1Initialized "Na ten menší."
      Say "Na gamu? Počkej, podívám se..."
      Delay 300
      ChangeChar foreman campaign/foreman-list.png
      Delay 500
      Say "Tak ti si objednali čtyři bedny trhavin."
      SET toMine1
      EXIT_MENU
    END_OF_OPT
    OPT_IF mine2Initialized "Na ten ještě menší."
      Say "Beta, že? Moment, kde mám tu jejich objednávku..."
      Delay 300
      ChangeChar foreman campaign/foreman-list.png
      Delay 500
      Say "Tady. Chtějí šest beden trhavin."
      SET toMine2
      EXIT_MENU
    END_OF_OPT
    OPT_IF canAsk3 "Na ten úplně maličkej."
      Say "Eh, na alfě už těžba dávno skončila, tam nelítáme."
      Ask "Zkus nějaký jiný."
      SET smallestAsked
      RESET canAsk3
    END_OF_OPT
    OPT_IFNOT canAsk3 "Vlastně na žádnej."
      GO forgeEnd
    END_OF_OPT
  END_OF_MENU
  MENU
    OPT "Bez problémů, to zvládnu."
      Say "Fajn. Tak se vydej na cestu."
      SET explosivesPending
      IF toMine0
        ShowPath U2-14a
      END_OF_IF
      IF toMine1
        ShowPath U2-15a
      END_OF_IF
      IF toMine2
        ShowPath U2-16a
      END_OF_IF
      GO forgeAcceptEnd
    END_OF_OPT
    OPT_IFNOT minesWithoutDelivery "Jen pro zajímavost, dá se dostat na asteroidy i bez doručování čehokoliv?"
      SET minesWithoutDelivery
      Say "Kdepak. Tihle roboti mají rádi své soukromí."
      Say "Buďto tam máš nějakou práci, nebo bys je měl nechat na pokoji."
    END_OF_OPT
    OPT "Hmm... To nerisknu, cesta je moc nebezpečná."
      GO forgeEnd
    END_OF_OPT
  END_OF_MENU

  LABEL forgeAcceptEnd
  IFNOT class3
    IFNOT forgeAccepted
      SET forgeAccepted
    ELSE
      IFNOT class3Recommended
        SET class3Recommended
        Say "Kéž by bylo víc takových truckerů jako ty, [g(chlapče|děvče)]. Šikovných, co se nebojí práce."
        Ask "Jen tak mimochodem, už sis požádal[g(|a)] o licenci na lodě třídy III?"
        MENU
          OPT_IFNOT class3Asked "Zatím ne. Jak se to dělá?"
            Say "Samozřejmě na Regule. Až se tam příště stavíš, měl[g(|a)] bys to určitě udělat."
            Say "Ale teď šup do práce."
            SetFocus bclerk
            EXIT_MENU
          END_OF_OPT
          OPT_IF class3Asked "Požádal[g(|a)]. Ale víte co, není jednoduché ji získat."
            Say "Pche, ty to určitě zvládneš!"
            Say "Ale teď šup do práce."
            EXIT_MENU
          END_OF_OPT
        END_OF_MENU
      ELSE
        IFNOT class3Asked
          Say "Jen připomínám, že by sis vážně měl[g(|a)] na Regule požádat o licenci na lodě třídy III."
        END_OF_IF
      END_OF_IF
    END_OF_IF
  ELSE
    SET forgeAccepted
  END_OF_IF

  LABEL forgeEnd
  HideChar foreman
  HideDlgScene
  RETURN
END_OF_SCRIPT

SCRIPT sailorZoomOut

  Delay 200
  ChangeChar sailor campaign/sailor.png
  SET_TO sailor 4
END_OF_SCRIPT

SCRIPT sailorAnim

  Delay 200
  IF_IS sailor 4
    SET_TO sailor 0
  ELSE
    IF_MIN sailor 2
      IF_IS sailor 3
        IF_PROB 33
          SET_TO sailor 2
        ELSE
          IF_PROB 50
            SET_TO sailor 1
          ELSE
            SET_TO sailor 0
          END_OF_IF
        END_OF_IF
      ELSE
        IF_PROB 33
          SET_TO sailor 3
        ELSE
          IF_PROB 50
            SET_TO sailor 1
          ELSE
            SET_TO sailor 0
          END_OF_IF
        END_OF_IF
      END_OF_IF
    ELSE
      IF_IS sailor 1
        IF_PROB 33
          SET_TO sailor 3
        ELSE
          IF_PROB 50
            SET_TO sailor 2
          ELSE
            SET_TO sailor 0
          END_OF_IF
        END_OF_IF
      ELSE
        IF_PROB 33
          SET_TO sailor 3
        ELSE
          IF_PROB 50
            SET_TO sailor 2
          ELSE
            SET_TO sailor 1
          END_OF_IF
        END_OF_IF
      END_OF_IF
    END_OF_IF
  END_OF_IF
  IF_IS sailor 0
    ChangeChar sailor campaign/sailor0.png
  END_OF_IF
  IF_IS sailor 1
    ChangeChar sailor campaign/sailor1.png
  END_OF_IF
  IF_IS sailor 2
    ChangeChar sailor campaign/sailor2.png
  END_OF_IF
  IF_IS sailor 3
    ChangeChar sailor campaign/sailor3.png
  END_OF_IF
  Delay 200
END_OF_SCRIPT

SCRIPT treasureFound

  ShowDlgScene
  Announce "Naš[g(el|la)] jsi planetu označenou velkým červeným X."
  Delay 500
  Announce "Po chvíli hledání jsi vykopal[g(|a)] obrovskou starou černou truhlu."
  Delay 1000
  Announce "Otevřel[g(|a)] jsi víko..."
  Delay 1000
  Announce "A na dně truhly jsi naš[g(el|la)] 20 kreditů!"
  MENU
    OPT "Není to zrovna jmění, ale pořád lepší než nic."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  AddMissionCredits 20
  Delay 500
  UnlockAchievement camp_treasure
  Delay 500
  RESET treasurePlanet
  HideDlgScene
END_OF_SCRIPT

SCRIPT barGuestsDlg

  ShowDlgScene
  Delay 300
  IF sailorAvailable
    IF ratAvailable
      ShowChar sailor left
      Delay 300
      ShowChar rat right
      Delay 500
      MENU
        OPT "Přisednout k námořníkovi nalevo."
          Delay 500
          HideChar rat
          Delay 300
          GO sailorDlg
        END_OF_OPT
        OPT "Přisednout ke chlápkovi napravo."
          Delay 500
          HideChar sailor
          Delay 300
          GO ratDlg
        END_OF_OPT
        OPT "Je tu nějak plno, radši vypadnout."
          HideChar sailor
          Delay 100
          HideChar rat
          HideDlgScene
          RETURN
        END_OF_OPT
      END_OF_MENU
    ELSE
      ShowChar sailor left
      Delay 300
      GO sailorDlg
    END_OF_IF
  ELSE
    IF ratAvailable
      ShowChar rat right
      Delay 300
      GO ratDlg
    ELSE
      Announce "V baru je úplně prázdno."
      IF ratFinished
        IF sailorFinished
          HideButton bguests
        END_OF_IF
      END_OF_IF
      HideDlgScene
      RETURN
    END_OF_IF
  END_OF_IF

  LABEL sailorDlg
  SUB_SCRIPT sailorZoomOut
  FocusChar sailor
  Delay 1000
  IF sailorVisited
    SUB_SCRIPT sailorAnim
    Say "Arrr! Grrreenhorrrn[g(|ka)] se vrrrátil[g(|a)]."
    IF sailorFulfilled
      SUB_SCRIPT sailorAnim
      Ask "Tak co, už jsi naš[g(el|la)] poklad?"
      IF treasurePlanet
        MENU
          OPT "Zatím ne. Ale pracuju na tom."
            GO sailorEnd
          END_OF_OPT
        END_OF_MENU
      ELSE
        MENU
          OPT "Jasně. Díky. Koupím vám za to drink."
            SUB_SCRIPT sailorAnim
            Say "To není potřřřeba."
            SUB_SCRIPT sailorZoomOut
            Say "Jen jsem si řřříkal, že nebudu moct v klidu umřřřít, dokaváď ten poklad někdo nenajde."
            Delay 1500
            ChangeChar sailor campaign/sailor-dying.png
            Delay 800
            ChangeChar sailor campaign/sailor-dead.png
            Delay 2000
            HideChar sailor
            HideDlgScene
            RESET sailorAvailable
            SET sailorFinished
            RETURN
          END_OF_OPT
          OPT "Uhm... Zatím ne."
            SUB_SCRIPT sailorAnim
            Say "Tak to je oprrravdu škoda."
            GO sailorEnd
          END_OF_OPT
          OPT_IFNOT sailorRude "Do toho vám nic není."
            SUB_SCRIPT sailorAnim
            SUB_SCRIPT sailorAnim
            SUB_SCRIPT sailorAnim
            Say "Ale no tak... Toho kapitána, co poklad ztrrratil, to dost trrrápí."
            SET sailorRude
          END_OF_OPT
        END_OF_MENU
      END_OF_IF
    END_OF_IF
    SUB_SCRIPT sailorAnim
    Ask "Co, [g(chlapče|děvče)], už jsi viděl[g(|a)] aspoň sedm stanic?"
    IF_MIN visited 7
      RESET almostEnoughSeen
      IF_MIN visited 9
        SET moreSeen
      ELSE
        SET enoughSeen
      END_OF_IF
    ELSE
      IF_MIN visited 5
        SET almostEnoughSeen
      END_OF_IF
    END_OF_IF
    MENU
      OPT_IF moreSeen "Viděl[g(|a)] jsem jich více, než bys spočítal, starý vesmírný vlku."
        GO sailorSecret
      END_OF_OPT
      OPT_IF enoughSeen "Viděl[g(|a)], starý vesmírný vlku."
        GO sailorSecret
      END_OF_OPT
      OPT_IF almostEnoughSeen "Viděl[g(|a)] jsem jich hodně, ale sedm ještě ne, starý vesmírný vlku."
        SUB_SCRIPT sailorAnim
        Say "Tak co mě rrrušíš?"
        GO sailorEnd
      END_OF_OPT
      OPT "Jen jsem přiš[g(el|la)] pozdravit vašeho papouška."
        SUB_SCRIPT sailorAnim
        Say "Papouška? Ach tak, chceš pozdrrravit můj trrranslátorrr..."
        SUB_SCRIPT sailorAnim
        Say "Tak pozdrrravuj... třřřeba svou ledničku..."
        GO sailorEnd
      END_OF_OPT
    END_OF_MENU
  END_OF_IF
  SUB_SCRIPT sailorAnim
  Ask "Arrr! Co za grrravitacemilnou krrrysu zavítalo do našeho barrru?"
  MENU
    OPT_IFNOT sailorAnswer "Dobrý den. Jsem ve vesmíru nov[g(ý|á)]."
      SUB_SCRIPT sailorAnim
      Ask "To je jasné na prrrvní pohled. Tvůj skafandrrr je ještě cítit hlínou!"
      SET sailorAnswer
    END_OF_OPT
    OPT_IFNOT sailorAnswer "Náhodou, jsem kosmickým zářením křtěn[g(ej|á)] [g(trucker|truckerka)]."
      SUB_SCRIPT sailorAnim
      Ask "Arrr! To ani náhodou. Tvůj skafandrrr je ještě cítit hlínou!"
      SET sailorAnswer
    END_OF_OPT
    OPT "Pane, váš papoušek krásně mluví, ale je trochu drzý."
      SUB_SCRIPT sailorAnim
      Say "Jaký papoušek? Já nemám papouška..."
      Delay 1000
      SUB_SCRIPT sailorAnim
      Say "Ach tak, tohle... To není papoušek, to je pouhý telepatický trrranslátorrr."
      SUB_SCRIPT sailorAnim
      Say "Chladné a vlhké noci na kapitánském můstku si vyžádaly krrrutou daň."
      SUB_SCRIPT sailorAnim
      Ask "Při prrroplouvání rrrok trrrvající kosmickou bouřřří v sychrrravé mlhovině Aldebarrranu jsem ztrrratil hlas."
      MENU
        OPT "Ach, to je mi líto."
          SUB_SCRIPT sailorAnim
          Say "Nemusí. Skrrrz tento trrranslátorrr můžu mluvit, jak chci. A ani mě drrržka nebolí."
          GO sailorCont
        END_OF_OPT
        OPT "Dobře vám tak."
          SUB_SCRIPT sailorAnim
          Say "Jsi drrrz[g(ej|á)]. To se mi líbí."
          GO sailorCont
        END_OF_OPT
        OPT "Ale mlhovina přece neznamená, že..."
          SUB_SCRIPT sailorAnim
          Delay 200
          SUB_SCRIPT sailorAnim
          Delay 200
          SUB_SCRIPT sailorAnim
          Ask "... ?"
          MENU
            OPT "... to je fuk, zapomeňte na to."
              GO sailorCont
            END_OF_OPT
          END_OF_MENU
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL sailorCont
  SET sailorVisited
  SUB_SCRIPT sailorZoomOut
  Ask "Tak co tě přřřivádí ke stolu starrrého vesmírrrného vlka?"
  MENU
    OPT_IFNOT sailorButton "Jen tak, nuda."
      SUB_SCRIPT sailorAnim
      Say "Arrr. Vy mladí. Nevíte nic o tajemstvích vesmírrrru."
      EXIT_MENU
    END_OF_OPT
    OPT "Rád[g(|a)] bych poznal[g(|a)] něco z tajemství vesmíru."
      SUB_SCRIPT sailorAnim
      Say "Dobrrrá."
      EXIT_MENU
    END_OF_OPT
    OPT_IFNOT sailorButton "Bylo tam tlačítko, tak jsem na něj [v(ťapnul|klikl)][g(|a)]."
      SUB_SCRIPT sailorAnim
      Say "Ach, ta doba. Za mých mladých let jsme ve hrrrách žádná tlačítka neměli."
      SUB_SCRIPT sailorAnim
      Ask "Když jsme se chtěli porrrozhlédnout po barrru, museli jsme vyťukat 'PRRROHLEDNOUT BARRR'."
      SET sailorButton
    END_OF_OPT
  END_OF_MENU
  SUB_SCRIPT sailorAnim
  Say "Rrrrád bych ti něco prrrozrrradil."
  SUB_SCRIPT sailorAnim
  Say "Ale tobě by to bylo stejně na prrrd. Jsi ještě úpln[g(ej|á)] grrreenhorrrn[g(|ka)]."
  SUB_SCRIPT sailorAnim
  Say "Víš co? Přřřijď znovu, až uvidíš kus vesmírrru."
  SUB_SCRIPT sailorAnim
  Ask "Až navštívíš aspoň takovejch 7 stanic, prrrozrrradím ti starrré tajemství."
  IF_MIN visited 7
    MENU
      OPT "Podceňuješ mě, vesmírný vlku. Už jsem jich navštívil[g(|a)] dost."
        SUB_SCRIPT sailorAnim
        Say "Arrr! Přřřekvapuješ mě, [g(mladej|holka)]!"
        GO sailorSecret
      END_OF_OPT
    END_OF_MENU
  ELSE
    MENU
      OPT "Jasně. Nějaké obecné moudro, co zní dobře, ale k ničemu není, že?"
        SUB_SCRIPT sailorAnim
        Say "Ha, drrrzoun[g(e|ko)]. Náhodu, vím o starrrém vesmírrrném pokladu."
        EXIT_MENU
      END_OF_OPT
      OPT "Mohu se zeptat, co to je za tajemství?"
        SUB_SCRIPT sailorAnim
        Say "Nechme to rrraději jako tajemství."
        SUB_SCRIPT sailorAnim
        Say "Kdybych ti rrrovnou řekl, že je to o starrrém vesmírrrném pokladu, ztrrratilo by se napětí."
        EXIT_MENU
      END_OF_OPT
      OPT "Dobrá, nechám se překvapit."
        EXIT_MENU
      END_OF_OPT
    END_OF_MENU
    SUB_SCRIPT sailorAnim
    Say "A teď běž. Zanech mě tu s mými bohatými vzpomínkami."
    SUB_SCRIPT sailorAnim
    Say "Arrr. Kdybych ti vyprrrávěl o svém divokém rrrománku se souhvězdím panny, v noci bys oka nezamhouřřřil[g(|a)]!"
    GO sailorEnd
  END_OF_IF

  LABEL sailorSecret
  ResetFocus bguests
  SET sailorFulfilled
  SUB_SCRIPT sailorAnim
  Say "Dobrrrá tedy. Prrrozrrradím ti to tajemství..."
  SUB_SCRIPT sailorAnim
  Say "Bývaj jednou jeden vesmírrrný kapitán. Prrroslulý vesmírrrný kapitán. Nechvalně prrroslulý."
  SUB_SCRIPT sailorAnim
  Say "Býval to lump a pirrrát. Ale na sklonku dní svých činů litoval."
  SUB_SCRIPT sailorAnim
  Say "Hlavně litoval toho, jak důkladně schoval svůj poklad."
  SUB_SCRIPT sailorAnim
  Say "Ukrrryl ho na malé zastrrrčené planetě. A její souřřřadnice zapsal do nedobytné schrrránky."
  SUB_SCRIPT sailorAnim
  Say "Ta schrrránka se dala otevřřřřít jen heslem vysloveným jeho hlasem."
  SUB_SCRIPT sailorAnim
  Say "Jenže pak ten slavný kapitán přřřišel o hlas, a schrrránku už nikdy neotevřřřel."
  SUB_SCRIPT sailorAnim
  Ask "Jediné, co si pamatuje, že tu planetu označil velkým čerrrveným X."
  MENU
    OPT "Kdybych tu planetu naš[g(el|la)], mám vám... Mám to tomu kapitánovi říct?"
      SUB_SCRIPT sailorAnim
      Say "Arrr. Vlastně ani ne. On to nebyl zas tak úspěšný pirrrát, takže ten poklad není moc velkej."
      SUB_SCRIPT sailorAnim
      Say "Spíš ho štve, že i to málo, co upirrrátil, dokázal zašantrrročit."
      SUB_SCRIPT sailorAnim
      Ask "Jestli to najdeš, klidně si to nechej!"
      MENU
        OPT "Rozumím. Planeta označená velkým červeným X. Budu to mít na paměti."
          SUB_SCRIPT sailorAnim
          SET treasurePlanet
          Say "Tak ať máš přřříznivej sluneční vítrrr, [g(hochu|děvče)]."
          GO sailorEnd
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL sailorEnd
  SUB_SCRIPT sailorAnim
  Delay 300
  SUB_SCRIPT sailorAnim
  Delay 300
  SUB_SCRIPT sailorZoomOut
  Delay 750
  HideChar sailor
  HideDlgScene
  RETURN

  LABEL ratDlg
  IFNOT ratVisited
    SET ratVisited
    Delay 500
    Say "Tsss, tsss. Hej, [g(kámo|krásko)]! Pocem..."
    Delay 250
    ChangeChar rat campaign/rat-turn.png
    Delay 600
    ChangeChar rat campaign/rat.png
    Delay 400
    ChangeChar rat campaign/rat-smile.png
    Delay 1500
    ChangeChar rat campaign/rat.png
    Delay 150
    Ask "No, ty. Pocem. Něco ti řeknu. Něco děsně tajnýho."
    MENU
      OPT "Opravdu? Miluju tajemství..."
        Say "Tak to se ti bude líbit. To je totiž extra tajný tajemství..."
        ChangeChar rat campaign/rat-finger-smile.png
        Say "O úplně super kšeftu..."
        GO ratWaiting
      END_OF_OPT
      OPT "Já ti nevím, jestli to chci slyšet..."
        ChangeChar rat campaign/rat-finger-smile.png
        Say "Jasně, že chceš. Každej chce slyšet o úplně super kšeftu..."
        GO ratWaiting
      END_OF_OPT
      OPT "S neznámými lidmi se nebavím."
        ChangeChar rat campaign/rat-frown.png
        Say "No aby ses neto... Víš co... Si to tajemství stejně nezasloužíš znát, když seš takov[g(ej|á)]..."
        Delay 1500
        ChangeChar rat campaign/rat.png
        Delay 500
        Say "Ale že seš to ty, stejně ti ho řeknu... Je to totiž o úplně super kšeftu..."
        GO ratWaiting
      END_OF_OPT
    END_OF_MENU
  ELSE
    Delay 300
    ChangeChar rat campaign/rat-smile.png
    Delay 300
    Say "Hej, ty ses vrátil[g(|a)]!"
    ChangeChar rat campaign/rat-finger-smile.png
    Ask "Jdeš mi koupit pití, abych ti řekl tajemství. Že je to tak?"
    MENU
      OPT "Ani ne."
        Delay 300
        ChangeChar rat campaign/rat-frown.png
        GO ratBegging
      END_OF_OPT
      OPT "No, když jinak nedáš..."
        GO ratOrder
      END_OF_OPT
    END_OF_MENU
  END_OF_IF

  LABEL ratWaiting
  ChangeChar rat campaign/rat.png
  Delay 1000
  ChangeChar rat campaign/rat-turn.png
  Delay 500
  ChangeChar rat campaign/rat.png
  Delay 1500
  ChangeChar rat campaign/rat-turn.png
  Delay 800
  ChangeChar rat campaign/rat.png
  Delay 600
  MENU
    OPT "Na co čekáme?"
      Say "No přece až mi koupíš pití. Musíš cizinci koupit pití, když ti chce říct vo bezva kšeftu..."
      Ask "Viď, že mi koupíš pití, že jo?"
      MENU
        OPT "Tak na to zapomeň."
          GO ratBegging
        END_OF_OPT
        OPT "No tak... Dobře..."
          GO ratOrder
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL ratBegging
  Ask "Hele, počkej, neblázni. Jednu jedinou lahvinku a řeknu ti vo úplně suprovým kšeftu, to je přece výhra, ne?"
  MENU
    OPT "V žádném případě."
      Delay 300
      ChangeChar rat campaign/rat-frown.png
      Say "Tak si trhni, [g(lakomče|náno lakomá)]..."
      Delay 1500
      Ask "Ach jo. Hele, můžu ti ho říct stejně? Tajemství strašně tíží. Zvlášť takhle na sucho..."
      MENU
        OPT "No tak povídej."
          GO ratSaying
        END_OF_OPT
        OPT "Nic, já radši půjdu."
          GO ratLeaving
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
    OPT "No tak... Dobře..."
      GO ratOrder
    END_OF_OPT
  END_OF_MENU

  LABEL ratOrder
  ChangeChar rat campaign/rat-finger-smile.png
  Say "Bezva. Hej, barmane!"
  Delay 500
  ShowChar bartender left
  Delay 500
  SayChar rat "Dones pití, tady [g(ten milej pán|ta milá slečna)] to platí. Hodně pití. A večeři. Jo, a taky pokoj. Se sprchou. Tu budu ráno fakt potřebovat, jak se znám."
  ChangeChar rat campaign/rat-smile.png
  AskChar bartender "Opravdu, [g(pane|slečno)]? Bude to dělat tak 3 kredity."
  RESET hasEnough
  IF_HAS_CREDITS 3
    SET hasEnough
  END_OF_IF
  MENU
    OPT_IF hasEnough "Co už s ním mám dělat. Zaplatím."
      Delay 300
      SayChar bartender "V tom případě, jak je ctěná libost..."
      AddCredits -3
      Announce "Zaplatil[g(|a)] jsi 3 kredity."
      SET paidForRat
      Delay 300
      HideChar bartender
      GO ratSaying
    END_OF_OPT
    OPT_IFNOT hasEnough "Tak tolik sebou nemám. Snad někdy příště."
      Delay 300
      HideChar bartender
      GO ratLeaving
    END_OF_OPT
    OPT "Tak na to ať zapomene, ožrala. Já mizím."
      Delay 300
      HideChar bartender
      GO ratLeaving
    END_OF_OPT
  END_OF_MENU

  LABEL ratLeaving
  ChangeChar rat campaign/rat-frown.png
  Delay 500
  HideChar rat
  Delay 250
  HideDlgScene
  RETURN

  LABEL ratSaying
  ChangeChar rat campaign/rat.png
  SayChar rat "Takže to tajemství. Vím o strašlivě a pohádkově bohatý oblasti. Skoro nikdo vo ní neví, vážně."
  ChangeChar rat campaign/rat-finger-smile.png
  Say "Je to strašně bezpečná trasa. Samý bohatý planety a tak. Vyznačím ti ji na mapě... Tady."
  Delay 250
  Delay 250
  ShowNode pirateTrap
  CenterMapByNode pirateTrap
  Delay 500
  ShowPath U2-17a
  Delay 300
  ChangeChar rat campaign/rat.png
  Say "Fakt bezpečný. Vem si jen nákladový prostory a jiný cenný dílky."
  Say "Žádný zbraně. Vůbec je nebudeš potřebovat. Úplně žádňoučký nebezpečí, fakt."
  IF paidForRat
    Say "Dobrý, ne? Stálo to za těch pár drobných, ne?"
    ChangeChar rat campaign/rat-smile.png
    Say "A teď mě omluv, čeká mě báječná večeře..."
  ELSE
    ChangeChar rat campaign/rat-frown.png
    Say "A teď mě omluv, jdu někam tiše zdechnout žízní."
  END_OF_IF
  HideChar rat
  Delay 1300
  ChangeChar rat campaign/rat-peek.png
  ShowChar rat right
  Delay 700
  Say "Nezapomeň, zbraně nepotřebuješ. By ti jen překážely a vůbec... Šťastnou cestu, he he..."
  Delay 200
  HideChar rat
  SET ratFinished
  RESET ratAvailable
  HideDlgScene
  RETURN
END_OF_SCRIPT

SCRIPT pirateDlg1

  ShowDlgScene
  ShowChar pirate left
  Say "Tak co, jaká byla dneska kořist, chlapi?"
  Say "Eh... u rezavé gravitační kotvy, to nejsou mí chlapi!"
  Ask "Kdo sakra jsi a co tady děláš[g(|, ženská)]? A kde jsou moji chlapi?"
  MENU
    OPT "O tvoje chlapy jsem se postaral[g(|a)]. A teď si jdu pro tebe, piráte!"
      GO pirateArrestAttempt
    END_OF_OPT
    OPT "Tvoji chlapi byli neschopní. Od teď pro tebe budu pracovat já."
      Say "No, trochu nezvyklé... Ale co už, beru tě. Poslouchej, o co půjde:"
      Say "Moji agenti sem budou lákat cizí lodi a ty je budeš přepadat."
      Ask "Budeš mi odevzdávat 90 procent lupu. Ale můžeš za to používat moje logo."
      MENU
        OPT_IFNOT pirateLogo "Jaké logo?"
          Ask "Nevidíš? Lebku a zkřížené hnáty. Už jsem požádal o registraci značky."
          SET pirateLogo
        END_OF_OPT
        OPT "To zní jako dobrý konec této kampaně, jdu do toho."
          GO pirateChoice
        END_OF_OPT
        OPT "To mi nepřipadne jako dobré podmínky."
          Say "Dá se o nich diskutovat."
          Say "Dávej mi 95 procent, a můžeš používat i mé trademarkové hlášky:"
          Say "'Peníze a život!' a 'Projdi se po přechodové komoře.'"
          Ask "Tak co, plácneme si?"
          MENU
            OPT "Ani náhodou. Půjdeš se mnou, lumpe."
              GO pirateArrestAttempt
            END_OF_OPT
            OPT "Tak jo. Budu přepadat lodi a používat hlášky, 95 procent je tvých."
              GO pirateChoice
            END_OF_OPT
            OPT "Tak jo. Ale musí ti stačit 90 procent, hlášky si vymyslím vlastní."
              GO pirateChoice
            END_OF_OPT
          END_OF_MENU
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
    OPT_IFNOT pirateMeta "Počkej, musím se zamyslet. Vypadá to, že se tu kampaň zásadně větví."
      SET pirateMeta
      Say "Jasně, dej si na čas..."
      ChangeChar pirate campaign/pirate-hands-closed.png
      Delay 3500
      ChangeChar pirate campaign/pirate.png
      Ask "Ale mezi tím mi řekni, kde jsou moje lodě a moje posádky?"
    END_OF_OPT
  END_OF_MENU

  LABEL pirateChoice
  Say "Fajn, takže platí."
  Delay 300
  ChangeChar pirate campaign/pirate-freeze.png
  Delay 600
  ShowChar programmer right
  Delay 600
  Say "Promiňte, [g(hráči|hráčko)], ale musím zde vstoupit do hry, abych se ujistil, že je vám vše jasné."
  Ask "Tohle je totiž nevratné rozhodnutí a přijetím té nabídky ztratíte možnost pokračovat v kariéře trucker[g(a|ky)] a dokončit kampaň."
  MENU
    OPT "Jasně. Ale stejně do toho jdu."
      Say "Ach jo, tak já to vzdávám."
      Say "Vsadil jsem se s klukama ve vývojářském týmu, že tuhle možnost si nikdo nevybere, takže ji nebudeme ani implementovat."
      Say "Tak gratuluju, dokázal[g(|a)] jste, že jsem se mýlil. Budu muset večer koupit pizzu pro celou kancelář. Spokojen[g(ej|á)]?"
      Ask "A teď už buďte hodn[g(ej|á)] hráč[g(|ka)] a vyberte si tu jedinou správnou větev, ano?"
      MENU
        OPT "Tak dobře, když jinak nedáte, jdu piráta zatknout."
          Say "Díky, věděl jsem, že s vámi bude rozumná řeč."
          GO pirateInterventionAccepted
        END_OF_OPT
        OPT_IFNOT pirateLimits "Ne. A mimochodem, proč mi vlastně stále nabízíte možnost odmítnout?"
          Ask "Aha, to je pravda. Hned to napravím..."
          SET pirateLimits
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
    OPT "Errr, kašlu na to. Radši piráta zatknu a dostanu za něj tučnou odměnu."
      Say "Fajn."
      GO pirateInterventionAccepted
    END_OF_OPT
  END_OF_MENU

  LABEL pirateInterventionAccepted
  Delay 300
  HideChar programmer
  Delay 500
  ChangeChar pirate campaign/pirate.png
  Delay 500

  LABEL pirateArrestAttempt
  SayChar pirate "Ha. Chtěl[g(|a)] bys odměnu, co na mou hlavu vypsali na Regule, co?"
  Say "Tak pojď, pojď si mě zatknout, jestli si troufneš."
  Ask "V souboji muže proti muži se mi nikdo nevyrovná!"
  MENU
    OPT "Hm, to máš asi pravdu."
      Say "O tom vůbec nepochybuj."
      GO pirateEnd
    END_OF_OPT
    OPT "Pokud vím, v této hře žádný souboj muže proti muži není."
      Say "No a? Kdyby byl, nikdo by se mi v něm nevyrovnal."
      GO pirateEnd
    END_OF_OPT
    OPT_IFNOT pirateBragging "Přemohl[g(|a)] jsem tři tvoje lodě i s posádkama."
      Ask "To ale neznamená, že bys porazil[g(|a)] mne, v souboji muže proti muži.[g(| Nebo muže proti ženě, to je fuk.)]"
      SET pirateBragging
    END_OF_OPT
  END_OF_MENU

  LABEL pirateEnd
  Say "Ale teď vypadni. Musím si vážně promluvit s tím mizerou, co nezvládl svou práci."
  Delay 400
  HideChar pirate
  Delay 300
  HideDlgScene
  HideButton bpirate1
  ShowButton bpirate2
  SetFocus bpirate2
END_OF_SCRIPT

SCRIPT pirateDlg2

  ShowDlgScene
  ShowChar pirate left
  IFNOT ropeCut
    ChangeChar rat campaign/rat-tied.png
    ShowChar rat right
  END_OF_IF
  IF pirateEscaped
    RESET pirateEscaped
    AskChar pirate "To jsi zase ty se svou bandou?"
    MENU
      OPT "Jo, a ty zase půjdeš se mnou."
        SayChar pirate "Jak chceš. Stejně ti zase uteču."
        Say "Zastavíš na planetě... Nebo aby sis vyzvedl[g(|a)] odměnu za poraženého nepřítele..."
        Say "Nebo aby sis vyčistil[g(|a)] konektory od hvězdného prachu..."
        Say "A než se naděješ, já zmizím."
        GO pirateArrested
      END_OF_OPT
    END_OF_MENU
  END_OF_IF
  SayChar pirate "Tak, milánku, čemu jsi na tom nerozumněl?"
  SayChar rat "Mhm. Mhmmm. Mhmmm mhmmm mhm."
  SayChar pirate "Říkal jsem, že máš vybírat jen neschopné ňoumy. A ty mi sem pošleš [g(takovýho bouchače|takovou mlátičku)]."
  SayChar rat "Mhmmm mhm."
  SayChar pirate "Řekl jsi [g(mu|jí)] vůbec, že si nemá brát zbraně?"
  AskChar rat "Mhm."
  MENU
    OPT "Ehm. Nerad ruším váš přátelský rozhovor, ale [g(bouchač|mlátička)] je zpátky, i s celou posádkou."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  SayChar pirate "To jsi zase ty? Říkal jsem ti, že v souboji muže proti..."
  SayChar rat "Mhm, mhmmmm!"
  AskChar pirate "Oh... Pravda... V souboji muže proti posádce si zas tolik nevěřím."
  MENU
    OPT "Správně. A proto teď půjdeš se mnou!"
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  SayChar pirate "Eh, prozatím jsi vyhrál[g(|a)]."
  AskChar pirate "Ale moc se neraduj. Při nejbližší příležitosti vezmu roha. Na Regulu mě nedostaneš."
  MENU
    OPT_IFNOT pirateExplains "Jak bys mi mohl utéct?"
      SET pirateExplains
      SayChar pirate "Počkej si a uvidíš. Zastavíš na planetě... Nebo kvůli cizí lodi..."
      Say "Nebo abys vyčistil[g(|a)] konektory od hvězdného prachu..."
      Ask "A než se naděješ, já zmizím."
    END_OF_OPT
    OPT "To se ještě uvidí. Tak jdeme!"
      Say "Tak jo."
      GO pirateArrested
    END_OF_OPT
  END_OF_MENU

  LABEL pirateArrested
  Delay 300
  HideChar pirate
  HideButton bpirate2
  IFNOT ropeCut
    ShowButton bpirate3
    Delay 700
    SayChar rat "Mhm, mhmm? Mhmmmmmmm!"
    Delay 500
    HideChar rat
  END_OF_IF
  Delay 300
  ShowPath U2-17x
  HideDlgScene
END_OF_SCRIPT

SCRIPT pirateDlg3

  ShowDlgScene
  ChangeChar rat campaign/rat-tied.png
  ShowChar rat right
  Ask "Mhm... mhm mhmmmm?"
  MENU
    OPT "Ty bys chtěl odříznout, mizero, co?"
      Say "Mhm."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  MENU
    OPT "Sice si to nezasloužíš, ale dobrá..."
      EXIT_MENU
    END_OF_OPT
    OPT "Ani mě nenapadne..."
      GO pirateEnd3
    END_OF_OPT
  END_OF_MENU
  Delay 500
  ChangeChar rat campaign/rat-tied-down.png
  Delay 800
  UnlockAchievement camp_cut_the_rope
  SET ropeCut
  Say "Mhm mhmmm."
  MENU
    OPT "Nemáš zač."
    END_OF_OPT
    EXIT_MENU
  END_OF_MENU
  HideButton bpirate3

  LABEL pirateEnd3
  Delay 300
  HideChar rat
  Delay 300
  HideDlgScene
END_OF_SCRIPT

END_OF_SCRIPTS
