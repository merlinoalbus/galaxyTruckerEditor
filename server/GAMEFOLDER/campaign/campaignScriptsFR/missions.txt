SCRIPTS

SCRIPT purpleSupportInfo

  ShowDlgScene
  ChangeChar ambassador campaign/ambassador-peek.png
  ShowChar ambassador right
  Delay 300
  Say "Merci de choisir une belle cabine pour Monsieur K'wwfrg. Et n'oubliez pas d'attacher le bon support de vie à sa cabine !"
  HideDlgScene
  AddInfoWindow "tutorial/infoPurpleLifeSupport.png"
  ShowInfoWindow "tutorial/infoPurpleLifeSupport.png"
  ShowDlgScene
  ChangeChar ambassador campaign/ambassador-peek.png
  ShowChar ambassador right
  Say "Merci encore. J'espère que vous arriverez sans encombre."
  Delay 300
  HideChar ambassador
  HideDlgScene
END_OF_SCRIPT

SCRIPT purpleAlienInfo

  AddInfoWindow "tutorial/infoPurpleAliens.png"
  ShowInfoWindow "tutorial/infoPurpleAliens.png"
END_OF_SCRIPT

SCRIPT brownInfo

  AddInfoWindow "tutorial/infoBrownLifeSupport.png"
  AddInfoWindow "tutorial/infoBrownAliens.png"
  ShowInfoWindow "tutorial/infoBrownLifeSupport.png"
  SET brownTutorial
END_OF_SCRIPT

MISSION purpleAlienEscort
  SUB_SCRIPT add2RandomOpponentsMedium
  SetDeckPreparationScript "originalDeck"
  SetSpecCondition "purplealien"
  INIT_BUILD
  BuildingHelpScript 1000 "purpleSupportInfo"
  SetAdvPile 1 3
  SetSecretAdvPile 1 3
  SetShipType STI
  SUB_SCRIPT stdParts
  AddPartToAsideSlot alienGun 2 1 2 0
  START_BUILDING
  END_BUILDING
  INIT_FLIGHT
  START_FLIGHT
  AlienHelpScript "purpleAlienInfo"
  EVALUATE_FLIGHT
  IF_MISSION_WON
    ShowDlgScene
    ShowChar purpleGuard left
    Ask "Kzwrrr' kh! Hr ghz trrzk! TRRZK!"
    IfMissionResultMin 1
      MENU
        OPT "Relax, je vous amène ce mec mauve."
          Delay 300
          Say "Kzwrrr' kh! Hr ghz trrzk! TRRZK!"
          Delay 1000
          ShowChar purpleDipl right
          Delay 500
          Say "Zrrg. H-hrng khzz q Th'kzw-r ..."
          Delay 300
          SayChar purpleGuard "Kh gz kwrr-g. Zhg."
          Delay 500
          HideChar purpleGuard
          Delay 300
          AskChar purpleDipl "Wrwg'gg kwrr-g. W'wg zhg-k."
          MENU
            OPT "Euh… c'est bon, on peut se poser ?"
              EXIT_MENU
            END_OF_OPT
          END_OF_MENU
          SayChar purpleDipl "Kzzh."
          Delay 300
          HideChar purpleDipl
          EXIT_MENU
        END_OF_OPT
      END_OF_MENU
    ELSE
      MENU
        OPT "Eh, j'ai eu un long voyage. J'espère que vous allez me laisser me poser ?"
          Say "KHHH'WZ GHZ TRRZK!"
          EXIT_MENU
        END_OF_OPT
      END_OF_MENU
      MENU
        OPT "J'imagine que vous n'êtes pas content que je ne sois pas avec le mec mauve, hein ?"
          Say "TRRZK!"
          Delay 1000
          HideChar purpleGuard
          SetMissionAsFailed
          EXIT_MENU
        END_OF_OPT
      END_OF_MENU
    END_OF_IF
    HideDlgScene
  END_OF_IF
  END_FLIGHT
  FINISH_MISSION
END_OF_MISSION

SCRIPT tutorialSideMeteors

  AddInfoWindow "tutorial/infoBigSideMeteor.png"
  ShowInfoWindow "tutorial/infoBigSideMeteor.png"
  SET wasTutorialSideMeteors
END_OF_SCRIPT

MISSION meteorsSide
  AddOpponent richGirl
  IF_PROB 50
    SetDeckPreparationScript "meteor1LeftDeck"
  ELSE
    SetDeckPreparationScript "meteor1RightDeck"
  END_OF_IF
  SetSpecCondition "tilesBet"
  INIT_BUILD
  BuildingHelpScript 500 "tutorialSideMeteors"
  SetShipType STI
  SUB_SCRIPT stdParts
  START_BUILDING
  END_BUILDING
  INIT_FLIGHT
  START_FLIGHT
  EVALUATE_FLIGHT
  IF_MISSION_WON
    ShowDlgScene
    ShowChar richgirl left
    IfMissionResultMin 0
      Delay 300
      IfMissionResultMin 1
        Say "Eh bien, j'imagine que vous avez eu plus de chance que moi cette fois."
      ELSE
        Say "Hum. On est ex-aequo."
      END_OF_IF
      Say "Je suis impressionnée. Votre vaisseau est presque aussi beau que le mien."
      IFNOT designSport1Unlocked
        Say "Voilà. Vous pouvez utiliser les plans de mon vaisseau. Vous les avez mérités."
        Delay 300
        HideChar richgirl
        Delay 200
        UnlockShipPlan "I-sport"
        SET designSport1Unlocked
        Announce "Nouveau vaisseau de classe I déverrouillé."
        Delay 500
        SetAchievementProgress camp_design_sporty 1
        Delay 500
        HidePath "U1-4a"
        ShowPath "R1-4a1"
        IF class2
          HideButton bvip
          HideButton bvip2
          ShowButton brichgirl2
        END_OF_IF
      END_OF_IF
    ELSE
      Delay 300
      Say "On dirait que j'ai gagné."
      Say "Bon, ce n’est pas une surprise, mais j'espère que vous avez pris quelques notes."
      Delay 500
      HideChar richgirl
    END_OF_IF
    HideDlgScene
  END_OF_IF
  END_FLIGHT
  FINISH_MISSION
END_OF_MISSION

MISSION meteorsSide2
  AddOpponent richGirl
  SetDeckPreparationScript "meteor2SidesDeck"
  SetSpecCondition "tilesBet"
  INIT_BUILD
  SetShipType STII
  SUB_SCRIPT stdParts
  START_BUILDING
  END_BUILDING
  INIT_FLIGHT
  START_FLIGHT
  EVALUATE_FLIGHT
  IF_MISSION_WON
    ShowDlgScene
    ShowChar richgirl left
    IfMissionResultMin 0
      Delay 300
      IfMissionResultMin 1
        Say "Incroyable. Comment avez-vous pu être aussi [g(chanceux|chanceuse)] ?"
      ELSE
        Say "Est-ce qu'on est vraiment à égalité ?"
      END_OF_IF
      Say "Vous n'êtes finalement pas [g(mauvais|mauvaise)] , pour [g(un|une)] simple pilote."
      IFNOT designSport2Unlocked
        Say "Tenez. Prenez ces plans aussi. Je sais qu'ils seront entre de bonnes mains."
        Delay 300
        HideChar richgirl
        Delay 200
        UnlockShipPlan "II-sport"
        SET designSport2Unlocked
        Announce "Nouveau vaisseau de classe II déverrouillé."
        Delay 500
        SetAchievementProgress camp_design_sporty 2
        Delay 500
      END_OF_IF
    ELSE
      Delay 300
      Say "On dirait que j'ai gagné."
      Say "Ben oui, vous ne pensiez pas avoir de la chance à chaque fois. Au moins, vous aurez essayé."
      Delay 500
    END_OF_IF
    HideChar richgirl
    HideDlgScene
  END_OF_IF
  END_FLIGHT
  FINISH_MISSION
END_OF_MISSION

MISSION meteorsSideWithBot
  AddOpponent medium_safe
  IF_PROB 50
    SetDeckPreparationScript "meteor1LeftDeck"
  ELSE
    SetDeckPreparationScript "meteor1RightDeck"
  END_OF_IF
  INIT_BUILD
  SetShipType STI
  SUB_SCRIPT stdParts
  START_BUILDING
  END_BUILDING
  INIT_FLIGHT
  START_FLIGHT
  EVALUATE_FLIGHT
  ShowDlgScene
  ShowChar securitybot2 left
  Delay 300
  IF_MISSION_WON
    Say "BIEN, VOUS Y ÊTES [g(ARRIVÉ|ARRIVÉE)]. TANT MIEUX POUR MOI."
  ELSE
    Say "JE VOUS L'AVAIS DIT."
  END_OF_IF
  Delay 300
  HideChar securitybot2
  Delay 300
  HideDlgScene
  END_FLIGHT
  FINISH_MISSION
END_OF_MISSION

SCRIPT tutorialBooze

  ShowDlgScene
  ShowChar bartender left
  Say "La cargaison est prête."
  Say "Bien entendu, je ne vous paierai que si tout est intact à l'arrivée. Mais si vous cassez quoi que ce soit, vous devrez me rembourser."
  Delay 100
  HideDlgScene
  AddInfoWindow "tutorial/infoFragileCargo.png"
  ShowInfoWindow "tutorial/infoFragileCargo.png"
  ShowDlgScene
  ChangeChar bartender campaign/bartender-warehouse.png
  ShowChar bartender left
  Delay 200
  Say "Voyagez prudemment. Oh, et prévoyez suffisamment d'hommes !"
  Say "Certains de mes clients ont eu des accidents fâcheux en partant avec un équipage réduit sur cette route."
  HideChar bartender
  HideDlgScene
END_OF_SCRIPT

SCRIPT bartenderOverIntercom

  ChangeChar bartender campaign/bartender-overIntercom.png
  ShowChar bartender left
  Delay 100
  ChangeChar bartender campaign/bartender-overIntercom1.png
  Delay 100
  ChangeChar bartender campaign/bartender-overIntercom.png
  Delay 100
  ChangeChar bartender campaign/bartender-overIntercom1.png
  Delay 100
  ChangeChar bartender campaign/bartender-overIntercom.png
  Delay 100
  ChangeChar bartender campaign/bartender-overIntercom1.png
  Delay 200
  ChangeChar bartender campaign/bartender-overIntercom.png
  Delay 300
  ChangeChar bartender campaign/bartender-overIntercom1.png
  Delay 300
  ChangeChar bartender campaign/bartender-overIntercom.png
  Delay 300
END_OF_SCRIPT

MISSION booze2
  SUB_SCRIPT add3RandomOpponentsEasy
  SetDeckPreparationScript "boozeDeck"
  SetSpecCondition "booze"
  INIT_BUILD
  ModifyOpponentsBuildSpeed 75
  SetShipType STI
  SUB_SCRIPT stdParts
  AddPartToAsideSlot booze 2 1 2 0
  AddPartToAsideSlot booze 1 2 1 0
  BuildingHelpScript 500 "tutorialBooze"
  START_BUILDING
  END_BUILDING
  INIT_FLIGHT
  START_FLIGHT
  EVALUATE_FLIGHT
  ShowDlgScene
  IF_MISSION_WON
    SUB_SCRIPT bartenderOverIntercom
    IfMissionResultMin 2
      ADD totalBooze 2
      Say "Les deux caisses ? Merveilleux ! Voici 5 Crédits pour chacune."
      AddMissionCredits 10
      Delay 500
      UnlockAchievement camp_booze
      Delay 500
    ELSE
      IfMissionResultMin 1
        ADD totalBooze 1
        Say "Vous avez cassé mes bouteilles ? Bon, j'imagine que vous avez au moins réussi à sauver l'autre caisse. Disons que nous sommes quittes."
      ELSE
        Say "Vous avez cassé toutes mes bouteilles ? Nous étions d'accord pour que vous couvriez les pertes. Ce sera 5 Crédits par caisse."
        AddMissionCredits -10
      END_OF_IF
    END_OF_IF
  ELSE
    ChangeChar bartender campaign/bartender-warehouse.png
    ShowChar bartender left
    Delay 300
    IfMissionResultMin 2
      Say "Vous renoncez ? Bien, au moins, vous n'avez pas abîmé mes caisses."
    ELSE
      IfMissionResultMin 1
        Say "Quoi ? Vous renoncez ? Et vous avez cassé une de mes caisses ? Je suis navré, mais vous me devez 5 Crédits."
        AddMissionCredits -5
      ELSE
        Say "Qu'est-il arrivé à mes bouteilles ? Donnez-moi 10 Crédits pour couvrir les pertes."
        AddMissionCredits -10
      END_OF_IF
    END_OF_IF
  END_OF_IF
  HideChar bartender
  HideDlgScene
  SET boozeQuestTried
  HidePath R1-3a2
  ShowPath R1-3a
  END_FLIGHT
  FINISH_MISSION
END_OF_MISSION

MISSION booze1
  SUB_SCRIPT add3RandomOpponentsEasy
  SetDeckPreparationScript "boozeDeck"
  SetSpecCondition "booze"
  INIT_BUILD
  ModifyOpponentsBuildSpeed 75
  SetShipType STI
  SUB_SCRIPT stdParts
  AddPartToAsideSlot booze 2 1 2 0
  BuildingHelpScript 500 "tutorialBooze"
  START_BUILDING
  END_BUILDING
  INIT_FLIGHT
  START_FLIGHT
  EVALUATE_FLIGHT
  ShowDlgScene
  IF_MISSION_WON
    SUB_SCRIPT bartenderOverIntercom
    IfMissionResultMin 1
      ADD totalBooze 1
      Say "Vous avez livré la caisse ? Sublime. Je vous transfère immédiatement 5 Crédits."
      AddMissionCredits 5
    ELSE
      Say "Vous avez cassé la cargaison ? Soit, cela veut dire que vous me devez 5 Crédits."
      AddMissionCredits -5
    END_OF_IF
  ELSE
    ChangeChar bartender campaign/bartender-warehouse.png
    ShowChar bartender left
    Delay 300
    IfMissionResultMin 1
      Say "Vous renoncez ? Bien, au moins, vous n'avez pas abîmé mes caisses."
    ELSE
      Say "Qu'est-il arrivé à mes bouteilles ? Vous me devez 5 Crédits."
      AddMissionCredits -5
    END_OF_IF
  END_OF_IF
  HideChar bartender
  HideDlgScene
  SET boozeQuestTried
  HidePath R1-3a1
  ShowPath R1-3a
  END_FLIGHT
  FINISH_MISSION
END_OF_MISSION

MISSION nobooze
  SUB_SCRIPT add3RandomOpponentsEasy
  SetDeckPreparationScript "boozeDeck"
  INIT_BUILD
  SetShipType STI
  SUB_SCRIPT stdParts
  START_BUILDING
  END_BUILDING
  INIT_FLIGHT
  START_FLIGHT
  EVALUATE_FLIGHT
  END_FLIGHT
  FINISH_MISSION
END_OF_MISSION

SCRIPT tutorialAdditionalFlightConcepts

  IFNOT shouldShowFlightTutorial
    RETURN
  END_OF_IF
  AddInfoWindow "tutorial/infoShipInfo.png"
  AddInfoWindow "tutorial/infoGivingUpFlight.png"
  AddInfoWindow "tutorial/infoGivingUpFlight2.png"
  ShowInfoWindow "tutorial/infoShipInfo.png"
END_OF_SCRIPT

SCRIPT tutorialAdditionalConcepts

  IF tutorialAdditional
    RETURN
  END_OF_IF
  IF_TUTORIAL_SEEN
    SET showAdditional
  ELSE
    IF tutorialRefused
      RESET showAdditional
    ELSE
      SET showAdditional
    END_OF_IF
  END_OF_IF
  IFNOT showAdditional
    RETURN
  END_OF_IF
  SET tutorialAdditional
  SET shouldShowFlightTutorial
  ShowDlgScene
  ShowChar tutor right
  Delay 300
  Say "Salut, novice. ll y a encore quelques petites choses qu'il vous faut savoir sur la construction."
  Say "Hé, vieil homme. Où sont des diapos ?"
  HideChar tutor
  HideDlgScene
  AddInfoWindow "tutorial/infoAsideSlots.png"
  AddInfoWindow "tutorial/infoLookingAtCards.png"
  ShowInfoWindow "tutorial/infoAsideSlots.png"
  ShowDlgScene
  ShowChar tutor right
  Delay 300
  Say "Hum. Vous devez vous demander où trouver ces paquets de cartes que vous êtes supposés consulter."
  Say "Ils apparaissent en haut de l'écran dès que vous avez soudé votre premier composant."
  Say "Bien, allez-y. Bonne chance."
  HideChar tutor
  HideDlgScene
END_OF_SCRIPT

MISSION firstMission
  SUB_SCRIPT add2RandomOpponentsEasy
  SetDeckPreparationScript "firstMissionDeck"
  INIT_BUILD
  BuildingHelpScript 500 "tutorialAdditionalConcepts"
  RESET shouldShowFlightTutorial
  ModifyOpponentsBuildSpeed 75
  SetShipType STI
  SUB_SCRIPT stdParts
  START_BUILDING
  END_BUILDING
  INIT_FLIGHT
  START_FLIGHT
  FlightHelpScript "tutorialAdditionalFlightConcepts"
  EVALUATE_FLIGHT
  END_FLIGHT
  FINISH_MISSION
END_OF_MISSION

SCRIPT merchantGivesUp

  IF merchForcedFail
    RETURN
  END_OF_IF
  ShowDlgScene
  ChangeChar merchant campaign/merch-disappointed.png
  ShowChar merchant left
  Say "Ça ne s'est pas passé comme vous l'espériez."
  Say "Eh bien. Rentrons. Nous réessaierons une prochaine fois."
  SetMissionAsFailed
  SET merchForcedFail
  HideChar merchant
  HideDlgScene
END_OF_SCRIPT

MISSION merchantProtection
  AddOpponent merchant
  SetDeckPreparationScript "merchantDeck"
  RESET merchForcedFail
  INIT_BUILD
  SetAdvPile 1 3
  SetSecretAdvPile 1 3
  SetShipType STI
  SUB_SCRIPT stdParts
  SetSpecCondition "merchantProtect"
  START_BUILDING
  AddPartToShip 1 7 alienEngine 3333 0
  END_BUILDING
  INIT_FLIGHT
  START_FLIGHT
  EVALUATE_FLIGHT
  IF_MISSION_WON
    IfMissionResultMin 3
      ShowDlgScene
      ShowChar merchant left
      Say "Ouiii ! On y est arrivé. Et avec plein de marchandises."
      Say "Ce fut un plaisir de faire des affaires avec vous."
      Say "Voici votre part : [missionResult] Crédits."
      AddMissionCreditsByResult
      SubOpponentCreditsByResult
      HideButton bmerchant
      Delay 500
      SetAchievementProgress camp_merchant 1
      Delay 300
      HidePath U1-9a
      ShowPath R1-9a
      ShowPath R1-9b
    ELSE
      ShowDlgScene
      ShowChar merchant left
      Say "Bon, on y est arrivé. Mais nous n'avons pas assez de marchandises."
      Say "Aucun intérêt de nous poser, vraiment. Nous n'avons même pas de quoi payer les frais d'accostage."
      Say "Rentrons. Nous retenterons notre chance une autre fois."
      AllShipsGiveUp
      SET merchantFailed
      HidePath U1-9a
      SetFocus bmerchant
    END_OF_IF
    HideChar merchant
    HideDlgScene
  ELSE
    SET merchantFailed
    HidePath U1-9a
    SetFocus bmerchant
    IFNOT merchForcedFail
      ShowDlgScene
      ChangeChar merchant campaign/merch-disappointed.png
      ShowChar merchant left
      Say "Vous me laissez tomber ? Je ne veux pas courir le risque de poursuivre cette route tout seul."
      Say "Je rentre avec vous. Peut-être ferons-nous mieux la prochaine fois."
      AllShipsGiveUp
      HideChar merchant
      HideDlgScene
    END_OF_IF
  END_OF_IF
  END_FLIGHT
  FINISH_MISSION
END_OF_MISSION

MISSION merchantProtection2
  AddOpponent merchant
  SetDeckPreparationScript "merchantDeck2"
  RESET merchForcedFail
  INIT_BUILD
  SetAdvPile 1 1
  SetAdvPile 2 3
  SetSecretAdvPile 1 1
  SetSecretAdvPile 2 3
  SetShipType STII
  SUB_SCRIPT stdParts
  SetSpecCondition "merchantProtect"
  IF brownUnlocked
    IFNOT brownTutorial
      BuildingHelpScript 500 brownInfo
    END_OF_IF
  END_OF_IF
  START_BUILDING
  AddPartToShip 1 17 alienEngine 3333 0
  END_BUILDING
  INIT_FLIGHT
  START_FLIGHT
  EVALUATE_FLIGHT
  IF_MISSION_WON
    IfMissionResultMin 5
      ShowDlgScene
      ShowChar merchant left
      Say "Ouiii ! On y est arrivé. Et avec plein de marchandises."
      Say "Une fois de plus, ce fut un véritable plaisir de faire des affaires avec vous."
      Say "Voici votre part : [missionResult] Crédits."
      AddMissionCreditsByResult
      SubOpponentCreditsByResult
      Delay 500
      SetAchievementProgress camp_merchant 2
      Delay 300
      HidePath U2-19a
      ShowPath R2-19a
      ShowPath R2-19b
      Say "Oh… et voici votre attestation de la part des mecs bruns."
      Say "Je l'avais dans ma poche depuis le départ."
      SUB_SCRIPT merchantLaughA
      Announce "Vous avez obtenu une attestation pour un vaisseau de classe III de la part des extraterrestres bruns."
      SET brownLicenseApproval
      SUB_SCRIPT licenseApprovalCheck
      ShowButton bcyanmerch
    ELSE
      ShowDlgScene
      ShowChar merchant left
      Say "Bon, on y est arrivé. Mais nous n'avons pas assez de marchandises."
      Say "Aucun intérêt de nous poser, vraiment. Nous n'avons même pas de quoi payer les frais d'accostage."
      Say "Rentrons. Nous retenterons notre chance une autre fois."
      AllShipsGiveUp
      SET merchantFailed
    END_OF_IF
    HideChar merchant
    HideDlgScene
  ELSE
    SET merchantFailed
    IFNOT merchForcedFail
      ShowDlgScene
      ChangeChar merchant campaign/merch-disappointed.png
      ShowChar merchant left
      Say "Vous me laissez tomber ? Je ne veux pas courir le risque de poursuivre cette route tout seul."
      Say "Je rentre avec vous. Peut-être ferons-nous mieux la prochaine fois."
      AllShipsGiveUp
      HideChar merchant
      HideDlgScene
    END_OF_IF
  END_OF_IF
  END_FLIGHT
  FINISH_MISSION
END_OF_MISSION

MISSION merchantProtection3
  AddOpponent merchant
  SetDeckPreparationScript "merchantDeck3"
  RESET merchForcedFail
  INIT_BUILD
  SetAdvPile 1 1
  SetAdvPile 2 1
  SetAdvPile 3 3
  SetSecretAdvPile 1 1
  SetSecretAdvPile 2 1
  SetSecretAdvPile 3 3
  SetShipType STIII
  SUB_SCRIPT stdParts
  SetSpecCondition "merchantProtect"
  START_BUILDING
  AddPartToShip 1 22 alienEngine 3333 0
  END_BUILDING
  INIT_FLIGHT
  START_FLIGHT
  EVALUATE_FLIGHT
  IF_MISSION_WON
    ShowDlgScene
    ShowChar merchant left
    Say "Ouiii ! Nous sommes sortis du territoire extraterrestre !"
    IfMissionResultIs 0
      Say "Je n'ai pas la moindre marchandise, mais on s'en moque ! Au moins, nous sommes sains et saufs."
    ELSE
      IfMissionResultMin 8
        Say "Et cerise sur le gâteau, j'ai des tonnes de marchandises à vendre !"
      ELSE
        IfMissionResultMin 4
          Say "Et j'ai même chargé plein de marchandises en chemin !"
        ELSE
          Say "Et j'ai même chargé quelques marchandises en chemin."
        END_OF_IF
      END_OF_IF
      Say "Voici votre part : [missionResult] Crédits."
      AddMissionCreditsByResult
      SubOpponentCreditsByResult
    END_OF_IF
    Delay 500
    SetAchievementProgress camp_merchant 3
    Delay 300
    HidePath U3-29a
    ChangeChar merchant campaign/merch-inviting.png
    Say "Comptez sur ma recommandation. Personne ne le mérite plus que vous."
    SET recommendationMerchant
    SET recommendationMerchantUntold
    Announce "Vous avez gagné une lettre de recommandation de l'homme d'affaires."
    SUB_SCRIPT recommendationCheck
    Say "Merci pour tout. Ce fut une sacrée aventure !"
    SUB_SCRIPT merchantLaughB
    Say "Prenez soin de vous."
    HideChar merchant
    HideDlgScene
  ELSE
    SET merchant3Failed
    IFNOT merchForcedFail
      ShowDlgScene
      ChangeChar merchant campaign/merch-disappointed.png
      ShowChar merchant left
      Say "Vous me laissez tomber ? Je ne veux pas courir le risque de poursuivre cette route tout seul."
      Say "Je rentre avec vous. Peut-être ferons-nous mieux la prochaine fois."
      AllShipsGiveUp
      HideChar merchant
      HideDlgScene
    END_OF_IF
  END_OF_IF
  END_FLIGHT
  FINISH_MISSION
END_OF_MISSION

MISSION alienPlanets
  IF_PROB 50
    AddOpponent purple2
  ELSE
    AddOpponent brown2
  END_OF_IF
  IF_PROB 75
    IF_PROB 50
      AddOpponent purple2
    ELSE
      AddOpponent brown2
    END_OF_IF
    IF_PROB 50
      IF_PROB 50
        AddOpponent purple2
      ELSE
        AddOpponent brown2
      END_OF_IF
    END_OF_IF
  END_OF_IF
  SetDeckPreparationScript "digitalDeck"
  INIT_BUILD
  SetShipType STI
  SUB_SCRIPT stdParts
  IF brownUnlocked
    IFNOT brownTutorial
      BuildingHelpScript 500 brownInfo
    END_OF_IF
  END_OF_IF
  START_BUILDING
  END_BUILDING
  INIT_FLIGHT
  START_FLIGHT
  EVALUATE_FLIGHT
  END_FLIGHT
  FINISH_MISSION
END_OF_MISSION

MISSION alienPlanets2brown
  IF_PROB 50
    AddOpponent cyan2
  ELSE
    AddOpponent brown2
  END_OF_IF
  IF_PROB 75
    IF_PROB 50
      AddOpponent cyan2
    ELSE
      AddOpponent brown2
    END_OF_IF
    IF_PROB 50
      IF_PROB 50
        AddOpponent cyan2
      ELSE
        AddOpponent brown2
      END_OF_IF
    END_OF_IF
  END_OF_IF
  SetDeckPreparationScript "digitalDeck"
  INIT_BUILD
  SetShipType STII
  SUB_SCRIPT stdParts
  START_BUILDING
  END_BUILDING
  INIT_FLIGHT
  START_FLIGHT
  EVALUATE_FLIGHT
  END_FLIGHT
  FINISH_MISSION
END_OF_MISSION

MISSION alienPlanets2purple
  IF_PROB 50
    AddOpponent cyan2
  ELSE
    AddOpponent purple2
  END_OF_IF
  IF_PROB 75
    IF_PROB 50
      AddOpponent cyan2
    ELSE
      AddOpponent purple2
    END_OF_IF
    IF_PROB 50
      IF_PROB 50
        AddOpponent cyan2
      ELSE
        AddOpponent purple2
      END_OF_IF
    END_OF_IF
  END_OF_IF
  SetDeckPreparationScript "digitalDeck"
  INIT_BUILD
  SetShipType STII
  SUB_SCRIPT stdParts
  START_BUILDING
  END_BUILDING
  INIT_FLIGHT
  START_FLIGHT
  EVALUATE_FLIGHT
  END_FLIGHT
  FINISH_MISSION
END_OF_MISSION

MISSION warriorTest
  AddOpponent purple2
  AddOpponent purple2
  AddOpponent purple2
  SetDeckPreparationScript "battleZones"
  INIT_BUILD
  SetShipType STII
  SUB_SCRIPT stdParts
  START_BUILDING
  END_BUILDING
  INIT_FLIGHT
  START_FLIGHT
  EVALUATE_FLIGHT
  IF_MISSION_WON
    ShowDlgScene
    ShowChar purple left
    Delay 300
    Say "Zh wrrzk-hrrg krz'h!"
    Delay 300
    ShowChar purple2 right
    Delay 300
    Say "Ghrrk-zhr ww'gh wrrzk-hrrg krz'h!"
    Delay 300
    Announce "L'extraterrestre mauve vous donne une attestation pour un vaisseau de classe III."
    SET purpleLicenseApproval
    SUB_SCRIPT licenseApprovalCheck
    HideChar purple2
    Delay 300
    HideChar purple
    HideDlgScene
    HidePath U2-20a
    ShowPath R2-20a
  END_OF_IF
  END_FLIGHT
  FINISH_MISSION
END_OF_MISSION

MISSION noMerchant
  SUB_SCRIPT add2RandomOpponentsMedium
  SetDeckPreparationScript "merchantDeck"
  INIT_BUILD
  SetShipType STI
  SUB_SCRIPT stdParts
  START_BUILDING
  END_BUILDING
  INIT_FLIGHT
  START_FLIGHT
  EVALUATE_FLIGHT
  END_FLIGHT
  FINISH_MISSION
END_OF_MISSION

SCRIPT tutorialEpidemy

  ShowDlgScene
  ShowChar mech right
  Delay 200
  Say "Hé. C'est encore moi."
  ChangeChar mech campaign/mech-explaining.png
  Say "J'ai entendu dire qu'il y avait pas mal de soucis techniques en ce moment"
  Ask "Juste pour être tranquille, évitez de placer deux cabines l'une à côté de l'autre, ok ? Sinon, les gens pourraient tomber malades."
  MENU
    OPT "Bien compris. Ne vous inquiétez pas, je serai [g(prudent|prudente)]."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  Delay 300
  ChangeChar mech campaign/mech-thumbup.png
  Delay 300
  Say "Ok, merci encore. Et bonne chance !"
  HideChar mech
  HideDlgScene
END_OF_SCRIPT

MISSION peopleDelivery
  SUB_SCRIPT add2RandomOpponentsMedium
  SetDeckPreparationScript "epidemyDeck"
  INIT_BUILD
  IFNOT wasTutorialEpidemy
    SET wasTutorialEpidemy
    BuildingHelpScript 1000 tutorialEpidemy
  END_OF_IF
  SetShipType STI
  SUB_SCRIPT stdParts
  SetSpecCondition "peopleDelivery"
  START_BUILDING
  END_BUILDING
  INIT_FLIGHT
  START_FLIGHT
  EVALUATE_FLIGHT
  IF_MISSION_WON
    ShowDlgScene
    ShowChar mech right
    IFNOT nextPeopleDelivery
      Say "Bienvenue à notre usine. M'avez-vous ramené des hommes ?"
    ELSE
      Say "Ravie de vous voir ! Me ramenez-vous de la main-d'œuvre ?"
    END_OF_IF
    IfMissionResultMin 0
      ChangeChar mech campaign/mech-explaining.png
      IfMissionResultMin 10
        Say "Tant de monde ? Et tous ont l'air en forme ?! Vous êtes [g(le meilleur|la meilleure)]."
        Delay 300
        ChangeChar mech campaign/mech-kiss.png
        Delay 300
        Say "* bises ! *"
        Delay 300
        ChangeChar mech campaign/mech.png
        Delay 600
        ChangeChar mech campaign/mech-blush.png
        Delay 300
        Say "Désolé. Je crois que je me suis un peu emportée."
        UnlockAchievement camp_kiss
        Delay 300
        ChangeChar mech campaign/mech.png
        SET wasKiss
      ELSE
        IfMissionResultMin 8
          Say "Ça fait beaucoup de monde. Et ils vont tous bien ? Vous êtes [g(le meilleur|la meilleure)] !"
          Delay 200
          ChangeChar mech campaign/mech-blush.png
          Delay 200
          IFNOT wasKiss
            Say "Désolé. J'étais si enthousiaste que je vous ai presque [g(embrassé|embrassée)]."
          ELSE
            Say "Désolé. J'ai failli m'emporter de nouveau."
          END_OF_IF
          Delay 300
          ChangeChar mech campaign/mech.png
        ELSE
          IfMissionResultMin 6
            Say "Bon travail. Je suis contente que tout le monde aille bien."
          ELSE
            Say "Ils sont tous là ? Eh bien, au moins, aucun n'est blessé. Beau travail."
          END_OF_IF
        END_OF_IF
      END_OF_IF
      IFNOT nextPeopleDelivery
        Say "Voici [missionResult] Crédits. Encore merci."
        AddMissionCreditsByResult
        Say "La prochaine fois que vous passez dans le coin, arrêtez-vous au dock d'Administra histoire de voir si l'un de nos hommes n'a pas besoin de voyager."
        Say "Je ne sais pas si je pourrai vous payer, mais je suis sûre qu'ils pourront vous filer un coup de main."
        SET nextPeopleDelivery
        HidePath "U1-7a"
        ShowPath "R1-7a"
        ShowPath "R1-7b"
      ELSE
        Say "Merci beaucoup. Je n'étais pas sûr de pouvoir vous payer pour ce voyage…"
        IfMissionResultMin 8
          Say "Mais je crois que je peux vous laisser 3 Crédits. Vous le méritez."
          AddMissionCredits 3
        ELSE
          IfMissionResultMin 5
            Say "Mais voici 2 Crédits pour votre peine."
            AddMissionCredits 2
          ELSE
            Say "Mais voici 1 Crédit pour avoir essayé."
            AddMissionCredits 1
          END_OF_IF
        END_OF_IF
        Say "Et transportez-en davantage la prochaine fois que vous passerez dans le coin, ok ?"
      END_OF_IF
    ELSE
      SET peopleDeliveryFailed
      IfMissionResultMin -2
        Say "Il ne manque… presque personne."
      ELSE
        IfMissionResultMin -4
          Say "Oh non. Qu'est-il arrivé aux autres ?"
        ELSE
          Say "Oh non. Quel massacre !"
        END_OF_IF
      END_OF_IF
      IFNOT nextPeopleDelivery
        Say "Que c'est triste !"
        MENU
          OPT "Je suis tellement [g(désolé|désolée)] !"
            EXIT_MENU
          END_OF_OPT
        END_OF_MENU
        ChangeChar mech campaign/mech.png
        Say "Je sais que vous l'êtes. Je sais que vous avez essayé."
        ChangeChar mech campaign/mech-f.png
        Say "Bon, que puis-je faire ? Rentrer sur Administra pour trouver plus de monde."
        ChangeChar mech campaign/mech.png
        Ask "Vous pouvez m'y conduire ?"
        MENU
          OPT "Bien sûr. Si ça peut rendre service."
            Say "Merci."
            SetMissionAsFailed
            EXIT_MENU
          END_OF_OPT
        END_OF_MENU
      ELSE
        Say "Je suis sûr que vous avez essayé de les protéger."
        Say "Mais la prochaine fois, soyez plus [g(attentif|attentive)] !"
      END_OF_IF
    END_OF_IF
    HideChar mech
    HideDlgScene
  ELSE
    IFNOT nextPeopleDelivery
      ShowDlgScene
      ChangeChar mech campaign/mech-intercom.png
      ShowChar mech right
      Ask "Quoi de neuf ?"
      IfMissionResultMin 0
        MENU
          OPT "Nous faisons demi-tour. Mais pas de souci, tout le monde va bien."
            EXIT_MENU
          END_OF_OPT
        END_OF_MENU
      ELSE
        MENU
          OPT "Vous ne voulez pas le savoir."
            EXIT_MENU
          END_OF_OPT
        END_OF_MENU
      END_OF_IF
      Say "Ok. J'imagine que ce sont des choses qui arrivent. Je rentre moi aussi sur Administra."
      SET peopleDeliveryFailed
      HideChar mech
      HideDlgScene
    END_OF_IF
  END_OF_IF
  END_FLIGHT
  FINISH_MISSION
END_OF_MISSION

MISSION universalEpidemic
  SUB_SCRIPT addRandomOpponentsMedium
  SetDeckPreparationScript "epidemyDeck"
  INIT_BUILD
  SetShipType STI
  SUB_SCRIPT universalParts
  START_BUILDING
  END_BUILDING
  INIT_FLIGHT
  START_FLIGHT
  EVALUATE_FLIGHT
  END_FLIGHT
  FINISH_MISSION
END_OF_MISSION

MISSION universalSabotages
  SUB_SCRIPT addRandomOpponentsMedium
  SetDeckPreparationScript "boozeDeck"
  INIT_BUILD
  SetShipType STI
  SUB_SCRIPT universalParts
  START_BUILDING
  END_BUILDING
  INIT_FLIGHT
  START_FLIGHT
  EVALUATE_FLIGHT
  END_FLIGHT
  FINISH_MISSION
END_OF_MISSION

MISSION universal3long
  SUB_SCRIPT addRandomOpponentsMedium
  SetDeckPreparationScript "digitalDeck"
  INIT_BUILD
  SetAdvPile 1 1
  SetAdvPile 2 1
  SetAdvPile 3 3
  SetSecretAdvPile 1 1
  SetSecretAdvPile 2 1
  SetSecretAdvPile 3 3
  SetShipType STIII
  SUB_SCRIPT universalParts
  START_BUILDING
  END_BUILDING
  INIT_FLIGHT
  START_FLIGHT
  EVALUATE_FLIGHT
  END_FLIGHT
  FINISH_MISSION
END_OF_MISSION

SCRIPT ingotsIntro

  ShowDlgScene
  ShowChar foreman left
  Delay 750
  IF ingotsFailedDelivery
    ChangeChar foreman campaign/foreman-no.png
    Say "Hé, j'ai entendu dire que vous n'aviez pas réussi la dernière fois."
    ChangeChar foreman campaign/foreman.png
    Say "Ne vous inquiétez pas. Je vais vous donner une autre chance. Je suis un gars sympa."
    Say "(C'est surtout que personne d'autre ne veut faire ce boulot.)"
    RESET ingotsFailedDelivery
  ELSE
    ChangeChar foreman campaign/foreman-whatsup.png
    IF oneingot
      IF ing3
        Say "Oki doki, nous y voilà. Un lingot ultramassif de pur métal cosmique."
        ChangeChar foreman campaign/foreman-smile.png
        Say "N'oubliez pas de bien l'arrimer à votre vaisseau, ok ?"
      ELSE
        Say "Oki doki, voici un autre lingot. C'est une merveille."
      END_OF_IF
    END_OF_IF
    IF twoingots
      Say "Ok, nous y voilà. Deux lingots étincelants. Faites-y bien attention."
    END_OF_IF
    IF threeingots
      Say "Ô mon Dieu ! Trois lingots. Regardez-moi ces beautés !"
      ChangeChar foreman campaign/foreman-smile.png
      Say "Je dois en souder un à votre vaisseau. J'espère que ça ne pose pas de problème."
    END_OF_IF
  END_OF_IF
  HideDlgScene
  AddInfoWindow "tutorial/infoHeavyCargo.png"
  ShowInfoWindow "tutorial/infoHeavyCargo.png"
  ShowDlgScene
  ShowChar foreman left
  Delay 500
  ChangeChar foreman campaign/foreman-hello.png
  Say "Soyez [g(vigilant|vigilante)] maintenant."
  HideChar foreman
  HideDlgScene
END_OF_SCRIPT

MISSION ingotDelivery
  SUB_SCRIPT add2RandomOpponentsMedium
  SetDeckPreparationScript "ingotDeck"
  INIT_BUILD
  SetSpecCondition "ingots"
  BuildingHelpScript 1000 ingotsIntro
  SetShipType STII
  SUB_SCRIPT stdParts
  START_BUILDING
  IF ing2
    AddPartToAsideSlot ingot2 2 1 0 0
  END_OF_IF
  IF ing3
    AddPartToAsideSlot ingot3 3 2 0 0
  END_OF_IF
  IF ing4
    AddPartToAsideSlot ingot4 2 3 2 0
  END_OF_IF
  IF ing5
    AddPartToAsideSlot ingot5 1 3 1 0
  END_OF_IF
  IF ing6
    AddPartToAsideSlot ingot4 1 3 2 3
  END_OF_IF
  IF ing8
    AddPartToAsideSlot ingot4 2 1 2 0
  END_OF_IF
  IF twoingots
    IF ing2
      AddPartToAsideSlot ingot2 3 0 3 0
    END_OF_IF
    IF ing3
      AddPartToAsideSlot ingot3 1 2 1 0
    END_OF_IF
    IF ing4
      AddPartToAsideSlot ingot4  3 0 3 0
    END_OF_IF
  END_OF_IF
  IF threeingots
    IF ing2
      AddPartToAsideSlot ingot2 1 3 2 0
      AddPartToShip 0 17 ingot2 2323 0
    END_OF_IF
    IF ing3
      AddPartToAsideSlot ingot3 3 2 3 0
      AddPartToShip 0 17 ingot3 1212 0
    END_OF_IF
  END_OF_IF
  END_BUILDING
  INIT_FLIGHT
  START_FLIGHT
  EVALUATE_FLIGHT
  RESET allok
  RESET somemissing
  RESET allmissing
  IF_MISSION_WON
    IfMissionResultMin 1
      IfMissionResultMin 2
        IfMissionResultMin 3
          SET allok
        ELSE
          IF threeingots
            SET somemissing
          ELSE
            SET allok
          END_OF_IF
        END_OF_IF
      ELSE
        IF oneingot
          SET allok
        ELSE
          SET allmissing
        END_OF_IF
      END_OF_IF
    END_OF_IF
    ShowDlgScene
    ShowChar mech right
    Delay 500
    IFNOT ingotDeliveryAttempt
      Say "Vous devez être le transporteur. Je m'appelle … Oh ! C'est vous !"
      ChangeChar mech campaign/mech-explaining.png
      Say "Contente de vous revoir. Je suis en charge de l'inspection des livraisons ici."
      SET ingotDeliveryAttempt
    ELSE
      ChangeChar mech campaign/mech-explaining.png
      Say "Salut ! C'est bon de vous revoir."
    END_OF_IF
    IF allok
      ChangeChar mech campaign/mech-thumbup.png
      Say "Ouaip. Cette cargaison a l'air en règle."
      Say "Voici pour vous :"
      HideAllPaths forge workshop
      ShowPath R2-13a
      RESET ingotsPending
      SET ingotsFulfilledOnce
      IF oneingot
        IF ing3
          AddMissionCredits 9
          Announce "Vous avez reçu 9 Crédits."
          SetAchievementAttempt camp_ingots 9
        END_OF_IF
        IF ing5
          AddMissionCredits 12
          Announce "Vous avez reçu 12 Crédits."
          SetAchievementAttempt camp_ingots 12
        END_OF_IF
        IF ing6
          AddMissionCredits 15
          Announce "Vous avez reçu 15 Crédits."
          SetAchievementAttempt camp_ingots 15
        END_OF_IF
        IF ing8
          AddMissionCredits 18
          Announce "Vous avez reçu 18 Crédits."
          SetAchievementAttempt camp_ingots 18
        END_OF_IF
      END_OF_IF
      IF twoingots
        IF ing2
          AddMissionCredits 12
          Announce "Vous avez reçu 12 Crédits."
          SetAchievementAttempt camp_ingots 12
        END_OF_IF
        IF ing3
          AddMissionCredits 18
          Announce "Vous avez reçu 18 Crédits."
          SetAchievementAttempt camp_ingots 18
        END_OF_IF
        IF ing4
          AddMissionCredits 22
          Announce "Vous avez reçu 22 Crédits."
          SetAchievementAttempt camp_ingots 22
        END_OF_IF
      END_OF_IF
      IF threeingots
        IF ing2
          AddMissionCredits 21
          Announce "Vous avez reçu 21 Crédits."
          SetAchievementAttempt camp_ingots 21
        END_OF_IF
        IF ing3
          AddMissionCredits 30
          Announce "Vous avez reçu 30 Crédits."
          SetAchievementProgress camp_ingots 30
        END_OF_IF
      END_OF_IF
    ELSE
      IF somemissing
        ChangeChar mech campaign/mech-f.png
        Say "Hum. La cargaison n'est pas complète là."
        Say "Désolé, mais je ne peux pas signer pour une livraison partielle."
        ChangeChar mech campaign/mech-explaining-f.png
        Ask "Ce sont les règles, en direct d'Administra."
      ELSE
        ChangeChar mech campaign/mech-f.png
        Say "Attendez… où est la livraison ?"
        ChangeChar mech campaign/mech-explaining-f.png
        Ask "Ecoutez : nous avons besoin de lingots. Nous ne pouvons pas simplement arrêter la production."
      END_OF_IF
      MENU
        SET ingotsFailedDelivery
        OPT "Je sais. J'y retourne et je réessaye."
          SetMissionAsFailed
          ChangeChar mech campaign/mech-thumbup.png
          Say "Merci. J'apprécie beaucoup. Je sais que transporter ces lingots n'est pas un boulot facile."
          EXIT_MENU
        END_OF_OPT
      END_OF_MENU
    END_OF_IF
    Delay 300
    HideChar mech
    HideDlgScene
  ELSE
    SET ingotsFailed
  END_OF_IF
  END_FLIGHT
  FINISH_MISSION
END_OF_MISSION

MISSION roughMission
  AddOpponent "roughTrucker"
  SetDeckPreparationScript "meteor1Deck"
  SetSpecCondition "bet"
  INIT_BUILD
  BuildingHelpScript 500 tutorialAdditionalConcepts
  RESET shouldShowFlightTutorial
  ModifyOpponentsBuildSpeed 75
  SetShipType STI
  SUB_SCRIPT stdParts
  START_BUILDING
  END_BUILDING
  INIT_FLIGHT
  START_FLIGHT
  FlightHelpScript tutorialAdditionalFlightConcepts
  EVALUATE_FLIGHT
  ShowDlgScene
  ShowChar roughtrucker left
  Delay 500
  IF wasBet
    IfMissionResultIs 1
      Say "* Soupir *"
      Say "Ces paris vont me ruiner."
      AddMissionCredits 3
      AddOpponentsCredits 1 -3
      Announce "Vous recevez 3 Crédits de l'autre pilote."
      UnlockAchievement camp_bet
    ELSE
      IfMissionResultIs 0
        Say "J'ai gagné. C'est l'heure de payer votre dette [g(l'ami|l'amie)]."
        AddMissionCredits -3
        AddOpponentsCredits 1 3
        Announce "Vous devez payer 3 Crédits à l'autre pilote."
      ELSE
        Say "Ouf. Voilà qui est fait. Nous nous sommes tous les deux plantés."
        Say "J'imagine que personne n'a gagné."
        Say "Peut-être la prochaine fois."
      END_OF_IF
    END_OF_IF
  ELSE
    IfMissionResultIs 1
      Say "Hum. Je suis content que vous ayez décliné ce pari."
    ELSE
      IfMissionResultIs 0
        Say "Ah, j'ai gagné ! Dommage que nous n'ayons rien parié."
      ELSE
        Say "Euh. Voilà qui est fait. Nous nous sommes tous les deux plantés."
      END_OF_IF
    END_OF_IF
  END_OF_IF
  Delay 300
  HideChar roughtrucker
  Delay 200
  HideDlgScene
  END_FLIGHT
FINISH_MISSION
END_OF_MISSION

END_OF_SCRIPTS
