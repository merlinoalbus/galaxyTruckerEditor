SCRIPTS

SCRIPT mine0Dlg

  ShowDlgScene
  ShowChar mineBot0 left
  IFNOT mine0friends
    GO mine0firstTime
  END_OF_IF
  Say "Empfangsrobot AX-5- ... Oh, du bist das. Hallo."
  RESET mine0again
  RESET mine0first
  IFNOT mine0pending
    IF mine0accepted
      SET mine0again
    ELSE
      SET mine0first
    END_OF_IF
  END_OF_IF
  MENU
    OPT_IF mine0first "Hallo. Hast du einen Job für mich?"
      Say "Sicher. Wir haben einige radioaktive Isotope, die zur Gießerei geliefert werden sollen."
      Say "Dazu müsstest du 3 Credits hinterlegen. Allerdings verdienst du bei Ablieferung 9."
      Ask "Was meinst du?"
      MENU
        OPT "Sicher, ich bin dabei."
          SET mine0accepted
          GO mine0friendlyAccept
        END_OF_OPT
        OPT "Hm. Vielleicht später."
          Say "Okay. Kein Problem."
          GO mine0End
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
    OPT_IF mine0again "Hallo. Muss ich noch eine Lieferung für dich erledigen?"
      Say "Sicher. Es gelten dieselben Bedingungen."
      GO mine0friendlyAccept
    END_OF_OPT
    OPT "Ich bin nur mal vorbeigehuscht, um Hallo zu sagen."
      Say "Schön, dich wiederzusehen."
      IF mine0talksPending
        GO mine0Talk
      END_OF_IF
      GO mine0End
    END_OF_OPT
  END_OF_MENU

  LABEL mine0friendlyAccept
  SET mine0pending
  ShowPath U2-14b
  Say "Die Ladung steht in der Lagerhalle bereit."
  IF mine0talksPending
    GO mine0Talk
  END_OF_IF
  Say "Danke, dass du reingeschaut hast."
  GO mine0End

  LABEL mine0firstTime
  Say "Empfangsrobot AX-5-B32-3 Delta zu Diensten."
  Say "Willkommen bei der Bergbaueinrichtung AX-5-B32-3 Delta, mein Herr/meine Dame (Nichtzutreffendes streichen)."
  Ask "Was ist der Zweck Ihres Besuches?"
  MENU
    OPT_IFNOT mine0pending "Ich suche einen Job."
      Say "Anfrage wurde registriert."
      Say "Durchsuche Datenbank ..."
      Delay 500
      Say "..."
      Delay 500
      Say "Ein offener Job gefunden."
      Say "Job: Auslieferung radioaktiver Isotope."
      Say "Ziel: Planetare Giesserei."
      Say "Fracht: 1 Einheit(en) radioaktiver Isotope."
      Say "Hinterlegung notwendig: 3 Credits."
      Say "Bezahlung bei Auslieferung: 9 Credits + Rueckgabe der hinterlegten Summe."
      Ask "Annehmen/Ablehnen?"
      MENU
        OPT "Klingt gut, ich bin dabei."
          SET mine0accepted
          SET mine0pending
          Say "Job zugewiesen."
          Say "Fracht wird in Lagerhalle verlegt."
          HideAllPaths mine0 forge
          ShowPath U2-14b
          Ask "Noch ein Wunsch?"
          EXIT_MENU
        END_OF_OPT
        OPT "Hm. Kein Interesse."
          Say "Registriere kein Interesse."
          Say "Noch ein Wunsch?"
          EXIT_MENU
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
    OPT_IFNOT mine1shown "Ich würde gerne wissen, wie man zu den Asteroiden kommt."
      SET mine1shown
      Say "Anfrage wurde registriert."
      Say "Verbindung zu Ihrer Karte ..."
      Delay 500
      Say "Verbindung hergestellt. Karte wird aktualisiert."
      Delay 800
      ShowNode mine1
      CenterMapByNode mine1
      Delay 500
      ShowPath R1-22a
      Delay 500
      Say "Karte ist aktualisiert. Bergbau-Asteroid AX-5-B32-3 Gamma hinzugefuegt."
      Say "Durchsuche Datenbank nach Zusatzinformationen."
      Delay 750
      Say "1 Eintraege gefunden. Titel: Andock-Vorschriften."
      Ask "Eintrag anzeigen? Ja/Nein."
      MENU
        OPT "Ja, gerne."
          EXIT_MENU
        END_OF_OPT
        OPT "Dämliche Roboter."
          Say "Durchsuche Datenbank ..."
          Delay 300
          Say "..."
          Delay 300
          Say "Keine Eintraege zu Suchbegriff 'daemliche roboter' gefunden."
          EXIT_MENU
        END_OF_OPT
      END_OF_MENU
      Say "Zitiere Eintrag:"
      Say "WEGEN GERINGER VERFUEGBARER OBERFLAECHE DES ASTEROIDEN WERDEN ANDOCK-ANFRAGEN UEBLICHERWEISE ABGEWIESEN."
      SET mineRegulations
      Say "Zitat Ende."
    END_OF_OPT
    OPT_IF mineRegulations "Erzähl noch mal von der Landung auf Gamma."
      Say "WEGEN GERINGER VERFUEGBARER OBERFLAECHE DES ASTEROIDEN WERDEN ANDOCK-ANFRAGEN UEBLICHERWEISE ABGEWIESEN."
    END_OF_OPT
    OPT "Ich glaube, ich bin hier fertig."
      Say "Registriere keine Anfrage."
      Say "Empfangsrobot AX-5-B32-3 Delta Ende."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL mine0afterDialogue
  IFNOT mine1shown
    GO mine0End
  END_OF_IF
  IF mine0talksPending
    GO mine0Talk
  END_OF_IF
  IF mine0friends
    GO mine0End
  END_OF_IF
  Ask "(Mann, wie ich diesen blöden Job hasse.)"
  MENU
    OPT "Wirklich?"
      Say "Ja. Weißt du, wie nervtötend es ist, den ganzen Tag wie eine Vollspacke zu reden?"
      Say "Bla bla ... Anfrage registriert ... bla bla ... Durchsuche Datenbank ... Daemlichkeitslevel auf Maximum."
      Say "Diese Roboterprotokollsprache nervt. Aber, weißt du, die Menschen erwarten das einfach von uns."
      Ask "Ich will diesen Mistjob natürlich nicht verlieren. Ich bin schon 30 Jahre über die Garantiezeit hinaus. Dürfte schwer sein, was anderes zu finden."
      SET mine0friends
      SET mine0talksPending
      GO mine0Talk
    END_OF_OPT
    OPT "Halt die Klappe und mach deine Arbeit, du Toaster!"
      ChangeChar mineBot0 avatars/ai/easy_angry.png
      Say "Ähmmm...verstanden. Anweisung registriert."
      Say "Klappe halten und daemliche Arbeit machen."
      GO mine0End
    END_OF_OPT
  END_OF_MENU

  LABEL mine0Talk
  MENU
    OPT_IFNOT mine0howMany "He, kannst du mir sagen, wie viele Asteroiden es gibt?"
      Say "Vier. Aber Bergbau findet nur noch auf dreien statt."
      Say "Der älteste, AX-5-B32-3 Alpha, hat schon vor langer Zeit keine Isotope mehr ausgespuckt."
      Say "Das seltsame ist, dass in der Datenbank nichts über Alpha drinsteht. Aber ich habe da so Geschichten gehört ..."
      Say "Die alten Roboter sagen, dass auf Alpha der Robot-Guru sitzt, der weiseste Roboter der Galaxis."
      SET mine0howMany
      SET mine0canAboutGuru
    END_OF_OPT
    OPT_IF mine0canAboutGuru "Erzähl mehr von diesem Robot-Guru."
      RESET mine0canAboutGuru
      SET mine0guru
      IF mine0truckingAnswered
        RESET mine0talksPending
      END_OF_IF
      Say "Besucht man ihn, folgt sofort die Erleuchtung, heißt es."
      Say "Etwa wie ... kennst du deinen Lebenszweck? Deinen WAHREN Lebenszweck?"
      Say "Kennst du nicht, was?"
      Ask "Der Robot-Guru hilft dir, zu verstehen."
    END_OF_OPT
    OPT_IFNOT mine0worthy "He, kannst du mir mehr über die Andock-Vorschriften der anderen Asteroiden erzählen?"
      Say "Oh, sicher. Die älteren Asteroiden sind ziemlich begehrt."
      Say "Jeder Roboter möchte ein Haus und einen Job auf einem älteren Asteroiden."
      Say "Problem ist bloß, dass sie so klein sind. Sie wurden derart ausgebeutet, dass kaum noch Platz auf ihnen ist."
      Say "Daher darf bei jedem Konvoi aus Truckern nur der beste dort landen. Die anderen müssen umkehren."
      Ask "Ich würde ja gerne nach Gamma umziehen, aber ich bin kein guter Trucker."
      SET mine0worthy
      SET mine0trucking
    END_OF_OPT
    OPT_IF mine0trucking "Moment mal: Roboter können Trucker sein?"
      RESET mine0trucking
      SET mine0truckingAnswered
      IF mine0guru
        RESET mine0talksPending
      END_OF_IF
      Say "Oh ja. Aber wir machen das komplett anders."
      Say "Du weißt doch, was Trucker in einer Lagerhalle machen? Jeder grabscht sich so schnell wie möglich Bauteile zusammen. Barbarisch!"
      Say "Wir Roboter sind da feinsinniger. Für uns ist Schiffsbau eine Sache des Verstands, nicht der Reflexe."
      Say "Außerdem sind unsere Regeln den älteren und langsamen Modellen gegenüber fairer."
      Ask "Du wirst sehen, was ich meine, wenn du mal zu einem älteren Asteroiden fliegst."
    END_OF_OPT
    OPT "Okay. Danke. Ich denke, ich gehe dann mal."
      Say "Bis später. War nett, inoffiziell mit dir zu plaudern."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL mine0End
  Delay 300
  HideChar mineBot0
  Delay 300
  HideDlgScene
END_OF_SCRIPT

SCRIPT mine1Dlg

  ShowDlgScene
  ShowChar mineBot1 left
  Say "Empfangsrobot AX-5-B32-3 Gamma zu Diensten."
  Say "Willkommen bei der Bergbaueinrichtung AX-5-B32-3 Gamma, mein Herr/meine Dame (Nichtzutreffendes streichen)."
  Ask "Was ist der Zweck Ihres Besuches?"
  MENU
    OPT_IFNOT mine1relax "He, was soll dieser mechanische Ton? Mach dich mal locker."
      SET mine1relax
      Ask "Anfrage nicht erkannt. Bitte erneut anfragen."
      MENU
        OPT_IFNOT mine1relax1 "Du kannst ganz normal mit mir reden."
          SET mine1relax1
          Ask "Normal = regulaer. Regulaer = protokollarisch vorgeschrieben."
        END_OF_OPT
        OPT_IFNOT mine1relax2 "Ich weiß doch, dass du dir blöd vorkommen musst, ständig so zu reden."
          SET mine1relax2
          Ask "Negativ, mein Herr/meine Dame (Nichtzutreffendes streichen). Mein Job sorgt fuer Zufriedenheitsmaximierung."
        END_OF_OPT
        OPT "Ach, vergiss es."
          Say "Anfrage wurde registriert."
          Say "Speicherung bisheriger Konversation wird geloescht."
          EXIT_MENU
        END_OF_OPT
      END_OF_MENU
      Ask "Noch ein Wunsch?"
    END_OF_OPT
    OPT_IFNOT mine1pending "Gib mir einen Job."
      IFNOT mine1accepted
        Say "Anfrage wurde registriert."
        Say "Durchsuche Datenbank ..."
        Delay 500
        Say "..."
        Delay 500
        Say "Ein offener Job gefunden."
      ELSE
        Say "Vorigen Job unter gleichen Bedingungen erneut ausfuehren?"
        MENU
          OPT "Genau. Sicher."
            SET mine1pending
            ShowPath U2-15b
            Say "Job zugewiesen."
            Ask "Noch ein Wunsch?"
            EXIT_MENU
          END_OF_OPT
          OPT "Könntest du diese Bedingungen noch mal wiederholen?"
            EXIT_MENU
          END_OF_OPT
        END_OF_MENU
      END_OF_IF
      IFNOT mine1pending
        Say "Job: Auslieferung radioaktiver Isotope."
        Say "Ziel: Planetare Giesserei."
        Say "Fracht: 2 Einheit(en) radioaktiver Isotope."
        Say "Hinterlegung notwendig: 3 Credits pro Einheit."
        Say "Bezahlung bei Auslieferung: 9 Credits pro ausgelieferter Einheit + Rueckgabe der hinterlegten Summe."
        Ask "Annehmen/Ablehnen?"
        MENU
          OPT "Ja, okay."
            SET mine1accepted
            SET mine1pending
            Say "Job zugewiesen."
            Say "Fracht wird in Lagerhalle verlegt."
            HidePath R2-15b
            ShowPath U2-15b
            Ask "Noch ein Wunsch?"
            EXIT_MENU
          END_OF_OPT
          OPT "Kein Interesse."
            Say "Registriere kein Interesse."
            Ask "Noch ein Wunsch?"
            EXIT_MENU
          END_OF_OPT
        END_OF_MENU
      END_OF_IF
    END_OF_OPT
    OPT_IFNOT mine2shown "Ich würde gerne zum nächsten Asteroiden weiter."
      SET mine2shown
      Say "Anfrage wurde registriert."
      Say "Verbindung zu Ihrer Karte ..."
      Delay 500
      Say "Verbindung hergestellt. Karte wird aktualisiert."
      Delay 800
      ShowNode mine2
      CenterMapByNode mine2
      Delay 500
      ShowPath R2-23a
      Delay 500
      Say "Karte ist aktualisiert. Bergbau-Asteroid AX-5-B32-3 Beta hinzugefuegt."
      Say "Durchsuche Datenbank nach Zusatzinformationen."
      Delay 750
      Say "1 Eintraege gefunden. Titel: Andock-Vorschriften."
      Ask "Eintrag anzeigen? Ja/Nein."
      MENU
        OPT "Ja, gerne."
          Say "Zitiere Eintrag:"
          Say "WEGEN GERINGER VERFUEGBARER OBERFLAECHE DES ASTEROIDEN WERDEN ANDOCK-ANFRAGEN UEBLICHERWEISE ABGEWIESEN."
          Say "Zitat Ende."
          Ask "Noch ein Wunsch?"
          EXIT_MENU
        END_OF_OPT
        OPT "Ja, ich weiß, weil der Asteroid keine so große Fläche hat ..."
          Say "... WERDEN ANDOCK-ANFRAGEN UEBLICHERWEISE ABGEWIESEN."
          Say "Zitat Ende."
          Ask "Noch ein Wunsch?"
          EXIT_MENU
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
    OPT "Ich glaube, ich bin hier fertig."
      Say "Registriere keine Anfrage."
      Say "Empfangsrobot AX-5-B32-3 Gamma Ende."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  HideChar mineBot1
  HideDlgScene
END_OF_SCRIPT

SCRIPT mine2Dlg

  ShowDlgScene
  ShowChar mineBot2 left
  Say "Beta hier."
  Ask "Du willst radioaktive Suppe transportieren oder bloß fragen, wie's nach Alpha geht?"
  MENU
    OPT_IFNOT mine2what "Was ist mit den offiziellen Roboterprotokollen los?"
      SET mine2what
      Say "Was soll damit los sein?"
      Ask "Sag einfach, was du von mir willst."
    END_OF_OPT
    OPT_IFNOT mine2pending "Ich will einen Job."
      Say "Okay. Du kannst drei Suppen-Eimer zur Gießerei bringen."
      Say "Für jeden lässt du ein Pfand von 3 Credits hier. Das kriegst du zurück, sowie 9 Credits extra pro ausgeliefertem Eimer."
      Ask "In Ordnung?"
      MENU
        OPT "Ja. Sicher."
          SET mine2accepted
          SET mine2pending
          ShowPath U2-16b
          Ask "Na gut. Sonst noch was?"
          EXIT_MENU
        END_OF_OPT
        OPT "Ich glaube nicht."
          Ask "Na gut. Was willst du noch?"
          EXIT_MENU
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
    OPT_IFNOT mine3shown "Ich möchte nach Alpha."
      SET mine3shown
      Say "Soll mir recht sein."
      Delay 800
      ShowNode mine3
      IF tutorWhatDoNext
        ShowButton bmine3
        SetFocus bmine3
      END_OF_IF
      CenterMapByNode mine3
      Delay 500
      IF class3
        ShowPath R3-24a
        Delay 500
      END_OF_IF
      Ask "Da ist Alpha. Es gibt ein paar Einschränkungen auf der Strecke. Noch was?"
      IFNOT class3
        MENU
          OPT "He, ich kann da gar keine Route sehen!"
            Say "Nicht mein Problem. Ich hab's dir hochgeladen."
            Ask "Vielleicht fehlt dir ja die Lizenz dafür. Ist 'ne Klasse-III-Route."
            EXIT_MENU
          END_OF_OPT
        END_OF_MENU
        MENU
          OPT "Oh. Komme ich da noch irgendwie anders hin?"
            Ask "Nö. Dazu brauchst du 'ne Klasse-III-Lizenz. Noch was?"
            EXIT_MENU
          END_OF_OPT
        END_OF_MENU
      END_OF_IF
    END_OF_OPT
    OPT "Ich brauche sonst nichts."
      Say "Gut."
      Say "(Mann! Menschen sind echt nervig!)"
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  HideChar mineBot2
  HideDlgScene
END_OF_SCRIPT

SCRIPT OnlyWinnerDlg

  ShowDlgScene
  IF_IS destination 1
    ShowChar limitBot1 right
    Say "STOP!"
    Say "NUR DER BESTE TRUCKER DARF ANDOCKEN."
    Say "DIE ANDEREN MUESSEN UMKEHREN."
    HideChar limitBot1
  END_OF_IF
  IF_IS destination 2
    ShowChar limitBot2 right
    Say "STOP!"
    Say "NUR DER BESTE TRUCKER DARF ANDOCKEN."
    Say "DIE ANDEREN MUESSEN UMKEHREN."
    HideChar limitBot2
  END_OF_IF
  IF_IS destination 3
    ShowChar guru right
    Say "Sei gegrüßt."
    Say "Nur ein Würdiger darf auf diesem Asteroiden landen."
    Say "Die anderen müssen umkehren und über ihr Versagen nachdenken."
    HideChar guru
  END_OF_IF
  IF_IS destination 0
    ShowChar alterEgo left
    IF_ORDER 0 1
      Ask "Frohlocke, denn du hast die größte Herausforderung überwunden. Du hast das Rennen gegen dich selbst gewonnen."
      MENU
        OPT "Ja, ich bin auch recht zufrieden mit mir."
          EXIT_MENU
        END_OF_OPT
        OPT_IFNOT guruRecognized "He, ich kenne dich, Robot-Guru."
          Ask "Namen sind Schall und Rauch. Ich bin dein anderes Ich – und du hast mich besiegt."
          SET guruRecognized
        END_OF_OPT
      END_OF_MENU
      Delay 200
      UnlockAchievement camp_alter_ego
      Delay 500
      Say "Deine spirituelle Entwicklung ist abgeschlossen."
      Say "Hier bitte, nimm einen Flyer vom Goldclub."
    ELSE
      Say "Verzweifle nicht an deinem Versagen."
      MENU
        OPT_IFNOT guruRecognized "He, ich kenne dich, Robot-Guru."
          Say "Suche nicht die Fehler der anderen. Ich bin dein anderes Ich. Dich selber zu besiegen, ist eine höchst schwierige Aufgabe."
          SET guruRecognized
        END_OF_OPT
        OPT "Das war eine interessante Prüfung. Die würde ich gerne nochmal probieren."
          Say "Gerne. Wir fliegen zurück."
          SetSpecCondition "Noone"
          EXIT_MENU
        END_OF_OPT
        OPT "Urgh. Das reicht mir jetzt aber. Lass mich in Ruhe."
          Say "Niemand hat behauptet, dass der Pfad zur Erleuchtung ein einfacher sein würde."
          Say "Doch wenn es dein Wunsch ist, magst du ihn alleine gehen."
          Say "Doch zunächst nimm dir diese Broschüre vom Goldclub."
          SetSpecCondition "OnlyLoser"
          EXIT_MENU
        END_OF_OPT
      END_OF_MENU
    END_OF_IF
    HideChar alterEgo
  END_OF_IF
  HideDlgScene
END_OF_SCRIPT

END_OF_SCRIPTS
