SCRIPTS

SCRIPT newbieDlg

  IF class3
    SUB_SCRIPT tutorClass3Dlg
    RETURN
  END_OF_IF
  IF tutor_moremissions
    Delay 200
    ShowDlgScene
    ChangeChar tutor campaign/tutor-hidden.png
    ShowChar tutor left
    Delay 1500
    ChangeChar tutor campaign/tutor-pizza.png
    Delay 500
    SayChar tutor "Siehst du nicht, dass ich beschäftigt bin? Hau ab!"
    Delay 500
    ChangeChar tutor campaign/tutor-hidden.png
    Delay 500
    HideChar tutor
    Delay 500
    HideDlgScene
    RETURN
  END_OF_IF
  Delay 200
  ShowDlgScene
  ShowChar tutor left
  Delay 200
  IF tutor_somemissions
    Say "Ach, wieder mal? Hoffe, du bist mit unserer langweiligen regulären Strecke zufrieden."
    MENU
      OPT "Ja, ich wollte nur mal Hallo sagen."
        HideDlgScene
        RETURN
      END_OF_OPT
      OPT "Könntest du eine andere Route empfehlen?"
        GO more_missions
      END_OF_OPT
    END_OF_MENU
  END_OF_IF
  IF tutorialRefused
    IF playerIsCocky
      Ask "Ha, [g(Herr|Frau)] Naseweis! Hast du deine Meinung also geändert?"
    ELSE
      Ask "Ha, [g(Herr|Frau)] Ich-finde-Tutorials-blöd! Hast du deine Meinung also geändert?"
    END_OF_IF
    MENU
      OPT "Nee, zeig mir bloß, wie ich hier wegkomme."
        Say "Okay. Du kannst eine reguläre Strecke von hier bis zur Weltraumbar und zurück nehmen."
        Delay 500
        ShowNode "bar"
        CenterMapByNode bar
        Delay 750
        ShowPath "R1-1a"
        ShowPath "R1-1b"
        Delay 500
        CenterMapByPath R1-1a
        Delay 1000
        SET tutor_somemissions
        Ask "Eine sichere und langweile Stecke. Du könntest den Rest deines jämmerlichen Lebens auf dieser Route verbringen."
        MENU
          OPT "Klingt gut."
            GO basic_route_agreed
          END_OF_OPT
          OPT "Hey, ich will eine echte Mission!"
            GO more_missions
          END_OF_OPT
        END_OF_MENU
      END_OF_OPT
    END_OF_MENU
  ELSE
    Ask "Ha! [g(Der Anfänger|Die Anfängerin)]! Deine Ausbildung ist abgeschlossen. Was brauchst du denn noch?"
    MENU
      OPT "Du hast mir nicht gesagt, was ich als Nächstes tun soll."
        Say "Oh, richtig. Du solltest eine reguläre Route von hier zur Weltraumbar nehmen."
        Delay 250
        ShowNode "bar"
        CenterMapByNode bar
        Delay 750
        ShowPath "R1-1a"
        ShowPath "R1-1b"
        Delay 500
        CenterMapByPath R1-1a
        Delay 1000
        SET tutor_somemissions
        Ask "Eine sichere und langweile Stecke. Du könntest den Rest deines jämmerlichen Lebens auf dieser Route verbringen."
        MENU
          OPT "Klingt gut. Danke."
            Say "Großartig. [v(Tipp|Klick)]e einfach die blinkende Rakete an und es kann losgehen."
            GO basic_route_agreed
          END_OF_OPT
          OPT "Gibt es noch andere Optionen?"
            GO more_missions
          END_OF_OPT
        END_OF_MENU
      END_OF_OPT
    END_OF_MENU
  END_OF_IF

  LABEL basic_route_agreed
  HideDlgScene
  RETURN

  LABEL more_missions
  Ask "Ich glaube, mehr würde dich jetzt überfordern."
  MENU
    OPT "Okay, vermutlich hast du recht. Ich nehme die reguläre Strecke."
      GO newbie_end
    END_OF_OPT
    OPT "Nein, ich suche die Herausforderung."
      Say "Ganz schön vorwitzig bist du. Du hast es so gewollt."
      Say "Es gibt noch eine Strecke, zu einem entlegenen Außenposten."
      Delay 1000
      ChangeChar tutor campaign/tutor-scary.png
      Delay 500
      AskChar tutor "Voller Gefahren und schrecklicher Dinge ist sie."
      MENU
        OPT "Erzähl mir von diesen Gefahren."
          Say "Schreckliche und unaussprechliche Sachen sind's."
          Say "Deine Fragen ich nicht beantworten werde, junger Padawan."
          Ask "Zu viel schon habe ich gesagt ..."
          MENU
            OPT "He, komm schon."
              Delay 250
              ChangeChar tutor campaign/tutor.png
              Delay 250
              Say "Nun, da gibt es ein paar üble Meteoriten. Reicht dir das?"
              Say "Und jetzt: hau ab. Ich kriege gleich eine Pizzalieferung."
              GO more_missions1
            END_OF_OPT
          END_OF_MENU
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL more_missions1
  HideChar tutor
  ResetFocus btutor
  Delay 250
  ShowNode outpost
  CenterMapByNode outpost
  Delay 500
  ShowPath "R1-2a"
  ShowPath "R1-2b"
  CenterMapByPath "R1-2a"
  Delay 500
  SET tutor_moremissions

  LABEL newbie_end
  HideDlgScene
  RETURN
END_OF_SCRIPT

SCRIPT embassyDlg

  ShowDlgScene
  IF purpleEscortFinished
    ShowChar ambassador left
    Delay 250
    ChangeChar ambassador campaign/ambassador-happy.png
    Delay 100
    Say "Danke für Ihren Service. Das hat sehr geholfen."
    Say "Hier, nehmen Sie diese 10 Credits als Belohnung. Sie kommt aus unserem Fonds für Unerwartete Zahlungen an Unbekannte."
    AddCredits 10
    Announce "Du hast 10 Credits erhalten."
    Delay 250
    HideButton bambassador
    HideChar ambasador
    HideDlgScene
    RETURN
  END_OF_IF
  IF purpleAccepted
    ShowChar ambassador left
    Delay 250
    Say "Bitte, wir müssen uns beeilen."
    Say "Wir können keine weiteren Probleme mit den Aliens von Th'kzw gebrauchen. Sie sind ausgezeichnete Schützen."
    Delay 250
    HideChar ambassador
    HideDlgScene
    RETURN
  END_OF_IF
  IFNOT wasAtEmbassy
    SET wasAtEmbassy
    ChangeChar ambassador campaign/ambassador-peek.png
    ShowChar ambassador right
    Delay 250
    Ask "Pssst! Könnten Sie mir bitte durch diesen dunklen Gang folgen?"
    MENU
      OPT "Natürlich. Warum nicht?"
        GO corridor
      END_OF_OPT
      OPT "Meine Mama hat mich immer davor gewarnt, Leuten in dunkle Gänge zu folgen."
        Ask "Bitte. Es handelt sich um eine delikate Angelegenheit. Ich kann darüber nicht offen sprechen."
        MENU
          OPT "Oh, na gut."
            GO corridor
          END_OF_OPT
          OPT "Nein, nein, nein!"
            SET corridorRefused
            GO refused
          END_OF_OPT
        END_OF_MENU
      END_OF_OPT
    END_OF_MENU
  ELSE
    IF purpleOfferMade
      ChangeChar ambassador campaign/ambassador.png
      ShowChar ambassador left
      Delay 250
      Ask "Ja?"
      MENU
        OPT "Brauchen Sie immer noch jemanden, um Herrn Sie-wissen-schon-wen nach Sie-wissen-schon-wo zu bringen?"
          Ask "Ja. Möchten Sie das übernehmen?"
          MENU
            OPT "Ja, ich bin dabei."
              GO acceptPurple
            END_OF_OPT
            OPT "Nee, ich war bloß neugierig."
              GO refused
            END_OF_OPT
          END_OF_MENU
        END_OF_OPT
      END_OF_MENU
    ELSE
      ChangeChar ambassador campaign/ambassador-peek.png
      ShowChar ambassador right
      Delay 250
      Say "Ja?"
      MENU
        OPT "Ich hab's mir anders überlegt. Lassen Sie uns in den dunklen Gang gehen."
          GO corridor
        END_OF_OPT
        OPT "Wollte mich bloß mal umsehen."
          GO refused
        END_OF_OPT
      END_OF_MENU
    END_OF_IF
  END_OF_IF

  LABEL corridor
  Say "Großartig, danke."
  Delay 250
  HideChar ambassador
  Delay 1500
  ChangeChar ambassador campaign/ambassador.png
  ShowChar ambassador left
  Delay 250
  Say "Also ... wir haben hier eine vertrackte Situation. Wir hatten eine Abordnung von Th'kzw-r hier. Als sie uns verlassen haben, ließen sie versehentlich einen ihrer Delegierten zurück."
  Ask "Würden Sie die Freundlichkeit besitzen, und Herrn K'wwfrg zu seinem Heimatplaneten bringen?"
  SET purpleOfferMade
  MENU
    OPT "Sicher. Ich bringe Herrn K'wwfrg nach Th'kzw-r. Kein Problem."
      GO acceptPurple
    END_OF_OPT
    OPT "Warum nehmen Sie nicht einfach ein Diplomatenschiff für den Trip?"
      Ask "Naja, die Situation ist ein wenig peinlich für Herrn K'wwfrg. Wir möchten das so unauffällig und leise wie möglich erledigen."
      MENU
        OPT "Gut, ich bin dabei."
          GO acceptPurple
        END_OF_OPT
        OPT "Ich glaube, Sie sollten lieber jemand anderen suchen."
          GO refused
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL acceptPurple
  Say "Oh, das ist wunderbar. Dankeschön."
  Delay 250
  ChangeChar ambassador campaign/ambassador-happy.png
  Delay 250
  Say "Herr K'wwfrg?"
  Delay 1000
  ShowChar purpleDipl right
  Delay 250
  SayChar ambassador "[g(Dieser Herr|Diese Dame)] wird sie nach Hause begleiten."
  Delay 250
  AskChar purpleDipl "Wzrr kh'gwh qt Th'kzw-r!"
  MENU
    OPT "Gerne geschehen."
      Delay 250
      HideChar purpleDipl
      Delay 500
      ChangeChar ambassador campaign/ambassador.png
      Delay 250
      SayChar ambassador "Vielen Dank. Wir werden Sie nach Ihrer Rückkehr entlohnen."
      SET purpleAccepted
      ShowNode "purpleHome"
      ShowPath "U1-5purple"
      SET purpleAccepted
      ResetFocus bambassador
      IF VIPTried
        HideButton bvip
        ShowButton bvip2
      END_OF_IF
      HideChar ambassador
      HideDlgScene
      RETURN
    END_OF_OPT
  END_OF_MENU

  LABEL refused
  Say "Ähm, tja ... einen schönen Tag noch, dann."

  LABEL sceneEnd
  Delay 1000
  HideChar ambassador
  HideDlgScene
  RETURN
END_OF_SCRIPT

SCRIPT barDlg

  ShowDlgScene
  ShowChar bartender left
  Ask "Hallo. Was kann ich für Sie tun?"
  IF boozeQuestHeard
    MENU
      OPT_IF mechDebtPending "Eine Mechanikerin hat mich hierher geschickt."
        GO bar_debt
      END_OF_OPT
      OPT_IF boozeQuestTried "Muss ich noch eine Lieferung für Sie erledigen?"
        GO booze_quest
      END_OF_OPT
      OPT_IF boozeQuestRefused "Brauchen Sie immer noch jemanden für eine Lieferung?"
        GO booze_quest
      END_OF_OPT
      OPT "Heute möchte ich bloß ein bisschen Geld hier verprassen. Geben Sie mir von Ihrem Teuersten."
        GO drinking
      END_OF_OPT
      OPT "Oh, sorry. Ich bin aus Versehen in Ihre Bar gelatscht."
        Say "Schon gut. Noch einen schönen Tag."
        GO leaving_bar
      END_OF_OPT
    END_OF_MENU
  END_OF_IF
  MENU
    OPT_IF mechDebtPending "Eine Mechanikerin hat mich hierher geschickt."
      GO bar_debt
    END_OF_OPT
    OPT "Mixen Sie mir Ihren besten Cocktail."
      Ask "Natürlich. Das macht bitte 1 Credit."
      IF_HAS_CREDITS 1
        MENU
          OPT "Das kann ich mir leisten. Ich hab auf dem Weg hierher ein bisschen Geld gemacht. Hier bitte:"
            Say "Dankeschön."
            AddCredits -1
            Announce "Du hast 1 Credit bezahlt."
            GO anything_else
          END_OF_OPT
          OPT "Das ist viel zu teuer. Ich gehe."
            Say "Na gut. Noch einen schönen Tag."
            GO leaving_bar
          END_OF_OPT
        END_OF_MENU
      ELSE
        MENU
          OPT "Bah, vergessen Sie's. Ich bin sowieso gerade knapp bei Kasse."
            Say "Also gut. Noch einen schönen Tag."
            GO leaving_bar
          END_OF_OPT
        END_OF_MENU
      END_OF_IF
    END_OF_OPT
    OPT "Wollte mich bloß mal umsehen. Schöne Bar ist das hier."
      GO nice_bar
    END_OF_OPT
    OPT "Diese Spelunke ist ja widerlich. Ich gehe wieder."
      Say "Na gut. Noch einen schönen Tag."
      GO leaving_bar
    END_OF_OPT
  END_OF_MENU
  HideChar programmer
  HideDlgScene

  LABEL anything_else
  Say "Gibt's noch etwas?"
  MENU
    OPT "Danach wollte ich noch fragen. Gibt's noch etwas? Eine Mission oder so was?"
      Say "Wie bitte?"
      MENU
        OPT "Ich habe gerade Geld für einen virtuellen Drink rausgeschmissen. Das sollte dann doch automatisch zu einer neuen Mission führen, oder?"
          Delay 200
          ChangeChar bartender campaign/bartender-close.png
          Delay 200
          Say "Ach, jetzt verstehe ich. Tut mir leid, aber Sie haben die falsche Handlungsoption genommen."
          Say "Diejenige, die zu einer Mission führt, wäre „bloß mal umsehen“ gewesen. Das hätte außerdem nichts gekostet."
          Delay 200
          ChangeChar bartender campaign/bartender.png
          Delay 200
          Say "Gibt's noch etwas?"
          MENU
            OPT_IFNOT bar_quest_insist "Gib mir schon eine Mission."
              SET bar_quest_insist
              Ask "Tut mir leid, aber ich mache hier bloß Drinks. Gibt's noch etwas?"
            END_OF_OPT
            OPT "Noch einen Drink, bitte!"
              GO drinking
            END_OF_OPT
            OPT "Nee. Ich brauche nichts. Wollte mich bloß mal umsehen. Schöne Bar ist das hier."
              GO nice_bar
            END_OF_OPT
          END_OF_MENU
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL drinking
  IF_HAS_CREDITS 1
    Say "Gleich fertig! Hier bitte. Ein Credit bitte."
    AddCredits -1
    Announce "Du hast 1 Credit bezahlt."
    MENU
      OPT "*Gluck*"
        Ask "Darf's noch etwas sein?"
        MENU
          OPT "Ach, machen Sie mir noch einen."
            GO drinking
          END_OF_OPT
          OPT "Hm. Ich glaube, dass war ein Drink zu viel. Ich sollte lieber gehen."
            Say "Dankeschön. Schönen Tag noch."
            GO leaving_bar_in_shame
          END_OF_OPT
        END_OF_MENU
      END_OF_OPT
    END_OF_MENU
  ELSE
    Say "Ein Credit bitte ... Oh, Sie haben kein Geld mehr, sehe ich gerade."
    Say "Hier, nehmen Sie einen Dauerlutscher. Geht auf's Haus. Und jetzt machen Sie bitte, dass Sie fortkommen."
    GO leaving_bar_in_shame
  END_OF_IF

  LABEL nice_bar
  Say "Dankeschön. Das war früher mal ein toller Ort."
  MENU
    OPT "Früher?"
      Say "Die Geschäfte gehen heutzutage nicht mehr so gut. Wir werden vermutlich bald schließen."
      Say "Ich habe hier eine Menge teures, hochprozentiges Zeug. Die Trucker wollen aber lieber billigen Fusel."
      Ask "Hätte ich doch bloß eine Bar in Regula eröffnet. Dort weiß man Qualität noch zu schätzen."
      MENU
        OPT "Schicken Sie doch Ihre unverkäuflichen Getränke nach Regula."
          SET boozeQuestHeard
          Say "Das ist leichter gesagt als getan. Die Route ist für so eine zerbrechliche Fracht nicht gerade ungefährlich."
          Ask "Ich bezweifle, dass es einen Trucker gibt, der mir etwas nach Regula transportieren würde."
          MENU
            OPT "Sie haben soeben einen gefunden!"
              GO booze_quest
            END_OF_OPT
            OPT "Das bezweifle ich auch. Viel Glück noch, ich bin dann mal weg."
              SET boozeQuestRefused
              Say "Dankeschön. Noch einen schönen Tag."
              GO leaving_bar
            END_OF_OPT
          END_OF_MENU
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL leaving_bar_in_shame
  Delay 200
  ChangeChar bartender campaign/bartender-close.png
  Delay 200
  Say "Oh ... wären Sie so nett und würden Sie durch den Hintereingang verschwinden? Dankeschön."

  LABEL leaving_bar
  Delay 300
  HideChar bartender
  HideDlgScene
  RETURN

  LABEL booze_quest
  Delay 200
  ChangeChar bartender campaign/bartender-close.png
  Delay 200
  Say "Oh, das würden Sie wirklich machen?"
  Ask "Wie viele Kisten Getränke würden Sie denn mitnehmen wollen?"
  MENU
    OPT "Geben Sie mir zwei Kisten. Kein Problem, die auszuliefern."
      HidePath "R1-3a"
      ShowPath "R1-3a2"
      Say "Das ist sehr großzügig!"
      Say "Die Kisten werden in der Teile-Lagerhalle bereit stehen."
      GO quest_agreed
    END_OF_OPT
    OPT "Hm, mein Schiff ist ziemlich klein. Eine Kiste sollte gehen."
      HidePath "R1-3a"
      ShowPath "R1-3a1"
      Say "Sehr freundlich."
      Say "Die Kiste wird in der Teile-Lagerhalle bereit stehen."
      GO quest_agreed
    END_OF_OPT
    OPT "Wissen Sie was? Vergessen Sie's. Die Strecke ist für zerbrechliches Zeug zu gefährlich."
      SET boozeQuestRefused
      RESET boozeQuestTried
      Say "*Seufz*... Wahrscheinlich haben Sie recht. Schönen Tag noch."
      GO leaving_bar
    END_OF_OPT
  END_OF_MENU

  LABEL quest_agreed
  ResetFocus bbartender
  SET boozeQuestAccepted
  RESET boozeQuestRefused
  Delay 300
  ChangeChar bartender campaign/bartender.png
  Say "Danke und einen schönen Tag noch."
  GO leaving_bar

  LABEL bar_debt
  IF debtRefused
    ChangeChar bartender campaign/bartender-close.png
    Ask "Und Sie sind jetzt hier, um ihre Schulden zu bezahlen?"
  ELSE
    Say "Ach. Ich glaube, ich kenne die Dame, die Sie meinen. Sie hatte so einen einprägsamen Namen."
    Say "Und die Party, die sie geschmissen hat, war ebenfalls einprägsam."
    Say "Schlauerweise habe ich mir auch eingeprägt, wie viel Kohle sie ausgegeben hat."
    ChangeChar bartender campaign/bartender-close.png
    Ask "Könnte es sein, dass sie Sie geschickt hat, um ihre Schulden zu begleichen?"
  END_OF_IF
  MENU
    OPT "Jepp. Deswegen bin ich hier."
      IF_HAS_CREDITS 21
        SET hasEnoughForDebt
      END_OF_IF
      Ask "Oh, wunderbar! Es waren 21 Credits."
      MENU
        OPT_IFNOT hasEnoughForDebt "Oh. Ich ... glaube, ich habe meine [g(Brieftasche|Handtasche)] auf meinem Schiff liegenlassen."
          GO bar_debt_refused
        END_OF_OPT
        OPT_IF hasEnoughForDebt "Natürlich. Hier bitte:"
          RESET mechDebtPending
          Delay 300
          AddCredits -21
          Announce "Du hast 21 Credits bezahlt."
          ChangeChar bartender campaign/bartender.png
          ResetFocus bbartender
          Ask "Vielen, herzlichen Dank. Ich hoffe, Sie haben noch einen wunderschönen Tag."
          MENU
            OPT "Und da bin ich wieder."
              UnlockAchievement camp_honesty
              GO leaving_bar
            END_OF_OPT
            OPT "Moment mal. Das war's schon? Bloß ein Dankeschön?"
              EXIT_MENU
            END_OF_OPT
          END_OF_MENU
          Ask "Naja, die Schulden sind bezahlt. Alles ist in Ordnung. Was hatten Sie denn erwartet?"
          MENU
            OPT "Ich weiß nicht. Irgendeine Belohnung. Oder zumindest eine neue Mission."
              Say "Tut mir leid. Ich habe keine neue Mission für Sie."
              Say "Wie wäre es mit einem Erfolg?"
              UnlockAchievement camp_honesty
              Delay 1500
              Say "Hier bitte. Ich hoffe, Sie sind zufrieden."
              Say "Schönen Tag noch."
              GO leaving_bar
            END_OF_OPT
          END_OF_MENU
        END_OF_OPT
        OPT_IFNOT debtConfusion "Hm. Sie hat gesagt, es wären bloß 20 Credits gewesen."
          SET debtConfusion
          ChangeChar bartender campaign/bartender.png
          Ask "Nein, ich bin recht sicher, dass es 21 waren. Ich habe hier ja noch die Rechnung."
        END_OF_OPT
        OPT "Hm. Wissen Sie was? Ich glaube, wir sprechen nicht von derselben Mechanikerin. Ich behalte das Geld."
          GO bar_debt_refused
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
    OPT "Hm. Wissen Sie was? Ich glaube, wir sprechen nicht von derselben Mechanikerin. Ich behalte das Geld."
      GO bar_debt_refused
    END_OF_OPT
  END_OF_MENU

  LABEL bar_debt_refused
  SET debtRefused
  Delay 500
  Say "Ach. Verstehe."
  ChangeChar bartender campaign/bartender.png
  Say "Tja. Schönen Tag noch."
  GO leaving_bar
END_OF_SCRIPT

SCRIPT regulaDlg

  ShowDlgScene
  ShowChar clerk left
  Delay 1200
  ChangeChar clerk campaign/clerk-frown.png
  Delay 500
  IF class3
    Ask "Sie schon wieder? Was wollen Sie denn jetzt?"
    MENU
      OPT "Äh, eigentlich nichts."
        GO leaveClerk
      END_OF_OPT
      OPT "Wie wäre es mit einer Lizenz für die Schiffsklasse IV?"
        ChangeChar clerk campaign/clerk.png
        Ask "So eine Klasse gibt es nicht. Wie kommen Sie denn auf so eine hirnverbrannte Idee?"
        MENU
          OPT_IFNOT gtabe "Äh, wahrscheinlich vom Brettspiel Galaxy Trucker: Noch eine GROßE Erweiterung."
            SET gtabe
            Ask "Ich habe nicht die geringste Ahnung, wovon Sie reden. Es gibt keine Klasse IV."
          END_OF_OPT
          OPT "Nun, was mache ich denn jetzt bloß?"
            Say "Das ist nicht mein Problem. Ich bin nicht Ihr Kindermädchen."
            Delay 300
            ChangeChar clerk campaign/clerk-frown.png
            Say "Fragen Sie Ihren Flugausbilder, wenn Ihnen nichts einfällt."
            GO leaveClerk
          END_OF_OPT
        END_OF_MENU
      END_OF_OPT
    END_OF_MENU
  END_OF_IF
  IF class3Asked
    Say "Sie schon wieder? Ich sagte bereits, dass Schiffe der Klasse III für Leute wie Sie nichts sind."
    ChangeChar clerk campaign/clerk.png
    Ask "Oder ... besitzen Sie 450 Credits? Haben Sie Gutscheine von zwei Alienrassen?"
    RESET oneApproval
    RESET noApproval
    IF brownLicenseApproval
      IF purpleLicenseApproval
        SET twoApprovals
      ELSE
        SET oneApproval
      END_OF_IF
    ELSE
      IF purpleLicenseApproval
        SET oneApproval
      ELSE
        SET noApproval
      END_OF_IF
    END_OF_IF
    IF_HAS_CREDITS 450
      MENU
        OPT_IF twoApprovals "Natürlich. Was denken Sie denn?"
          ChangeChar clerk campaign/clerk.png
          Delay 200
          Say "Wirklich? Lassen Sie mal sehen ..."
          Delay 500
          ChangeChar clerk campaign/clerk-frown.png
          Delay 1500
          ChangeChar clerk campaign/clerk.png
          Delay 300
          Say "Hm ... Ihre Papiere scheinen in Ordnung zu sein ... Bankauszug sieht gut aus."
          Delay 300
          Say "Oh ..."
          Delay 300
          Say "Also gut. [g(Mein Herr|Gnädige Frau)], erlauben Sie mir, Ihnen zum Erwerb Ihrer kostbaren Klasse-III-Lizenz zu gratulieren."
          AddCredits -450
          Announce "Du hast 450 Credits bezahlt. Schiffsklasse III freigeschaltet!"
          UnlockAchievement camp_class3
          Delay 500
          SUB_SCRIPT class3_inits
          ResetFocus bclerk
          GO afterUnlock
        END_OF_OPT
        OPT_IF oneApproval "Das Geld ist kein Problem. Außerdem habe ich bereits einen Gutschein."
          Say "Bevor Sie nicht alles haben, haben Sie gar nichts."
          Say "Bitte gehen Sie. Ich habe zu tun."
          GO leaveClerk
        END_OF_OPT
        OPT_IF noApproval "Nun ... Ich habe das Geld."
          Say "Schön für Sie. Viel Spaß mit Ihrem Geld. Es wird Ihnen hier ohne die Gutscheine aber nicht nützen."
          Say "Jetzt gehen Sie bitte. Ich habe zu tun."
          GO leaveClerk
        END_OF_OPT
      END_OF_MENU
    ELSE
      MENU
        OPT_IF twoApprovals "Nun ... Ich habe die Unterlagen, die Sie wollten ..."
          Say "Diese Dokumente sind wertlos, wenn Sie die Bearbeitungsgebühr nicht zahlen können."
          Say "Wenn Sie mich jetzt entschuldigen wollen, ich habe zu tun."
          GO leaveClerk
        END_OF_OPT
        OPT_IF oneApproval "Ich habe einen Gutschein."
          Say "EINEN Gutschein? Sie haben mich unterbrochen, um mir mitzuteilen, dass Sie EINEN Gutschein haben? Hrmpf!"
          GO leaveClerk
        END_OF_OPT
        OPT_IF noApproval "Naja, noch nicht. Aber ich arbeite daran."
          Say "Nein, das tun Sie nicht. Was Sie tun, [g(mein Herr|meine Dame)], ist mir meine Zeit zu stehlen."
          GO leaveClerk
        END_OF_OPT
      END_OF_MENU
    END_OF_IF
  END_OF_IF
  IF class2
    Ask "Oh. Sie sind das. Sie haben Ihre Klasse-II-Lizenz. Was wollen Sie hier noch?"
    MENU
      OPT "Äh, eigentlich nichts."
        GO leaveClerk
      END_OF_OPT
      OPT "Wie wäre es mit einer Lizenz für die Schiffsklasse III?"
        Say "Schiffsklasse III? Nein, sicher nicht."
        ChangeChar clerk campaign/clerk.png
        Say "Die Bedingungen zum Erwerb der Klasse III sind äußerst strikt."
        Say "Die Lizenz kostet 450 Credits, sowie zwei unterschriebene Gutscheine zweier Alienrassen."
        Say "Bei allem Respekt, [g(mein Herr|gnädige Frau)], ich glaube nicht, dass Sie das nötige Taktgefühl besitzen, um die Zustimmung der Aliens zu bekommen."
        SET class3Asked
        ResetFocus bclerk
        IF purpleUnlocked
          SetFocus bpurple
        END_OF_IF
        IF brownUnlocked
          ShowButton bmerch2
          SetFocus bmerch2
        END_OF_IF
        Say "Jetzt gehen Sie bitte. Ich habe zu tun."
        GO leaveClerk
      END_OF_OPT
    END_OF_MENU
  END_OF_IF
  Ask "Was wollen Sie?"
  MENU
    OPT "Äh ... nichts."
      GO leaveClerk
    END_OF_OPT
    OPT "Ich hätte gerne eine Lizenz für ein Klasse-II-Schiff."
      ResetFocus bclerk
      SetFocusIfCredits bclerk 100
      Ask "Die Lizenz kostet 100 Credits!"
      RESET tooMuchFor2
      RESET whereToGetSoMuch
      RESET hasFor2
      RESET hasFewer
      IF_HAS_CREDITS 100
        SET hasFor2
      ELSE
        SET tooMuchFor2
      END_OF_IF
      IF_HAS_CREDITS 50
        SET hasFewer
        IF clerkBribe
          IF clerkDiscount
            RESET hasFewer
          END_OF_IF
        END_OF_IF
      END_OF_IF
      MENU
        OPT_IF hasFor2 "Kein Problem. Nehmen Sie das Geld und geben Sie mir die Lizenz."
          Delay 100
          ChangeChar clerk campaign/clerk.png
          Delay 300
          Say "Oh, Sie haben tatsächlich so viel?"
          Say "In diesem Fall ist hier Ihre Lizenz, [g(mein Herr|gnädige Frau)]."
          Delay 300
          AddCredits -100
          Announce "Du hast 100 Credits bezahlt. Schiffsklasse II freigeschaltet!"
          UnlockAchievement camp_class2
          Delay 500
          SUB_SCRIPT class2_inits
          HideButton bclerk
          GO afterUnlock
        END_OF_OPT
        OPT_IF tooMuchFor2 "Das ist viel zu teuer!"
          RESET tooMuchFor2
          SET whereToGetSoMuch
          Delay 300
          ChangeChar clerk campaign/clerk.png
          Say "Ich versichere Ihnen, dass die Kosten exakt so hoch sind, wie sie sein sollten."
          Delay 500
          ChangeChar clerk campaign/clerk-frown.png
          Ask "Wir können nicht zulassen, dass Unterschichtsgesindel Oberklasse-Schiffe fliegt."
        END_OF_OPT
        OPT_IF whereToGetSoMuch "Wo soll ich denn so viel Geld herkriegen?"
          RESET whereToGetSoMuch
          Say "Sie müssen dafür arbeiten. So wie ich für mein Geld arbeiten muss."
          Say "Nämlich in diesem Büro sitzen, vier Stunden täglich mit gerade mal einer Stunde Mittagspause und das drei Tage die Woche."
          Say "Trucker dagegen, ist mir zu Ohren gekommen, fliegen einfach so durch die Gegend und klauben von überall her Geld auf."
          Ask "Und dieses Geld geben sie dann hier aus. Als Gegenleistung für meine Unterschrift auf diesem Formular."
        END_OF_OPT
        OPT_IF hasFewer "Wie wär's, wenn ich Ihnen 50 gebe und sie ein Auge zudrücken?"
          Delay 900
          Ask "Wollen Sie mich etwa bestechen?"
          MENU
            OPT_IFNOT clerkDiscount "Nein, ich will bloß einen Rabatt rausschlagen."
              Delay 300
              ChangeChar clerk campaign/clerk.png
              Say "[g(Mein Herr|Gnädige Frau)], Sie sind hier auf Regula."
              Say "Hier hat es seit 200 Jahren keinen Rabatt mehr gegeben."
              Delay 200
              ChangeChar clerk campaign/clerk-frown.png
              Delay 300
              Say "Ich bin sogar sicher, dass ich bei genauer Prüfung der Regularien noch ein paar Zuschläge finden kann, die in Ihrem Fall wirksam wären."
              Ask "Oder Sie bezahlen einfach 100 Credits."
              SET clerkDiscount
              EXIT_MENU
            END_OF_OPT
            OPT_IFNOT clerkBribe "Und wenn's so wäre?"
              Delay 300
              ChangeChar clerk campaign/clerk.png
              Say "Oh. In diesem Fall benötigen Sie ein Formular B-29, Bestechungen und andere regelwidrige Entlohungen."
              Say "Wobei ich sicher bin, dass Sie es niemals korrekt ausfüllen könnten."
              Say "Die Verfahrensweisen bezüglich Bestechung sind außerordentlich komplex."
              Say "Ich schlage vor, Sie vergessen es. Die Kunst der Korruption wurde hierzulande bereits vor Jahrhunderten perfektioniert."
              Delay 200
              ChangeChar clerk campaign/clerk-frown.png
              Delay 300
              Ask "Als Anfänger dürfte Ihnen besser damit gedient sein, die 100 Credits zu zahlen."
              SET clerkBribe
              EXIT_MENU
            END_OF_OPT
            OPT "Äh ... vergessen Sie bitte, was ich gesagt habe."
              Ask "Natürlich. Also, zahlen Sie die 100 Credits jetzt?"
              EXIT_MENU
            END_OF_OPT
          END_OF_MENU
        END_OF_OPT
        OPT "Nun, ich werde darüber nachdenken. Auf Wiedersehen."
          GO leaveClerk
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL afterUnlock
  MENU
    OPT "Gut. Ich bin dann mal weg."
      GO leaveClerk
    END_OF_OPT
    OPT "Dankeschön. Sehr freundlich."
      GO leaveClerk
    END_OF_OPT
  END_OF_MENU

  LABEL leaveClerk
  Delay 500
  ChangeChar clerk campaign/clerk-frown.png
  Delay 500
  HideChar clerk
  HideDlgScene
  RETURN
END_OF_SCRIPT

END_OF_SCRIPTS
