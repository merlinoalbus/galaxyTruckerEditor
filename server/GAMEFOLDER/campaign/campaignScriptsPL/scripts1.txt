SCRIPTS

SCRIPT newbieDlg

  IF class3
    SUB_SCRIPT tutorClass3Dlg
    RETURN
  END_OF_IF
  IF tutor_moremissions
    Delay 200
    ShowDlgScene
    ChangeChar tutor campaign/tutor-hidden.png
    ShowChar tutor left
    Delay 1500
    ChangeChar tutor campaign/tutor-pizza.png
    Delay 500
    SayChar tutor "Nie widzisz, że jestem zajęty? Spadaj!"
    Delay 500
    ChangeChar tutor campaign/tutor-hidden.png
    Delay 500
    HideChar tutor
    Delay 500
    HideDlgScene
    RETURN
  END_OF_IF
  Delay 200
  ShowDlgScene
  ShowChar tutor left
  Delay 200
  IF tutor_somemissions
    Say "Witaj ponownie. Mam nadzieję, że jesteś zadowolon[g(y|a)] ze swojej zwykłej, nudnej trasy."
    MENU
      OPT "Tak, chciał[g(em|am)] się tylko przywitać."
        HideDlgScene
        RETURN
      END_OF_OPT
      OPT "Mógłbyś polecić mi inną trasę?"
        GO more_missions
      END_OF_OPT
    END_OF_MENU
  END_OF_IF
  IF tutorialRefused
    IF playerIsCocky
      Ask "Ha, [g(panie|pani)] „wiem wszystko”. Zmienił[g(eś|aś)] zdanie, co?"
    ELSE
      Ask "Ha, [g(panie|pani)] „nienawidzę samouczków”. Zmienił[g(eś|aś)] zdanie, co?"
    END_OF_IF
    MENU
      OPT "Nie, po prostu pokaż mi jak się stąd wydostać."
        Say "W porządku. Stąd możesz polecieć zwykłą trasą do kosmicznego baru i z powrotem."
        Delay 500
        ShowNode "bar"
        CenterMapByNode bar
        Delay 750
        ShowPath "R1-1a"
        ShowPath "R1-1b"
        Delay 500
        CenterMapByPath R1-1a
        Delay 1000
        SET tutor_somemissions
        Ask "To bezpieczna, nudna trasa. Możesz nią latać w kółko, do końca swojego żałosnego życia."
        MENU
          OPT "Brzmi dobrze."
            GO basic_route_agreed
          END_OF_OPT
          OPT "Hej, zleć mi prawdziwą misję!"
            GO more_missions
          END_OF_OPT
        END_OF_MENU
      END_OF_OPT
    END_OF_MENU
  ELSE
    Ask "Ha! Czyż to nie mój żółtodziób! Twoje szkolenie się skończyło. Mogę ci jeszcze w czymś pomóc?"
    MENU
      OPT "Nie powiedziałeś mi, co mam robić dalej."
        Say "Ach, prawda. Pow[g(inieneś|innaś)] polecieć zwykłą trasą stąd do kosmicznego baru."
        Delay 250
        ShowNode "bar"
        CenterMapByNode bar
        Delay 750
        ShowPath "R1-1a"
        ShowPath "R1-1b"
        Delay 500
        CenterMapByPath R1-1a
        Delay 1000
        SET tutor_somemissions
        Ask "To bezpieczna, nudna trasa. Możesz nią latać w kółko, do końca swojego żałosnego życia."
        MENU
          OPT "Brzmi dobrze. Dzięki."
            Say "Świetnie. Stuknij tylko w tamtą pulsującą rakietę i możesz lecieć."
            GO basic_route_agreed
          END_OF_OPT
          OPT "Jakieś inne opcje?"
            GO more_missions
          END_OF_OPT
        END_OF_MENU
      END_OF_OPT
    END_OF_MENU
  END_OF_IF

  LABEL basic_route_agreed
  HideDlgScene
  RETURN

  LABEL more_missions
  Ask "Myślę, że z żadną inną trasą sobie nie poradzisz."
  MENU
    OPT "Hmmm, pewnie masz rację. Polecę zwykłą trasą."
      GO newbie_end
    END_OF_OPT
    OPT "Nieprawda. Stać mnie na dużo więcej."
      Say "Och, [g(taki pewien|taka pewna)] jesteś? W porządku, sam[g(|a)] się o to prosił[g(eś|aś)]."
      Say "Jest inna trasa – do odległej placówki."
      Delay 1000
      ChangeChar tutor campaign/tutor-scary.png
      Delay 500
      AskChar tutor "Niebezpieczeństw i straszliwych rzeczy pełna jest."
      MENU
        OPT "Opowiedz coś o tych niebezpieczeństwach."
          Say "Po prostu nieopisanie straszliwe rzeczy."
          Say "Na więcej pytań nie odpowiem teraz, [g(mój młody padawanie|moja młoda padawanko)]."
          Ask "Powiedziałem za dużo już."
          MENU
            OPT "No przestań, nie żartuj."
              Delay 250
              ChangeChar tutor campaign/tutor.png
              Delay 250
              Say "No dobrze. Na trasie natrafisz na naprawdę parszywe meteory. Szczęśliw[g(y|a)]?"
              Say "A teraz spadaj. Mają mi zaraz dostarczyć pizzę."
              GO more_missions1
            END_OF_OPT
          END_OF_MENU
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL more_missions1
  HideChar tutor
  ResetFocus btutor
  Delay 250
  ShowNode outpost
  CenterMapByNode outpost
  Delay 500
  ShowPath "R1-2a"
  ShowPath "R1-2b"
  CenterMapByPath "R1-2a"
  Delay 500
  SET tutor_moremissions

  LABEL newbie_end
  HideDlgScene
  RETURN
END_OF_SCRIPT

SCRIPT embassyDlg

  ShowDlgScene
  IF purpleEscortFinished
    ShowChar ambassador left
    Delay 250
    ChangeChar ambassador campaign/ambassador-happy.png
    Delay 100
    Say "Dziękuję za twoje usługi. Naprawdę się przydały."
    Say "Proszę, oto nagroda w wysokości 10 kredytów. Pochodzi z naszego funduszu na nieoczekiwane wydatki, płacone nieznanym nieznajomym."
    AddCredits 10
    Announce "Dostał[g(eś|aś)] 10 kredytów."
    Delay 250
    HideButton bambassador
    HideChar ambasador
    HideDlgScene
    RETURN
  END_OF_IF
  IF purpleAccepted
    ShowChar ambassador left
    Delay 250
    Say "Proszę, musimy się spieszyć."
    Say "Nie możemy sobie pozwolić na jakiekolwiek scysje z kosmitami z Th'kzw-r. To strzelcy wyborowi."
    Delay 250
    HideChar ambassador
    HideDlgScene
    RETURN
  END_OF_IF
  IFNOT wasAtEmbassy
    SET wasAtEmbassy
    ChangeChar ambassador campaign/ambassador-peek.png
    ShowChar ambassador right
    Delay 250
    Ask "Pssst! Czy możesz pójść za mną w ten ciemny korytarz?"
    MENU
      OPT "Jasne, czemu nie?"
        GO corridor
      END_OF_OPT
      OPT "Mama przestrzegła mnie, żebym nie podążał[g(|a)] za ludźmi w ciemne korytarze."
        Ask "Daj spokój. To naprawdę delikatna sprawa. Nie mogę o niej rozmawiać publicznie."
        MENU
          OPT "No dobrze, niech będzie."
            GO corridor
          END_OF_OPT
          OPT "Nie, nie, nie!"
            SET corridorRefused
            GO refused
          END_OF_OPT
        END_OF_MENU
      END_OF_OPT
    END_OF_MENU
  ELSE
    IF purpleOfferMade
      ChangeChar ambassador campaign/ambassador.png
      ShowChar ambassador left
      Delay 250
      Ask "Tak?"
      MENU
        OPT "Nadal potrzebujesz kogoś, kto zawiezie sam-wiesz-kogo sam-wiesz-gdzie?"
          Ask "Tak. Oferujesz mi swoje usługi?"
          MENU
            OPT "Tak, możesz na mnie liczyć."
              GO acceptPurple
            END_OF_OPT
            OPT "Nie, był[g(em|am)] tylko ciekaw[g(|a)]."
              GO refused
            END_OF_OPT
          END_OF_MENU
        END_OF_OPT
      END_OF_MENU
    ELSE
      ChangeChar ambassador campaign/ambassador-peek.png
      ShowChar ambassador right
      Delay 250
      Say "Tak?"
      MENU
        OPT "Zmienił[g(em|am)] zdanie. Chodźmy do tego ciemnego korytarza."
          GO corridor
        END_OF_OPT
        OPT "Tylko się rozglądam."
          GO refused
        END_OF_OPT
      END_OF_MENU
    END_OF_IF
  END_OF_IF

  LABEL corridor
  Say "Świetnie, dzięki."
  Delay 250
  HideChar ambassador
  Delay 1500
  ChangeChar ambassador campaign/ambassador.png
  ShowChar ambassador left
  Delay 250
  Say "Więc… mamy tu bardzo delikatną sytuację. Ostatnio odwiedziła nas delegacja z Th'kzw-r. Kiedy odlecieli, przypadkowo zostawili tu jednego ze swoich przedstawicieli."
  Ask "Czy by[g(łbyś|łabyś)] tak dobr[g(y|a)] i odstawił[g(|a)] pana K’wwfrga na jego planetę macierzystą?"
  SET purpleOfferMade
  MENU
    OPT "Jasne, zawiozę pana K'wwfrga na Th'kzw-r, nie ma problemu."
      GO acceptPurple
    END_OF_OPT
    OPT "Czemu nie znajdziesz jakiegoś luksusowego statku dyplomatycznego, który go odwiezie?"
      Ask "Cóż, cała ta sytuacja jest dla pana K'wwfrga ciutkę upokarzająca. Chciałby załatwić całą sprawę po cichu, dyskretnie."
      MENU
        OPT "OK, możesz na mnie liczyć."
          GO acceptPurple
        END_OF_OPT
        OPT "Być może powinieneś poszukać do tej roboty kogoś innego."
          GO refused
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL acceptPurple
  Say "Och, świetnie. Dziękuję."
  Delay 250
  ChangeChar ambassador campaign/ambassador-happy.png
  Delay 250
  Say "Panie K'wwfrg?"
  Delay 1000
  ShowChar purpleDipl right
  Delay 250
  SayChar ambassador "[g(Ten dżentelmen|Ta dama)] odwiezie pana do domu."
  Delay 250
  AskChar purpleDipl "Wzrr kh'gwh qt Th'kzw-r!"
  MENU
    OPT "Nie ma za co."
      Delay 250
      HideChar purpleDipl
      Delay 500
      ChangeChar ambassador campaign/ambassador.png
      Delay 250
      SayChar ambassador "Serdecznie dziękuję. Wynagrodzimy ci to po powrocie."
      SET purpleAccepted
      ShowNode "purpleHome"
      ShowPath "U1-5purple"
      SET purpleAccepted
      ResetFocus bambassador
      IF VIPTried
        HideButton bvip
        ShowButton bvip2
      END_OF_IF
      HideChar ambassador
      HideDlgScene
      RETURN
    END_OF_OPT
  END_OF_MENU

  LABEL refused
  Say "To, yyy… w takim razie miłego dnia."

  LABEL sceneEnd
  Delay 1000
  HideChar ambassador
  HideDlgScene
  RETURN
END_OF_SCRIPT

SCRIPT barDlg

  ShowDlgScene
  ShowChar bartender left
  Ask "Dzień dobry. W czym mogę służyć?"
  IF boozeQuestHeard
    MENU
      OPT_IF mechDebtPending "Wysłała mnie tu pewna pani mechanik."
        GO bar_debt
      END_OF_OPT
      OPT_IF boozeQuestTried "Mam wykonać kolejną dostawę?"
        GO booze_quest
      END_OF_OPT
      OPT_IF boozeQuestRefused "Nadal potrzebujesz kogoś do wykonania dostawy?"
        GO booze_quest
      END_OF_OPT
      OPT "Dzisiaj chciałbym tu wydać trochę pieniędzy. Lej, co masz najlepszego!"
        GO drinking
      END_OF_OPT
      OPT "Och, przepraszam. Wszedłem tu przypadkowo."
        Say "Nie ma sprawy. Miłego dnia."
        GO leaving_bar
      END_OF_OPT
    END_OF_MENU
  END_OF_IF
  MENU
    OPT_IF mechDebtPending "Wysłała mnie tu pewna pani mechanik."
      GO bar_debt
    END_OF_OPT
    OPT "Zrób mi wasz najlepszy koktajl!"
      Ask "Oczywiście. Jeden kredyt."
      IF_HAS_CREDITS 1
        MENU
          OPT "Stać mnie na to. Zarobiłem trochę po drodze. Proszę bardzo:"
            Say "Dziękuję."
            AddCredits -1
            Announce "Zapłacił[g(eś|aś)] 1 kredyt."
            GO anything_else
          END_OF_OPT
          OPT "To za drogo. Wychodzę."
            Say "W porządku. Miłego dnia."
            GO leaving_bar
          END_OF_OPT
        END_OF_MENU
      ELSE
        MENU
          OPT "Ech, zapomnij. Nie jestem ostatnio przy kasie."
            Say "Jasne. Miłego dnia."
            GO leaving_bar
          END_OF_OPT
        END_OF_MENU
      END_OF_IF
    END_OF_OPT
    OPT "Tylko się rozglądam. Macie tu ładny bar."
      GO nice_bar
    END_OF_OPT
    OPT "Ale w tej zapadłej dziurze cuchnie! Spadam stąd."
      Say "W porządku. Miłego dnia."
      GO leaving_bar
    END_OF_OPT
  END_OF_MENU
  HideChar programmer
  HideDlgScene

  LABEL anything_else
  Say "Coś jeszcze?"
  MENU
    OPT "To ja chciałem o to zapytać. Coś jeszcze? Jakaś misja albo coś?"
      Say "Słucham?"
      MENU
        OPT "Właśnie wydałem trochę waluty w grze na wirtualny napój. Takie działanie powinno prowadzić do jakiejś misji!"
          Delay 200
          ChangeChar bartender campaign/bartender-close.png
          Delay 200
          Say "Ach, teraz rozumiem. Przykro mi, ale wybrałeś złą gałąź dialogową."
          Say "Do misji prowadziła ta z rozglądaniem się. I była darmowa."
          Delay 200
          ChangeChar bartender campaign/bartender.png
          Delay 200
          Say "Coś jeszcze?"
          MENU
            OPT_IFNOT bar_quest_insist "Zleć mi misję."
              SET bar_quest_insist
              Ask "Bardzo mi przykro, ale podajemy tu tylko napoje. Coś jeszcze?"
            END_OF_OPT
            OPT "Jeszcze jednego drinka!"
              GO drinking
            END_OF_OPT
            OPT "Nie, dzięki. Tylko się rozglądam. Macie tu ładny bar."
              GO nice_bar
            END_OF_OPT
          END_OF_MENU
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL drinking
  IF_HAS_CREDITS 1
    Say "Już się robi! Proszę bardzo. Jeden kredyt proszę."
    AddCredits -1
    Announce "Zapłacił[g(eś|aś)] 1 kredyt."
    MENU
      OPT "*Chlup*"
        Ask "Coś jeszcze?"
        MENU
          OPT "Wiesz co? Nalej mi jeszcze jednego."
            GO drinking
          END_OF_OPT
          OPT "Myślę, że to było o jednego drinka za dużo. Chyba czas na mnie."
            Say "Dziękujemy za odwiedziny. Miłego dnia."
            GO leaving_bar_in_shame
          END_OF_OPT
        END_OF_MENU
      END_OF_OPT
    END_OF_MENU
  ELSE
    Say "Jeden kredyt proszę… och, widzę, że skończyły ci się pieniądze!"
    Say "Proszę, weź tego lizaczka. Na koszt baru. A teraz proszę się stąd ulotnić."
    GO leaving_bar_in_shame
  END_OF_IF

  LABEL nice_bar
  Say "Dziękuję. To było kiedyś niezwykłe miejsce."
  MENU
    OPT "Kiedyś?"
      Say "Ostatnimi czasy interes się nie kręci. W zasadzie niedługo będziemy musieli zwinąć podwoje."
      Say "Na stanie mam mnóstwo wysokiej jakości trunków. Ale piloci ciężarówek wolą tańsze napitki."
      Ask "Szkoda, że nie otworzyłem baru na Reguli. Tamtejsi doceniają prawdziwą jakość."
      MENU
        OPT "Czemu nie sprzedasz tych trunków z zaplecza na Reguli?"
          SET boozeQuestHeard
          Say "To nie takie proste. Wiesz, to bardzo delikatny towar, a trasa nie należy do bezpiecznych."
          Ask "Wątpię, czy znalazłby się pilot kosmicznej ciężarówki gotowy dostarczyć moje napoje na Regulę."
          MENU
            OPT "Właśnie mnie znalazłeś!"
              GO booze_quest
            END_OF_OPT
            OPT "Też tak myślę. Powodzenia, na mnie już czas."
              SET boozeQuestRefused
              Say "Dziękuje. Miłego dnia."
              GO leaving_bar
            END_OF_OPT
          END_OF_MENU
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL leaving_bar_in_shame
  Delay 200
  ChangeChar bartender campaign/bartender-close.png
  Delay 200
  Say "Och… Czy był[g(byś|abyś)] tak dobr[g(y|a)] i wysz[g(edł|ła)] tylnymi drzwiami? Z góry dziękuję."

  LABEL leaving_bar
  Delay 300
  HideChar bartender
  HideDlgScene
  RETURN

  LABEL booze_quest
  Delay 200
  ChangeChar bartender campaign/bartender-close.png
  Delay 200
  Say "Och, naprawdę chcesz to zrobić?"
  Ask "Ile skrzyń napojów udałoby ci się przewieźć?"
  MENU
    OPT "Daj mi dwie skrzynie. Dostarczę je bez problemu"
      HidePath "R1-3a"
      ShowPath "R1-3a2"
      Say "Jesteś niebywale łaskaw[g(y|a)]!"
      Say "Skrzynie będą na ciebie czekać w magazynie części."
      GO quest_agreed
    END_OF_OPT
    OPT "Mój statek jest dość mały. Wystarczy jedna skrzynia."
      HidePath "R1-3a"
      ShowPath "R1-3a1"
      Say "Jesteś bardzo uprzejm[g(y|a)]."
      Say "Skrzynia będzie na ciebie czekać w magazynie części."
      GO quest_agreed
    END_OF_OPT
    OPT "Wiesz co? Zapomnij o tym. Ta trasa jest zbyt niebezpieczna dla tak delikatnego ładunku."
      SET boozeQuestRefused
      RESET boozeQuestTried
      Say "Ech, pewnie masz rację. Miłego dnia."
      GO leaving_bar
    END_OF_OPT
  END_OF_MENU

  LABEL quest_agreed
  ResetFocus bbartender
  SET boozeQuestAccepted
  RESET boozeQuestRefused
  Delay 300
  ChangeChar bartender campaign/bartender.png
  Say "Bardzo dziękuję. Miłego dnia!"
  GO leaving_bar

  LABEL bar_debt
  IF debtRefused
    ChangeChar bartender campaign/bartender-close.png
    Ask "Przy[g(szedłeś|szłaś)] uregulować jej dług?"
  ELSE
    Say "Ach, chyba wiem, o kogo ci chodzi. Miała takie charakterystyczne imię."
    Say "Impreza, jaką tu urządziła, też była niezapomniana."
    Say "Niestety pamiętam też, ile pieniędzy wydała."
    ChangeChar bartender campaign/bartender-close.png
    Ask "Przysłała cię może, żeby uregulować dług?"
  END_OF_IF
  MENU
    OPT "Tak, po to tu jestem."
      IF_HAS_CREDITS 21
        SET hasEnoughForDebt
      END_OF_IF
      Ask "Wybornie! Dług wynosi 21 kredytów."
      MENU
        OPT_IFNOT hasEnoughForDebt "Och… chyba zostawił[g(em|am)] portfel na statku."
          GO bar_debt_refused
        END_OF_OPT
        OPT_IF hasEnoughForDebt "Jasne. Proszę:"
          RESET mechDebtPending
          Delay 300
          AddCredits -21
          Announce "Zapłacił[g(eś|aś)] 21 kredytów."
          ChangeChar bartender campaign/bartender.png
          ResetFocus bbartender
          Ask "Dziękuję, [g(proszę pana|proszę pani)]. Życzę wspaniałego dnia."
          MENU
            OPT "I wzajemnie."
              UnlockAchievement camp_honesty
              GO leaving_bar
            END_OF_OPT
            OPT "Czekaj. To wszystko? Dziękuję i tyle?"
              EXIT_MENU
            END_OF_OPT
          END_OF_MENU
          Ask "Dług został uregulowany. Wszystko jest w porządku. Czego jeszcze się spodziewał[g(eś|aś)]?"
          MENU
            OPT "Nie wiem… jakiejś nagrody. A przynajmniej kolejnej misji."
              Say "Przykro mi, ale nie mam dla ciebie nowych misji."
              Say "Może zadowolisz się osiągnięciem?"
              UnlockAchievement camp_honesty
              Delay 1500
              Say "Mam nadzieję, że to wystarczy."
              Say "Miłego dnia."
              GO leaving_bar
            END_OF_OPT
          END_OF_MENU
        END_OF_OPT
        OPT_IFNOT debtConfusion "Hm. Powiedziała, że dług wynosi tylko 20 kredytów."
          SET debtConfusion
          ChangeChar bartender campaign/bartender.png
          Ask "Nie, jestem pewien, że to było 21. Nadal mam tu rachunek."
        END_OF_OPT
        OPT "Hm. Wiesz co? Chyba myślimy o innej pani mechanik. (A ja zatrzymam swoje pieniądze.)"
          GO bar_debt_refused
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
    OPT "Hm. Wiesz co? Chyba myślimy o innej pani mechanik. (A ja zatrzymam swoje pieniądze.)"
      GO bar_debt_refused
    END_OF_OPT
  END_OF_MENU

  LABEL bar_debt_refused
  SET debtRefused
  Delay 500
  Say "Ach, rozumiem. Co poradzić."
  ChangeChar bartender campaign/bartender.png
  Say "Miłego dnia."
  GO leaving_bar
END_OF_SCRIPT

SCRIPT regulaDlg

  ShowDlgScene
  ShowChar clerk left
  Delay 1200
  ChangeChar clerk campaign/clerk-frown.png
  Delay 500
  IF class3
    Ask "Znowu ty? Czego chcesz tym razem?"
    MENU
      OPT "W zasadzie niczego."
        GO leaveClerk
      END_OF_OPT
      OPT "Licencję na statki klasy IV."
        ChangeChar clerk campaign/clerk.png
        Ask "Nie ma takiej klasy. Skąd ten pomysł?"
        MENU
          OPT_IFNOT gtabe "Prawdopodobnie z dodatku „Kolejna dodatkowa przyczepa”."
            SET gtabe
            Ask "Nie mam pojęcia o czym mówisz. Nie ma żadnej klasy IV."
          END_OF_OPT
          OPT "To co mam teraz zrobić?"
            Say "A ja wiem? Nie jestem twoją niańką."
            Delay 300
            ChangeChar clerk campaign/clerk-frown.png
            Say "Jeśli nie potrafisz myśleć samodzielnie, spytaj swojego instruktora lotniczego."
            GO leaveClerk
          END_OF_OPT
        END_OF_MENU
      END_OF_OPT
    END_OF_MENU
  END_OF_IF
  IF class3Asked
    Say "Znowu ty? Już ci mówiłem, statki klasy III nie są dla takich jak ty."
    ChangeChar clerk campaign/clerk.png
    Ask "A może masz 450 kredytów? Albo vouchery od dwóch gatunków kosmitów?"
    RESET oneApproval
    RESET noApproval
    IF brownLicenseApproval
      IF purpleLicenseApproval
        SET twoApprovals
      ELSE
        SET oneApproval
      END_OF_IF
    ELSE
      IF purpleLicenseApproval
        SET oneApproval
      ELSE
        SET noApproval
      END_OF_IF
    END_OF_IF
    IF_HAS_CREDITS 450
      MENU
        OPT_IF twoApprovals "Jasne, że mam. Za kogo ty mnie bierzesz?"
          ChangeChar clerk campaign/clerk.png
          Delay 200
          Say "Doprawdy? Pozwól, że sprawdzę…"
          Delay 500
          ChangeChar clerk campaign/clerk-frown.png
          Delay 1500
          ChangeChar clerk campaign/clerk.png
          Delay 300
          Say "Twoje dokumenty wyglądają w porządku. Konto bankowe też."
          Delay 300
          Say "Hmm…"
          Delay 300
          Say "Niech ci będzie. Pozwoli [g(pan|pani)], że jako pierwszy pogratuluję [g(mu|jej)] zdobycia prestiżowej licencji pilota na statki klasy III."
          AddCredits -450
          Announce "Zapłacił[g(eś|aś)] 450 kredytów. Odblokował[g(eś|aś)] statki klasy III."
          UnlockAchievement camp_class3
          Delay 500
          SUB_SCRIPT class3_inits
          ResetFocus bclerk
          GO afterUnlock
        END_OF_OPT
        OPT_IF oneApproval "Pieniądze nie stanowią problemu. Mam też jeden voucher."
          Say "Dopóki nie masz wszystkiego, nie masz niczego."
          Say "Proszę sobie iść. Mam dużo pracy."
          GO leaveClerk
        END_OF_OPT
        OPT_IF noApproval "No cóż… mam pieniądze."
          Say "To się chwali. Ciesz się nimi. Dopóki nie będziesz miał[g(|a)] tych voucherów, nic u mnie nie kupisz."
          Say "A teraz proszę sobie iść. Mam dużo pracy."
          GO leaveClerk
        END_OF_OPT
      END_OF_MENU
    ELSE
      MENU
        OPT_IF twoApprovals "Mam oba dokumenty, o które prosiłeś…"
          Say "Jeśli nie możesz uiścić opłaty manipulacyjnej, dokumenty na nic ci się zdadzą."
          Say "A teraz wybacz, mam dużo pracy."
          GO leaveClerk
        END_OF_OPT
        OPT_IF oneApproval "Mam jeden voucher."
          Say "Jeden voucher? Przerywasz moją pracę, żeby powiedzieć mi, że masz jeden voucher? Hmpf!"
          GO leaveClerk
        END_OF_OPT
        OPT_IF noApproval "Jeszcze nie, ale pracuję nad tym."
          Say "Nie, nie pracujesz nad tym –bo jesteś tutaj i tracisz mój czas!"
          GO leaveClerk
        END_OF_OPT
      END_OF_MENU
    END_OF_IF
  END_OF_IF
  IF class2
    Ask "Och, to ty. Masz swoją licencję klasy II. Czego jeszcze chcesz?"
    MENU
      OPT "W zasadzie niczego."
        GO leaveClerk
      END_OF_OPT
      OPT "Licencję na statki klasy III."
        Say "Statki klasy III? Nie, nie sądzę, żeby to było dla ciebie."
        ChangeChar clerk campaign/clerk.png
        Say "Wymagania klasy III są bardzo surowe."
        Say "Licencja kosztuje 450 kredytów, potrzebujesz też vouchery podpisane przed przedstawicieli dwóch gatunków kosmitów."
        Say "Z całym szacunkiem, myślę, że brak [g(panu|pani)] dyplomatycznego taktu potrzebnego do zdobycia zgody kosmitów."
        SET class3Asked
        ResetFocus bclerk
        IF purpleUnlocked
          SetFocus bpurple
        END_OF_IF
        IF brownUnlocked
          ShowButton bmerch2
          SetFocus bmerch2
        END_OF_IF
        Say "A teraz proszę sobie iść. Mam dużo pracy."
        GO leaveClerk
      END_OF_OPT
    END_OF_MENU
  END_OF_IF
  Ask "Czego chcesz?"
  MENU
    OPT "W zasadzie niczego."
      GO leaveClerk
    END_OF_OPT
    OPT "Chciałbym dostać licencję na statki klasy II."
      ResetFocus bclerk
      SetFocusIfCredits bclerk 100
      Ask "Licencja kosztuje 100 kredytów."
      RESET tooMuchFor2
      RESET whereToGetSoMuch
      RESET hasFor2
      RESET hasFewer
      IF_HAS_CREDITS 100
        SET hasFor2
      ELSE
        SET tooMuchFor2
      END_OF_IF
      IF_HAS_CREDITS 50
        SET hasFewer
        IF clerkBribe
          IF clerkDiscount
            RESET hasFewer
          END_OF_IF
        END_OF_IF
      END_OF_IF
      MENU
        OPT_IF hasFor2 "Jasne. Proszę, oto pieniądze. Gdzie moja licencja?"
          Delay 100
          ChangeChar clerk campaign/clerk.png
          Delay 300
          Say "Och, naprawdę masz aż tyle kredytów?"
          Say "W takim razie proszę, oto [g(pana|pani)] licencja."
          Delay 300
          AddCredits -100
          Announce "Zapłacił[g(eś|aś)] 100 kredytów. Odblokował[g(eś|aś)] statki klasy II."
          UnlockAchievement camp_class2
          Delay 500
          SUB_SCRIPT class2_inits
          HideButton bclerk
          GO afterUnlock
        END_OF_OPT
        OPT_IF tooMuchFor2 "Och, ale… ale… to stanowczo za dużo!"
          RESET tooMuchFor2
          SET whereToGetSoMuch
          Delay 300
          ChangeChar clerk campaign/clerk.png
          Say "Ręczę, że cena licencji jest adekwatna do jej wartości."
          Delay 500
          ChangeChar clerk campaign/clerk-frown.png
          Ask "Nie możemy pozwolić, żeby jakiś motłoch latał statkami wyższych klas."
        END_OF_OPT
        OPT_IF whereToGetSoMuch "Skąd mam wziąć tyle pieniędzy?"
          RESET whereToGetSoMuch
          Say "Zapracuj na nie. Tak jak ja pracuję na swoje."
          Say "Siedząc w biurze przez cztery godziny dziennie, tylko z jedną godzinną przerwą obiadową, trzy dni w tygodniu."
          Say "Z tego co słyszałem, piloci ciężarówek mogą sobie latać i dorabiać gdzie tylko się da."
          Ask "A potem wydawać te ciężko zarobione pieniądze tutaj. W zamian za mój podpis na tym formularzu."
        END_OF_OPT
        OPT_IF hasFewer "A może dam ci 50 i uznamy, że jesteśmy kwita?"
          Delay 900
          Ask "Chcesz mnie przekupić?"
          MENU
            OPT_IFNOT clerkDiscount "Nie, targuję się o zniżkę."
              Delay 300
              ChangeChar clerk campaign/clerk.png
              Say "Proszę [g(pana|pani)], jest [g(pan|pani)] na Reguli."
              Say "Ostatniej udokumentowanej zniżki udzielono tutaj 200 lat temu."
              Delay 200
              ChangeChar clerk campaign/clerk-frown.png
              Delay 300
              Say "W zasadzie jestem pewien, że jeśli dostatecznie dokładnie przejrzę przepisy, znajdzie się kilka opłat dodatkowych, które należałoby uiścić w twojej sytuacji."
              Ask "Albo możesz po prostu zapłacić 100 kredytów."
              SET clerkDiscount
              EXIT_MENU
            END_OF_OPT
            OPT_IFNOT clerkBribe "A jeśli tak?"
              Delay 300
              ChangeChar clerk campaign/clerk.png
              Say "Ach. W takim razie powin[g(ieneś|naś)] złożyć wniosek B-29, dotyczący łapówek i innych niestandardowych wynagrodzeń."
              Say "A nie sądzę, byś był[g(|a)] dostatecznie sprytn[g(y|a)], żeby wypełnić go poprawnie."
              Say "Przepisy dotyczące łapówek są bardzo skomplikowane."
              Say "Radzę o tym zapomnieć. Sztuka korupcji została tu opanowana do perfekcji wieki temu."
              Delay 200
              ChangeChar clerk campaign/clerk-frown.png
              Delay 300
              Ask "Jesteś amator[g(em|ką)], więc radzę ci po prostu zapłacić 100 kredytów."
              SET clerkBribe
              EXIT_MENU
            END_OF_OPT
            OPT "Eeee… zapomnij, że o tym wspomniałem."
              Ask "Dobrze. Zapłacisz więc 100 kredytów?"
              EXIT_MENU
            END_OF_OPT
          END_OF_MENU
        END_OF_OPT
        OPT "Muszę to przemyśleć. Do widzenia."
          GO leaveClerk
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL afterUnlock
  MENU
    OPT "W porządku. Znikam."
      GO leaveClerk
    END_OF_OPT
    OPT "Dziękuję, jesteś bardzo miły."
      GO leaveClerk
    END_OF_OPT
  END_OF_MENU

  LABEL leaveClerk
  Delay 500
  ChangeChar clerk campaign/clerk-frown.png
  Delay 500
  HideChar clerk
  HideDlgScene
  RETURN
END_OF_SCRIPT

END_OF_SCRIPTS
