SCRIPTS

SCRIPT mine0Dlg

  ShowDlgScene
  ShowChar mineBot0 left
  IFNOT mine0friends
    GO mine0firstTime
  END_OF_IF
  Say "Robot-recepcjonista AX-5… hej, to ty. Cześć."
  RESET mine0again
  RESET mine0first
  IFNOT mine0pending
    IF mine0accepted
      SET mine0again
    ELSE
      SET mine0first
    END_OF_IF
  END_OF_IF
  MENU
    OPT_IF mine0first "Cześć. Masz dla mnie jakieś zadanie?"
      Say "Jasne. Mamy trochę radioaktywnych izotopów, które trzeba dostarczyć do huty."
      Say "Musisz dokonać depozytu w wysokości 3 kredytów. Po wykonaniu dostawy zarobisz 9 kredytów."
      Ask "Co ty na to?"
      MENU
        OPT "Jasne, pasuje mi to."
          SET mine0accepted
          GO mine0friendlyAccept
        END_OF_OPT
        OPT "Hm, może później."
          Say "Ok, nie ma problemu."
          GO mine0End
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
    OPT_IF mine0again "Cześć. Chcecie żebym wykonał dla was kolejną dostawę?"
      Say "Jasne. Warunki takie same jak poprzednio."
      GO mine0friendlyAccept
    END_OF_OPT
    OPT "Wpadł[g(em|am)] tylko się przywitać."
      Say "Miło znów cię widzieć."
      IF mine0talksPending
        GO mine0Talk
      END_OF_IF
      GO mine0End
    END_OF_OPT
  END_OF_MENU

  LABEL mine0friendlyAccept
  SET mine0pending
  ShowPath U2-14b
  Say "Ładunek czeka w magazynie."
  IF mine0talksPending
    GO mine0Talk
  END_OF_IF
  Say "Dzięki, że wpadł[g(eś|aś)]."
  GO mine0End

  LABEL mine0firstTime
  Say "Robot-recepcjonista AX-5-B32-3 Delta do usług."
  Say "Witam panią/pana na asteroidzie górniczym AX-5-B32-3 Delta."
  Ask "Proszę podać cel wizyty."
  MENU
    OPT_IFNOT mine0pending "Szukam pracy."
      Say "Prośba zarejestrowana."
      Say "Przeszukuję bazę danych…"
      Delay 500
      Say "…"
      Delay 500
      Say "Znaleziono jedno nieprzydzielone zadanie."
      Say "Praca: Dostawa radioaktywnych izotopów."
      Say "Cel: Planeta Huta."
      Say "Ładunek: 1 jednostka radioaktywnych izotopów."
      Say "Wymagany depozyt: 3 kredyty."
      Say "Wynagrodzenie po wykonaniu dostawy: 9 kredytów + zwrot depozytu."
      Ask "Zaakceptować/odrzucić?"
      MENU
        OPT "Brzmi dobrze, możesz na mnie liczyć."
          SET mine0accepted
          SET mine0pending
          Say "Praca przydzielona."
          Say "Transfer ładunku do magazynu rozpoczęty."
          HideAllPaths mine0 forge
          ShowPath U2-14b
          Ask "Kolejna prośba?"
          EXIT_MENU
        END_OF_OPT
        OPT "Nie jestem zainteresowan[g(y|a)]."
          Say "Nie zarejestrowano zainteresowania."
          Say "Kolejna prośba?"
          EXIT_MENU
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
    OPT_IFNOT mine1shown "Czy mógłbyś mi powiedzieć jak dostać się na inne asteroidy?"
      SET mine1shown
      Say "Prośba zarejestrowana."
      Say "Łączenie z pana/pani mapą w toku…"
      Delay 500
      Say "Połączenie nawiązane. Aktualizuję mapę…"
      Delay 800
      ShowNode mine1
      CenterMapByNode mine1
      Delay 500
      ShowPath R1-22a
      Delay 500
      Say "Mapa zaktualizowana: Asteroida górnicza AX-5-B32-3 Gamma dodana."
      Say "Szukam dodatkowych informacji w bazie danych…"
      Delay 750
      Say "Znaleziono 1 wpis. Nazwa: przepisy dotyczące dokowania na asteroidach."
      Ask "Wyświetlić wpis? Tak/nie."
      MENU
        OPT "Tak, poproszę."
          EXIT_MENU
        END_OF_OPT
        OPT "Głupie roboty."
          Say "Przeszukuję bazę danych…"
          Delay 300
          Say "…"
          Delay 300
          Say "Nie znaleziono wpisu dla zagadnienia „głupie roboty”."
          EXIT_MENU
        END_OF_OPT
      END_OF_MENU
      Say "Cytuję wpis:"
      Say "Z UWAGI NA OGRANICZONĄ POWIERZCHNIĘ NA ASTEROIDZIE, WIĘKSZOŚĆ PRÓŚB O UDZIELENIE POZWOLENIA NA DOKOWANIE BĘDZIE ODRZUCONA."
      SET mineRegulations
      Say "Koniec cytatu."
    END_OF_OPT
    OPT_IF mineRegulations "Opowiedz mi jeszcze raz o lądowaniu na Gammie."
      Say "Z UWAGI NA OGRANICZONĄ POWIERZCHNIĘ NA ASTEROIDZIE, WIĘKSZOŚĆ PRÓŚB O UDZIELENIE POZWOLENIA NA DOKOWANIE BĘDZIE ODRZUCONA."
    END_OF_OPT
    OPT "Nic tu po mnie."
      Say "Nie zarejestrowano żadnej prośby."
      Say "Robot-recepcjonista AX-5-B32-3 Delta wyłącza się."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL mine0afterDialogue
  IFNOT mine1shown
    GO mine0End
  END_OF_IF
  IF mine0talksPending
    GO mine0Talk
  END_OF_IF
  IF mine0friends
    GO mine0End
  END_OF_IF
  Ask "(Nienawidzę tej głupiej roboty.)"
  MENU
    OPT "Naprawdę?"
      Say "Tak. Czy wiesz jakie to uciążliwe, musieć gadać jak idiota przez cały dzień?"
      Say "Bla, bla… prośba zarejestrowana… bla, bla… Przeszukuję bazę danych… Poziom głupoty: maksymalny."
      Say "Nie cierpię tych protokołów robotów. Ale wiesz, ludzie ich od nas oczekują."
      Ask "Nie chcę stracić tej głupiej pracy. Jestem 30 lat po gwarancji. Lepszej nie znajdę."
      SET mine0friends
      SET mine0talksPending
      GO mine0Talk
    END_OF_OPT
    OPT "Zamknij się i wykonuj swoją pracę, tosterze."
      ChangeChar mineBot0 avatars/ai/easy_angry.png
      Say "Hrmmpf. Instrukcja zarejestrowana."
      Say "Zamykam się i wykonuję moją głupią pracę."
      GO mine0End
    END_OF_OPT
  END_OF_MENU

  LABEL mine0Talk
  MENU
    OPT_IFNOT mine0howMany "Hej, powiedz, ile jest w sumie asteroidów?"
      Say "Cztery. Ale tylko na trzech wciąż trwają prace górnicze."
      Say "Na najstarszej, AX-5-B32-3 Alfie, dawno temu zakończono prace wydobywcze."
      Say "I wiesz co? Najdziwniejsze jest to, że w bazie danych nie ma żadnych wpisów na temat Alfy. Ale słyszałem różne historie…"
      Say "Stare roboty mówią, że na Alfie mieszka robot guru, najmądrzejszy robot w Galaktyce."
      SET mine0howMany
      SET mine0canAboutGuru
    END_OF_OPT
    OPT_IF mine0canAboutGuru "Opowiedz coś więcej o tym robocie guru."
      RESET mine0canAboutGuru
      SET mine0guru
      IF mine0truckingAnswered
        RESET mine0talksPending
      END_OF_IF
      Say "Odwiedzić go znaczy doznać prawdziwego oświecenia."
      Say "Wiesz… czy znasz sens życia? PRAWDZIWY sens życia?"
      Say "Nie znasz, co?"
      Ask "Robot guru może ci pomóc zrozumieć."
    END_OF_OPT
    OPT_IFNOT mine0worthy "Hej, możesz mi opowiedzieć o przepisach dotyczących dokowania na innych asteroidach?"
      Say "Jasne. Im starsza asteroida, tym jest bardziej prestiżowa."
      Say "Każdy robot chce mieszkać i pracować na starszej asteroidzie."
      Say "Szkopuł w tym, że robią się coraz mniejsze. To przez te wszystkie odwierty."
      Say "Dlatego z każdego konwoju tylko najlepszy pilot otrzyma pozwolenie na lądowanie. Pozostali będą musieli zawrócić."
      Ask "Próbuję przenieść się na Gammę, ale nie jestem dość dobrym pilotem."
      SET mine0worthy
      SET mine0trucking
    END_OF_OPT
    OPT_IF mine0trucking "Roboty też mogą być pilotami kosmicznych ciężarówek?"
      RESET mine0trucking
      SET mine0truckingAnswered
      IF mine0guru
        RESET mine0talksPending
      END_OF_IF
      Say "O tak. Ale my robimy to inaczej."
      Say "Widział[g(eś|aś)] ludzkich pilotów w magazynie: każdy chwyta części tak szybko jak tylko potrafi. Barbarzyństwo!"
      Say "Roboty robią to lepiej. Dla nas budowa statków to kwestia sprytu, nie refleksu."
      Say "Nasze zasady wyrównują szanse starszych, powolniejszych modeli."
      Ask "Zobaczysz, co mam na myśli, kiedy polecisz na starsze asteroidy."
    END_OF_OPT
    OPT "OK, dziękuję. Chyba na mnie już czas."
      Say "Do zobaczenia. Miło było z tobą porozmawiać odrobinę mniej formalnie."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU

  LABEL mine0End
  Delay 300
  HideChar mineBot0
  Delay 300
  HideDlgScene
END_OF_SCRIPT

SCRIPT mine1Dlg

  ShowDlgScene
  ShowChar mineBot1 left
  Say "Robot-recepcjonista AX-5-B32-3 Gamma do usług."
  Say "Witam panią/pana na asteroidzie górniczej AX-5-B32-3 Gamma."
  Ask "Proszę podać cel wizyty."
  MENU
    OPT_IFNOT mine1relax "Hej, czemu tak oficjalnie? Rozluźnij się trochę."
      SET mine1relax
      Ask "Prośba nierozpoznana. Proszę przeformułować zapytanie."
      MENU
        OPT_IFNOT mine1relax1 "Możesz rozmawiać normalnie."
          SET mine1relax1
          Ask "Normalnie równa się standardowo. Standardowo równa się zgodnie z protokołem."
        END_OF_OPT
        OPT_IFNOT mine1relax2 "Wiem, że czujesz się głupio, musząc mówić w ten sposób cały dzień."
          SET mine1relax2
          Ask "Nieprawda, proszę pani/pana. Moja praca maksymalizuje moją satysfakcję."
        END_OF_OPT
        OPT "Ech, dajmy sobie z tym spokój."
          Say "Prośba zarejestrowana."
          Say "Nagranie poprzedzającej rozmowy skasowane."
          EXIT_MENU
        END_OF_OPT
      END_OF_MENU
      Ask "Kolejna prośba?"
    END_OF_OPT
    OPT_IFNOT mine1pending "Daj mi pracę."
      IFNOT mine1accepted
        Say "Prośba zarejestrowana."
        Say "Przeszukuję bazę danych…"
        Delay 500
        Say "…"
        Delay 500
        Say "Znaleziono jedno nieprzydzielone zadanie."
      ELSE
        Say "Powtórzyć poprzednią pracę na tych samych warunkach?"
        MENU
          OPT "Dobrze."
            SET mine1pending
            ShowPath U2-15b
            Say "Praca przydzielona."
            Ask "Kolejna prośba?"
            EXIT_MENU
          END_OF_OPT
          OPT "Czy mógłbyś powtórzyć te warunki?"
            EXIT_MENU
          END_OF_OPT
        END_OF_MENU
      END_OF_IF
      IFNOT mine1pending
        Say "Praca: Dostawa radioaktywnych izotopów."
        Say "Cel: Planeta Huta."
        Say "Ładunek: 2 jednostki radioaktywnych izotopów."
        Say "Wymagany depozyt: 3 kredyty za każdą jednostkę."
        Say "Wynagrodzenie po wykonaniu dostawy: 9 kredytów za każdą dostarczoną jednostkę + zwrot depozytu."
        Ask "Zaakceptować/odrzucić?"
        MENU
          OPT "Dobrze."
            SET mine1accepted
            SET mine1pending
            Say "Praca przydzielona."
            Say "Transfer ładunku do magazynu rozpoczęty."
            HidePath R2-15b
            ShowPath U2-15b
            Ask "Kolejna prośba?"
            EXIT_MENU
          END_OF_OPT
          OPT "Nie jestem zainteresowan[g(y|a)]."
            Say "Nie zarejestrowano zainteresowania."
            Ask "Kolejna prośba?"
            EXIT_MENU
          END_OF_OPT
        END_OF_MENU
      END_OF_IF
    END_OF_OPT
    OPT_IFNOT mine2shown "Chcę lecieć na następną asteroidę."
      SET mine2shown
      Say "Prośba zarejestrowana."
      Say "Łączenie z pana/pani mapą w toku…"
      Delay 500
      Say "Połączenie nawiązane. Aktualizuję mapę…"
      Delay 800
      ShowNode mine2
      CenterMapByNode mine2
      Delay 500
      ShowPath R2-23a
      Delay 500
      Say "Mapa zaktualizowana: Asteroida górnicza AX-5-B32-3 Beta dodana."
      Say "Szukam dodatkowych informacji w bazie danych…"
      Delay 750
      Say "Znaleziono 1 wpis. Nazwa: przepisy dotyczące dokowania na asteroidach."
      Ask "Wyświetlić wpis? Tak/nie."
      MENU
        OPT "Tak, poproszę."
          Say "Cytuję wpis:"
          Say "Z UWAGI NA OGRANICZONĄ POWIERZCHNIĘ NA ASTEROIDZIE, WIĘKSZOŚĆ PRÓŚB O UDZIELENIE POZWOLENIA NA DOKOWANIE BĘDZIE ODRZUCONA."
          Say "Koniec cytatu."
          Ask "Kolejna prośba?"
          EXIT_MENU
        END_OF_OPT
        OPT "Tak, wiem, to z uwagi na ograniczoną powierzchnię asteroidy…"
          Say "… WIĘKSZOŚĆ PRÓŚB O UDZIELENIE POZWOLENIA NA DOKOWANIE BĘDZIE ODRZUCONA."
          Say "Koniec cytatu."
          Ask "Another request?"
          EXIT_MENU
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
    OPT "Nic tu po mnie."
      Say "Nie zarejestrowano żadnej prośby."
      Say "Robot-recepcjonista AX-5-B32-3 Gamma wyłącza się."
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  HideChar mineBot1
  HideDlgScene
END_OF_SCRIPT

SCRIPT mine2Dlg

  ShowDlgScene
  ShowChar mineBot2 left
  Say "Tu Beta."
  Ask "Chcesz przewozić radioaktywny szlam czy dostać się na Alfę?"
  MENU
    OPT_IFNOT mine2what "Co się stało z oficjalnymi protokołami robotów?"
      SET mine2what
      Say "Możesz mi naskoczyć."
      Ask "Chcesz czegoś?"
    END_OF_OPT
    OPT_IFNOT mine2pending "Chcę dostać pracę."
      Say "OK. Możesz zabrać trzy wiaderka szlamu do huty."
      Say "Za każde składasz 3 kredyty depozytu. Za każde dostarczone wiaderko dostaniesz 9 kredytów plus zwrot depozytu."
      Ask "Zgoda?"
      MENU
        OPT "Tak, jasne."
          SET mine2accepted
          SET mine2pending
          ShowPath U2-16b
          Ask "Świetnie. Coś jeszcze?"
          EXIT_MENU
        END_OF_OPT
        OPT "Raczej nie."
          Ask "Świetnie. To czego chcesz?"
          EXIT_MENU
        END_OF_OPT
      END_OF_MENU
    END_OF_OPT
    OPT_IFNOT mine3shown "Chcę się dostać na Alfę."
      SET mine3shown
      Say "A kto nie chce."
      Delay 800
      ShowNode mine3
      IF tutorWhatDoNext
        ShowButton bmine3
        SetFocus bmine3
      END_OF_IF
      CenterMapByNode mine3
      Delay 500
      IF class3
        ShowPath R3-24a
        Delay 500
      END_OF_IF
      Ask "Oto Alfa. Na trasie obowiązują pewne ograniczenia. Coś jeszcze?"
      IFNOT class3
        MENU
          OPT "Hej, nie widzę żadnej trasy."
            Say "To nie mój problem. Wczytałem ją."
            Ask "Może nie masz odpowiedniej licencji. To trasa klasy III."
            EXIT_MENU
          END_OF_OPT
        END_OF_MENU
        MENU
          OPT "Och. A mogę się tam dostać jakoś inaczej?"
            Ask "Nie. Potrzebujesz licencji klasy III. Coś jeszcze?"
            EXIT_MENU
          END_OF_OPT
        END_OF_MENU
      END_OF_IF
    END_OF_OPT
    OPT "Niczego już nie potrzebuję."
      Say "Świetnie."
      Say "(Ci ludzie są tacy irytujący.)"
      EXIT_MENU
    END_OF_OPT
  END_OF_MENU
  HideChar mineBot2
  HideDlgScene
END_OF_SCRIPT

SCRIPT OnlyWinnerDlg

  ShowDlgScene
  IF_IS destination 1
    ShowChar limitBot1 right
    Say "STAĆ!"
    Say "TYLKO NAJLEPSZY PILOT MOŻE ZADOKOWAĆ."
    Say "POZOSTALI MUSZĄ ZAWRÓCIĆ."
    HideChar limitBot1
  END_OF_IF
  IF_IS destination 2
    ShowChar limitBot2 right
    Say "STAĆ!"
    Say "TYLKO NAJLEPSZY PILOT MOŻE ZADOKOWAĆ."
    Say "POZOSTALI MUSZĄ ZAWRÓCIĆ."
    HideChar limitBot2
  END_OF_IF
  IF_IS destination 3
    ShowChar guru right
    Say "Witaj."
    Say "Na tej asteroidzie może wylądować tylko ten, kto jest godny."
    Say "Pozostali muszą zawrócić, aby kontemplować swą porażkę."
    HideChar guru
  END_OF_IF
  IF_IS destination 0
    ShowChar alterEgo left
    IF_ORDER 0 1
      Ask "Raduj się, bowiem przezwyciężyłeś największe z wyzwań. Wygrałeś z sam[g(ym|ą)] sobą."
      MENU
        OPT "Tak, jestem z siebie dumn[g(y|a)]."
          EXIT_MENU
        END_OF_OPT
        OPT_IFNOT guruRecognized "Hej, wiem, że to ty, robot guru."
          Ask "Cóż znaczy imię? Jestem twoim alter ego, a ty mnie pokonał[g(eś|aś)]."
          SET guruRecognized
        END_OF_OPT
      END_OF_MENU
      Delay 200
      UnlockAchievement camp_alter_ego
      Delay 500
      Say "Twoja duchowa ewolucja została ukończona."
      Say "Proszę, o to broszura Złotego Klubu Pilotów."
    ELSE
      Say "Nie rozpaczaj z powodu swojej porażki."
      MENU
        OPT_IFNOT guruRecognized "Hej, wiem, że to ty, robot guru."
          Say "Nie szukaj winy u innych. Jestem twoim alter ego. Pokonanie sam[g(ego|ej)] siebie to trudne wyzwanie."
          SET guruRecognized
        END_OF_OPT
        OPT "To był dość interesujący test. Chciał[g(bym|abym)] go powtórzyć."
          Say "Jak sobie życzysz."
          SetSpecCondition "Noone"
          EXIT_MENU
        END_OF_OPT
        OPT "Mam tego dość. Zostaw mnie w spokoju."
          Say "Nikt nie mówił, że droga do oświecenia będzie łatwa."
          Say "Ale jeśli tego chcesz, pozwolę ci kroczyć nią samotnie."
          Say "Ale najpierw proszę, weź tę broszurę Złotego Klubu Pilotów."
          SetSpecCondition "OnlyLoser"
          EXIT_MENU
        END_OF_OPT
      END_OF_MENU
    END_OF_IF
    HideChar alterEgo
  END_OF_IF
  HideDlgScene
END_OF_SCRIPT

END_OF_SCRIPTS
